trait AnyEntity extends AnyRef {
  system ref field id: Id?
  native workspaceId: UInt

  func canRead = false
  func canWrite = false

  object native all: QueryN[this]
  object native search(s: String?): QueryN[this]
  object native allWithDeleted: QueryN[this]
  object native load(id: Id): Query1[this]
  object native uuid(uuid: Uuid): Query1[this]
  native save: this
  native delete: this
  native touch: this
  native updateRegisters: this
  native forceTouch: this
  native created: Bool
  native updated: Bool
  native dataUpdated: Bool
  native registerUpdated: Bool
  native touched: Bool
  native deleted: Bool
  native keepState[R](f: => Future[R]): Future[R]
  native updateWith(item: AnyEntity, onlyMissing: Bool): Void
  native replace[E extends AnyEntity](item: E): NoInfer[E] {
    deprecated(use = "replaceWith")
  }
  native replaceWith[E extends AnyEntity](item: E): NoInfer[E]
  func canReplaceWith(item: Entity) = item.dslType.fullName == this.dslType.fullName
  native convertTo[E extends AnyEntity](e: E.object): NoInfer[E]
  native entityText: PlainText?

  func converters: [Converter]? = []

  view quick
  object view list
  front event onLoad: Future[Any] = {}
  front event onOpen: Future[Any] {
    deprecated(use = "onLoad")
    value = {}
  }
  back event preDelete: Void = {
  }
  back event onActivity: Void = {
  }
  back event onSave: Future[Any] = {
    onActivity
    onSaveOrDelete
  }
  back event onDelete: Future[Any] = {
    onActivity
    onSaveOrDelete
  }
  back event onSaveOrDelete: Future[Any] = {}
  func multiupdate: [AnyEntity]? = []
  back func preComputeItems: [AnyEntity]? = []

  back final func get: Any = this
  back object func get: Any = this.all
  back object final func post(body: this): this = body.save
  back final func patch: this = save
  back func apiUrl: Url = {
    if (dslType.isObject) "$(Workspace.current.apiRoot)/$(dslType.fullName.dropSuffix("\$"))"
    else "$(Workspace.current.apiRoot)/$(dslType.fullName)/$id"
  }
  back func apiUrl(function: String): Url = {
    "$apiUrl/$function"
  }

  back func saveBy(key: this => Any): this = {
    if (id) {
      this
    } else {
      let k = key(this)
      if (let old = this.object.all.find(i => i != this && key(i) == k)) {
        old.updateWith(this, onlyMissing = true)
        this.replaceWith(old)
        old
      } else {
        this.save
      }
    }
  }
}

class Converter[TO](to: TO.object, convert: (to: TO) => Void)

trait NewEngine extends AnyEntity

trait InlineEntity extends AnyEntity
trait RefEntity extends AnyEntity {
  const system field uuid: Uuid = generate

  func title: String

  func entityNumbers: [String?]? = []
  func realEntity: Entity
  view ref
  object view refList
  view traitItem
  view refItem

  func entitySubject: String {
    default value = title
  }
  impl func toString: String? {
    default value = title
  }
  system field extUrl: Url?

  func redirectUrl: Url = "https://rd.flow2b.com/r/$(Workspace.current.authId)/$(dslType.fullName)/$id"

  func hashSuffix: Url = "$(Workspace.current.authId)/$(dslType.fullName.as[Url])/$(id.toString.as[Url])"
  func mention: Html = '<a href="javascript:;" data-href="#$hashSuffix">#$fullTitle</a>'
  func mentionPublic: Html = '<a href="javascript:;" data-href="#$hashSuffix">#$entitySubject</a>'

  front func writeNfc: Void = NfcMessage(records = [NfcRecord.item(this)]).write

  view nfcWriteBtn = button(icon = "tag", "NFC", visible = Nfc.canWrite) {
    writeNfc
  }
  func hasNum(num: String?): Bool = num.trim && (entityNumbers.exists(_ =%= num))
  func searchStr: String {
    default value = title
  }
  func searchText: PlainText? {
    default value = entityText
  }
  func searchWeight: UDec[10]? = 1.0
}
trait PseudoEntity extends RefEntity {
  func mainEntity: RefEntity
  impl final func realEntity: Entity = mainEntity.realEntity
  impl func title: String {
    default
    value = mainEntity.title
  }
}

trait Entity extends RefEntity {
  field tickets: [Ticket]? read by items {
    views = []
  }

  back func issueTypes: [IssueType]? = {
    [ITMessageDelivery] + ITNotifyOnHold
  }
  back func calcIssues: [IssueType[Entity]]? = {
    issueTypes*.as[IssueType[Entity]].filter(i => i.enabled && i.hasIssue(this))
  }

  compute issues: [IssueType[Entity]]? {
    available(Inline)
    availableFor = Inline
    value = observers*.issues.distinct.sortBy(priority(this).desc)
    views = []
  }

  view issuesView = issues >> list(label = "", frame = true) {
    issueView(this) >> field(background = level.panelBackground)
  }

  back native updateDashboard: Void

  view dash
  view fullDash = grid(labels = None, width = 1.3) {
    row{
      y(width = 0.3, align = Right) {
        dslType.singular >> field(fontStyle = Label)
        itemImage >> field(miniature = List)
      }
      dash >> field(width = 1.0, margin = NoneInner)
    }
  }
  func itemImage: Image? = null
  func itemRoles: [Role]? = []
  func getItemRoles: [Role] = itemRoles.alt{
    if (let roles = observers*.observer*.as[User].filter(_.userEnabled)*.roles - Role.UserRole) {
      if (let r = roles.onlyOne) [r] else (roles - Role.Admin).toSet!
    } else {
      [Role.Admin]
    }
  }
  func itemUser: User? = null
  func itemPriority: Dec[8]? = 0
  func itemOrder: NativeOrd = ("DateDesc", touchedOn.alt(Date.now).desc)
  func itemActiveOrder: NativeOrd = itemOrder

  impl final func realEntity: this = this
  const system field activities: [Activity]? inline by entities {availableFor = Overall touchCascade = false}
  const system field observers: [SubjectObserver]? inline by subject {availableFor = All touchCascade = false}
  system compute activeObservers {
    views = []
    value = observers.filter(!observer.is[Dashboard] || !hasObserverInRole(observer.as[Dashboard]!.role))
  }
  const system field touchedOn: Date? = now

  native at(date: Date): this?
  native revisions: [Revision[this]]?
  native revisions[T](field: this => T): [Revision[T]]?
  native convertToPseudo[E extends AnyEntity](e: E.object, mainEntity: RefEntity): NoInfer[E]
  func saveMain: this = {
    Activity.current!.setMainEntity(this)
    save
  }

  func formUrl: Url = "$(System.url)#$(Workspace.current.authId)/$(dslType.fullName)/form/$id"
  back native publicUrl(contact: Contact, readOnly: Bool): Url

  back object func numbers(in: String?): [String]? = {
    in.all(Regex("(((^|[\s(,:])(([A-Za-z]{1,3}[\-\.\#]?[0-9]+[A-Za-z0-9\-\.]{2,})|([0-9\-]{4,})))|(#\s*[A-Za-z0-9\-\.]+))(?=[\s),:;.]|\$)"))
      .mapOpt {n =>
        n.toString.trim.dropPrefix("#").trim
      }
  }
  back object func findReferences(in: String?): [Entity]? = {
    numbers(in).flatMap {number =>
      Entity.all.filter(entityNumbers.exists(n => n =%= number)).result.get
    }
  }
  func hasObserverInRole(role: Role): Bool = observers.exists(is(role))
  func hasObserver(observer: User?): Bool = observers.exists(i => i.observer == observer)
  func isForRole(role: Role): Bool = getItemRoles.contains(role)
  func isFor(owner: IUser?, role: Role): Bool =  isForRole(role) && isForUser(owner, role)
  func isForUser(owner: IUser?, role: Role): Bool =  !owner || !splitPerUser(role.fullName) || !owner!.is(role) || hasObserver(owner?.as[User]) || !observers.exists(_.is(role))
  back func _isForUser(owner: IUser?, role: String): Bool =  !owner || !splitPerUser(role) || !owner!._is(role) || hasObserver(owner?.as[User]) || !observers.exists(_._is(role))
  func isFor(owner: IUser?): Bool = {
    !owner?.as[User] ||
    (!itemRoles && (hasObserver(owner?.as[User]) || !observers.exists(observer.is[User]) ) ) ||
    (getItemRoles.intersect(owner?.roles) && (hasObserver(owner?.as[User]) || getItemRoles.intersect(owner?.roles).exists(r => !hasObserverInRole(r))))
  }
  func unreadCount(user: User?): Int? = {
    observers.filter(o => o.observer ==? user)*.unreadCount.sum
  }

  func isForCurrent(role: Role): Bool = isFor(IUser.current, role)
  func splitPerUser(role: String): Bool = false

  back func notify(role: Role): Void = {
    if (let observers = observers.filter(is(role))) {
      observers*.notify()
    } else {
      observers.find(observer.as[Dashboard]?.role == role)
        .alt(Dashboard.for(role).join(this))
        .notify
    }
  }

  back func notifyAll(role: Role): Void = {
    joinAll(role)*.notify
  }
  back func joinAll(role: Role): [SubjectObserver]? = {
    User.all.filter(active && is(role)).map(_.join(this))
  }

  report Report {
    impl view list = User.load(User.current!.id!).flatMap(subjects.mapOpt(try? subject).filter(touchedOn)).sortBy(touchedOn!.desc)
      >> nav{list(canAdd = false){ref}}
  }
}

class ReplaceDialog {
  field what: Entity
  field replaceWith: Entity {
    from = Entity.all.filter(_ != what && what.canReplaceWith(_))
  }
  impl view form = nav {
    grid {
      replaceWith
    }
    button("Replace", icon = "exchange-alt", color = "#ff7a00")  {
      let _ <- back {
        replace(what, replaceWith)
      }
      view.choose()
    }
  }
  back object func replace(what: Entity, replaceWith: Entity) = {
    what.replaceWith(replaceWith)
    replaceWith.touch.saveMain
    Activity.current!.comment("Replaced $(what.mention)")
  }

}

class Revision[T] {
  field begin: Date
  field end: Date?
  //field prev: Revision[T]?
  field value: T
  //field next: Revision[T]?
}



interface Report {
  func list: Layout
}

class AvailableList[R extends AnyEntity] {
  field v: R {
    available(List)
  }
}

class AvailableInline[R extends AnyEntity] {
  field v: R {
    available(Inline)
  }
}
