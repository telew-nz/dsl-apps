entity Ticket extends DashboardItem with HasN {
  icon = "ticket-alt"

  object field gen: Int?
  impl func numPrefix = "TK"
  impl func nextN = increment(Ticket.gen)

  system const field channel: CommChannel?
  list field items: [Entity]? by tickets {label = "Related Items"}
  ref field status: TicketStatus = Entered

  ref field subject: String
  field description: Text?
  ref field roles: [Role]? = {
    if (let roles = items*.itemRoles) roles.toSet
    else if (front) if (let c = Dashboard.current) [c.role]
  }

  impl func itemRoles = roles
  impl func activeRoles = if (status.active) if (let roles) roles else observers*.observer*.as[User]*.roles.toSet
  impl compute title = subject
  func active = status.active

  impl view form = nav(fullScreenComments = !description) {
    grid(labels = None) {
      x(flow = false){
        status >> field(editMode = AlwaysEdit, compact)
        subject >> field(stretch = 1, fontStyle = H2)
        number >> field(fontStyle = Label)
      }
      roles
      items
      tickets >> field(singular = "Sub-Ticket")
      description
    }
    extension("nav")
  }
  impl view ref = x(flow = false){number >> field(fontStyle = Label) status >> field(editMode = AlwaysEdit) title >> field(stretch = 1)}
  impl view quick = grid (labels = None) {
    x(flow = false){status subject >> field(stretch = 1, fontStyle = H1) number >> field(fontStyle = Label)}
    roles >> field(compact)
    description
  }
  impl func splitPerUser(role: String): Bool = true
}

enum TicketStatus(active: Bool) {
  case Entered(active = true) {
    background = "#ed9"
    icon = "play"
  }
  case InProgress(active = true) {
    background = "#8bf"
    icon = "spinner"
  }
  case Closed(active = false) {
    color = "white"
    background = "#090"
    icon = "check-circle"
  }
  case Canceled(active = false) {
    background = "#999"
    icon = "times-circle"
  }
}