trait SystemService extends Entity with IUser with DashboardItem {
  background = "#222"

  func itemRoles = [Admin]
  func isError: Bool = serviceState == Error || serviceState == Disabled
  func active = userEnabled && RegisterService.serviceState == Active
  func issues = if (serviceState == Error) [ServiceError] else if (serviceState == Disabled) [ServiceDisabled]
  func fixes(issue: DashboardIssue) = if (issue == ServiceError || issue == ServiceDisabled) [Fix(text = "Please contact support"),
    Fix(text = "If the problem has been resolved, activate the service", acts = [
      Act(label = "Activate", do = UI.save{
        serviceState = Active
        save
      })
    ])]

  func authorized: Bool = true
  field serviceState: SystemServiceState {
    default = Active
    update = {
      if (new == Active) errorsCount = 0
    }
  }
  func roles: [Role]? = [Admin]
  field errorsCount: Int?
  compute userEnabled {
    value = authorized && (serviceState == Active || serviceState == Error)
    update = {
      serviceState = if (new) Active else Disabled
    }
  }
  const system field debug: Bool = false
}

object System {
  back func apiUrl: Url = "https://$(apiDomain)/api/v1"
  back func publicUrl: Url = url + "pub/"
  back native apiDomain: Url
  native url: Url
}

extend enum DashboardIssue {
  case ServiceError(role = Admin, priority = major)
  case ServiceDisabled(role = Admin, priority = critical) = "Service has been disabled due to many errors"
}

enum SystemServiceState {
  case Active {background = "#dea"}
  case Error {background = "#f99"}
  case Disabled {color = "white" background = "#c00"}
  case Inactive {background = "#ddd"}
}

interface SystemJob {
  background = "#222"
  field repeatCount: Int?
  func service: IUser
  native run(): Void
  back func do: Future[Any]
  back func error(error: Any): JobError = service.error(error)
}

interface JobError
object IgnoreError extends JobError
class ReportError(error: Any) extends JobError
class RepeatJob(on: Date, times: Int, reason: Any?) extends JobError {
  func toString = "RepeatJob($times times $on: $reason)"
}

class PostErrorJob(service: IUser, text: Text) extends SystemJob {
  func do = {
    throw text
  }
}

trait ScheduledJob extends AnyEntity with SystemJob {
  func service: SystemService
  func schedule: String?
  back func do: Future[Any]
}

trait DeferredJob extends AnyEntity with SystemJob {
  func service: SystemService
  func jobDate: Date?
  back func do: Future[Any]
}

object PublicService extends SystemService {
  func title = "Public Service"
  func serviceAccess: [Role]? = [Public]
  func isPublic: Bool = true
  view form = nav{grid(labels = Top){
   issuesView
   serviceState
   errorsCount >> field(editMode = AlwaysRead)
  }}
  func traitItem = serviceState
}

trait IApiService extends SystemService {
  const field login: String? {
    default = String.randomKey(32)
    label = "Key"
  }
  const field password: String? {
    transient
    default = String.randomKey(8)
    label = "Secret"
  }
  back func error(error: Any): JobError = IgnoreError
}

entity ApiService extends IApiService {
  field roles: [Role]?
  func serviceAccess = roles

  field title: String
  field reportErrors: Bool
  object view list = ApiService.all >> nav{list{x{title serviceState}}}
  view form = nav{grid(labels = Top){
    issuesView
    title
    roles
    login >> field(visible = id)
    password >> field(visible = id)
    reportErrors
    serviceState
    errorsCount >> field(editMode = AlwaysRead)
  }}
  back func error(error: Any): JobError = if (reportErrors) ReportError(error = error) else IgnoreError
}

interface WorkspaceRouter[D] {
  back func parse(request: IncomingHttpRequest): D
  back func keys(request: IncomingHttpRequest, data: D): [String]?
}
trait WorkspaceEndpoint[D] extends AnyEntity {
  func service: SystemService
  back func keys: [String]?
  back func router: WorkspaceRouter[D]
  back func process(request: IncomingHttpRequest, data: D): Future[Any]
  back func error(error: Any): JobError = service.error(error)
}

trait Endpoint extends AnyEntity {
  func service: SystemService
  back func process(request: IncomingHttpRequest): Future[Any]
  back func error(error: Any): JobError = service.error(error)
}

