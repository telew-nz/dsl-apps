entity Person extends LegalEntity {
  icon = "user"
  impl ref field our: OurPerson? inline by person
  field photos: [Image]?
  field gender: Gender?

  impl func numPrefix = "PE"

  ref field user: User? inline by person

  ref field name: String {label = "Full Name"}
  field companies: [CompanyPerson]? inline by person
  field birthdate: UtcDate[day]?

  impl func isOur = our
  impl func addresseePerson: Person? = this
  impl func collectContacts: [ActorContact]? = contacts + companies*.contacts + people*.contacts

  func firstName: String = name[ .. " "?)
  func lastName: String = name(" "? .. ]

  plural = "People"

  object native current: Person?

  impl compute title: String {
    value = name
    update = {name = new}
  }
  compute companyPerson: CompanyPerson {
    value = companies.onlyOne!
    update = {}
    visible = companies.onlyOne
  }
  compute company: Actor? {
    value = companies.onlyOne?.company
    update = {
      if (let new) {
        if (let c = companies.first) {
          c.company = new
        } else {
          CompanyPerson(person = this, company = new)
        }
      } else {
        if (let c = companies.first) {
          c.delete
        }
      }
    }
    visible = companies.count < 2
  }
  impl func systemContact: Contact? = user

  impl view form = nav{
    tabs {
      grid(labels = Top, label = "Main"){
        photos >> field(label = "",height = 10)
        x(flow = false){
          name >> field(stretch = 1, fontStyle = H1)
          number >> field(fontStyle = Label)
        }
        gender
        birthdate
        company
        companyPerson.shortForm >> field(label = "")
        companies >> field(visible = companies.count > 1)
        contacts >> field(frame = false, label = "Personal Contacts", margin = None)
        people >> list(label = "Related People", separateEdit)
        extension("main")
        desc
      }
      grid(labels = None, label = "User", visible = user || ui.edit) {
        add ("User", visible = !user) {
          user = User.incomplete()
        }
        user!.perForm
      }
      grid (label = "Our Legal Person", labels = None, visible = our) {
        our!.form >> field(frame = false)
      }
      extension("tabs")
      grid(labels = Top, label = "Other") {
        actorWorkspaceId
        companies
        group
        currency
        extension
      }
    }
    extension("buttons")
  }

  report Report {
    impl view list = Person.all >> nav{list{grid(labels = None){
      name
    }}}
  }

  impl view shortForm = grid(labels = None){
    name
    contacts >> field(frame = false)
  }

  impl view quick = grid(labels = None) {
    name
    contacts >> field(frame = false)
  }

  func isCurrent: Bool = user?.isCurrent || contacts.exists(isPubCurrent)
  impl func isTheir = isCurrent || group?.isTheir
}

entity OurPerson extends OurLegalEntity {
  field person: Person inline by our
  impl func mainEntity = person
  impl func legalEntity = person
  impl view form = grid(labels = Top, label = "Our Legal Person") {
  }
  impl view traitItem = y{
    h3("Person")
    person.title
  }
}
