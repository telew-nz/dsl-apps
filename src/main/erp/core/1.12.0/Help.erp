entity HelpTopic extends NewEngine with HasN {
  icon = "info"
  impl field title: String
  impl func numPrefix = if (workspaceId == Workspace.currentId) "MYTOPIC" else "TOPIC"
  impl func nextN = increment(HelpArticle.gen)
  field description: Text?
  impl func canRead = true
  field articles: [HelpArticle]? read by topic

  impl view form = nav{grid(labels = None) {
    x {title >> field(fontStyle = H3, stretch = 1) number >> field(fontStyle = Label)}
    description
    articles
  }}

  impl event onSave = {
    articles*.touch
  }
}

entity HelpArticle extends NewEngine with HasN {
  icon = "info-circle"
  plural = "Help"

  object field gen: Int?
  impl func numPrefix = if (workspaceId == Workspace.currentId) "MYHELP" else "HELP"
  impl func nextN = increment(HelpArticle.gen)

  override func searchStr = "$(super.searchStr) $topic"
  ref impl field title: String
  field topic: HelpTopic
  field description: Text
  impl view form = nav{grid(labels = None) {
    x {title >> field(fontStyle = H3, stretch = 1) number >> field(fontStyle = Label)}
    x {topic >> field (title = title, stretch = 1) topic.number >> field(fontStyle = Label)}
    description
  }}
  impl func canRead = true
  report Report {
    generateFilters = false
    impl view list = HelpArticle.all.union(HelpArticle.all.inWorkspace(1260)).distinct
      .groupBy(topic = topic.title, number = topic.number)
    >> nav{
      subtotal {
        grid(margin = LeftRight, labels = None) {
          x {
            topic >> field(fontStyle = Bold, stretch = 1)
            number >> field(fontStyle = Label)
          }
        }
        list {
          grid(labels = None) {
            x {
              title >> field(margin = LeftRight, stretch = 1) number >> field(fontStyle = Label)
            }
          }
        }
      }
      add("Article") {
        HelpArticle.incomplete().form.open
      }
    }
  }
}
