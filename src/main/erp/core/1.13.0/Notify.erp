trait NotifyTemplate[T extends Entity] extends Entity with NewEngine {
  func title(data: T): String {
    default value = title
  }
  func send(data: T): Void = throw "Sending is not implemented"

  ref field mode: NotifyMode = Automatic

  back func required: Bool = true
  field contacts: [Contact]? {
    from = Contact.all.filter(isOur && !_.is[IUser])
  }
  //if true, do not run the text through formatters such as Email template
  back func fullFormat: Bool = false
  back func supported: [CommChannel]? {
    default value = CommChannel.all.toSet
  }
  back func preferred: Seq[CommChannel]? = []

  back func mainEntity(data: T): Entity {
    default value = data
  }
  back func to(data: T): [Addressee]?
  back func contacts(data: T, addressee: Addressee): [Contact]? {
    default value = addressee.collectContacts.filter(canSend || NotifyService.testMode)
  }
  back func comment(data: T): Text? = ""
  back func from(data: T, channel: CommChannel): Contact? {
    default value = contacts.find(c => c.channel == channel)
        .or(NotifyService.contacts.find(c => c.channel == channel))
        .or(OurLegalEntity.all*.legalEntity*.contacts.find(c => c.channel == channel).result.get)
        .or(Contact.all.find(c => c.isOur && c.channel == channel).result.get)
  }
  back func subject(data: T, channel: CommChannel): String? {
    default value = data.entitySubject
  }
  back func text(data: T, channel: CommChannel, activity: Activity): Text?
  impl view traitItem = x{mode dslType.singular}
}

enum NotifyMode {
  case Automatic {background = "#dea" icon = "robot"}
  case SemiAutomatic {background = "#ed9" icon = "user-astronaut"}
  case Inactive {background = "#ddd" icon = "stop-circle"}
}

object NotifyService extends SystemService {
  impl func title = "Notification Service"
  func serviceAccess: [Role]? = [Admin]
  field testMode: Bool
  field contacts: [Contact]? {
    label = "Outgoing contacts"
  }
  impl func ourContacts: [IContact]? = contacts
  impl view form = nav{grid(labels = Top){
     issuesView
     serviceState
     errorsCount >> field(editMode = AlwaysRead)
     contacts
  }}
  view test = nav{grid(labels = Top){
     issuesView
     serviceState
     errorsCount >> field(editMode = AlwaysRead)
     contacts
     testMode
  }}
}