trait IContact extends RefEntity {
  func isPublic: Bool = true
  func isAutoPublic: Bool = isPublic
  func join(entity: Entity): SubjectObserver = {
    if (let o = entity.observers.find(observer == this)) o else SubjectObserver(subject = entity, observer = this).save
  }
  system field subjects: [SubjectObserver]? inline by observer {availableFor = Overall}
  func roles: [Role]? = []
  func canSend: Bool = false
  func channel: CommChannel? = null
  func atMention: Html = '<a href="javascript:;" data-href="#@$hashSuffix">@$title</a>'

  object native pubCurrent: IContact?
  func isPubCurrent: Bool = pubCurrent == this

  func contactLegalEntity: LegalEntity? = null
}

trait Contact extends IContact {
  func addresseeName: String? {default value = title}
  back func realContacts: [Contact]? {
    default
    value = [this]
  }

  impl func canSend: Bool = false
  back object func defaultRecipients: [Contact]? = []
  back object func send(activity: Activity, contacts: [this]): Void = {}
}

trait RawContact extends Entity with Contact {
  impl ref field addresseeName: String?
  var address: String
  impl compute title {
    value = "$addresseeName <$address>".trim!
    update = {
      if (let a = new("<" .. ">")) {
        address = a
        addresseeName = (new[ .. "<")).trim
      } else {
        address =? new.trim
        addresseeName = ""
      }
    }
  }

  compute addressee: Addressee? {
    value = null
    addingType = LegalEntity
    init = {
      if (let p = new?.as[Person]) {
        p.name =? addresseeName
      } else if (let c = new?.as[Company]) {
        c.title =? addresseeName
      } else if (let cp = new?.as[CompanyPerson]) {
        cp.person.name =? addresseeName
      }
    }
    update = {
      if (let a = new) {
        forge(a)
      }
    }
    init = forge(new)
  }
  func actorContactType: ActorContact.object
  func forge(addressee: Addressee): ActorContact = {
    convertToPseudo(actorContactType, addressee).save
  }
}

trait ActorContact extends PseudoEntity with Contact {
  ref field addressee: Addressee by contacts
  impl func addresseeName {
    default value = addressee.title
  }
  impl compute mainEntity {
    value = addressee
    update = {addressee = new}
  }

  impl func contactLegalEntity: LegalEntity? = addressee?.legalEntity
  func legalEntity: LegalEntity? = addressee?.legalEntity
  func person: Person? = addressee?.addresseePerson
  func company: Actor? = addressee?.addresseeCompany
  func address: String
  impl func title = "$addressee <$address>"
}

trait Addressee extends RefEntity {
  object field gen: Int?

  func legalEntity: LegalEntity? = addresseeCompany?.as[LegalEntity].or(addresseePerson)
  func addresseePerson: Person? = null
  func addresseeCompany: Actor? = null
  func systemContact: Contact? = null

  field contacts: [ActorContact]? inline by addressee
  func collectContacts: [ActorContact]? {
    default value = contacts
  }
  func phones: [PhoneContact]? = {
    contacts*.as[PhoneContact].or(collectContacts*.as[PhoneContact])
  }
  func emails: [EmailContact]? = {
    contacts*.as[EmailContact].or(collectContacts*.as[EmailContact])
  }
  func websites: [WebsiteContact]? = {
    contacts*.as[WebsiteContact].or(collectContacts*.as[WebsiteContact])
  }
}

trait IEmailContact extends Contact {
  impl func channel: CommChannel? = EMail
  func email: Email
  impl compute address: String = email
  override func isAutoPublic: Bool = !email.endsWith("@flow2b.com")

  back object func filterUser(address: String?): [IUser]? = {
    User.all.filter{u =>
         u.userEnabled
      && address.lower.startsWith(u.login.lower + "@")
      && (u.is(Admin) || u.person.companies*.company.filter(our)*.contacts*.as[WebsiteContact].exists(c => c.domain =%= address("@" .. ]))
    }
  }
  back object func find(address: String?): [Contact]? = {
    IEmailContact.all.filter(c => c.email =%= address).result.get
      .or(filterUser(address))
  }
  back object func make(contact: ExtMsgContact): Contact = {
    IEmailContact.all.find(c => c.email =%= contact.address).result.get
      .or{filterUser(contact.address).first}
      .or{
        if (let website = contact.address("@" .. ]) {
          if (let addressee = WebsiteContact.all.find(domain =%= website).result.get?.addressee) {
            if (!contact.name || !contact.name.contains(" ") || contact.name == addressee.title || !addressee.is[Company] || contact.type == "Role") {
              addressee.addEmail(contact.address!)
            } else {
              addressee.as[Company]!.addPerson(contact.name.replace(addressee.title, "").replace("()", "").trim)!.addEmail(contact.address!)
            }
          } else if (let companyName = contact.company) {
            let company = Company.all.find(c => c.title =%= companyName).result.get.alt(Company(title = companyName))
            company.addWebsite(website.lower)
            company.addPerson(contact.name.replace(company, "").replace("()", "").trim)!.addEmail(contact.address!)
          }
        }
      }
      .alt(RawEmailContact(addresseeName = contact.name, email = contact.address!))
  }
}

object Flow2bSupport extends Entity with IEmailContact {
  order = -2

  impl func title = "Support"
  impl func email: Email = "support@flow2b.com"
  override func isAutoPublic: Bool = false
}

entity EmailContact extends ActorContact with IEmailContact {
  icon = "at"
  singular = "Email"
  plural = "Emails"

  impl ref field email: Email {
    validate = if (!EmailContact.isAddressValid(email)) "Email format is invalid for $email"
  }

  object func isAddressValid(address: Email?): Bool = {
    address.matches(Regex("^[a-zA-Z0-9.!#\$%&â€™*+/=?^_`{|}~-]+@[a-zA-Z0-9-]+(?:\.[a-zA-Z0-9-]+)*\$"))
  }
  object func filterAddress(address: Email?): Email? = if (EmailContact.isAddressValid(address)) address
  impl view form = grid(labels = None) {
    addressee
    email
  }
  back object func add(addressee: Addressee, email: Email?): IEmailContact? {
    deprecated(use = "addressee.addEmail(email)")
    value = addressee.addEmail(email)
  }
  impl event onSave = {
    email = email.lower.trim!
  }
}
entity RawEmailContact extends RawContact with IEmailContact {
  singular = "Email"
  plural = "Emails"
  impl ref field email: Email {
    validate = if (!EmailContact.isAddressValid(email)) "Email format is invalid for $email"
  }

  impl view form = nav{grid {
    addressee >> field(editMode = AlwaysEdit)
    addresseeName
    email
  }}

  impl func actorContactType = EmailContact
  impl event onSave = {
    email = email.lower.trim!
  }
}

trait IPhoneContact extends Contact {
  ref field phone: Phone
  ref field type: PhoneType = Phone
  impl func address: String = phone

  back object func find(type: PhoneType, address: String?): [Contact]? = {
    IPhoneContact.all.filter(c => c.type == type && c.phone =%= address).result.get
  }
  back object func make(type: PhoneType, contact: ExtMsgContact): Contact = {
    IPhoneContact.all.find(c => c.type == type && c.phone =%= contact.address).result.get
      .alt(RawPhoneContact(addresseeName = contact.name, type = type, phone = contact.address!))
  }
}

entity PhoneContact extends ActorContact with IPhoneContact {
  icon = "phone"
  singular = "Phone"
  plural = "Phones"

  impl view form = grid(labels = None){
    addressee
    x{type phone >> field(stretch = 1)}
  }
  back object func add(addressee: Addressee, type: PhoneType, phone: Phone?): PhoneContact? {
    deprecated(use = "addressee.addPhone(email)")
    value = addressee.addPhone(type, phone)
  }
}

entity RawPhoneContact extends RawContact with IPhoneContact {
  singular = "Phone"
  plural = "Phones"

  impl view form = nav{grid {
    addressee >> field(editMode = AlwaysEdit)
    addresseeName
    x{type phone >> field(stretch = 1)}
  }}

  impl func actorContactType = PhoneContact
}


extend entity Addressee {
  back func addEmail(email: Email?): IEmailContact? = if (let email = email.trim) {
    if (EmailContact.isAddressValid(email)) {
      contacts*.as[ActorContact with IEmailContact].find(a => a.email =%= email)
        .or(IEmailContact.all.find(a => a.email =%= email && a.is[IEmailContact with RawContact]).map(_.as[IEmailContact with RawContact]!.forge(this).as[ActorContact with IEmailContact]!))
        .alt(EmailContact(addressee = this, email = email).save)
    }
  }

  back func addPhone(type: PhoneType, phone: Phone?): PhoneContact? = if (let phone = phone.trim) {
    contacts*.as[PhoneContact].find(a => a.phone =%= phone && a.type == type)
      .alt(PhoneContact(addressee = this, type = type, phone = phone).save)
  }

  back func addWebsite(url: Url?): WebsiteContact? = if (let url = url.trim) {
     contacts*.as[WebsiteContact].find(a => a.url =%= url)
       .alt(WebsiteContact(addressee = this, url = url).save)
  }
}

enum PhoneType {
  case Phone
  case Mobile
  case Fax
}

entity WebsiteContact extends ActorContact {
  icon = "globe"
  singular = "Website"
  plural = "Websites"
  ref field url: Url
  impl func address: String = url
  compute domain: Url? = url.dropPrefixes(["https://", "http://", "www."]).dropSuffix("/").lower
  impl view form = grid(labels = None){
    addressee
    url
  }
}

trait ContactAddressee extends Contact with Addressee {
  impl object func send(activity: Activity, contacts: [this]): Void {
    default value = {
      contacts*.join(activity.entities.first)*.notify
    }
  }
  impl func systemContact: Contact? = this
}
trait IContactGroup extends Entity with ContactAddressee
entity RoleContactGroup extends IContactGroup {
  icon = "users"
  impl func isPublic: Bool = false
  ref field role: Role
  impl func canSend: Bool = true
  impl back func realContacts: [Contact]? = User.all(role)
  impl func title: String = "All $role"
  impl view form = nav(fullScreenComments){grid(labels = Top){
    role
    contacts >> field(frame = false, margin = None)
  }}
}


class Address {
  field person: String?
  field company: String?
  field number: String?
  field street: PlainText? {
    availableFor = List
    validate = if (isEmpty) "Address should not be empty"
  }
  field suburb: String?
  field city: String?
  field region: String?
  field country: String?
  field postcode: String?

  compute isEmpty = !person && !company && !number && !street && !region && !suburb && !city && !region && !postcode && !country
  compute title: PlainText = ["$person $company".trim, "$(number.trim) $(street.trim)".trim, [suburb, city, region, postcode].filter(nonEmpty).join(" "), country].filter(nonEmpty).join("\n")!
  impl func toString = title

  impl view form = y{
    edit {
      panel(frame = true) {
        grid {
          row {person company}
          row {number >> field(width = 0.2) street >> field(width = 0.8)}
          row {suburb city region}
          row {postcode >> field(width = 0.2) country >> field(width = 0.8)}
        }
      }
    }
    read {
      title
    }
  }
}


enum CommChannel(back findContacts: (address: String?) => [Contact]?, back makeContact: ExtMsgContact => Contact, back defaultRecipients: => [Contact]?) {
  case EMail(
      findContacts = IEmailContact.find(address)
      makeContact = IEmailContact.make(_)
      defaultRecipients = IEmailContact.defaultRecipients
  ) {background = "#fff" icon = "envelope"}
  case SMS(
      findContacts = IPhoneContact.find(Mobile, address)
      makeContact = IPhoneContact.make(Mobile, _)
      defaultRecipients = IPhoneContact.defaultRecipients
  ) {background = "#02a6dc" icon = "sms"}
}
