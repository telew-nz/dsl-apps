trait AnyEntity extends AnyRef {
  system ref field id: Id?

  object native all: QueryN[this]
  object native load(id: Id): Query1[this]
  native save: this
  native delete: this
  native touch: this
  native forceTouch: this
  native created: Bool
  native updated: Bool
  native dataUpdated: Bool
  native registerUpdated: Bool
  native touched: Bool
  native deleted: Bool
  native keepState[R](f: => Future[R]): Future[R]
  native replace[E extends AnyEntity](item: E): NoInfer[E]
  native convertTo[E extends AnyEntity](e: E.object): NoInfer[E]

  view form
  object native list : Layout[this.object]
  front event onLoad: Future[Any] = {}
  front event onOpen: Future[Any] = {}
  back event onSave: Future[Any] = {}
  back event onDelete: Future[Any] = {}
  func multiupdate: [AnyEntity]? = []

  back final func get: Any = this
  back object func get: Any = this.all
  back object final func post(body: this): this = body.save
  back final func patch: this = save
  back func apiUrl: Url = {
    "$(Workspace.current.apiRoot)/$(dslType.fullName)/$id"
  }
  back func apiUrl(function: String): Url = {
    "$apiUrl/$function"
  }
}

trait InlineEntity extends AnyEntity
trait RefEntity extends AnyEntity {
  func title: String
  func toString: String? = title
  func entityNumbers: [String?]? = []
  func realEntity: Entity
}
trait PseudoEntity extends RefEntity {
  func mainEntity: RefEntity
  final func realEntity: Entity = mainEntity.realEntity
  func title: String = mainEntity.title
}

trait Entity extends RefEntity {
  view ref
  view dash
  object view refList
  view traitItem
  view refItem

  func itemRoles: [Role]? = []
  func itemPriority: Priority = default
  func itemOrder: NativeOrd = ("DateDesc", touchedOn.alt(now).desc)

  func entitySubject: String = title
  final func realEntity: this = this
  const system field activities: [Activity]? inline by entities {availableFor = Overall}
  const system field observers: [SubjectObserver]? inline by subject {availableFor = All}

  const system field touchedOn: Date? = now

  native at(date: Date): this?
  native revisions: [Revision[this]]?
  native revisions[T](field: this => T): [Revision[T]]?

  back func joinAll(role: Role): [SubjectObserver]? = {
    User.all(role)*.join(this)
  }

  back object func filterByNumber(number: String?): [Entity]? = if (let number) Entity.all.filter(entityNumbers.contains(number)).result.get

  func hasObserverInRole(role: Role): Bool = observers.exists(is(role))
  func hasObserver(observer: User?): Bool = observers.exists(i => i.observer == observer)
  func isForRole(role: Role): Bool = !itemRoles || itemRoles.contains(role)
  func isFor(owner: IUser?, role: Role): Bool =  isForRole(role) && isForUser(owner, role)
  func isForUser(owner: IUser?, role: Role): Bool =  !owner || !splitPerUser(role) || !owner!.is(role) || hasObserver(owner?.as[User]) || !hasObserverInRole(role)
  func isFor(owner: IUser?): Bool = {
    !owner?.as[User] ||
    (!itemRoles && (hasObserver(owner?.as[User]) || !observers.exists(observer.is[User]) ) ) ||
    (!itemRoles.intersect(owner?.roles) && (hasObserver(owner?.as[User]) || itemRoles.intersect(owner?.roles).exists(r => !hasObserverInRole(r))))
  }

  func isForCurrent(role: Role): Bool = isFor(IUser.current, role)
  func splitPerUser(role: Role): Bool = false

  func mention: Html = '<a href="javascript:;" data-href="#$(Workspace.current.authId)/$(dslType.fullName)/$id">#$title</a>'

  report Report {
    view list = User.load(User.current!.id!).flatMap(subjects.mapOpt(try? subject).filter(touchedOn)).sortBy(touchedOn!.desc)
      >> nav{list(canAdd = false){ref}}
  }

  event onOpen = {
    if (let u = User.current) {
      if (observers.exists(badgeUnreadCount(u) && if (let d = observer.as[Dashboard]) User.current?.is(d.role) && !hasObserverInRole(d.role) else false)) {
        UI.save {
          u.join(this)
        }
      }
    }
  }
}

class Revision[T] {
  field begin: Date
  field end: Date?
  //field prev: Revision[T]?
  field value: T
  //field next: Revision[T]?
}



interface Report {
  func list: Layout
}
