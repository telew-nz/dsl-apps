entity Company extends LegalEntity with ContactAddressee {
  ref field our: OurCompany? inline by company
  field title: String
  field logo: Image?

  func addresseeCompany: Actor? = this
  func isTheir: Bool = people.exists(person.isCurrent) || group?.isTheir
  func canSend = our
  func realContacts: [Contact]? = people*.person*.user.distinct
  func collectContacts: [ActorContact]? = contacts + people*.contacts + group?.collectContacts

  report Report {
    view list = Company.all >> nav{list{grid(labels = None){
      group
      title
    }}}
  }

  view form = nav{
    tabs {
      grid(labels = Top, label = "Main"){
        logo >> field(height = 8, label = "")
        title >> field(label = "", fontStyle = H1)
        group
        currency
        contacts >> field(frame = false, margin = NoneInner)
        people >> field(separateEdit)
        extension("main")
      }
      our!.form
      extension("tabs")
      grid(labels = Top, label = "Other") {
        extension
      }
    }
    extension("buttons")
  }

  view shortForm = grid(labels = None) {
    title
    contacts >> field(frame = false)
  }
}

entity OurCompany extends OurLegalEntity {
  field company: Company inline by our
  func legalEntity = company
  func mainEntity = company
  view form = grid(labels = Top, label = "Our Company") {
  }
  view traitItem = y{
    h3("Company")
    company.title
  }
}

entity CompanyGroup extends Actor with ContactAddressee {
  ref field our: OurCompanyGroup? inline by companyGroup{
    validate =
      if (our && legalEntities.exists(!our)) "Our company group can include only our companies"
      else if (!our && legalEntities.exists(our)) "Not our company group cannot include our companies"
  }
  field logo: Image?
  field title: String
  func addresseeCompany: Actor? = this
  func top: Actor = this
  func isTop: Bool = true
  field legalEntities: [LegalEntity] by group {available(All).for(All)}
  func all: [Actor] = this.as[Actor]! + legalEntities
  func isTheir: Bool = people.exists(person.isCurrent)
  func collectContacts: [ActorContact]? = contacts + people*.contacts

  func canSend = our
  func realContacts: [Contact]? = all*.people*.person*.user.distinct

  view shortForm = title
  view form =  nav{
     tabs {
       grid(labels = Top, label = "Main"){
         logo >> field(height = 8, label = "")
         title
         currency
         contacts >> field (margin = None)
         legalEntities
         people >> field(separateEdit)
         extension("main")
       }
       our!.form
       extension("tabs")
       grid(labels = Top, label = "Other") {
         extension
       }
     }
     extension("buttons")
   }

  report Report {
    view list = CompanyGroup.all >> nav{list{grid(labels = None){
      title
    }}}
  }
}

entity OurCompanyGroup extends OurActor {
  field companyGroup: CompanyGroup inline by our
  func mainEntity = companyGroup
  func actor = companyGroup
  view form = grid(labels = Top, label = "Our Company Group") {
  }
  view traitItem = y{
    h3("Company Group")
    companyGroup.title
  }
}