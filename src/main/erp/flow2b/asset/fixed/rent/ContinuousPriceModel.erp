entity ContinuousPriceModel extends IContinuousPriceModel with TRentPriceModel {
  impl field title: String
  ref impl field start: Date = now
  impl field end: Date?
  ref impl field durations: [RentDuration]
  impl field schedule: ISchedule? {label = "Charging Schedule (a store schedule will be used if empty)"}
  field overnightCharge: Bool {
    canEditUnit
  }
  impl ref field pricesIncludingGst: Bool = (try? vendor.pricesIncludingGst).alt(true)
  impl field products: [ContinuousProductPrice]? inline by model {
    availableFor = Overall
    touchCascade = false
  }
  impl func createProduct(product: IBaseProduct, prices: [DurPrice]?, bond: UMoney[currency]?): ContinuousProductPrice = {
    ContinuousProductPrice(model = this, product = product, prices = prices, bond = bond)
  }
  impl func doPrice(level: IPriceLevel, type: PriceType, product: IBaseProduct) = if (type == Rent) {
    products.find(p => p.product == product)
  }
  
  impl func supports(product: IBaseProduct): Bool = true
  impl func findPrice(product: IBaseProduct) = products.find(p => p.product == product)
  impl func forceNewPrice(product: IBaseProduct): ContinuousProductPrice = {
    ContinuousProductPrice(model = this, product = product)
  }
  impl ref field rounding: Rounding? = Round()
  
  field precisionUnit: DateUnit = minute
  impl field precision: UDuration[precisionUnit] {
    default = 1_minute
    canEditUnit
  }
  impl field dispatchRoundMode: RoundMode = floor
  impl field returnRoundMode: RoundMode = ceil
  
  impl view form = nav{
    grid {
      title
      row {
        start
        end
      }
      row {vendor pricesIncludingGst currency}
      row {precision dispatchRoundMode >> field(compact) returnRoundMode >> field(compact)}
      levels
      rounding
      row {
        schedule
        overnightCharge >> field(label = "Half Day Overnight Charge (will be added if the order is overnight and less than 1Â½ days)")
      }
      durations 
    }
    pmNavButtons
  }
}

inline entity ContinuousProductPrice extends TRentProductPrice with IContinuousProductPrice {
  impl system field model: ContinuousPriceModel by products
  
  override func rentDur(dispatch: ScheduleDate, ret: ScheduleDate, unit: DurUnit?, forceUnit: Bool): UQty = {
    let r = super[IContinuousProductPrice].rentDur(dispatch, ret, unit, forceUnit)
    if (!model.overnightCharge || dispatch.date.date == ret.date.date) r
    else if (r.unit == DurUnit.Day && r < 1.5) r + 0.5.as[UQty[DurUnit.Day]]
    else if (r.unit == DurUnit.HalfDay && r < 3) (r + 1.as[UQty[DurUnit.HalfDay]]).as[UQty[DurUnit.Day]]/2
    else if (r.unit == DurUnit.Minimum && r == 1) 1.as[UQty[DurUnit.Day]]
    else r
  }
  
  override view form = y{
    levels >> field(visible = !IPriceLevel.onlyOne)
    x (visible = !ui.edit) {rentPrices >> field(frame = false) model >> field(title = fullTitle, stretch = 1, align = Right)}
    grid(visible = ui.edit, labels = None, spacing = false) {
      model
      prices >> list(frame = false, margin = None){
        grid(labels = None, margin = None){row{
          dur >> field(align = Right, fontStyle = Label, width = 0.3) 
          price >> field(align = Right, width = 0.7) 
        }}
      }
      row{label("Bond", visible = bond || ui.edit, width = 0.3, align = Right) bond >> field(align = Right, width = 0.7) }
    }
  }
  
}
