enum OpportunityState(ast: ActiveState) {
  case Open(ast = InProgress) {background = "#8bf" icon = "spinner"}
  
  case Lost(ast = Canceled) {color = "white" background = "#c00" icon = "skull-crossbones"}
  case Won(ast = Closed) {color = "white" background = "#090" icon = "glass-cheers"}
  case Dormant(ast = Canceled) {background = "#ecd" icon = "bed"}
  case Canceled(ast = Canceled) {background = "#999" icon = "times-circle"}
  case Blocked(ast = Canceled) {background = "#f99" icon = "stop-circle"}
  
  
  case AwaitingReply(ast = AwaitingReply) {background = "#ffca99" icon = "hourglass"}
  case FollowUp(ast = FollowUp) {background = "#8bf" icon = "bell"}
  
} 

trait IOpportunity[Stage extends Enum] extends Entity with NewEngine with IActiveItem {
  background = "#a73e6c"
  singular = "Opportunity"
  plural = "Opportunities"
  
  impl func splitPerUser(role: String): Bool = true
  impl func itemRoles = [Salesperson]
  override func itemOrder = ("Opp", followUpDate, number)
  object field generator: Int?
  const system ref field n: Int =? increment(IOpportunity.generator)
  compute number: String = prefix + n.format(3, 0)
  impl func entityNumbers = [number]
  override func entitySubject = OurActor.our!.title
  func prefix: String
  ref field _state: OpportunityState {
    default = Open
    update = {
      onActiveStateUpdated
    }
  }
  func isBlocked = false
  ref compute state: OpportunityState {
    value = if (isBlocked && _state == Open) Blocked else _state
    update = {
      _state = new
    }
  }
  impl compute activeState: ActiveState {
    value = state.ast
    update = {
      state = OpportunityState.all.find(ast == new)!
    }
  }
  override func processFixes =  [
    Fix(text = "Win the opportunity", acts = [
       Act(label = "Win", icon = "glass-cheers", color = "#090", do = {
         _state = Won
         UI.save(this)
       })
    ])
  ]
  override func waitForReplyFixes(text: String): [IFix]? = super.waitForReplyFixes(text) + {
    appointments.filter(period.end > now && state != Canceled).map(FollowUpAfterFix(op = this, ap = _))
  }
  override func cancelFixes = [
    Fix(text = "Mark as lost if they said no", acts = [
       Act(label = "Lost", icon = "skull-crossbones", color = "#c00", do = {
         _state = Lost
         UI.save(this)
       })
    ])
    Fix(text = "Mark as dormant no response", acts = [
       Act(label = "Dormant", icon = "glass-cheers", do = {
         _state = Dormant
         UI.save(this)
       })
    ])
    Fix(text = "Mark as blocked if it cannot be complete until something happens", acts = [
       Act(label = "Block", color = "#c00", icon = "stop-circle" do = {
         _state = Blocked
         UI.save(this)
       })
    ])
    Fix(text = "Cancel if you decided to not persue it", acts = [
       Act(label = "Cancel", icon = "times-circle" do = {
         _state = Canceled
         UI.save(this)
       })
    ])
  ]
  
  
  compute status {
    deprecated(use = "state")
    value = state
  }
  compute contacts: [ActorContact]? {
    value = actor?.contacts
    update = {
      if (let actor) actor.contacts = new
    }
  }
  field nextFollowUpDate: Date?
  compute nextFollowUpDateEditor: Date[day]? {
    value = nextFollowUpDate
    update = {nextFollowUpDate = new}
  }
  ref field owner: User? {
    from = User.all.filter(u => this.itemRoles.toSeq.exists(r => u.is(r)))
  }
  impl func itemUser = owner
  ref field actor: Actor? by opportunities {
    available(Inline).for(Inline)
    deleteCascade = false
  }
  field appointments: [Appointment]? read by opportunities {
    init = {
      new.roles = itemRoles
      if (let p = Person.current) {
        AppointmentParticipant(participant = p, acceptance = Accepted, appointment = new)
      }
      people.foreach {p =>
        AppointmentParticipant(participant = p.person, appointment = new, acceptance = Accepted)
      }
    }
  }

  field _currency: Currency?
  compute currency: Currency = _currency.alt(actor?.getCurrency.alt(our))
  func estimatedAmount: UMoney[currency]?
  func probability: UPercent?
  list compute orderedAmount: UMoney? = salesOrders*.total.sum
  list compute paidAmount: Money? = salesOrders*.paidAmount.sum
  compute people: [CompanyPerson]? {
    //available(Inline).field(person, with(canBook))
    value = actor?.people
    visible = actor
    update = {
      actor!.people = new
    }
    singular = "Person"
  }
  compute roles {
    value = people
    deprecated(use = "people")
  }
  impl func multiupdate = actor.toSeq

  field salesOrders: [SalesOrder]? read by opportunity {
    visible = actor.nonEmpty
    init = {new.customer = actor.alt(throw "Actor is empty").legalEntities.first.makeCustomer?.as[Customer]}
  }
  
  func clickNextStage: Stage? = null
  back event onDeliveryEvent(activity: Activity, delivery: ActivityDelivery, event: ActivityDeliveryEvent): Void = {
    if (event.type == Click) {
      if (let clickNextStage) {
        stage = clickNextStage
        if (state in [Lost, Dormant]) state = Open
        notify(Salesperson)
        Activity.current!.from = delivery.contact.as[Contact]
        if (activity.mainEntity != this) {
          Activity.comment("<p>Link opened on $(activity.mainEntity.mention.as[Html]):<br>$(event.url)</p>")
        } else {
          Activity.comment("<p>Link opened:<br>$(event.url)</p>")
        }
        saveMain
      }
    } 
  }

  field source: OpportunitySource? 
  var stage: Stage
  func lost: Bool = _state == Lost
  func won: Bool = _state == Won

  field description: Text? 
  impl func title: String = number + " " + actor
  
  view peopleView = people >> list{form.removeField(_.company)}
  
  func stateView: Layout[this]
  view headerView = grid(labels = Top) {
    x{stateView actor >> field(stretch = 1, fontStyle = H3) number}
    owner
    followUpDate
  }
  view footerView = grid(labels = Top) {
    source
    tickets
    appointments
    salesOrders
    description
    contacts >> list(frame = false){form.as[Layout[ActorContact]].removeField(_.addressee)}
    peopleView
  }
  impl view ref = x {stateView title number >> field (fontStyle = Label)}
  impl view traitItem = dash
  impl view dash = y {
    x{number >> field(fontStyle = Label) actor >> field(stretch = 1, title = title) }
    x{stateView probability owner}
  }
  
  report Report {
    field activeOnly: Bool
    field salesperson: User? {
      from = User.all.filter(is(Salesperson))
    }
    field state: [OpportunityState]?
    field probabilityHigher: UPercent?
    field probabilityLower: UPercent?
    impl view list = IOpportunity.all.filter(
         isFor(salesperson, Salesperson) && (!this.state || this.state.contains(state)) 
      && probabilityHigher ?< probability && (!probability || probability <? probabilityLower)
      && (!activeOnly || activeState.active)
    ).sortBy(followUpDate, n) >> nav{
      list{}
    }
  }
}
extend trait SalesOrder {
  field opportunity: IOpportunity? {
    from = customer?.legalEntity?.opportunities
    views = []
  }
  extend view posView = opportunity
}