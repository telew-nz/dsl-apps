entity DispatchList {
  field number: String
  field date: DateTime = now
  field location: Location {
    from = Location.ours
    update = {
      OutcomeDelivery.all
        .filter(source.location == new && readiness == ForDispatch)
        .execute {
          deliveries = result.alt(throw "There are no deliveries ready for dispatch at " + new)
        }
    }
  }
  func title = number + " " + date.date + " " + location
  field deliveries: [OutcomeDelivery] by dispatchList {
     from = OutcomeDelivery.all.filter(source.location == location && readiness == ForDispatch)
  }
  view form = nav{grid(width = 1.5){
    row{number date location}
    deliveries
  }}
}

extend trait OutcomeDelivery {
  field dispatchList: DispatchList? by SalesDelivery
}