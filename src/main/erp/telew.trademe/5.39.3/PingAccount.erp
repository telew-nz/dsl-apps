entity PingAccount extends FinAccount {
  ref const field integration: ITrademeIntegration by pingAccount
  impl func title = "$integration: Ping Account"
  func service = integration
  impl func currency: Currency = NZD
  impl func legalEntity = integration.legalEntity

  impl func ledgerAccount: LedgerAccount = Cash
  impl func hasBankData = true

  impl view form = nav{
    grid{
      integration      
      initBalance
      balance
    }
    ledgerBtn
  }
}

object ReconTrademePingTransfer extends IReconRule {
  impl func title = "Transfer from Trademe Ping account"
  impl func pr = 30
  impl func backDescr = "If the counterpary name or memo contains trade me wellington or trade me limited and <u>not</u> pay now for an incoming payment, it's a transfer from the trademe ping account"
  impl func apply(movements: [CashMovement]?, payment: Payment): CashMovement? = {
    let l = payment.bankData.fullMemo.lower + payment.bankData.thierName.lower
    
    if (payment.is[IncomingPayment] && (l.contains("trade me wellington") || l.contains("trade me limited")) && !l.contains("pay now")
        let contraAccount = PingAccount.all.result.get.onlyOne
    ) {
       ReconTransfer.make(movements, payment, contraAccount)
    }
  }
}


back class PingCsvData extends BankStatementItem {
  impl field date: Date {format = "yyyy-MM-dd'T'HH:mm:ss.SSSSSSS"}
  field memo: String
  field sCredit: String
  compute credit = m(sCredit)
  field sDebit: String
  compute debit = m(sDebit)
  field sBalance: String
  compute balance = m(sBalance)
  field referenceId: String
  field sku: String?
  field sSaleAmount: String
  compute saleAmount = m(sSaleAmount)
  field sFee: String
  compute fee = if (credit) m(sFee).as[UMoney] else -m(sFee).as[UMoney]
  field sGst: String?
  
  func amount = if (let credit) credit else debit
  
  func m(s: String): Money? = {
    let n = s.dropPrefix("-").dropPrefix("\$").toNum
    if (s.startsWith("-")) -n else n
  }
  impl func data(account: FinAccount): BankData = {
    BankData(memo = memo, reference = referenceId, code = sku, amount = amount, fee = fee, balance = balance)
  }
  
  func isDouble(o: PingCsvData): Bool = {
    date == o.date && amount == -o.amount 
  }
  
  object func filterPurchases(items: [PingCsvData]?): [PingCsvData]? = {
    if (items.count < 2) {
      items
    } else {
      var prev: PingCsvData? = null
      items.sliding(2).filter{i =>
        let b = !i.first.isDouble(i(2)) && (!prev || !prev!.isDouble(i.first))
        prev = i.first
        b
      }*.first + (if (!prev!.isDouble(items.last!)) [items!.last])
    }
  }
}
 
extend enum BankStatementType {
  case TrademePingCsv(
    canProcess = file.is("csv") && data.toString.lines(CSV).first == "Date,Transaction,Credit,Debit,Balance,ReferenceId,SKU,NetSaleAmount,Fee,Gst", 
    process = ProcessResult(
      account = PingAccount.all.result.get.onlyOne,
      items = PingCsvData.filterPurchases(data.toString.lines(CSV).drop(1).mapOpt(splitTo[PingCsvData](CSV)))), 
    knowOurAccount = true
  )
}