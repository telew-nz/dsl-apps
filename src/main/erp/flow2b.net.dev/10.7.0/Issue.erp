enum IssueType {
  case Bug {
    background = "#f99"
  }
  case Story {
    background = "#efd"
  }
  case Debt {
    background = "#efd"
  }
  case Incident {
    background = "#ed9"
  }
  case Tech {
    background = "#ed9"
  }
}

enum IssueStatus(active: Bool, dev: Bool, qa: Bool, po: Bool, backlog: Bool, blocking: Bool) {
  case Draft(active = false, dev = false, qa = false, po = false, backlog = false, blocking = false) {
    background = "#999"
  }
  case QAReview(active = true, dev = false, qa = true, po = false, backlog = false, blocking = true) {
    background = "#ffb57f"
  }
  case Revision(active = true, dev = false, qa = false, po = false, backlog = false, blocking = true) {
    background = "#ffb57f"
  }
  case Entered(active = true, dev = false, qa = false, po = true, backlog = true, blocking = true) {
    background = "#ed9"
  }
  case Approved(active = true, dev = false, qa = false, po = false, backlog = true, blocking = true) {
    background = "#ccf"
  }
  case Todo(active = true, dev = true, qa = false, po = true, backlog = true, blocking = true) {
    background = "#cce0ff"
  }
  case InProgress(active = true, dev = true, qa = false, po = false, backlog = true, blocking = true) {
    background = "#8bf"
  }
  case Implemented(active = true, dev = false, qa = true, po = false, backlog = true, blocking = false) {
    background = "#dea"
  }
  case Failed(active = true, dev = true, qa = false, po = false, backlog = true, blocking = true) {
    background = "#f99"
  }
  case Verified(active = false, dev = false, qa = false, po = false, backlog = false, blocking = false) {
    color = "white"
    background = "#090"
  }
  case Canceled(active = false, dev = false, qa = false, po = false, backlog = false, blocking = false) {
    background = "#999"
  }
  case Duplicate(active = false, dev = false, qa = false, po = false, backlog = false, blocking = false) {
    background = "#999"
  }
  case Postponed(active = false, dev = false, qa = false, po = false, backlog = false, blocking = true) {
    background = "#ecd"
  }
  case Pending(active = false, dev = false, qa = false, po = false, backlog = false, blocking = true) {
    background = "#ecd"
  }
}


trait Issue extends Blockable with DashboardItem with NewEngine {
  background = "#545790"
  object field generator: Int?
  const system ref field n: Int =? increment(Issue.generator)
  compute number: String = prefix + n
  func prefix: String
  impl func itemRoles = [Developer, ProductOwner, Stakeholder, QA]
  impl func activeRoles = ((if (status.dev) [Role.Developer]) + (if (status.po) [Role.ProductOwner]) + (if (status.qa) [Role.QA])).toSet
  impl func splitPerUser(role: String): Bool = role == "flow2b.net.dev.Stakeholder"
  override func itemOrder = ("Issue", sort.desc, number)
  override func isForUser(owner: IUser?, role: Role): Bool = {
    if (let u = owner?.as[User]) {
      if (role == Developer || role == Tech) u.developingBacklogs.contains(backlog)
      else if (role == ProductOwner || role == TechOwner) u.ownedBacklogs.contains(backlog)
      else super.isForUser(owner, role)
    } else true
  }
  impl func canRead = product.canRead
  impl func canWrite = product.canWrite
  
  func statuses: [IssueStatus] = IssueStatus.all.filter(_ != QAReview)!.toSet
  
  ref field _product: DevProduct by issues {
    default =? try? backlog?.oneProduct!
  }
  func product: DevProduct
  
  impl func products = [product]
  ref field backlog: Backlog {
    update = {
      this._product =? new.oneProduct
    }
  }
  ref field milestone: Milestone? from product.milestonesSelection by issues {
    init = {
      new.products = [product]
    }
  }
  func defaultStatus: IssueStatus = Entered
  ref field status: IssueStatus {
    default = defaultStatus
    from = statuses
  }
 
  
  func requireEfforts = true
  ref field effort: Effort? {
    label = "Effort for developers"
    validate = if (requireEfforts && status.dev && !effort) "Efforts should be selected for the statues $status"
  }
  field blocksItems: [Blockable]? by blockedByIssues 
  field desires: [IDesire]? inline by issue 
  impl func isInBacklog: Bool = status.backlog
  func isForPO = status.po
  func type: IssueType
  impl func active = status.active
  
  
  func activeBlocksItems: [Issue]? = blocksItems.filter(_.isInBacklog)*.as[Issue]
  func allDesires(er: UDec[2]): [RankedDesire]? = {
    (desires + additionalDesire).map(d => RankedDesire(desire = d, rank = d.rank*er)) + 
      activeBlocksItems.flatMap(bi => bi.allDesires(bi.effort?.rank.alt(1.00).min(er)))
  }
  func allProducts: [DevProduct] = activeBlocksItems*.allProducts + product
  func allMilestones(er: UDec[2]): [RankedMilestone]? = {
    (milestone.toSeq + blocksItems.filter(_.isInBacklog)*.as[Milestone])
      .map(m => RankedMilestone(milestone = m, rank = m.rank*er)) 
      + activeBlocksItems.flatMap(bi => bi.allMilestones(bi.effort?.rank.alt(1.00).min(er)))
  }

  impl system ref stored compute rank: UDec[2] = {
    let er = efforRank
    let products = allProducts.distinct
    let desires = allDesires(er)
    let milestones = allMilestones(er)
    let pps = products*.as[PersonalProject]*.actor
    (products*.rank.sum!*er*0.05
      + desires.filter(desire.is[Desire]).groupBy(desire.as[Desire]!.actor).agg(_*.rank.max).toSeq.filter(!pps.contains(actor))*.rank.sum
      + desires.filter(!desire.is[Desire]).groupBy(desire.issue.product).agg(_*.rank.max).values.sum
      + milestones.groupBy(milestone).agg(if(key.products.contains(this.product)) (_*.rank.max - this.product.rank).max(0.00).as[UDec[2]] else _*.rank.max).values.sum
      )
  }
  
  
  func additionalDesire: IIDesire? = null
  
  func efforRank: UDec[2] = {
    if (let bi = blockedByActiveIssues) 0.90*effort?.rank.alt(1.00).min(bi*.efforRank.min)
    else effort?.rank.alt(1.00)
  }
  //func productEfforRank(p: DevProduct): Dec[2] = if(this.product == p) effort?.rank.alt(1.00).max(1.00) else 1.00
  
  impl event onSave = {
    if (touched) {
      if (let la = activities.last) {
        if (status == Revision 
            && descUpdated
            && la.entities.first == this
            && !updated(status) 
           ) 
        {
          status = defaultStatus
        }
      }
      if (status == Pending && !blockedByActiveIssues) {
        status = defaultStatus
        blocksItems*.as[Issue].filter(_.status == Pending).foreach(_.status = defaultStatus)
      }
      blocksItems*.touch
      blockedByIssues*.touch
      milestone?.touch
      product?.touch
      checkBlockedIssues([])
    }
  }
  func checkBlockedIssues(issues: [Issue]?): Void = { 
    if (issues.contains(this)) {
      throw "Cycle in blocks for ${this} with $issues"
    }
    if (let bi = blocksItems*.as[Issue]) {
      let issues1 = issues + this
      bi.foreach(_.checkBlockedIssues(issues1))
    }
  }
  func descUpdated: Bool
  
  system stored compute sort: UDec[2] = {
    rank + (if (status == Approved || status == Entered) 0.00 else 2.00)*1000000.00
  }
  
  report Report {
    field backlog: Backlog? {auto} 
    
    field qa: User? {
      label = "QA"
      from = User.all.filter(is(QA))
    }
    field po: User? {
      label = "PO"
      from = User.all.filter(is(ProductOwner))
    }
    field dev: User? {
      label = "Developer"
      from = User.all.filter(is(Developer))
    }
    field stakeholder: User? {
      label = "Stakeholder"
      from = User.all.filter(is(Stakeholder))
    }
    field status: [IssueStatus]? {auto}
    impl view list = Issue.all.filter(
      (!qa || (status.qa && isFor(qa, QA))) &&
      (!po || (status.backlog && po!.ownedBacklogs.contains(backlog))) &&
      (!dev || (status.dev && dev!.developingBacklogs.contains(backlog))) &&
      (!stakeholder || status.backlog))
    .sortBy(sort.desc, number)
  }
}

trait DevIssue extends Issue {
  impl compute title {
    value = number + " " + name
    update = {this.name = new}
  }
  ref field name: String {
    label = "Title (keep it short)" 
    validate = if (name.length >= 90) "Title should be shorter than 90 symbols"
  }
  
  impl compute product: DevProduct {
    value = _product
    from = DevProduct.all.filter(p => p.backlog == (try? backlog) || !(try? backlog))
    update = {
      _product = new
      this.backlog = new.backlog.alt(throw "Please create backlog for the product " + new)
    }
  }
  ref field feature: Feature? by stories from product.features.filter(_.status.canChoose)
}

class RankedDesire {
  field desire: IIDesire
  field rank: UDec[2]?
}

class RankedMilestone {
  field milestone: Milestone
  field rank: UDec[2]?
}