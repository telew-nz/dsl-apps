entity UserStory extends DevIssue {
  impl func numPrefix: String = "S"
    
  field role: ProductRole {
    by = stories
    label = "As a"
  }
  field goal: Text {
    label = "I want"
  }
  field benefit: Text {
    label = "so that"
  }
  field additionalInfo: Text?
  impl func descUpdated: Bool = updated(role) || updated(goal) || updated(benefit) || updated(additionalInfo) || updated(title)
  
    
  impl view ref = y {
    x{number >> field(fontStyle = Label)  title product >> field(fontStyle = Label)}
    x{IssueType.Story effort blockStatus status >> field(editMode = AlwaysEdit) itemUser}
  }
  impl view traitItem = grid(labels = None) {
    x{title >> field(stretch = 1) number >> field(fontStyle = Label)}
    row {product milestone feature}
    x {IssueType.Story effort blockStatus status >> field(editMode = AlwaysEdit)}
  }
  
  impl view form = nav {
    grid {
      x(flow = false, label = ""){
        blockStatus 
        status >> field(editMode = AlwaysEdit)
        name >> field(stretch = 1, fontStyle = H2)
        number >> field(fontStyle = Label)
      }
      apps
      feature
      developer
      row {
        product
        milestone >> field(title = name)
        backlog >> field(editMode = AlwaysRead)
      }
      effort
      devType >> field (label = "", visible = estimations)
      estimations >> field (label = "")
      role >> field(label = "As a (choose carefully)", placeholder = "who")
      goal >> field(label = "I want (make it precise and understandable for a developer)", placeholder = "what (a developer should be able to understand how to implement it)")
      benefit >> field(label = "so that (put some efforts to make it clear)", placeholder = "why")
      additionalInfo >> field(label = "Additional Information")
      blockedByIssues >> field(label = "This story is blocked by")
      blocksItems >> field(label = "This story blocks")
      desires

    }
     
    edit {
      x{
        button("To Bug") {
          let g = goal
          let d = "$benefit\n$additionalInfo"
          let r = convertTo(Bug)
          r.steps = g
          r.additionalInfo = g
        }
        button("To Tech Task") {
          let d = "$goal\n$benefit\n$additionalInfo"
          let r = convertTo(TechTask)
          r.desc = d
        }
        button("To Code Debt") {
          let d = "$goal\n$benefit\n$additionalInfo"
          let r = convertTo(CodeDebt)
          r.description = d
        }

      }
    }
  }
  impl func type: IssueType = Story
}


