entity Incident extends Issue {
  ref const field date: Date = now
  compute unrespondedTime: Duration? {
    value = (Date.now - date).to[Duration[hour, 0]]
    visible = status.active 
  }
  impl func requireEfforts = false
  impl compute title {
    value = "$number $product $subject"
  }
  field urls: [Url]?
  ref impl compute product: PersonalProject {
    value = _product.as[PersonalProject]!
    update = {
      _product = new
      this.backlog = new.backlog.alt(throw "Please create backlog for the product " + new)
    }
  }
  func timeColor: String = {
    let ur = unrespondedTime
    if (ur >= urgency.norm) "#f00"
    else if (ur >= urgency.norm/2) "#dc8800"
    else "black"
  }
 
  override func statuses: [IssueStatus] = IssueStatus.all.filter(_ != QAReview && _ != Approved && _ != Entered && _ != Postponed && (_ != Pending || urgency != Urgent))!.toSet
  override func defaultStatus = Todo
  override func isBlocking(issue: Issue): Bool = issue.status.blocking || issue.status == Implemented
  field urgency: Urgency = Usual
  impl func prefix: String = "I"
  ref field subject: String?
  field desc: Text? {label = "Description"}
  impl func additionalDesire: IIDesire? = IntDesire(issue = this, rank = product.rank*urgency.rank)
  impl func itemRoles = [Support, Stakeholder]
  override func activeRoles = if (status.active) [Role.Support]
  impl view ref = y {
    x {title date}
    x {IssueType.Incident urgency effort blockStatus status unrespondedTime >> field(color = timeColor)}
  }
  impl view traitItem = grid(labels = None) {
    x {title date >> field(stretch = 1) rank}
    row {product milestone space}
    x {IssueType.Incident urgency effort blockStatus status  unrespondedTime >> field(color = timeColor)}
  } 
  impl func type: IssueType = Incident
  impl func descUpdated: Bool = updated(desc) || updated(subject)  
  impl view form = nav {
    grid {
      row {number date}
      subject
      urls
      row {
        product
        milestone >> field(title = name)
        backlog
      }
      rank
      x(label = "Status"){unrespondedTime >> field(color = timeColor) blockStatus status >> field(compact = false)}
      urgency
      desc      
      blockedByIssues >> field(label = "This incident is blocked by")
      blocksItems >> field(label = "This incident blocks")
    }
  }
}