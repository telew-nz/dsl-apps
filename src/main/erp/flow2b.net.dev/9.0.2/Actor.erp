extend trait Actor {
  field project: PersonalProject? inline by actor {views = []}
  field desires: [Desire]? inline by actor
  compute rank: UDec[2] = 1.00
  
  compute hasProject: Bool {
    label = "Project"
    value = project
    update = {
      project = if (new) PersonalProject()
    }
  }
  
  extend impl event onSave = {
    project?.touch
  }
}

extend entity Company {
  extend view form = extend("tabs") {
    grid(labels = Top, label = "Project", visible = project || ui.edit) {
      hasProject
      project!.form
    }
    grid (labels = None, label = "Text Requirements", visible = project) {
      project!.requirements
    }
    grid (labels = None, label = "Project Settings", visible = project) {
       project!.settings
    }
  }  
}
extend entity Person {
  extend view form = extend("tabs") {
    grid(labels = Top, label = "Project", visible = project || ui.edit) {
      hasProject
      project!.form
    }
    grid (labels = None, label = "Text Requirements", visible = project) {
      project!.requirements
    }
    grid (labels = None, label = "Project Settings", visible = project) {
       project!.settings
    }
  }  
}


enum CustomerHappiness(level: Dec[2]?) {
  case Angry(level = -1) { color = "white" background = "#c00"}
  case Unhappy(level = -0.5) {background = "#f99"}
  case OK(level = 0){background = "#ed9"}
  case Happy(level = 0.5) {background = "#dea"}
  case Excited(level = 1) {color = "white" background = "#090"}
}

enum CustomerState(active: Bool) {
  case Deployment(active = false) {background = "#ed9"}
  case Active(active = false) {background = "#dea"}
  case DeploymentFailed(active = false) {background = "#999"}
  case Churned(active = false) {background = "#999"}
}

extend entity Customer {
  field state: CustomerState {
    default = Deployment
    views = []
    update = {
      if (new == Active && !activeDate) activeDate = now
      if (new == Churned && !churnDate) churnDate = now
      if (new == DeploymentFailed && !failureDate) failureDate = now
    }
  }
  field happiness: CustomerHappiness {views = []}
  field deploymentDate: Date {
    default = now
    views = []
  }
  field activeDate: Date? {
    views = []
    visible = state == Active || state == Churned
    validate = if (!activeDate && (state == Active || state == Churned)) "Active date is Required"
  }
  field churnDate: Date? {
    views = []
    visible = state == Churned
    validate = if (!churnDate && state == Churned) "Churn date is Required"
  }
  field failureDate: Date? {
    views = []
    visible = state == DeploymentFailed
    validate = if (!failureDate && state == DeploymentFailed) "Failure date is Required"
  }
  extend view form = extend("top"){
    x{state happiness >> field(editMode = AlwaysEdit)}
    row{deploymentDate activeDate churnDate}
    row{failureDate space space}
  }
  extend view traitItem = extend("state"){state happiness >> field(editMode = AlwaysEdit)}
}