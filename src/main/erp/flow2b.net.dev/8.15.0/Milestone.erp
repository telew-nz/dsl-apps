enum MilestoneStatus(active: Bool, rankCount: Bool, rank: UDec[2]?, going: Bool) {
  case Entered(active = true, rankCount = true, rank = 1.00, going = false) {background = "#ed9"}
  case Planned(active = true, rankCount = true, rank = 2.00, going = false) {background = "#ccf"}
  case InProgress(active = true, rankCount = true, rank = 8.00, going = true) {background = "#8bf"}
  case Implemented(active = true, rankCount = true, rank = 10.00, going = true) {background = "#dea"}
  case Verified(active = true, rankCount = true, rank = 10.00, going = true) {background = "#dea"}
  case Production(active = true, rankCount = false, rank = 20.00, going = true) {
    color = "white"
    background = "#090"
  }
  case Closed(active = false, rankCount = false, rank = 0.00, going = false) {
    background = "#999"
  }
}
 
entity Milestone extends Blockable {
  background = "#545790"
  func title: String = if (let p = products.onlyOne) p.title + ": " + name else name
  ref field name: String
  ref field status: MilestoneStatus = Entered
  ref field products: [DevProduct] by milestones
  field releaseDate: Date?
  func remainingEffort: Int? = todo.map(effort?.value).sum
  func contentIssues: [Issue]? = issues
  func wholeEffort: Int? = remainingEffort + done.map(effort?.value).sum
  compute effort = remainingEffort + " of " + wholeEffort
  func currentNumber(product: DevProduct): UInt = (product.milestones.filter(status.rankCount).indexOf(this) + 1).max(1).as[UInt]!
  field description: Text?
  ref stored compute rank: UDec[2]? = status.rank * products.map(p => p.rank/currentNumber(p)).sum
  func allDesireRank: UDec[2]? = rank//*(1.00 - 1.00/status.rank)
  func active: Bool = status.active
  func isInBacklog: Bool = status.active
  compute todo: [Issue]? {
    value = issues.filter(status.active)
    update = {
      this.issues = new + this.issues.filter(!status.active)
    }
    init = {
      new.product = products.first
      new.milestone = _
    }
  }
  compute done: [Issue]? {
    value = issues.filter(status == Verified)
    canAdd = false
  }
  field issues: [Issue]? read by milestone {
  	views = []
  }
  event onSave = {
    issues*.touch
    blockedByIssues*.touch
  }
  
  view form = nav {
    grid {
      name
      rank
      x(label = "Status"){blockStatus status}
      products
      description
      effort
      todo >> list(editMode = AlwaysRefRead, frame = true){_}
      blockedByActiveIssues >> list(frame = true, label = "Blocked by Issues", singular = "Issue"){
        _
      }
      done >> list(editMode = AlwaysRefRead, frame = true){_}
    }
  }
  
  report Report {
    field status: [MilestoneStatus]? {auto = true}
    view list = Milestone.all >> nav {list{
       title
       x{blockStatus status >> field(visible = status != Entered) }
    }}
  }
}
