entity PersonalProject extends DevProduct with HasTasks {
  func isTangible = false
  func type: ProductType = ProductType.Project
  ref field actor: Actor by personalProjects {
    available(All).for(All)
  }
  field universalApps : [UniversalApp]? by personalProjects
  view traitItem = row{type, actor, title}
  func subProducts = [this]
  func isTheir = actor.isTheir
  func canRead: Bool = isTheir
  func canEdit: Bool = isTheir
  func multiupdate = [actor]
  func rank: Dec[2] = actor.rank
  field requirements: Text?
  func usersRank: Dec[2]? = actor.rank
  compute issuesByMilestone: [(milestone: Milestone?, issues: [Issue]?)] = milestones.map{m =>
    let milestone: Milestone? = (m)
    (milestone, issues = m.blockedByActiveIssues + m.issues.filter(status.active))
  } + (milestone = null[Milestone], issues.filter(!milestone))
  view form = nav{tabs {
    grid (labels = None, label = "Main") {
      title >> field(fontSize = 1.5)
      actor
      h2("Milestones")
      issuesByMilestone.filter(issues) >> subtotal {
        milestone?.name.alt("No Milestone") >> field(fontSize = 0.8)
        list {ref}
      }
      x {
        add("User Story") {
          UserStory(product = this, backlog = backlog!).form.open
        }
        add("Bug") {
          Bug(product = this, backlog = backlog!).form.open
        }
        add("Milestone") {
          Milestone(product = this).form.open
        }
      }
      h2("Desires")
      actor.desires 
      tasks
    }
    grid (labels = None, label = "Text Requirements") {
      requirements
    }
    grid (labels = Right, label = "Settings") {
      title
      actor
      sandboxWorkspace
      extension
      roadmap
      backlog
      universalApps
      milestones >> list {
        x(flow = false){status title}
      }
      features 
      docs
      packageDocs
      description
    }
  }}
  
  object view list = PersonalProject.all >> nav {list{
    row{type, actor, title}
  }}
}
