enum Severity(rank: Dec[2], description: Text) {
  case Critical(rank = 30.00, description = 
"* Data can be damaged or lost
* Security problem
* A user cannot execute his role completely with no workaround") 
{
    color = "white"
    background = "#c00"
  }
  case Major(rank = 6.10, description = 
"* A user cannot do something which should be able to
* It makes it hard to work
* System error displayed
* Spelling mistake
* Incorrect or misleading Dsl compilation errors") {
    background = "#f99"
  }
  case Minor(rank = 2.60, description = 
"* Inconvenience
* Bad look") {
    background = "#efd"
  }
}

enum Stability {
  case Always {background = "#f99"}
  case Often {background = "#ffb16c"}
  case Sometimes {background = "#ed9"}
  case Once {background = "#efd", label = "Once Upon a Time"}
}

entity Bug extends Issue {
  field severity: Severity
  field steps: Text {
    label = "Steps to reproduce (from beginning to end)"
  }
  field stability: Stability {
    label = "How often it can be reproduced (please try to reproduce again)"
  }
  field actualResult: Text
  field expectedResult: Text 
  field additionalInfo: Text?
  func prefix: String = "B"
  func rankK: Dec[2] = severity.rank 
  
  func descUpdated: Bool = updated(actualResult) || updated(expectedResult) || updated(additionalInfo) || updated(title)
  func defaultStatus: IssueStatus = if (Person.current!.is(QA)) Entered else QAReview
  func statuses: [IssueStatus] = IssueStatus.all!.toSet
  
  view ref = y {
    product
    x{title blockStatus status effort } 
  }
  view traitItem = grid (width = 1.5) {
    title >> field(label = "")
    row {
      IssueType.Bug
      effort
      x(label = "Status"){blockStatus status}
      owner
      product
      severity
      milestone
    }
  }
  view form = nav {
    grid {
      number
      name
      feature
      row {
        backlog
        product
        milestone
      }
      rank
      x(label = "Status"){blockStatus status >> field(compact = false)}
      owner
      effort
      severity
      severity.description >> field(label = "", editMode = AlwaysRead, fontSize = 0.75)
      steps
      stability
      actualResult
      expectedResult
      blockedByIssues >> field(label = "This bug is blocked by")
      blocksItems >> field(label = "This bug blocks") 
      desires
      edit {
        x{
           button("To User Story") {
             let s = steps
             let d = "$actualResult\n$expectedResult"
             let r = convertTo(UserStory)
             r.goal = s
             r.additionalInfo = d
           }
           button("To Tech") {
             let d = "$steps\n$actualResult\n$expectedResult"
             let r = convertTo(TechImprovement)
             r.description = d
           }
         }
      }
    }
  }
  func type: IssueType = Bug
}