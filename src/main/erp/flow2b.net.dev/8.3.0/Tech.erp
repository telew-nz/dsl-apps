enum TechSeverity(rank: Dec[2]) {
  case Critical(rank = 1.5) {
    color = "white"
    background = "#c00"
  }
  case Major(rank = 0.7) {
    background = "#f99"
  }
  case Minor(rank = 0.3) {
    background = "#efd"
  }
}

entity TechImprovement extends Issue {
  field severity: TechSeverity
  func prefix: String = "T"
  field description: Text
  func desireRank: Dec[2]? = product.usersRank * severity.rank
  
  view ref = y {
    product
    x{title blockStatus status} 
  }
  view traitItem = grid (width = 1.5) {
    x {
      IssueType.Tech
      title
    }
    row {
      rank
      product
      x(label = "Status"){blockStatus status}
      milestone
      effort >> field(label = "Effort")
      severity
      owner
    }
  }
  func type: IssueType = Tech
  func descUpdated: Bool = updated(description) || updated(title)  
  view form = nav {
    grid {
      number
      name
      feature
      row {
        backlog
        product
        milestone
      }
      rank
      x(label = "Status"){blockStatus status >> field(compact = false)}
      owner
      effort
      severity
      description
      blockedByIssues >> field(label = "This tech is blocked by")
      blocksItems >> field(label = "This tech blocks")
      desires
      edit {
        x{
           button("To User Story") {
             let d = description
             let r = convertTo(UserStory)
             r.goal = d
           }
           button("To Bug") {
             let d = description
             let r = convertTo(Bug)
             r.steps = d
           }
         }
      }
    }
  }
}