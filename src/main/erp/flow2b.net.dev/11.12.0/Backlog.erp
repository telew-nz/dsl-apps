extend enum DashboardIssue {
  case BacklogLimitsExceeded(role = ProductOwner, priority = major)
}

entity Backlog extends DashboardItem {
  background = "#545790"
  field active: Bool
  ref field name: String? {views = []}
  impl ref compute title {
    value = name.alt(products.first!.title)
    update = {
      this.name = new
    }
  }
  field products: [DevProduct]? by backlog 
  ref compute oneProduct: DevProduct? {
    value = if (products.count == 1) products.first else null
    views = []
  }
  
  impl func itemRoles = [ProductOwner, Developer]
  impl func activeRoles = if (active) [ProductOwner]
  impl func itemPriority = major
  impl func issues = if (backlogLimit < backlogSize || heapLimit < heapSize || enteredLimit < enteredSize || devLimit < devSize || qaLimit < qaSize) {
    [BacklogLimitsExceeded]
  }
 
  impl func canRead = products.all(_.as[PersonalProject]?.isTheir)

  system field allIssues: [DevIssue]? read by backlog {views = []}
  
  compute qaIssues {
    label = "QA"
    value = allIssues.filter(status == Implemented)
    update = {
      allIssues = new + allIssues.filter(_.status != Implemented)
    }
  }
  compute progressIssues {
    label = "In Progress"
    value = allIssues.filter(status in [InProgress, Failed])
    update = {
      allIssues = allIssues.filter(_.status == Implemented) + new + allIssues.filter(_.status in [Todo, Backlog, Heap, Entered])
    }
  }
  compute todoIssues {
    value = allIssues.filter(status == Todo)
    update = {
      allIssues = allIssues.filter(_.status in [InProgress, Failed, Implemented] ) + new + allIssues.filter(_.status in [Backlog, Heap, Entered])
    }
  }
  compute backlogIssues {
    value = allIssues.filter(status == Backlog)
    update = {
      allIssues = allIssues.filter(_.status !in [Backlog, Heap, Entered]) + new + allIssues.filter(_.status in [Heap, Entered])
    }
  }
  compute heapIssues {
    value = allIssues.filter(status == Heap)
    update = {
      allIssues = allIssues.filter(_.status !in [Heap, Entered]) + new + allIssues.filter(_.status == Entered)
    }
  }
  compute enteredIssues {
    value = allIssues.filter(status == Entered)
    update = {
      allIssues = allIssues.filter(_.status != Entered) + new
    }
  }
  
  field backlogLimit: UInt = 30
  field heapLimit: UInt = 50
  field enteredLimit: UInt = 10
  field devLimit: UInt = 10
  field qaLimit: UInt = 10
  
  compute backlogSize: UInt? = backlogIssues.count
  compute heapSize: UInt? = heapIssues.count
  compute enteredSize: UInt? = enteredIssues.count
  compute devSize: UInt? = allIssues.filter(status.dev).count
  compute qaSize: UInt? = qaIssues.count
  view backlogView = backlogIssues >> list(editMode = AlwaysRefRead, frame = true) {
    x(flow = false) {
      //grid (margin = None) {
        ref >> field(stretch = 1)
      //}
      button("↓") {
        backlog!.backlogIssues = backlog!.backlogIssues.filter(i => i != _) + _
        UI.save(backlog!.save)
      }
      button("↑") {
        backlog!.backlogIssues = _ + backlog!.backlogIssues.filter(i => i != _)
        UI.save(backlog!.save)
      }
    }
  }  
 
  
  impl view form = nav{
    tabs {
      grid(labels = None, label = "Backlog"){
        title
        row{
          "$backlogSize of $backlogLimit backlog" >> field(color = if (backlogLimit < backlogSize) "red" else "black")
          "$heapSize of $heapLimit heap" >> field(color = if (heapLimit < heapSize) "red" else "black")
          "$enteredSize of $enteredLimit entered" >> field(color = if (enteredLimit < enteredSize) "red" else "black")
          "$devSize of $devLimit dev" >> field(color = if (devLimit < devSize) "red" else "black")
          "$qaSize of $qaLimit qa" >> field(color = if (qaLimit < qaSize) "red" else "black")
        }
        backlogView
      }
      grid(labels = None, label = "Heap") {
        heapIssues >> list(frame = true) {ref}
      }
      grid(labels = None, label = "Entered") {
        enteredIssues >> list(frame = true) {ref}
      }
      grid(labels = None, label = "Development") {
        progressIssues >> list(frame = true) {ref}
        todoIssues >> list(frame = true) {ref}
      }
      grid(labels = None, label = "QA") {
        qaIssues >> list(frame = true) {ref}
      }
      grid(labels = Top, label = "Settings"){
        title 
        active
        row {backlogLimit heapLimit enteredLimit}
        row { devLimit qaLimit space}
        products
      }
    }
    
  }
  /*
  impl view form = nav{
    grid(labels = Top, label = "Settings"){
      title 
      status
      products
    }
    button("➠") {
      Issue.Report(backlog = this).list.open
    }
  }
  impl view ref = x(flow = false, margin = None) {
    title
    button("➠") {
      Issue.Report(backlog = this).list.open
    }
  }
  */
  impl object view list = Backlog.all >> nav{list{
    title
  }}
  
}
