extend entity POIInvoice {
  back func resolve: Void = if (OrderImportService.resolveReferences) {
    vendor = Vendor.all
      .find(legalEntity.legalInfo?.gstNumber =%= vendorNumber)
      .result.get
    items*.resolve
    save
  }
  
  impl event onSave = {
    if (!OrderImportService.isCurrent && updated(vendor) && !vendor?.legalEntity?.legalInfo?.gstNumber
        let vendorNumber = vendorNumber.trim
    ) {
      vendor?.legalEntity?.legalInfo?.gstNumber = vendorNumber
    } 
    if (canMakePO) {
      MakePOJob(invoice = this).enqueue
    }
  }
  
  back func canMakePO = poi.state == Complete && OrderImportService.resolveReferences && !purchaseOrder && !issues && invoiceType in [TaxInvoice, Order]
  
  back func makePO: PurchaseOrder? = if (canMakePO) {
    let date = invoiceDate.alt(Date.now)
    let po = PurchaseOrder(
      date = date
      vendor = vendor!, customer = legalEntity! 
      deliveries = [PurchaseDelivery(store = store)]
    ).saveMain
    this.purchaseOrder = po
    poi.touch
    poi.files*.touch
    
    save
    items*.makeItem(po)
    po
  }
}

class MakePOJob extends SystemJob {
  field invoice: POIInvoice
  impl func service = OrderImportService
  impl func do = {
    invoice.makePO
  }
}

extend entity POIInvoiceItem {
  back func resolve: Void = {
    product = ISubProduct.all
      .filter(p => p.hasNum(code) || p.title =%= desc)
      .result.get.onlyOne
  }
  
  impl event onSave = {
    if (!OrderImportService.isCurrent && updated(product) let product, let code = code.trim) {
      product.addRefNum(code)
    }
  }
  
  back func makeItem(po: PurchaseOrder): PurchaseItem = {
    PurchaseItem(
      order = po, 
      deliveryItem = DeliveryItem(product = product!, unit = product!.unit, qty = qty.positive.alt(1)), 
      gstRate = gstRate, sAmount = amount.positive, 
      storedWithGst = invoice.rowsWithGst
    )
  }
}