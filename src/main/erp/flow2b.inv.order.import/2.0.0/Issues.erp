extend enum DashboardIssue {
  case POINoInvoiceType(role = BusAdmin, priority = major) = "The document does not seem to be a Tax Invoice or Debit/Credit note"
  case POINoTotal(role = BusAdmin, priority = major) = "Unable to extract total amount"
  case POINoItems(role = BusAdmin, priority = major) = "Unable to extract items"
  case POINoTotalMatch(role = BusAdmin, priority = major) = "Total does not match to the sum of the items amount"
  case POINoVendor(role = BusAdmin, priority = major) = "Vendor is not found"
  case POINoLegalEntity(role = BusAdmin, priority = major) = "Legal Entity is not found"
  case POINoStore(role = BusAdmin, priority = major) = "Store is not found"
  case POINoProduct(role = BusAdmin, priority = major) = "Product is not found"
}

extend entity PurchaseOrderImport {
  override func issues = {
    if (state != Complete) {
      super.issues
    } else if (!invoiceType) {
      [POINoInvoiceType]
    } else if (!totalNoGst){
      [POINoTotal]
    } else if (!items) {
      [POINoItems]
    } else if (
         (rowsWithGst && items*.amount.sum != totalWithGst)
      || (!rowsWithGst && items*.amount.sum != totalNoGst)
    ) {
      [POINoTotalMatch]
    } else if (OrderImportService.resolveReferences) {
        (if (!vendor) [DashboardIssue.POINoVendor]) 
      + (if (!legalEntity) [DashboardIssue.POINoLegalEntity]) 
      + (if (!store && items.all(product) && items.exists(!product?.isTangible)) [DashboardIssue.POINoStore]) 
      + (if (items.exists(!product)) [DashboardIssue.POINoProduct]) 
    }.toSet
  }
  override func fixes(issue: DashboardIssue) = {
    if (issue == POINoInvoiceType) {
      [
        Fix(text = "Make sure the uploaded field is a valid tax invoice"),
        Fix(text = "Edit and select the invoice type")
      ]
    } else if (issue in [POINoVendor, POINoLegalEntity, POINoStore, POINoProduct] ) {
      [
        Fix(text = "Edit and select the correct value")
      ]
    } else {
      super.fixes(issue)
    }
  }
}