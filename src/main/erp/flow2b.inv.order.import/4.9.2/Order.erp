extend entity POIInvoice {
  back func resolve: Void = if (OrderImportService.resolveReferences) {
    if (vendorNumber.trim) {
      vendor = Vendor.all
        .find(legalEntity.legalInfo?.gstNumber =%= vendorNumber)
        .result.get
    }
    items*.resolve
    save
  }
  
  impl event onSave = {
    if (created) {
      resolve
    }
    if (!OrderImportService.isCurrent && updated(vendor) && !vendor?.legalEntity?.legalInfo?.gstNumber
        let vendorNumber = vendorNumber.trim
    ) {
      vendor?.legalEntity?.legalInfo?.gstNumber = vendorNumber
    } 
    if (canMakePO) {
      MakePOJob(invoice = this).enqueue
    }
  }
  
  back func canMakePO = poi.state == Complete && !purchaseOrder && !calcIssues && invoiceType in [TaxInvoice, Order]
  
  back func makePO: PurchaseOrder? = if (canMakePO) {
    let date = invoiceDate.alt(Date.now)
    let po = PurchaseOrder(
      date = date, accepted = now,
      vendor = vendor!, customer = legalEntity! 
      deliveries = [PurchaseDelivery(store = store)]
    ).saveMain
    this.purchaseOrder = po
    
    Activity.current!.entities += [poi, poi.file]
    items*.makeItem(po)
    
    po.updateDue(autoIssue = false)
    if (invoiceType == TaxInvoice) {
      po.createInvoice(date)
    }

    po
  }
}

class MakePOJob extends SystemJob {
  field invoice: POIInvoice
  impl func service = OrderImportService
  impl func do = {
    invoice.makePO
  }
}

extend entity POIInvoiceItem {
  back func resolve: Void = {
    product = ISubProduct.all
      .filter(p => p.hasNum(code) || p.title =%= desc)
      .result.get.onlyOne
    save
  }
  
  impl event onSave = {
    if (!OrderImportService.isCurrent && updated(product) let product, let code = code.trim) {
      product.addRefNum(code)
    }
  }
  
  back func makeItem(po: PurchaseOrder): PurchaseItem = {
    PurchaseItem(
      order = po, 
      deliveryItem = DeliveryItem(product = product!, unit = product!.unit, qty = qty.positive.alt(1)), 
      gstRate = gstRate, sAmount = amount.positive, 
      storedWithGst = invoice.rowsWithGst
    )
  }
}