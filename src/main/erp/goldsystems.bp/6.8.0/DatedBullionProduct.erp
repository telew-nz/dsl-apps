entity DatedBullionProduct extends BullionSuperProduct with ICustomerSuperProduct with IEcwidProduct {
  object func attrs = [BullionYear]
  field photos: [Image]?
  func customerProducts: [ICustomerProduct]? = dates
  field dates: [BullionProductDate]? inline by superProduct {
    singular = "Date"
  }
  back func ecwidOptions(service: IEcwidIntegration) = [
    EcwidOption(
      name="Date", type=Radio,
      choices=dates.filter(stockAvailable(service)).map{ EcwidOptionChoice(text = year.title) }
    )
  ]
  
  view form = nav{grid(labels = Top){
    photos >> field(height = 8, label = "")
    
    header
    dates >> table {year alwaysAvailable button("Details") {form.open} }
    
    ecwidIdsView
    accessories
    principals
  }}
  func isService = false
  func isTangible = true
  view traitItem = grid(labels = None) {
    x{h3("Dated Bullion Product:") title status}
  }
  func child: [BullionProductDate]? = dates.sortBy(year.year.desc)
}

entity BullionYear extends ProductAttrTerm {
  singular = "Year"
  plural = "Years"
  object func attrName: String = "Year"
  ref field year: Int
  field products: [BullionProductDate]? by year
  func title = year.unformattedString
}

entity BullionProductDate extends IComplexProduct with ICustomerComplexProduct with IEcwidProductVariation {
  func customerSuperProduct = superProduct
  field photos: [Image]?
  field alwaysAvailable: Bool {
    default =? try? superProduct.alwaysAvailable
    label = "Always Available for Sale"
  }
  func isTangible = true
  ref field superProduct: DatedBullionProduct by dates {
    available(All).for(All)
  }
  func getParent = superProduct
  ref field year: BullionYear by products
  func attrTerms: [ProductAttrTerm]? = [year]
  func title = "$year $superProduct"
  func ecwidOptionsValues = [EcwidOptionValue(name = "Date", value = year.title)]
  view form = nav{grid{
    photos >> field(height = 8, label = "")
    superProduct
    sku
    year
    alwaysAvailable
    stocksView
    ecwidCombinationIdsView
    trademeStatus
    trademeListings
  }
    productNavView
  }
  func unit: ProductUnit = superProduct.unit
}