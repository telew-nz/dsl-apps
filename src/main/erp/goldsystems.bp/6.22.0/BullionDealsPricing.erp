entity BullionDealsPricing extends IDynamicPriceModel with ScheduledJob {
  impl func title = "BDP $vendor"
  ref impl field start: Date = now
  impl field end: Date?
  
  impl func schedule = "0 3 */1 * * ?"
  impl func contains(level: IPriceLevel, type: PriceType, product: IBaseProduct): Bool = if (type == Sales) {
    if (let p = product.superProduct.as[ISuperProduct with IMetalSuperProduct]) {
      if (p.isMetalProduct && !p.is[IMetalSuperProduct with IKit]) (try? p.metalType) && vendor.hasGst != p.isPure
      else false
    } else false
  } else false
  field margins: [MetalMargin]
  field clearanceItems: [BullionDealsClearanceItem]? inline by model
  impl func price(level: IPriceLevel, type: PriceType, product: IBaseProduct) = {
    if (type == Sales 
        let p = product.superProduct.as[ISuperProduct with IMetalSuperProduct] 
        where !p.is[IMetalSuperProduct with IKit] && p.isMetalProduct && vendor.hasGst != p.isPure
    ) {
      BullionDealsPrice(model = this, superProduct = p)
    }
  }
  
  impl view form = nav{grid{
    vendor
    row{start end}
    margins >> table {
      metal minQty maxMargin maxQty minMargin defaultSupplierMargin
    }
    clearanceItems >> list { grid {
      product
      row {minQty maxMargin newMaxPrice originalMaxMargin originalMaxPrice}
      row {maxQty minMargin newMinPrice originalMinMargin originalMinPrice}
    }}
  }}
}

class BullionDealsPrice extends IPriceSales {
  impl field model: BullionDealsPricing
  field superProduct: IMetalSuperProduct
  impl func product = superProduct.as[IMetalSuperProduct with ISuperProduct]!
  impl func levels = model.levels
  
  impl func salesPrice(request: PriceRequest): Future[SalesPrice] = {
    amount(request, PriceOrderItem(prod = product, qty = 1.as[UQty[product.unit]]))
  }
  func originalAmount(currency: Currency, customer: ICustomer?, qty: UQty, inclGst: Bool): Future[UMoney?] = {
    let p = superProduct
    if (let metalMargins = model.margins.filter(metal == p.metalType).sortBy(minQty)) {
      let q = p.pureWeightOz.as[UQty[ProductUnit.Ounce, 5]]*qty
      let m = metalMargins.find(q.between(minQty, maxQty))
          .alt(if (q < metalMargins.first.minQty) metalMargins.first else metalMargins.last)
      let ps <- p.metalProducts.toSeq.map {pp =>
        productPrice(currency, pp.#0, (pp.#1*qty).positive!, m.margin(q), m.defaultSupplierMargin)
      }.allCompleted
      ps.sum
    } else Future(0)
  }
  
  private func productPrice(currency: Currency, p: MetalSuperProduct, qty: UQty, margin: Percent, defaultSupplierMargin: Percent): Future[UMoney?] = {
    let minSupplierPrice <- BullionSupplierProductPrice.all
      .filter(product.superProduct == p)
      .map(i => i.salesPrice(PriceRequest(level = PriceLevelDefault, currency = currency, inclGst = true)))
      .agg(current.min)
      .result
    if (minSupplierPrice) {
      Future((minSupplierPrice + minSupplierPrice*margin).positive*qty)
    } else {
      let metalPrice: UMoney[currency]? <- p.metalCost.to[UMoney[currency]](now)
      let sp = (metalPrice + metalPrice*defaultSupplierMargin)*qty
      (sp + sp*margin).positive
    }
  }
  impl func amount(request: PriceRequest, item: PriceOrderItem): Future[SalesPrice] = {
    let p = superProduct
    let qty = item.qty
    let oam <- originalAmount(request.currency, request.order?.customer, item.qty, request.inclGst)
    if (let margins = model.clearanceItems.filter(product == p).sortBy(minQty)) {
      let m = margins.find(qty.between(minQty, maxQty))
          .alt(if (qty < margins.first.minQty) margins.first else margins.last)
      let margin = m.margin(qty)
      let metalPrice: UMoney[currency]? <- p.metalCost.to[UMoney[currency]](now)
      SalesPrice(currency = currency, current = (metalPrice + metalPrice*margin).positive*qty, regular = oam)
    } else {
      Future(SalesPrice(currency = currency, current = oam, regular = oam))
    }
  }

}

interface IMetalMargin {
  func unit: ProductUnit
  field minQty: UQty[unit]
  field maxMargin: Percent[2]
    
  field maxQty: UQty[unit] {validate = if (maxQty < minQty) "Max < Min"}
  field minMargin: Percent[2]
  
  func margin(qty: UQty): Percent = {
    if (qty <= minQty) maxMargin
    else if (qty >= maxQty) minMargin
    else {
      let a = (qty*(minMargin - maxMargin)).as[Qty[8]]
      let b = maxQty.as[Qty[8]]*maxMargin - minQty.as[Qty[8]]*minMargin
      let k = (maxQty - minQty)!
      (a + b)!/k
    }
  }
}
class MetalMargin extends IMetalMargin {
  field metal: MetalType
  field defaultSupplierMargin: Percent[2]
  impl func unit = ProductUnit.Ounce
}

inline entity BullionDealsClearanceItem extends IMetalMargin {
  singular = "Clearance Item"
  impl func unit = product.unit
  system field model: BullionDealsPricing by clearanceItems
  field product: BullionSuperProduct
  compute originalMaxMargin: Future[Percent[2]?] = origMaxMarginPrice.map(origMargin)
  compute originalMaxPrice: Future[UMoney?] = origMaxMarginPrice.map(origAmount/minQty)
  compute newMaxPrice: Future[UMoney?] = {
    let o <- origMaxMarginPrice
    let p = o.spotAmount/minQty
    (p + p*maxMargin).positive
  }
  compute origMaxMarginPrice: Future[OrigPrice] = originalMargin(minQty)
  
  compute originalMinMargin: Future[Percent[2]?] = origMinMarginPrice.map(origMargin)
  compute originalMinPrice: Future[UMoney?] = origMinMarginPrice.map(origAmount/maxQty)
  compute newMinPrice: Future[UMoney?] = {
    let o <- origMinMarginPrice
    let p = o.spotAmount/maxQty
    (p + p*minMargin).positive
  }
  compute origMinMarginPrice: Future[OrigPrice] = originalMargin(maxQty)

  func originalMargin(qty: UQty[product.unit]): Future[OrigPrice] = {
    Query(model).map{m =>
      let salesPrice <- m.price(PriceLevelDefault, Sales, product)!.as[BullionDealsPrice].originalAmount(Currency.NZD, null, qty, true)
      let spotPrice <- (product.metalCost*qty).to[UMoney[NZD]](now)
      OrigPrice(spotAmount = spotPrice, origAmount = salesPrice)
    }.result
  }
}

class OrigPrice  {
  compute origMargin: Percent[2]? = if (let spotAmount) ((origAmount - spotAmount)/spotAmount).as[Percent[2]]
  field spotAmount: UMoney? 
  field origAmount: UMoney?
}