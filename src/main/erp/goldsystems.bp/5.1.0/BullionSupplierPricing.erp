entity BullionSupplierPricing extends PriceModel {
  ref field date: Date = now
  func title = "BSP $vendor $date"
  func currency = vendor.getCurrency
  func contains(superProduct: ISuperProduct): Bool  = products.exists(product == superProduct)
  field products: [BullionSupplierProductPrice]? inline by pricing
  func superProductSalesPrice(superProduct: ISuperProduct): Future[Money?] = {
    if (let i = products.find(product == superProduct)) i.calcSalesPrice else 0
  }
  view form = nav{grid{
    date
    vendor
    products >> table {
      product >> field(width = 0.4) percentMargin >> field(width = 0.2) fixedMargin >> field(width = 0.2) salesPrice >> field(width = 0.2)
    }
  }}
}
  
inline entity BullionSupplierProductPrice {
  func currency = pricing.currency
  system field pricing: BullionSupplierPricing inline by products
  field product: BullionSuperProduct
  field percentMargin: Percent?
  field fixedMargin: Money[currency]?
  compute salesPrice: Money[currency]? <- calcSalesPrice
  func calcSalesPrice: Future[Money[currency]?] = {
    let metalPrice <- product.metalCost.to[Money[currency]](now)
    metalPrice + metalPrice*percentMargin + fixedMargin
  }
  func onSave = product.touch
}
