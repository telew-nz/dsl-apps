class WorkspaceInfo {

	field id: String?
    field name: String?
    func equals(other: WorkspaceInfo) = this.id == other.id

}

entity WorkspaceAlertService extends SystemService {

    impl func title: String = "Workspace Alert Service"
    impl func roles: [Role]? = [Admin]
    impl func defaultDebug = true
    override func isPublic: Bool = true
    
    field workspaces: [WorkspaceInfo]?
    system field updateWorkspacesToMonitorJob: UpdateWorkspacesToMonitorJob inline by service
    
    impl event onSave = {
      	if (created) {
        	UpdateWorkspacesToMonitorJob(service = this).enqueue
            joinAll(Admin)*.notify
        }
    }

    impl view form = nav{grid(labels = Top){
        issuesView
        serviceState
        errorsCount >> field(editMode = AlwaysRead)
        y(visible = !id.toString.startsWith("-")){
            button("Update monitored workspaces") {
                updateWorkspacesToMonitorJob.enqueueFirst
            }
            workspaces >> table {
            	id name
            }
        }
    }}
  
}

inline entity UpdateWorkspacesToMonitorJob extends ScheduledJob {

	impl field service: WorkspaceAlertService by updateWorkspacesToMonitorJob
    impl func schedule = "0 35 * * * ?"
    impl func do: Future[Any] = {
        let winfo <- Workspace.getEveryWorkspaceInfo
        let newWorkspaces = winfo.map { w =>
            let p = w.split("@")
            var wname = ""
            p.drop(1).dropLast(1).foreach { wname += _ + "@" }
            wname += p.last
            WorkspaceInfo(id = p(1), name = wname)
        }
        let diffAdded = newWorkspaces.filter(x => !service.workspaces.exists(_.equals(x)))
        let diffRemoved = service.workspaces.filter(x => !newWorkspaces.exists(_.equals(x)))
        let anyUpdates = diffAdded || diffRemoved
        if (anyUpdates) {
			Activity.comment("<p>Workspaces got updated.</p>".as[Html])
            if (let d = diffAdded) Activity.comment("<p>Added workspaces:<ul>${d.map("<li>${_.name} (${_.id})</li>").join("\n")}</ul></p>".as[Html])
            if (let d = diffRemoved) Activity.comment("<p>Removed workspaces:<ul>${d.map("<li>${_.name} (${_.id})</li>").join("\n")}</ul></p>".as[Html])
        	service.workspaces = newWorkspaces
        	service.save
        }
    }

}
