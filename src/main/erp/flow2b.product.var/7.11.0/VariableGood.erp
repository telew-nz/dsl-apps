entity VariableGood extends ISuperProduct with IAltUnits {
  singular = "Variable Product"
  plural = "Variable Products"
  func isTangible = true
  field photos: [Image]?
  field title: String
  field description: Text?
  field status: ProductStatus = default
  field variations: [GoodVariation]? read by variableGood
  ref field trackItems: TrackItems? {
    label = "Track Serial Numbers"
  }
  func skuPrefix = "VGD"
  
  object func attrs: [ProductAttr]? = [ColorItem.as[ProductAttr]!, Size]
  func child = variations
  compute productCost: ProductCost? {
    availableFor = All
    value = childProductCost
  }
  
  view form = nav{
    tabs {
      grid (labels = Top, label = "Main") {
        photos >> field(height = 10, label = "")
        status >> field(label = "", compact)
        title >> field(label = "", fontStyle = H1)
        row {sku refNum trackItems}
        
        unitsView
        productCost >> field(frame = false)
        baseMainView
        
        variations
        extension("main")
        extension
        description
      }
      superProductTabView
      extension("tabs")
    }
    extension("nav")
  }
} 

entity GoodVariation extends IComplexProduct {
  singular = "Product Variation"
  plural = "Product Variations"
  func title: String = "$variableGood $(attrTerms.join(" "))"
  func subTitle(superTitle: String): String = "$superTitle $(attrTerms.join(" "))"
  
  field photos: [Image]?
  ref field variableGood: VariableGood
  func getParent = variableGood
  field obsolete: Bool
  field alwaysAvailable: Bool
  func isTangible = true
  
  func attrTerms = color.toSeq + size.toSeq
  
  ref field color: ColorItem?
  ref field size: Size?
  
  view form = nav(onBarcode = if (ui.edit) addBarcode(barcode)){
    tabs {
      grid (labels = Top, label = "Main") {
        photos >> field(height = 10, label = "")
        status >> field(label = "", compact)
        variableGood >> field(label = "", noTab, fontStyle = H1)
        color
        size
        row {sku refNum >> field(noTab) space}
        barcodes
        productCost >> field(frame = false)
        baseMainView
        extension("main")
        extension
        description
      }
      grid (labels = Top, label = "Related") {
        kits
        extension("rel")
      }
      subProductTabView
      productTabView
      extension("tabs")
    }
    extension("nav")
  }
  view ref = x(flow = false){"   " >> field(background = color?.color, visible = color) title}
}
