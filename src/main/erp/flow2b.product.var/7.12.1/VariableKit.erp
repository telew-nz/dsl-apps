entity VariableKit extends ISuperProduct with TWeightSize {
  impl func isTangible = true
  impl field title: String
  impl field photos: [Image]?
  impl field description: Text?
  impl field status: ProductStatus = default
  impl func trackItems = null
  field variations: [KitVariation]? read by variableKit
  impl func skuPrefix = "VKIT"
  
  impl object func attrs: [ProductAttr]? = [ColorItem.as[ProductAttr], Size]
  impl func child = variations
  impl compute productCost: ProductCost? {
    availableFor = All
    value = childProductCost
  }
  
  impl view form = nav{
    tabs {
      grid (labels = Top, label = "Main") {
        photos >> field(height = 10, label = "")
        status >> field(label = "", compact)
        title >> field(label = "", fontStyle = H1)
        row {sku refNum space}
        
        row {weight width height depth}
        productCost >> field(frame = false)
        baseMainView        
        variations
        extension("main")
        extension
        description
      }
      superProductTabView
      extension("tabs")
    }
    button("Add Missing Variations") {
      back {
        addMissingVariations()
      }
    }
    extension("nav")
  }
  back func addMissingVariations() = {
    if (let template = variations.first, let sampleProd = template.items*.product*.as[GoodVariation].find(attrTerms == template.attrTerms)) {
      sampleProd.variableGood.variations.foreach {sampleVar =>
        if (!this.variations.exists(_.attrTerms == sampleVar.attrTerms)) {
          let newItems = template.items.map {i =>
            if (let pv = i.product.as[GoodVariation]) {
              if (pv.attrTerms == template.attrTerms) {
                if (let product = pv.variableGood.variations.find(_.attrTerms == sampleVar.attrTerms)) {
                  (product, i.unit, i.qty)
                }
              } else {
                (i.product, i.unit, i.qty)
              }
            } else {
              (i.product, i.unit, i.qty)
            }
          }
          if (newItems.all(_)) {
            KitVariation(
              variableKit = this
              color = sampleVar.color, size = sampleVar.size,
              items = newItems.map {i =>
                KitItem(product = i!.product, unit = i!.unit, qty = i!.qty)
              }
            ).save
          }
        }
      }
    }
  }
}

entity KitVariation extends IKit with IComplexSubProduct {
  impl func title: String = "$variableKit $(attrTerms.join(" "))"
  impl func subTitle(superTitle: String): String = "$superTitle $(attrTerms.join(" "))"
  
  ref field variableKit: VariableKit
  impl func getParent = variableKit
  impl field photos: [Image]?
  
  impl func attrTerms = color.toSeq + size.toSeq + condition.toSeq
  
  ref field color: ColorItem?
  ref field size: Size?
  ref field condition: ProdCondition?
  
  impl view form = nav(onBarcode = if (ui.edit) addBarcode(barcode)){
    tabs {
      grid (labels = Top, label = "Main") {
        photos >> field(height = 10, label = "")
        status >> field(label = "", compact)
        variableKit >> field(label = "", noTab, fontStyle = H1)
        row {sku refNum >> field(noTab) space}
        row {color size condition}
        
        barcodes
        productCost >> field(frame = false)
        items >> table {
          product >> field(width = 0.7)
          qty >> field(width = 0.3)
        }
        baseMainView
        extension("main")
        extension
        description
      }
      grid (labels = Top, label = "Related") {
        kits
        extension("rel")
      }
      subProductTabView
      extension("tabs")
    }
    extension("nav")
  }
  
  impl view ref = x(flow = false){"   " >> field(background = color?.color, visible = color) title}
}