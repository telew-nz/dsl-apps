entity SalesOpportunity extends HasN with Entity with NewEngine with DashboardItem {
  ref field customer: Customer by opportunities {
    available(Inline).for(Inline)
  }
  object field gen: Int?
  
  impl func numPrefix = "OP"
  impl func title = "$fullNum $customer"
  
  compute person = customer.legalEntity.as[Person]!
  field _state: OpportunityState = Entered
  ref stored compute state: OpportunityState {
    value = calcState
    from = ([calcState, OpportunityState.Lost] + if (fittingAppointment) [OpportunityState.Complete]).toSet
    update = {
      _state = new
    }
  }
  func calcState: OpportunityState = {
    if (_state == Lost) Lost
    else if (let fa = fittingAppointment) {
      if (_state == Complete) Complete 
      else FittingBooked
    } else if (let dresses = salesOrders*.items*.as[SalesItem].filter{_.product.is[DressVariation]}) {
      if (dresses.exists(_.deliveryItem.isAv)) ReadyForFitting
      else Sold
    } else if (bridalAppointment) Booked
    else Entered
  }
  field measurments: PerMeasurments?
  
  impl func nextN = increment(gen)
  field appointments: [BridalAppointment]? read by opportunity
  field details: BridalDetails?
  
  field salesOrders: [SalesOrder]? read by opportunity
  compute contacts = customer.legalEntity.contacts*.address
  field dresses: [OpportunityDress]? inline by opportunity {
    label = "Selected Dresses"
  }
  compute bridalAppointment = appointments.find(type == Bridal)
  compute fittingAppointment = appointments.find(type == Fitting)
  
  impl view form = nav{grid{
    x(label = ""){state >> field (editMode = AlwaysEdit) customer >> field (stretch = 1, fontStyle = H2) number >> field(fontStyle = Label)}
    contacts >> field(label = "")
    appointments
    details
    measurments
    dresses >> list {grid(labels = None){
      //x {dress >> field(stretch = 1) add("Sales Order", visible = !salesOrders) {newSO}}
      dress
      notes
    }}
    salesOrders
  }}
  impl view ref = x {state title}
  
  impl func itemRoles = [Salesperson]
  impl func activeRoles = if (state.active) [Salesperson]
  
  report Report {
    impl view list = SalesOpportunity.all >> nav{list{
      ref
    }}
  }
}

inline entity OpportunityDress {
  field opportunity: SalesOpportunity by dresses
  field dress: Dress
  field notes: Text?
  
  /*front func newSO = {
    SalesOrder.incomplete(customer = opportunity.customer, items = [
     SalesItem(deliveryItem = DeliveryItem(product = dress))
    ]).form.open
  }*/
}

enum OpportunityState(active: Bool) {
  case Entered(active = true) {
    background = "#ed9"
    icon = "play"
  }
  case Booked(active = true) {
    background = "#8bf"
    icon = "book"
  }
  case Sold(active = true) {
    background = "#dea"
    icon = "check"
  }
  case ReadyForFitting(active = true) {
    icon = "flag-checkered"
    background = "#dea"
  }
  case FittingBooked(active = true) {
    icon = "book-reader"
    background = "#dea"
  }
  case Complete(active = false) {
    color = "white"
    background = "#090"
    icon = "check-circle"
  }
  
  case Lost(active = false) {
    background = "#999"
    icon = "times-circle"
  }

}


class BridalDetails {
  field weddingDate: Date?
  field budget: UMoney?
  field address: Text?
  field notes: Text?
}


extend entity SalesOrder {
  const field opportunity: SalesOpportunity? by salesOrders
  
  impl event onSave = {
    if (touched) opportunity?.touch
  }
}

extend entity Customer {
  field opportunities: [SalesOpportunity]? read by customer
}

class PerMeasurments {
  field unit: LengthUnit = Centimetre
  field shoulder: Dec[unit] {canEditUnit}
  field chest: Dec[unit] {canEditUnit}
  field waist: Dec[unit] {canEditUnit}
  field hip: Dec[unit] {canEditUnit}
  impl view form = grid {
    row{shoulder chest waist hip}
  }
}
