entity BookApi extends SystemService with IEndpoint {
  object func one: BookApi = Workspace.current.bookApi!
  impl func title = "Book API"
  impl func service = this
  impl view form = nav{grid(labels = Top){
    issuesView
    serviceState
    errorsCount >> field(editMode = AlwaysRead)
    
    model
    bridalProduct
    fittingProduct
    legalEntity
    store
    
    fittingConfirm
    bridalConfirm
  }}
  
  field model: BookPriceModel
  ref field bridalProduct: JobType
  ref field fittingProduct: JobType
  field legalEntity: OurLegalEntity =? onlyOne
  field store: IStore =? onlyOne
  field fittingConfirm: PlainText {
    label = "Final fitting confirmation text"
  }
  field bridalConfirm: PlainText {
    label = "Bridal appointment confirmation text"
  }
  impl event onSave = {
    if (created) {
      Workspace.current.bookApi = this
      Workspace.current.save
    }
  }
  
  /*
   *  POST $endpoint/newAppointment
   */
  back func postNewAppointment(body: NewApptRequest): NewApptResponse = {
    body.trace
    let opp: SalesOpportunity = {
      let person = EmailContact.all.find(email =%= body.email && addressee.title =%= body.name).result.get?.addressee?.as[Person].alt{
        Person(name = body.name)
      }
      person.addEmail(body.email)
      person.addPhone(Mobile, body.phone)
      person.makeCustomer.opportunities
        .find(_.state.active)
        .alt(SalesOpportunity(customer = person.makeCustomer))
    }
    
    let appId = if (body.type == "bridal") opp.n! else -opp.n!
    let prod = product(appId)
    NewApptResponse(
      appointmentId = appId,
      dates = rentAvPeriods(prod, Date.now)
         .map(period.start.date).distinct
         .first(300)
         .map(format("yyyy-MM-dd"))
    )
  }
  
  
  /*
   *  GET $endpoint/periods?appointmentId=...&date=yyyy-MM-dd
   */
  back func getPeriods(appointmentId: Int, date: Date): PeriodsResponse = {
    let prod = product(appointmentId)
    PeriodsResponse(
      periods = rentAvPeriods(prod, date)
         .first(200) 
         .takeWhile(period.start.date == date)
         .groupBy(period).agg(item = first.items.first!).toSeq.sortBy(period.start)
         .map(PeriodDataItem(start = period.start.format("HH:mm"), end = period.end.format("HH:mm"), data = item.id))
    )
  }
  
  /*
   *  POST $endpoint/selectPeriod
   */
  back func postSelectPeriod(body: SelPeriodRequest): Future[ConfirmResponse] = {
    let opp = opportunity(body.appointmentId)
    //let prod = product(body.appointmentId)
    
    if (body.appointmentId > 0 ){
      if (let a = opp.bridalAppointment) {
        a.state = Cancelled
        a.save
      }
    } else {
      if (let a = opp.fittingAppointment) {
        a.state = Cancelled
        a.save
      }
    }
    
    let appt = BridalAppointment(
      type = if (body.appointmentId > 0) Bridal else Fitting
      opportunity = opp
      period = Period(start = Date(body.date + " " + body.period.start), end = Date(body.date + " " + body.period.end))
      salesperson = Person.load(body.period.data!).result.get
    )
    
    ConfirmResponse(confirm = if (body.appointmentId < 0) fittingConfirm)
  }
  
  /*
   * POST $endpoint/details
   */
  back func postDetails(body: DetailsRequest): ConfirmResponse = {
    let appt = opportunity(body.appointmentId)
    appt.details = body.copy(BridalDetails, budget = try? body.budget.toUNum!, address = body.address, notes = body.notes)
    appt.saveMain
    ConfirmResponse(confirm = bridalConfirm)
  }
  
////////////////////////////////////////////////////////////////////////////////////////////////////
  back func opportunity(num: Int): SalesOpportunity = {
    SalesOpportunity.all.find{a => a.n == num.abs}.result.get!
  }
  
  back func product(appId: Int): JobType = {
    if (appId > 0) bridalProduct else fittingProduct
  }
  
  back func rentAvPeriods(prod: JobType, date: Date) = {
    model.priceRent(prod)
      ?.rentAvPeriods(date, rentCalcItem(prod))
  }
  back func rentCalcItem(prod: ISubProduct) = {
    RentAvCalcItem(
      priorityDate = now
      le = legalEntity
      store = store
      retStore = store
      product = prod
      dur = 1, qty = 1
    )
  }
}

extend entity Workspace {
  ref field bookApi: BookApi? 
}
extend role Public {
  entity BookApi = readOnly
}


class NewApptRequest {
  field type: String
  field email: String
  field phone: String
  field name: String
}

class NewApptResponse {
  field appointmentId: Int
  field dates: [String]?
}

class PeriodsResponse {
  field periods: [PeriodDataItem]?
}
class PeriodDataItem {
  field start: String
  field end: String
  field data: String?
}

class SelPeriodRequest {
  field appointmentId: Int
  field date: String
  field period: PeriodDataItem
}

class ConfirmResponse {
  field confirm: PlainText?
}
class DetailsRequest {
  field appointmentId: Int
  field weddingDate: Date?
  field budget: String?
  field address: PlainText?
  field notes: PlainText?
}