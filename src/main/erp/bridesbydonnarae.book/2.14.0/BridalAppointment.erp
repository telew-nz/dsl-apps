extend enum DashboardIssue {
  case SalespersonUnav(role = Salesperson, priority = major) = "Salesperson is unavailable"
}

entity BridalAppointment extends Event with EventParticipant with NewEngine {
  ref field opportunity: SalesOpportunity by appointments
  impl field period: Period
  field type: ApptType
  field state: ApptState = Confirmed
  system field cancelDate: Date?
  compute person: Person = opportunity.customer.legalEntity.as[Person]!
  func personTo: [Person]? = opportunity.customer.legalEntity.as[Person].toSeq
  field salesperson: Person {
    from = Person.all.filter(user?.is(Salesperson))
  }
  impl func event = this
  impl func participant = salesperson
  impl func numPrefix = "BAP"
  impl func title = "$fullNum $(opportunity.customer)"
  impl func active = state.active
  impl func issues = if (isUnav && period.end > now) [SalespersonUnav]
  
  impl view form = nav{grid{
    x (label = "") {type state opportunity >> field(stretch = 1 fontStyle = H2, title = title) number >> field(fontStyle = Label) } 
    period
    salesperson
    label ("Unavailable", color = "red", visible = isUnav)
    periods >> list(frame = false, visible = ui.edit, label = "Available At $(period.start.date)") {
      button(label = "$(period.start.time) - $(period.end.time) $(items.first)") {
        this.period = _.period
        this.salesperson = _.items!.first.as[Person]!
      }
    }
  }}
  impl view ref = x {type state title}
  impl view traitItem = grid(labels = None){
    x {type state opportunity >> field(stretch = 1 title = title) number >> field(fontStyle = Label)}
    x {salesperson period >> field(stretch = 0) }
  }
  impl event onSave = {
    if (created) salesperson.user!.join(this).notify
    opportunity.touch
    if (updated(state) && state == Cancelled) cancelDate = now
  }
  
  compute periods: Future[[RentAvPeriodItem]?] = if (state.active) {
    back{
      let d = period.start.start(of = day)
      BookApi.one.rentAvPeriods(type.product, d).takeWhile(_.period.start.start(of = day) == d)
    }
  }
}

enum ApptType(product: JobType) {
  case Bridal(product = BookApi.one.bridalProduct) {
    icon = "book"
    background = "#8bf"
  }
  case Fitting(product = BookApi.one.fittingProduct) {
    icon = "book-reader"
    background = "#dea"
  }
}

enum ApptState(active: Bool) {
  case Pending(active = true) {
    background = "#8bf"
    icon = "spinner"
  }
  case Confirmed(active = true) { 
    icon = "check"
    background = "#dea"
  }
  case Complete(active = false) {
    color = "white"
    background = "#090"
    icon = "check-circle"
  }
  case Cancelled(active = false) {
    background = "#999"
    icon = "times-circle" 
  }
}
