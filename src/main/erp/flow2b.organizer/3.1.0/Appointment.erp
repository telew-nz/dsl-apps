entity Appointment extends Event {
  icon = "calendar-check"
  impl field title: String
  impl field period: Period = Period(start = Date.now.start(of = hour) + 1_hour, end = Date.now.start(of = hour) + 2_hour)
  list compute state: AppointmentState = {
    let requiredParticipants = participants.filter(!optional)
    if (clashes) Clash
    else if (requiredParticipants.exists(acceptance == Declined)) Declined
    else if (requiredParticipants.all(acceptance == Accepted)) Accepted
    else if (requiredParticipants.all(acceptance == Accepted || acceptance == Tenative)) Tenative
    else Pending
  }
  impl compute allParticipants: [Participant]? = participants*.participant
  impl func requiredParticipants: [Participant]? = participants.filter(!optional)*.participant
  field participants: [AppointmentParticipant] inline by appointment = [
    AppointmentParticipant(participant = Person.current!, acceptance = Accepted)
  ]
  override func contains(participant: Participant?): Bool = {
    !participant || participants.exists(i => i.participant == participant && i.acceptance != Declined)
  }
  
  field description: Text?
  impl view traitItem = grid(labels = None) {
    title
    row {state >> field(width = 0.2) period >> field(width = 0.8, frame = false)}
  }
  impl view form = nav{grid{
    title
    state
    period >> field(frame = false)
    extension("fields")
    participants >> list {grid(labels = None) {
      row{acceptance >> field(editMode = AlwaysEdit, width = 0.23, compact = true) participant >> field(width = 0.57) optional >> field(width = 0.2)}
    }}
    clashes
    alerts
    description
    tickets
  }}
  
  impl event onSave = {
    allParticipants.filter(!autoAccept)*.user.join(this)
  }
}

enum Acceptance {
  case Pending {background = "#ed9"}
  case Declined {background = "#fcc"} 
  case Tenative {background = "#dea"}
  case Accepted {
    color = "white"
    background = "#090" 
  }
}

enum AppointmentState {
  case Pending {background = "#ed9"}
  case Declined {background = "#fcc"}
  case Accepted { 
    color = "white"
    background = "#090" 
  }
  case Tenative {background = "#dea"}
  case Canceled {background = "#999"}
  case Clash { background = "#fcc" }

}

inline entity AppointmentParticipant {
  singular = "Participant"
  field appointment: Appointment
  field optional: Bool
  field participant: Participant {
    update = {
      if (participant.autoAccept) acceptance = Accepted
    }
  }
  field acceptance: Acceptance {
    default = Pending
    from = {
      if (participant.autoAccept || participant == Person.current) [Acceptance.Pending, Acceptance.Declined, Acceptance.Tenative, Acceptance.Accepted] 
      else [acceptance]
    }
  }
}