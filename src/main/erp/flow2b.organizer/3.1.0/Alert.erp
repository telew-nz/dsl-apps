inline entity EventAlert extends DeferredJob {
  system field event: Event by alerts
  field before: Duration[unit]? = 0_hour
  field unit: DateUnit = hour
  compute timeType: AlertTimeType {
    value = {
      if (!before) AtTimeOfEvent
      else if(before.hours == 1_hour) OneHour
      else Before
    }
    update = {
      this.unit = new.duration.unit!.as[DateUnit]!
      this.before = new.duration
    }
  }
  impl func service = AlertService
  impl func jobDate: Date? = {
    let d = event.period.start - before
    if (d > Date.now) d
  }
  impl func do = {
    event.observers*.notify
  }
  impl view form = x { 
    timeType
    before >> field(canEditUnit = true, visible = timeType == Before)
  }
}

object AlertService extends SystemService {
  impl func title = "AlertService"
  impl func roles: Set[Role]? = [Admin]
  
  impl view form = nav{grid(labels = Top){
    issuesView
    serviceState
    errorsCount >> field(editMode = AlwaysRead)
  }}
}

enum AlertTimeType(duration: Duration?) {
  case AtTimeOfEvent(duration = 0_hour) = "At time of event"
  case OneHour(duration = 1_hour) = "1 hour before"
  case Before(duration = 10_minute)
}