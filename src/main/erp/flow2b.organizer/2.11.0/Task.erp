entity Task {
  background = "#498898"
  func done: Bool = status == Done
  list field subjects: [HasTasks]? by tasks
  ref field status: TaskStatus = Entered
  field title: String
  ref field owner: User =? current
  field description: Text?
     
  func active = status.active
  report Report {
    field owner: User? = User.current
    field activeOnly: Bool = true
    view list = Task.all.filter(owner ==? this.owner && (!this.activeOnly || active)) >> nav{list{grid(labels = None){
      subjects
      x{status >> field(editMode = AlwaysEdit, label = "") title owner}
    }}}
  }
  view form = nav{grid {
    title
    status >> field(editMode = AlwaysEdit, compact = true) 
    owner
    subjects
    description
  }}
  view ref = x(margin = Around){owner status >> field(editMode = AlwaysEdit, compact = true) title}
  event onSave = {
    owner?.join(this)
  }
  object view widget = Task.all.filter(active && owner == User.current)
      >> nav(label = "Tasks", limit = 5, more = Task.Report().list.open){list{}}
}

trait HasTasks {
  field tasks: [Task]? read by subjects
}


enum TaskStatus(active: Bool) {
  case Entered(active = true) {
    background = "#ed9"
  }
  case InProgress(active = true) {
    background = "#8bf"
  }
  case Done(active = false) {
    color = "white"
    background = "#090"
  }
  case Canceled(active = false) {
    background = "#999"
  }
}