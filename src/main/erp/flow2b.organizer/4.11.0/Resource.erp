extend trait IProductItem {
  func canBook: Bool = true
  func user: IUser? = null
  func manualAcceptance = user?.userEnabled && canBook
}

basicProduct ResourceType(notInventory = true, skuPrefix = "RST", noAltUnits = true) {
  impl func status = NotForSale
  impl ref field trackItems: TrackItems? {
    label = "Track Resources"
    default = Always
  }
}

extend entity ResourceType {
  impl func rentStockType = Resource
  impl func productCost: ProductCost? = null
  impl func productItems = resources
  field resources: [Resource]? by type
  
  impl view customView = grid (labels = Top) {
    resources
  }   
  impl func productItemsLabel: String = "Resources"
}

entity Resource extends IProductItem with HasN {
  impl func numPrefix: String = "RES"
  impl func nextN = increment(ProductItem.gen)
  impl func title = "$type $(refNum.alt(number))"
  impl compute products = [type]
  
  ref field type: ResourceType by resources
  field legalEntity: OurLegalEntity =? onlyOne
  field store: IStore =? onlyOne
  field address: Address?
  field desc: Text? {label = "Description"}
  impl view form = nav{grid{
    type
    row{refNum number}
    legalEntity
    store
    desc
  }}
  impl view itemView = grid{_}
  
  register prodReg: ProdItemReg = {
    ProdItemReg(
      item = this, date = Date.min, doc = this, 
      newLegalEntity = legalEntity, 
      newStockType = Resource,
      newStore = store, qty = 1, 
      isStocktake = true, autoProdReg = true
    )
  }
}