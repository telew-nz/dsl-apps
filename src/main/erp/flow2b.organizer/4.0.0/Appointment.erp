entity Appointment extends Event {
  icon = "calendar-check"
  impl field title: String
  
  const system field enterDate: Date = now
  impl field period: Period = Period(start = Date.now.start(of = hour) + 1_hour, end = Date.now.start(of = hour) + 2_hour)
  
  impl func numPrefix = "AP"
  field canceled: Bool = false
  list compute state: AppointmentState {
    value = if (canceled) Canceled else calcState
    from = [calcState, Canceled]
    update = {
      canceled = new == Canceled
    }
  }

  func calcState: AppointmentState = {
    let requiredParticipants = participants.filter(!optional)
    if (requiredParticipants.exists(acceptance == Declined)) Declined
    else if (requiredParticipants.all(acceptance == Accepted)) Accepted
    else if (requiredParticipants.all(acceptance == Accepted || acceptance == Tenative)) Tenative
    else Pending
  }
  //impl compute allParticipants: [Participant]? = participants*.participant
  //impl func requiredParticipants: [Participant]? = participants.filter(!optional)*.participant
  field participants: [AppointmentParticipant] inline by appointment = [
    AppointmentParticipant(participant = Person.current!, acceptance = Accepted)
  ]
  
  field description: Text?
  impl view traitItem = grid(labels = None) {
    x(flow = false){
      state >> field(editMode = AlwaysEdit, compact)
      title >> field(stretch = 1, fontStyle = H2)
      number >> field(fontStyle = H3)
    }
    period >> field(frame = false)
  }
  impl view form = nav{grid{
    x(flow = false){
      state >> field(editMode = AlwaysEdit, compact)
      title >> field(stretch = 1, fontStyle = H2)
      number >> field(fontStyle = H3)
    }
    period >> field(frame = false)
    extension("fields")
    participants >> list {grid(labels = None) {
      row{acceptance >> field(editMode = AlwaysEdit, width = 0.23, compact = true) participant >> field(width = 0.57) optional >> field(width = 0.2)}
      label ("Unavalable", color = "red", visible = isUnav)
    }}
    alerts
    description
    tickets
  }}
  
  impl event onSave = {
    participants*.participant.filter(manualAcceptance)*.user.join(this)
  }
}

enum Acceptance(sort: UDec[2]?) {
  case Pending(sort = 2) {background = "#ed9"}
  case Declined(sort = 0) {background = "#fcc"} 
  case Tenative(sort = 1.5) {background = "#dea"}
  case Accepted(sort = 1) {
    color = "white"
    background = "#090" 
  }
}

enum AppointmentState {
  case Pending {background = "#ed9"}
  case Declined {background = "#fcc"}
  case Accepted { 
    color = "white"
    background = "#090" 
  }
  case Tenative {background = "#dea"}
  case Canceled {background = "#999"}
  //case Clash { background = "#fcc" }
}

inline entity AppointmentParticipant {
  singular = "Participant"
  field appointment: Appointment
  field optional: Bool
  field participant: IProductItem {
    available(Ref).with(products, canBook)
    update = {
      if (!participant.manualAcceptance) acceptance = Accepted
    }
  }
  field acceptance: Acceptance {
    default = Pending
    from = {
      if (participant.manualAcceptance || participant == Person.current) {
        [Acceptance.Pending, Acceptance.Declined, Acceptance.Tenative, Acceptance.Accepted] 
      } else [acceptance]
    }
  }
  func isUnav = out?.outlook < 0
  register out: ProdItemReg? {
    autoRecalc
    availableFor = Inline
    available(Inline)
    value = if (acceptance != Declined && participant.canBook) {
      ProdItemReg(
        item = participant, date = appointment.period.start, 
        doc = appointment, docItem = this, sort = acceptance.sort
        qty = -1, autoProdReg = true, priorityDate = appointment.enterDate,
        isRent = true
      )
    }
  }
  register in: ProdItemReg? = if (acceptance != Declined && participant.canBook) {
    ProdItemReg(
      item = participant, date = appointment.period.end, 
      doc = appointment, docItem = this, sort = acceptance.sort
      qty = 1, autoProdReg = true, priorityDate = appointment.enterDate,
      isRent = true
    )
  }
}