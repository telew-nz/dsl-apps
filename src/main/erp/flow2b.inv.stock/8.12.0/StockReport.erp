report StockReport {
  plural = "Stock"
  field legalEntity: OurLegalEntity?
  field store: IStore?
  field product: IProduct? {from = IProduct.all.filter(isTangible)}
  field category: IProductCategory?
  field brand: Brand?
  field tag: IProductTag?
  field stockType: StockType?
  field priceModel: IPriceModel?
  field priceLevel: IPriceLevel {
    default = PriceLevelDefault
    visible = priceModel
  }
  
  field stocktakeEarlier: Date?
  func query = ProdReg.current.filter(
       !isEmpty
    && legalEntity ==? this.legalEntity
    && store ==? this.store
    && product ==? this.product
    && (!category || product.superProduct.allCategories.contains(category!))
    && (!tag || product.superProduct.allTags.contains(tag!))
    && (!brand || product.superProduct.brands.contains(brand!))
    && stockType ==? this.stockType
    && stocktakeEarlier ?>= lastStocktake?.journalDate.alt(Date.min)
    && (!priceModel || priceModel!.contains(priceLevel, product))
  ).query
  
  impl view list = query >> nav(background = "#2e8e67"){
    table(mode = Static){
      product >> field (width = 0.4), legalEntity, store, group(label = "Stock") {
        fact, reserve, available, expectation, outlook, quotes
      }, lastStocktake
    }
    button("Print Labels", icon = "barcode") {
      labelsView.print
    }
  }
  
  view labelsView = y (newEngine) {
    query.flatMap(prodLabels(product, fact)) >> list(frame = false){
      prod.labelView  >> field(pageBreakAfter)
    }
  }
  
  object func prodLabels(product: ISubProduct, qty: Qty?): [ProdWrap]? = {
    if (qty > 0) ProdWrap(prod = product) + prodLabels(product, qty - 1)
  }
}

class ProdWrap{
  field prod: ISubProduct {
    available(Inline)
  }
}

extend trait IPriceModel { 
  extend view pmNavButtons = button("Stock") {
    StockReport(priceModel = this).list.open
  }
}
