register ProdReg {
  key field legalEntity: OurLegalEntity
  key field store: IStore  
  key field product: IProduct
  key field stockType: StockType
  order field sort: UDec[2]?
  order field date: Date
  field doc: Entity
  field docItem: AnyEntity? inline {deleteCascade = false}
  field productItems: [ProductItem]?
  func unit = product.unit
  compute isFact: Bool {
    value = !sort
    update = {
      sort = if (new) 0 else 1
    }
  }
  func isReserve = sort > 0 && sort <= 1
  field priorityDate: Date?
   
  field isStocktake: Bool
  field qty: Qty[unit]?
  compute factualQty: Qty[unit]? = if (isFact) qty
  compute reservedQty: Qty[unit]? = if (isReserve && qty < 0) -qty
  compute expectedQty: Qty[unit]? = if (isReserve && qty > 0) qty
  compute quotedQty: Qty[unit]? = if (sort > 1 && qty < 0) -qty
  
  compute factualChange: Qty[unit]? {
    value = if (isStocktake) stocktakeDifference else factualQty
    label = "Fact"
  }
   
  stored compute fact: Qty[unit]? = if (isStocktake) factualQty else prev?.fact + factualQty
  
  stored compute reserve: Qty[unit]? = prev?.reserve + reservedQty
  compute available: Qty[unit]? = fact - reserve
  stored compute expectation: Qty[unit]? = prev?.expectation + expectedQty
  compute outlook: Qty[unit]? = fact - reserve + expectation
  stored compute quotes: Qty[unit]? = prev?.quotes + quotedQty
  
  stored compute stocktakeDifference: Qty[unit]? = if (isStocktake) factualQty - prev?.fact else 0
  field differencePrice: Money[legalEntity.getCurrency]?
  stored compute lastStocktake: Journal? = if (isStocktake) doc.as[Journal]! else prev?.lastStocktake
  func isEmpty = !fact && !reserve && !expectation && !quotes
    
  stored compute awaitingForDocs: [(qty: Qty, item: ProdReg)]?
  compute awaitingForDate: Date? = awaitingForDocs*.item*.date.max
  stored compute reserveQueue: QtyQueue[Qty, ProdReg]?
  event onSave = {
    if (let e = expectedQty) {
      reserveQueue = prev?.reserveQueue.enqueue(e, this)
      awaitingForDocs = null
    } else if (let r = reservedQty) {
      if (reserve > fact) {
        let e = prev?.reserveQueue.dequeue((reserve - fact).min(r))
        awaitingForDocs = e.items
        reserveQueue = e.queue
      } else {
        awaitingForDocs = null
        reserveQueue = prev?.reserveQueue
      }
    } else {
      awaitingForDocs = null
      reserveQueue = null
    }
    if (updated) {
      doc.touch
      if (!next) {
        product.onStockUpdated(this)
      }
    }
  }
    
  func prevStock: Stock = Stock(
    unit = unit, fact = fact - factualQty, reserve = reserve - reservedQty, expectation = expectation - expectedQty)
  func stock: Stock = {
    Stock(unit = unit, fact = fact, reserve = reserve, expectation = expectation)
  }
  func stockStore: StockStore = {
    StockStore(product = product, store = store, legalEntity = legalEntity, stockType = stockType, stock = stock)
  }
  func prevStockStore: StockStore = StockStore(product = product, store = store, legalEntity = legalEntity, stock = prevStock)
  register invLedgers: [LInvBase]? = if (isStocktake && stocktakeDifference) {
    ProductItem.map(product, stocktakeDifference.abs.unsigned!, items = productItems) {
      stockType.obj.new(
        journal = doc.as[Journal]!, productItem = item, 
        product = product, qty = if (stocktakeDifference < 0) -trQty else trQty,
        contraAccount = InventoryAdjustments, 
        trAmount = if (stocktakeDifference > 0) differencePrice*stocktakeDifference else 0
      )
    }
  }
  
  func availableQtyPeriods(start: Date): [QtyPeriod] = {
    if (let n = next) {
      if (n.outlook != outlook && start < n.date) {
	    QtyPeriod(period = Period(start = start, end = n.date), qty = outlook)
          + n.availableQtyPeriods(n.date)
      } else {
        n.availableQtyPeriods(start)
      }
    } else [QtyPeriod(period = Period(start = start, end = Date.max), qty = outlook)]
  }
  
  func availablePeriods(qty: Qty[ProductUnit.Item], start: Date, priorityDate: Date?, excludeItems: [Id?]?): [Period]? = {
    let av = (qty <= outlook)
    let dates = (if (av) start) + next?.availabilitySwitchDates(qty, av, priorityDate, 0, 0, excludeItems) + Date.max
    dates.sliding(2, 2).filter(count == 2).map(Period(start = first, end = last))
  }
  
  func availabilitySwitchDates(qty: Qty[ProductUnit.Item], wasAvailable: Bool, priorityDate: Date?, exclDelta: Qty[ProductUnit.Item]?, priorDelta: Qty[ProductUnit.Item]?, excludeItems: [Id?]?): [Date]? = {
    var exclDelta1 = exclDelta
    var priorDelta1 = priorDelta
    if (excludeItems.contains(id)) exclDelta1 = exclDelta - expectedQty
    else if (let pd1 = this.priorityDate, let pd2 = priorityDate) {
      if (pd2 < pd1) {
        priorDelta1 = (priorDelta - expectedQty).max(0)
      }
    }
   
    let av = (qty <= fact + expectation + exclDelta1 + priorDelta1)
    (if (av != wasAvailable) date) + next?.availabilitySwitchDates(qty, av, priorityDate, exclDelta1, priorDelta1, excludeItems)
  }
}

class QtyPeriod {
  field period: Period
  field qty: Qty?
  func toString = "$period: $qty"
}
