extend trait IBaseProduct {
  compute stocks: Future[[StockStore]?] = Query(this).map(_.calcStocks).result
  func calcStocks: [StockStore]?
  
  func calcStock(legalEntity: LegalEntity?, stores: [Store]?): Stock? = {
    if (isTangible) {
      let sts = calcStocks.filter(i => i.legalEntity ==? legalEntity && (!stores || stores.contains(i.store)))*.stock
      Stock.sum(unit, sts)
    } 
  }
}
 
extend trait ISuperProduct {
  func calcStocks: [StockStore]? = {
    subProducts*.calcStocks
      .groupBy(store, legalEntity).agg(stock = Stock.sum(unit, _*.stock)).toSeq
      .map{
        StockStore(product = this, legalEntity = legalEntity, store = store, stock = stock)
      }
  }
}

extend trait ISubProduct {
  final func calcStocks: [StockStore]? = doCalcStocks
  func doCalcStocks: [StockStore]? = {
    products.toSeq.map(#0.calcStocks)
      .flatMap(i => i).groupBy(store, legalEntity).toSeq.map{i =>
        StockStore(
          product = this, legalEntity = i.legalEntity, store = i.store,
          stock = kitStock(i.#2)!
        )
      }
  }
  func kitStock(stockStores: [StockStore]?): Stock? = {
    if (let ss = stockStores) {
      Stock.min(
        this.unit, 
        products.leftJoin[Stock, Stock](
          ss.map(product.as[IProduct]! -> stock).toMap, 
          right.alt(Stock()).div(this.unit, left)
        ).values
      )
    }
  }
  list compute stock: Future[Stock] = {
    let sts <- stocks
    Stock.sum(unit, sts*.stock)
  }
  
  view stocksView = stocks >> list (mode = Static, visible = isTangible) {
    grid(labels = None, width = 1) {
      row{store >> field(width = 0.2) legalEntity  >> field(width = 0.2) 
          stock.shortView  >> field(width = 0.6)}
    }
  }
}
extend trait IProduct {
  func doCalcStocks: [StockStore]? = {
    if (isTangible) StockRegister.current.filter(good == this)*.stockStore.result.get
  }
  func kitStock(stockStores: [StockStore]?): Stock? = stockStores.first?.stock
  
  view movementsButton = button("Movements" visible = isTangible) { GoodsMovement(good = this).list.open}
  
  func onStockUpdated(register: StockRegister): Void = {
    touch
    superProduct.touch
  }
  
  func minStock: Qty? = 0
  func minStock(store: Store): Qty? = 0
}

extend entity Good {
  extend view form = extend("main"){ stocksView >> field(label = "Stock")}
  extend view form = extend("nav") {
    movementsButton
  }
  extend view traitItem = stock.get.shortView
}
extend entity Kit {
  extend view form = extend("main"){stocksView >> field(label = "Stock")}
  extend view traitItem = stock.get.shortView
}
