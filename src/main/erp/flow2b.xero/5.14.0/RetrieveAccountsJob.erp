extend entity FinAccount extends HasExtId


class RetrieveAccountsJob extends SystemJob {
  impl field service: XeroIntegration
  
  impl func do = {
    // Xero Accounts api endpoint doesn't support paging
    let res <- service.https("Accounts").get
    let rb = res.body!.parseJson[XeroAccountsResponse]
    
    rb.accounts.map { a =>
      saveAccount(a)
    }
  }
  
  
  back func saveAccount(xeroAccount: XeroAccount) = {
    if(xeroAccount.type == bank){
      let xeroNum = xeroAccount.bankAccountNumber.replace("-", "").replace(" ", "").trim!
      let acc = service.findExtId(xeroAccount.accountID, _.as[BankAccount])
      				.or(BankAccount.all.find(
                          legalEntity == service.legalEntity 
                       && number.replace("-", "").replace(" ", "").trim == xeroNum
                       && !extId(service)
                     ))
      				.alt(BankAccount(legalEntity = service.legalEntity,  
                                     number = xeroAccount.bankAccountNumber, name = xeroAccount.name!, currency = xeroAccount.currency!))

      acc.updateExtId(service, xeroAccount.accountID)
      acc.updateExtCode(service, xeroAccount.code)
      acc.save
    }
  }
}