class RetrieveAssetsJob extends SystemJob {
  field service: XeroIntegration
  
  func do = {
    nextPage(1)
  }
  
  back func nextPage(number: UInt): Future[Any] = {
    let res <- service.assetsHttps("Assets").param("status", "REGISTERED").param("page", number).get // status=Registered
    let rb = res.body!.parseJson[XeroAssetsResponse]
    
    rb.items.map { c =>
      RetrieveOneAssetJob(service = this.service, assetId = c.assetId!).run()
    }
    
    if(rb.pagination.pageCount > number) nextPage(number + 1)
  }  
}


class RetrieveOneAssetJob extends SystemJob {
  field service: XeroIntegration
  field assetId: String
  
  func do = {
    let res <- service.assetsHttps("Assets/$assetId").get
    let xeroAsset = res.body!.parseJson[XeroAsset]
    saveAsset(xeroAsset)
  }
  
  back func saveAsset(xeroAsset: XeroAsset) = {
    if(!service.findExtId(xeroAsset.assetId, _.as[ProductItem])) {
      let existingGood = Good.all.find(title == xeroAsset.assetName && status == NotForSale).result.get
      let product = existingGood.alt(Good(
        title = xeroAsset.assetName! 
        status = NotForSale
      ))

      if(let ds = xeroAsset.bookDepreciationSettings) {
        if(ds.depreciationMethod == StraightLine) {
          product.assetType = AssetType(
            depreciation = StraightLine(
              rate = ds.depreciationRate!
            )
          )
        }else if([XeroDepreciationMethod.DiminishingValue100,
                  XeroDepreciationMethod.DiminishingValue150,
                  XeroDepreciationMethod.DiminishingValue200]
                 .contains(ds.depreciationMethod!)){
          product.assetType = AssetType(
            depreciation = DiminishingValue(
              rate = ds.depreciationRate!
            )
          )
        } 
      }
      
      if(xeroAsset.serialNumber) product.trackItems = Sometimes
      
      product.save
      
      let productItem = ProductItem(
        product = product
        serialNum = xeroAsset.serialNumber
        refNum = xeroAsset.assetNumber
      )
      productItem.save
      productItem.updateExtId(service, xeroAsset.assetId)
      
      
      let writeUp = InventoryWriteUp(
        product = product
        stockType = Asset
        productItems = [productItem] // ???
        date = xeroAsset.bookDepreciationDetail?.depreciationStartDate.alt(xeroAsset.purchaseDate!)
        price = xeroAsset.purchasePrice! // ???
      ) 
      writeUp.save   
    }
  }
}
