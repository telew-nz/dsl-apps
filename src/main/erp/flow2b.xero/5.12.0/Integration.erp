entity XeroIntegration extends SystemService with ExtIdSource[String] with OAuth1Rsa {
  impl func title: String = "Xero $legalEntity"
  ref field legalEntity: OurLegalEntity =? onlyOne
  field store: IStore =? onlyOne
  func isRentalCompany: Bool = setup.isRentalCompany

  func authTmpTokenUrl: Url = "https://api.xero.com/oauth/RequestToken"
  func authAuthorizeUrl: Url = "https://api.xero.com/oauth/Authorize"
  func authTokenExchangeUrl: Url = "https://api.xero.com/oauth/AccessToken"

  impl field authConsumerKey: String?
  
  field organisationId: String? // Should be got after save integration
  func webhookUrl = if(let organisationId) "https://ws.flow2b.com/api/v1/flow2b.xero.XeroWebhookRouter?id=$organisationId"
  
  func xeroApiUrl: Url = "api.xero.com/api.xro/2.0/"
  func assetsApiUrl: Url = "api.xero.com/assets.xro/1.0/"
  func webUrl: Url = "http://xero.com/"

  back func basicHttps(path: String): HttpRequest = 
    HttpRequest(url = path, ssl = true)
      .oauth(this)
      .header("Accept", "application/json")
  back func https(path: String): HttpRequest = basicHttps("$xeroApiUrl$path")
  back func assetsHttps(path: String): HttpRequest = basicHttps("$assetsApiUrl$path")
  
  impl back func error(error: Any): JobError = {
    if (let e = error.as[JobError]) e
    else if (let r = error.as[HttpResponse]) {
      if (r.code == 503) {
        //"Xero service limit error. Job will be repeated in a minute.".log
        if (r.headers("X-Rate-Limit-Problem") == "Daily") {
          let date = Date.now + 24_hour
          Activity.comment("Daily limit is achieved. Will retry at $date")
          RepeatJob(times = 2, on = date)
        } else {
          let date = Date.now + 1_minute
          Activity.comment("Minute limit is achieved. Will retry at $date")
          RepeatJob(times = 2, on = date)
        }
      } else if (r.code == 400) {
        //It should not happen but it looks like there is an error in Xero side 
        // that it sometimes give this error on random items 
        RepeatJob(times = 4, on = Date.now + 1_minute)
      } else {
        ReportError(error = error)
      }
    } else {
      ReportError(error = error)
    }
  }

  
  field webhookKey: Password?
  system field webhookEndpoint: XeroWebhookEndpoint inline by service
  system field rentIdSrc: RentIdSrc inline by service
  func idSrc(isRent: Bool): ExtIdSource[String] = {
    if (isRent) rentIdSrc.as[ExtIdSource[String]]
    else this
  }
  
  impl func roles: [Role]? = [Admin]

  impl event onSave = {
    if(!setup.started && authConsumerKey) {
      if(!organisationId) RetrieveOrganisationJob(service = this).enqueue()
      
      RetrieveMatchedItemsJob(service = this).enqueue()
      if(setup.pullContacts) RetrieveContactsJob(service = this).enqueue()
      if(setup.pullBankAccounts) RetrieveAccountsJob(service = this).enqueue()
      if(setup.pullItems) RetrieveItemsJob(service = this).enqueue()
      
      if(setup.pullInvoices) RetrieveInvoicesJob(service = this).enqueue()
      if(setup.pullFixedAssets) RetrieveAssetsJob(service = this).enqueue()
      if(setup.pushContacts) PushAllContacsJob(service = this).enqueue()
      if(setup.pullBankTransactions) {
        RetrieveBankTransactionsJob(service = this).enqueue()
        RetrieveBankTransfersJob(service = this).enqueue()
      }
      //if(setup.pushInvoices) PushAllInvoicesJob(service = this).enqueue()
      
      setup.started = true
      this.save()
    }
  }

  field setup: XeroIntegrationSetup
  
  impl view form = nav{
    tabs{
    grid(label = "Settings"){
      issuesView
      legalEntity
      store
      serviceState
      button("Download .cer File", visible = id) {
        downloadPemFile("Flow2b-Xero.cer")
      }
      authConsumerKey
      setup >> field(editMode = Inherit) // FIXME: if created() .... throws error 
      panel(label="WebHook"){grid{
        webhookKey >> field(label="Key")
        webhookUrl >> field(label="URL")
      }}
    }}
  }

  impl object view list = XeroIntegration.all >> list{grid(labels = None){
    x {title serviceState}
  }}
  
  view test = nav(label = "$title (test)"){tabs{
    grid(label = "Test"){
      issuesView
      serviceState
      organisationId
      setup.started
      button("Retrieve Contacts") {
        UI.info("Retrieving contacts")
        RetrieveContactsJob(service = this).enqueue
      }
      button("Push Contacts") {
        UI.info("Pushing contacts")
        LegalEntity.all*.touch
      }
      button("Push all invoices") {
        UI.info("Pushing invoices")
        Order.all.map { i =>
          PushInvoiceJob(order = i, service = this).enqueue()
        }
      }

      setup.pullInvoicesFrom
      button("Retrieve invoices") {
        UI.info("Retrieving invoices")
        RetrieveInvoicesJob(service = this).enqueue
      }

      button("Retrieve Bank Accounts") {
        UI.info("Retrieving bank accoutns")
        RetrieveAccountsJob(service = this).enqueue()
      }

      button("Retrieve Payments") {
        UI.info("Retrieving payments")
        RetrievePaymentsJob(service = this).enqueue()
      }

      button("Retrieve Items") {
        UI.info("Retrieving items")
        RetrieveItemsJob(service = this).enqueue()
      }
      
      button("Sync Matched Items") {
        UI.info("Syncing matched items")
        RetrieveMatchedItemsJob(service = this).enqueue()
      }

      button("Retrieve Assets") {
        UI.info("Retrieving assets")
        RetrieveAssetsJob(service = this).enqueue()
      }
      
      button("Retrieve Bank Transaction") {
        UI.info("Retrieving bank transactions")
        RetrieveBankTransactionsJob(service = this).enqueue()
      }
      button("Retrieve Bank Transfers") {
        UI.info("Retrieving bank transfers")
        RetrieveBankTransfersJob(service = this).enqueue()
      }
      
    }}}
  
  class XeroIntegrationSetup {
    
    field started: Bool = false
    
  	field pullContacts: Bool
    field pullBankAccounts: Bool = true
    field pullItems: Bool
    field pullInvoices: Bool
    field pullInvoicesFrom: Date?
    field pullFixedAssets: Bool
    
    field pushContacts: Bool
    //field pushInvoices: Bool
    field drawingsCurrentAccount: CurrentAccount? {
      visible = pullBankTransactions
    }
    field pullBankTransactions: Bool
    
    field isRentalCompany: Bool
    
    impl view form = panel(label = "Initial Setup"){grid{
      isRentalCompany
      
      pullContacts
      pullBankAccounts
      pullBankTransactions
      drawingsCurrentAccount
      pullItems
      
      row{
       pullInvoices
       pullInvoicesFrom
      }
      
      pullFixedAssets
      
      pushContacts
      //pushInvoices
    }}
  }
  /*class PushAllInvoicesJob extends SystemJob {
  	field service: XeroIntegration
    
    func do = {
      Order.all.map { i =>
        PushInvoiceJob(order = i, service = this.service).enqueue()
      }
    }
  }*/
  
}

class PushAllContacsJob extends SystemJob {
  impl field service: XeroIntegration

  impl func do = {
    LegalEntity.all.map { le =>
      if(le.as[Person]?.companies.isEmpty) {
        PushContactJob(service = this.service, company = le).enqueue()
      }
    }
  }
}


entity RentIdSrc extends PseudoEntity with ExtIdSource[String] {
  ref field service: XeroIntegration
  impl func mainEntity = service
  impl func title = "Hire $service"
}