class RetrieveAssetsJob extends SystemJob {
  field service: XeroIntegration
  
  func do = {
    nextPage(1)
  }
  
  back func nextPage(number: UInt): Future[Any] = {
    let res <- service.assetsHttps("Assets").param("status", "REGISTERED").param("page", number).get // status=Registered
    let rb = res.body!.parseJson[XeroAssetsResponse]
    
    rb.items.map { c =>
      saveAsset(c)
    }
    
    if(rb.pagination.pageCount > number) nextPage(number + 1)
  }
  
  
  back func saveAsset(xeroAsset: XeroAsset) = {
    //xeroAsset.json.log 
    
    if(!service.findExtId(xeroAsset.assetId, _.as[ProductItem])) {
      let product = Good(
        title = xeroAsset.assetName! 
        refNum = xeroAsset.assetNumber
        status = NotForSale
      )

      if(let ds = xeroAsset.bookDepreciationSettings) {
        if(ds.depreciationMethod == StraightLine) {
          product.assetType = AssetType(
            depreciation = StraightLine(
              rate = ds.depreciationRate!
            )
          )
        }else if([XeroDepreciationMethod.DiminishingValue100,
                  XeroDepreciationMethod.DiminishingValue150,
                  XeroDepreciationMethod.DiminishingValue200]
                 .contains(ds.depreciationMethod!)){
          product.assetType = AssetType(
            depreciation = DiminishingValue(
              rate = ds.depreciationRate!
            )
          )
        } 
      }
      
      product.save
      
      let productItem = ProductItem(product = product)
      productItem.save
      productItem.updateExtId(service, xeroAsset.assetId)
      
      
      let writeUp = InventoryWriteUp(
        product = product
        stockType = Asset
        productItems = [productItem] // ???
        date = xeroAsset.bookDepreciationDetail?.depreciationStartDate.alt(xeroAsset.purchaseDate!)
        price = xeroAsset.purchasePrice! // ???
      ) 
      writeUp.save
     
      
    }

  }
}
