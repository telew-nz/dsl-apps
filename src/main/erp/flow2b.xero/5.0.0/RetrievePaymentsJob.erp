class RetrievePaymentsJob extends SystemJob with IXeroPaymentSaver {
  field service: XeroIntegration
  
  func do = {
    // Xero Payments api endpoint doesn't support paging
    let res <- service.https("Payments").get
    
    let rb = res.body!.parseJson[XeroPaymentsResponse]

    rb.payments.map { p =>
      savePayment(p)
    }
  }
}

class RetrievePaymentJob extends SystemJob with IXeroPaymentSaver {
  field service: XeroIntegration
  field paymentID: String
  
  func do = {
    let res <- service.https("Payments/$paymentID").get
    
    let rb = res.body!.parseJson[XeroPaymentsResponse]

    rb.payments.map { p =>
      savePayment(p)
    }
  }

}

class RetrieveInvoicePaymentsJob extends SystemJob {
  field service: XeroIntegration
  field invoiceID: String
  
  func do = {
    let res <- service.https("Invoices/$invoiceID").get
    
    let rb = res.body!.parseJson[XeroInvoicesResponse]

    rb.invoices.filter {
      i => service.findExtId(i.invoiceID, _.as[Invoice])
    }*.payments.map { p =>
      if(let pID = p.paymentID) RetrievePaymentJob(service = this.service, paymentID = pID).do
    }.allCompleted
  }
}

interface IXeroPaymentSaver {
  func service: XeroIntegration

  back func savePayment(xeroPayment: XeroPayment) = {
    if(let inv = service.findExtId(xeroPayment.invoice?.invoiceID, _.as[Order])){
      if(let acc = service.findExtId(xeroPayment.xeroAccount?.accountID, _.as[BankAccount])){
        let pay = service.findExtId(xeroPayment.paymentID, _.as[Payment])
        .or( inv.payments.find { p =>
          !p.extId(service) && 
          //"p.date == xeroPayment.date! &&" is bad solution because date in Xero better than our
          p.account == acc &&
          ((p.amount - xeroPayment.amount!).abs < 1) // for precision issues 
        })
        .alt(if(xeroPayment.paymentType == accRecPayment) { 
          IncomingPayment(
            source = inv,
            account = acc,
            amount = xeroPayment.amount!.as[Money[acc.currency]]!
          )
        } else {
          OutgoingPayment(
            source = inv, 
            account = acc,
            amount = xeroPayment.amount!.as[Money[acc.currency]]!
          )
        })

        pay.date = xeroPayment.date!
        pay.updateExtId(service, xeroPayment.paymentID)
        pay.save
      } else throw "Retreive XeroPayment error: There is no BankAccount with extId = ${xeroPayment.xeroAccount?.accountID}"
    }else throw "Retreive XeroPayment error: There is no Invoice with extId = ${xeroPayment.invoice?.invoiceID}"
  }
}