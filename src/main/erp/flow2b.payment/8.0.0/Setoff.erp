inline entity Setoff {
  field debit: Debt by credits {
    update = this.default(amount)
    from = Debt.all.filter(counterparty == credit.counterparty && currency == credit.currency && legalEntity == credit.legalEntity)
  }
  field credit: Debt by debits {
    update = this.default(amount)
    from = Debt.all.filter(counterparty == debit.counterparty && currency == debit.currency && legalEntity == debit.legalEntity)
  }
  func journal: Journal = if (debit.journalPriority > credit.journalPriority) debit else credit
  func currency = debit.currency
  field storedDate: Date?
  compute date: Date {
    value = {
      if (storedDate && !debit.definesDate && !credit.definesDate) storedDate!
      else if (debit.journalPriority > credit.journalPriority) debit.journalDate 
      else credit.journalDate 
    } 
    update = {
    	storedDate = new
	}
  }
  
  field amount: Money[currency] {
    default =? try? debit.balance.min(credit.balance)! 
  }
  
  register debitLedger: Ledger <- debit.originLedger(this, -amount)
  register creditLedger: Ledger <- credit.originLedger(this, amount)
  register trading: [Ledger]? <- CurrencyTrading.tradeAt(journal, date, [debitLedger.trAmount, creditLedger.trAmount])
  view form = group {
    edit {
      grid (labels = None) {
        row {debit credit amount}
        date >> field(visible = !debit.definesDate && !credit.definesDate)
      }
    }
    read {
      x {
        debit credit 
        amount >> field(visible = debit.debitAmount != amount || credit.creditAmount != amount) 
        date >> field(visible = storedDate && !debit.definesDate && !credit.definesDate)
      }
    }
  }
}

