entity IncomingPayment extends Payment {
  background = "#d68400"
  func itemRoles = [Accountant, BusAdmin, Salesperson]
  func legalEntity = payee
  func srcAccount: FinanceAccount? = null
  func dstAccount: FinanceAccount? = account
  compute srcAmount: Number? {
    value = 0
    update = {}
  }
  compute dstAmount: Number? {
    value = accountAmountEditor
    update = {accountAmountEditor =? new}
  }
  compute srcBankData: BankData? {
    value = null
    update = {}
  }
  compute dstBankData: BankData?{
    value = bankData
    update = {bankData =? new}
  }
  func creditAmount: Money[currency]? = amount
  
  func startingBalance: Money[currency]? = amount - incomes.mapOpt(try? _.amount).sum
  
  compute unreconciledAmount: Money[currency]? = balance
  func amountUpdated(): Void = {
    if (origins.count == 1) origins.first!.amount = amount 
  }
  func title = "$number $(date.date) $payer"
  func number: String = "IP" + n 
  func entityNumbers = [number]
  compute debtCounterparty: LegalEntity? {
    value = payer
    update = {
      if (let n = new) this.payer = n
    }
  }
  ref field payer: LegalEntity? = try? origin!.debtCounterparty!
  ref field payee: OurLegalEntity {
    available(Ref).with(accounts)
    default =? (try? origin!.legalEntity).or(OurLegalEntity.onlyOne)
    update = {
      this.account =? new.accounts.onlyOne
    }
  }
  func isTheir = payer?.isTheir
  func isIncoming: Bool = true
  event onSave = {
    debits*.credit*.touch
  }
  func additOrigins: [IAdditOrigin]? = incomes
  field incomes: [IPaymentIncome]? inline by payment
  compute origins: [IIPOrigin]? {
    value = debits.map(IPOrigin(setoff = _)) + credits.map(IPReturn(setoff = _)) + incomes
    init = {
      new.init(this)
    }
    update = {}
    remove = old.remove()
  }
  list compute recRefs: [RefEntity]? = credits*.credit + debits*.debit + incomes*.refs
  
  object view list = IncomingPayment.all >> nav{list{grid(labels = None, width = 1.2){
    row{number, date.as[Date[day]]!, payer, space, account, recRefs >> list(frame = false){_ >> field(title = title)}}
    row{stateView >> field(width = 0.8), amount >> field(width = 0.2), space(width = 0.2)}
  }}}
  
  view form = nav{grid{
      issuesView
      number
      date
      stateView >> field(label = "State")
      unreconciledAmount
      payer
      payee
      account
      amount >> field(canEditUnit = true)
      accountAmount
      origins
      bankData
    }
    generalLedgerButton
    button("Convert to Transfer", visible = !origin) {
      let to = account
      let income = amount
      let d = bankData
      let r = convertTo(CashTransfer)
      r.to = to
      r.dstBankData = d
      r.income = income
    }                
  }
  view stateView = x{reconcileState}
}

interface IIPOrigin {
  func init(payment: IncomingPayment): Void
  func remove(): Void
  var amount: Money?
}
class IPOrigin extends IIPOrigin {
  singular = "Origin"
  field setoff: Setoff inline = Setoff()
  func init(payment: IncomingPayment): Void = {
    setoff = Setoff(credit = payment)
  }
  func remove = setoff.delete
  view form = setoff.form.removeField(credit)
  compute amount: Money {
    value = setoff.amount
    update = {setoff.amount = new}
  }
}

class IPReturn extends IIPOrigin {
  singular = "Return"
  field setoff: Setoff inline = Setoff()
  func remove = setoff.delete
  func init(payment: IncomingPayment): Void = {
    setoff = Setoff(debit = payment)
  }
  view form = y{
    h3("Return")
    setoff.negativeForm.removeField(debit)
  }
  compute amount: Money {
    value = setoff.negativeAmount
    update = {setoff.negativeAmount = new}
  }
}


