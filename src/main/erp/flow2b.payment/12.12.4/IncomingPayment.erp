entity IncomingPayment extends Payment {
  singularShort = "⬆︎ In"
  background = "#d68400"
  func itemRoles = [Accountant, BusAdmin, Salesperson]
  func legalEntity = payee
  func srcAccount: FinAccount? = null
  func dstAccount: FinAccount? = account
  func debtAmount = if (!isInstant) -(amount - (try? reasons!)*.amount.sum).max(0)
  
  compute srcAmount: Number? {
    value = 0
    update = {}
  }
  compute dstAmount: Number? {
    value = accountAmountEditor
    update = {accountAmountEditor =? new}
  }
  compute srcBankData: BankData? {
    value = null
    update = {}
  }
  compute dstBankData: BankData?{
    value = bankData
    update = {bankData =? new}
  }
  func creditAmount: Money[currency]? = amount
  func debtCashFlows = if (let debtAmount) [CashFlowAmount(cashFlow = PurchaseReturns, amount = debtAmount)]
  func isPurchase = false
  func title = "$number $payer"
  view ref = x {title amount date}
  func number: String = "IP" + n 
  compute debtCounterparty: LegalEntity? {
    value = if (let source) source.debtCounterparty else payer
    update = {
      if (let n = new) this.payer = n
    }
  }
  func counterparty = debtCounterparty
  ref field payer: LegalEntity? {
    default = try? origin!.debtCounterparty!
  }
  field paymentMethod: PaymentMethod? {
    from = payer?.paymentMethods
    init = {
      new.legalEntity =? payer
      if (let bm = new.as[PMBankAccount]) {
        bankData.initMethod(bm)
      }
    }
  }
  ref field payee: OurLegalEntity {
    available(Ref).with(accounts)
    default =? (try? origin!.legalEntity).or(OurLegalEntity.onlyOne)
    update = {
      this.account =? new.accounts.onlyOne
    }
  }
  func isTheir = payer?.isTheir
  func isIncoming: Bool = true
  field reasons: [IPReason]? inline by payment
  
  event onSave = {
    if (let p = payer) {
      if (created || !origin(payer)) {
        bankData.addTo(p)
	    if (!paymentMethod) {
          paymentMethod = bankData.forcePaymentMethod(p, currency)
        }
      }
    }
  }
  
  object view list = IncomingPayment.all >> nav{list{grid(labels = None){
    row{number, date.as[Date[day]]!, payer, space, account}
    row{stateView >> field(width = 0.8), amount >> field(width = 0.2)}
    recRefs >> list(frame = false){_ >> field(title = title)}
  }}}
  
  view form = nav{grid{
      issuesView
      number
      refNum
      date
      stateView >> field(label = "State")
      unreconAmount.abs >> field(label = "Unreconciled", visible = !source && unreconAmount)
      payer >> field(visible = !source) 
      debtCounterparty >> field(visible = source, label = "Payer")
      paymentMethod >> field(visible = paymentMethod)
      payee
      account
      amount >> field(canEditUnit = true)
      accountAmount
      source 
      reasons 
      manLinks >> field(visible = !source)
      debtOriginsView >> field(label = "Origins")
      bankData
    }
    generalLedgerButton
    button("Convert to Transfer", visible = !origin) {
      toTransfer
    }                
  }
  view stateView = x{eReconState}
  view posViewDebit = x{account amount}
  view posViewCredit = x{account mAmount}
  func toTransfer: CashTransfer = {
    let to = account
    let income = amount
    let d = bankData
    let r = convertTo(CashTransfer)
    r.to = to
    r.dstBankData = d
    r.income = income
    r
  }
}

trait IPReason extends InlineEntity with IReason {
  field payment: IncomingPayment
}



