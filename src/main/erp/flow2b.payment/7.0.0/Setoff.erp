inline entity Setoff {
  field debit: DebtDebit by credits {
    update = updateAmount()
    from = DebtDebit.all.filter(counterparty == credit.counterparty && currency == credit.currency && legalEntity == credit.legalEntity)
  }
  field credit: DebtCredit by debits {
    update = updateAmount()
    from = DebtCredit.all.filter(counterparty == debit.counterparty && currency == debit.currency && legalEntity == debit.legalEntity)
  }
  func journal: Journal = if (debit.journalPriority > credit.journalPriority) debit else credit
  func currency = debit.currency
  field storedDate: Date?
  compute date: Date {
    value = {
      if (storedDate && !debit.definesDate && !credit.definesDate) storedDate!
      else if (debit.journalPriority > credit.journalPriority) debit.journalDate 
      else credit.journalDate 
    } 
    update = {
    	storedDate = new
	}
  }
  
  field amount: Money[currency]
  func updateAmount(): Void = {
    amount =? try? debit.debitUnpaid.min(credit.creditUnpaid)!
  }
  
  register debitLedger: Ledger <- debit.debitOriginLedger(this)
  register creditLedger: Ledger <- credit.creditOriginLedger(this)
  register trading: [Ledger]? <- CurrencyTrading.tradeAt(journal, date, [debitLedger.trAmount, creditLedger.trAmount])
  view form = group {
    edit {
      grid (labels = None) {
        row {debit credit amount}
        date >> field(visible = !debit.definesDate && !credit.definesDate)
      }
    }
    read {
      x {
        debit credit 
        amount >> field(visible = debit.debitAmount != amount || credit.creditAmount != amount) 
        date >> field(visible = storedDate && !debit.definesDate && !credit.definesDate)
      }
    }
  }
}

