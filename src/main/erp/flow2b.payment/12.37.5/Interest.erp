inline entity IncomingInterest extends IPReason {
  icon = "percent"
  singular = "Interest"
  field withholdingTax: UMoney[payment.currency]? 
  impl field amount: UMoney[payment.currency] {
    label = "Amount excl Tax"
    default =? payment.unreconAmount.abs
  }
  compute amountInclTax: UMoney[payment.currency] {
    label = "Amount incl Tax"
    value = ((try? amount) + withholdingTax)!
    update = {
      withholdingTax = (new - try? amount).positive
    }
  }
  impl func cashFlows = {
    [CashFlowAmount(cashFlow = InterestRevenue, amount = amountInclTax)] 
    + if (let w = withholdingTax) [CashFlowAmount(cashFlow = Taxes, amount = -w)]
  }
  impl func ledgers: Future[[Ledger]?] = {
    let baseAmount <- amountInclTax.to[Money[payment.legalEntity.getCurrency]](payment.date)
    let cp: Ledger = LCounterparty(account = Interest, journal = payment, counterparty = payment.payer, trAmount = -baseAmount)
    if (let wt = withholdingTax) {
      let bwt <- wt.to[Money[payment.legalEntity.getCurrency]](payment.date)
      let tax: Ledger = LTax(tax = WithholdingTax, trType = Accrual, currency = payment.legalEntity.getCurrency, 
         journal = payment, trAmount = bwt, taxedAmount = baseAmount)
      [cp, tax]
    } else Future([cp])
  }
  
  impl view form = grid(labels = TopRight) {
    h3("Interest")
    row{amountInclTax withholdingTax amount}
  }
}

inline entity OutgoingInterest extends OPReason {
  icon = "percent"
  singular = "Interest"
  impl field amount: UMoney[payment.currency] {
    default =? payment.unreconAmount.abs
  }
  impl func cashFlows = {
    [CashFlowAmount(cashFlow = InterestExpense, amount = -amount)] 
  }
  impl func ledgers: Future[[Ledger]?] = {
    let baseAmount <- amount.to[Money[payment.legalEntity.getCurrency]](payment.date)
    [LCounterparty(account = Interest, journal = payment, counterparty = payment.payee, trAmount = baseAmount)]
  }
  
  impl view form = grid (labels = None) {
    row{h3("Interest") space amount >> field(align = Right) }
  }
}