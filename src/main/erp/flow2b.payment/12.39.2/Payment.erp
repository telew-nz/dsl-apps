account UnreconciledCredits(template = "Currency", credit = acc(Error, 0)) 
account UnreconciledDebits(template = "Currency", credit = acc(Error, 1)) 
  
extend enum DashboardIssue {
  case UnreconPayment(role = Accountant, priority = critical) = "Unreconciled Payment"
  case MissingInBSU(role = Accountant, priority = major) = "Missing in the uploaded bank statement"
}

enum PaymentDirection {
  case Incoming
  case Outgoing
}


trait PaymentSource extends DebtOrigin {
  field payments: [Payment]? inline by source {
    deleteCascade = false
  }
  override func allManLinks: [DebtOrigin]? = payments
  impl event onDelete = {
    payments.filter(bankData.uploadState != Uploaded)*.delete
  }
}
 
trait Payment extends CashMovement with DebtOrigin with IReasonOwner {
  background = "#d68400"
  
  impl func isInstant = if (let source) source.isInstant else true
  func number: String
  func isIncoming: Bool
  impl var debtCounterparty: LegalEntity?
  
  impl func journal = this
  impl func noCpTitle = number
  override func allManLinks: [DebtOrigin]? = source.toSeq
  impl func hasSource: Bool = source
  field source: PaymentSource? {
    visible = source
  }
  field desc: Text? {label = "Description"}
  field feeTr: CashMovement? by fees {label = "Fee for transaction"}
  func movedAmount: Money[currency]
  impl func debtCurrency = currency
  impl func validateReasons = if (reasons*.amount.sum > amount) "Reasons amount should be â‰¤ payment amount"
  
  system stored compute sReconState: ReconState {
    label = "Reconciliation State"
  }
  ref compute eReconState: ReconState {
    label = "Reconciliation State"
    value = if (debtAmount) (try? sReconState).alt(reconState) else Recon
  }
  impl event onSave = {
    if (moneyCalced) {
      sReconState = reconState
    } else if (!try? eReconState) {
      sReconState = Unrecon
    }
    if (!created && origin(uploadState) == Uploaded && (account != origin(account) || date != origin(date) || accountAmount != origin(accountAmount) && currency != account.currency || amount != origin(amount) && currency == account.currency)) {
      throw "The uploaded payment $(this) cannot be modified"
    }
  }
  impl event onDelete = {
    if (uploadState == Uploaded && !User.isSu) {
      throw "The uploaded payment $(this) cannot be deleted"
    }
  }
  
  back func moneyCalced: Bool = {
    ledgers*.as[LMoneyDebt].all(calced)
  }
  
  impl func issues = {
    (if (moneyCalced && sReconState.isIssue) [DashboardIssue.UnreconPayment]) 
    +
    (if (uploadState == Missing || (uploadState == NotUploaded && (if (isIncoming) account.uploadedIncoming else account.uploadedOutgoing)?.containsIncl(date))) [DashboardIssue.MissingInBSU])
  }.toSet
  impl func fixes(issue: DashboardIssue) = {
    if (issue == UnreconPayment, let da = -this.debtAmount) { 
      let paymentDate = date
      let isCard = account.is[CardAccount]
      let possibleSources <- if (isCard) {
        LMoneyDebt.all.filter{o => 
             (o.amountDue == da || o.balanceDue == da)
           && o.origin.reconDates.exists(d => d.between(paymentDate - 2_day, paymentDate + 2_day))
          }
          .sortBy(o => ((o.amountDue + da).abs, (o.dueDate.alt(o.date) - paymentDate).abs))
         *.journal
          .result
      } else {
        LMoneyDebt.all.filter{o => 
       	   (o.amountDue.between(da - 1, da + 1) || o.balanceDue.between(da - 1, da + 1))
	     && o.origin.reconDates.exists(d => d.between(paymentDate - 90_day, paymentDate + 2_day))
        }
        .sortBy(o => ((o.amountDue + da).abs, (o.dueDate.alt(o.date) - paymentDate).abs))
       *.journal
        .result
      }
      let isUploaded = bankData.uploadState == Uploaded
      let payments: [Payment]? <- Payment.all
        .filter{p => 
              p.date.between(paymentDate - 30_day, paymentDate + 30_day) 
           && p.debtAmount.between(this.debtAmount - 1, this.debtAmount + 1)
           && p != this
           && (!isUploaded || p.bankData.uploadState != Uploaded)
        }
        .sortBy(o => (o.date - paymentDate).abs)
        .result
      [
        Fix(text = "Select the same $(cpName) as in another document", acts = possibleSources.map {s => Act(ref = s)}),
        Fix(text = "Merge duplicate payments", acts = payments.map {s => 
          Act(ref = s, label = "Merge", do = {
            let n <- back {
              let old = this
              let new = s
              new.date = old.date
              new.amount = old.amount
              new.bankData = old.bankData
              new.updateWith(old, onlyMissing = true)
              old.replace(new)
              new.saveMain
              Activity.comment("Merged with $(old.mention)")
              new
            }
            n.form.open
          })
        }),
        Fix(text = "Add a reason for the payment into the Origin field to match $amount")
      ]
    } else if (issue == MissingInBSU) {
      [Fix(text = "Usually the payment should be deleted")]
    }
  }
  
  func cpName: String
  func debtDueDate: Date? = null
  func origin = allManLinks.first
  ref field amount: UMoney[currency] {
    default =? try? origin?.unreconAmount!.abs
    label = "Paid Amount"
    validate = if (back) if (debtAmount && (debtAmount > 0) == isIncoming) "$amount is larger than reconciled amount $(reasons*.amount.sum)"
    update = {
      amountUpdated()
    }
  }
  compute mAmount: Money[currency] {
    label = "Paid Amount"
    value = -amount
    update = {
      amount = new.abs
    }
  }
  func amountUpdated(): Void = {
    if (reasons.count == 1 && !source && !manLinks) reasons.first!.amount = amount 
  }
  field bankData: BankData = BankData()
  impl back func debtLedgers = try? ledgers*.as[LMoneyDebt]!
 
  func paidAmount = amount
  
  object field gen: Int?
  
  impl ref field date: Date =? if (!updated(dslType)) now
  var paymentMethod: PaymentMethod? 
  impl ref field account: FinAccount {
    default =? BankAccount.default
    update = {
      if (!try? this.currency) this.currency = new.currency
    }
  }
  impl compute hasBankAccount: Bool = account.hasBankData
  impl ref field currency: Currency {
    default =? try? (origin?.debtCurrency).alt(account.currency)
    update = {
      if (currency != account.currency && !accountAmount) {
        if (let a = try? paidAmount) {
          accountAmount = a.as[UMoney[account.currency]]
        }
      }
    }
  }
  compute recRefs: [RefEntity]? {
    availableFor = List
    value = back{reasons*.refs + debtOrigins}
  }
  field accountAmount: UMoney[account.currency]? {
    visible = currency != account.currency
    validate = if (currency != account.currency && !accountAmount) "Account amount is required" 
  }   
  compute accountAmountEditor: UMoney[account.currency] {
    value = if (currency == account.currency) amount else accountAmount!
    update = {
      if (currency == account.currency) {
        amount = new
      } else {
        accountAmount = new
      }
    }
  }
  override func paymentCashFlows = {
    super.paymentCashFlows + reasons*.cashFlows
  }
  
  register ledgers: [Ledger]? <- back{
    if (!canceled) {
      let insts <- if (source && source!.isInstant && !manLinks) {
        source!.instantLedgers(this, date)
      }
      var ledgers: [Ledger]? = insts
      if (!ledgers) if (let b = debtAmount) {
        if (let cp = debtCounterparty) {
          ledgers = ledgers + LMoneyDebt(
                journal = this, date = date,
                legalEntity = this.legalEntity, trAmount = b, 
                counterparty = cp, currency = currency)
        } else {
          ledgers = ledgers + LCurrency(account = if (isIncoming) UnreconciledCredits else UnreconciledDebits, 
                    contraAccount = account.ledgerAccount, currency = currency, journal = this, 
                    trAmount = b)
        }
      } 
      let a = if (currency != account.currency) accountAmount else amount
      let cashLedger = LCash(
        journal = this, 
        account = account.ledgerAccount, legalEntity = account.legalEntity, 
        finAccount = account, trAmount = if (isIncoming) a else -a)
      let reasonLedgers <- if (let reasons) {
        let es <- reasons*.ledgers.allCompleted
        es.flatMap(_) + reasons*.gstLedger
      } 
      ledgers = ledgers + reasonLedgers
      procLedgers(cashLedger + ledgers!)
    }
  }
}


