entity OutgoingPayment extends Payment {
  singularShort = "⬇︎ Out"
  background = "#d68400"
  impl func legalEntity = payer
  impl func srcAccount: FinAccount? = account
  impl func dstAccount: FinAccount? = null
  impl func debtAmount = if (!isInstant) (amount - (try? reasons!)*.amount.sum).max(0)
  impl compute srcAmount: Number? {
    value = -accountAmountEditor
    update = {accountAmountEditor =? (-new).positive}
  }
  impl compute dstAmount: Number? {
    value = 0
    update = {}
  }
  impl compute dstBankData: BankData? {
    value = null
    update = {}
  }
  impl compute srcBankData: BankData?{
    value = bankData
    update = {bankData =? new}
  }
  impl func debtCashFlows = if (let debtAmount) [CashFlowAmount(cashFlow = SalesReturns, amount = debtAmount)]

    
  impl func title = "$number $payee"
  impl view ref = x {title amount date}
  impl func number: String = "OP" + n 
  field feeTr: CashMovement? by fees {label = "Fee for transaction"}
  impl compute debtCounterparty: LegalEntity? {
    value = if (let source) source.debtCounterparty else payee
    update = {
      if (let n = new) this.payee = n
    }
  }
  ref field payer: OurLegalEntity {
    available(Ref).with(accounts)
    default =? (try? origin!.legalEntity).or(OurLegalEntity.onlyOne)
    update = {
      this.account =? new.accounts.onlyOne
    }
  }
  
  impl func counterparty = debtCounterparty
  ref field payee: LegalEntity? {
    available(Ref).with(vendor, vendor.expense)
    default = try? origin!.debtCounterparty!
    update = {
      if (let e = new?.vendor?.expense) {
        if (!manLinks && !source) {
          PaymentExpense(payment = this, expense = e)
        }
      }
    }
    validate = {
      if (!payee && reasons*.as[PaymentExpense]) "Payee is requiered for expenses"
    }
  }
  field paymentMethod: PaymentMethod? {
    from = payee?.paymentMethods
    visible = payee
    init = {
      new.legalEntity =? payee
      if (let bm = new.as[PMBankAccount]) {
        bankData.initMethod(bm)
      }
    }
  }
  impl list field reasons: [OPReason]? inline by payment {
    visible = !source
    default = {
      if (let e = payee?.vendor?.expense) {
        if (!manLinks && !source) {
          [PaymentExpense(payment = this, expense = e)]
        }
      }
    }
    singular = "Expense"
  }
  
  func isTheir = payee?.isTheir
  impl func isIncoming: Bool = false

  impl event onSave = {
    if (let p = payee) {
      if (created || !origin(payee)) {
        bankData.addTo(p)
	    if (!paymentMethod) {
          paymentMethod = bankData.forcePaymentMethod(p, currency)
        }
      }
    }
  }
  impl object view list = OutgoingPayment.all >> nav{list{grid(labels = None){
    row{number, date.as[Date[day]], payee, account, space }
    row{stateView >> field(width = 0.6), -amount >> field(width = 0.2), space(width = 0.2)}
    recRefs >> list(frame = false){_ >> field(title = title)}
  }}}
 
  impl func isPurchase = true

  impl view form = nav{grid{
      issuesView
      number
      refNum
      date
      feeTr >> field(visible = feeTr)
      stateView >> field(label = "State")
      unreconAmount.abs >> field(label = "Unreconciled", visible = !source && unreconAmount)
      payer
      payee >> field(visible = !source)
      debtCounterparty >> field(visible = source, label = "Payee")
      paymentMethod >> field(visible = paymentMethod)
      account
      
      amount >> field(canEditUnit = true)
      accountAmount
      source
      reasons
      manLinks >> field(visible = !source)
      debtOriginsView >> field(label = "Origins")
      desc
      bankData 
      fees >> field(visible = fees)
    }
    button("Convert to Transfer", visible = !origin) {
      toTransfer
    }   
    generalLedgerButton
    extension("nav")
  }
  impl func toTransfer: CashTransfer = {
    let from = account
    let outcome = amount
    let d = bankData
    let date = this.date
    let r = convertTo(CashTransfer)
    r.dateIn = date
    r.dateOut = date
    r.srcBankData = d
    r.from = from
    r.outcome = outcome
    r
  }
  view stateView = x{eReconState uploadState}
  override view posViewDebit = x{account mAmount}
  override view posViewCredit = x{account amount}  
}

trait OPReason extends InlineEntity with IReason {
  impl field payment: OutgoingPayment
  
  impl func gstLedger: LTax? = null
}
