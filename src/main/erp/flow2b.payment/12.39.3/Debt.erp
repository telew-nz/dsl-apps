trait HasDebtCounterparty extends AnyEntity {
  func debtCounterparty: LegalEntity?
}

extend trait DebtOrigin extends HasDebtCounterparty {
  func canceled = false
  func hasSource: Bool = false
  func reconDates: [Date] = [journalDate] 
  back func instantLedgers(journal: Journal, date: Date): Future[[Ledger]?] = []
  
  func reconState: ReconState = {
    if (canceled) Canceled
    else if (!debtAmount || hasSource) {
      if (debtAllowance) Account 
      else Recon
    } else if (reconAmount) {
      if (unreconAmount) if (debtUnpaidAmount < 0 != debtAmount < 0) OverRecon else PartRecon 
      else if (debtAllowance) Account 
      else Recon
    } else {
      Unrecon
    }
  }
  
  back func procLedgers(ledgers: [Ledger]?): Future[[Ledger]?] = {
    if (let ledgers) {
      if (ledgers*.legalEntity.allTheSame) CurrencyTrading.with(ledgers)
      else {
        let lgs: [[Ledger]?]? <- ledgers*.legalEntity.distinct.map {le =>
          let debts: [Ledger]? = ledgers.filter(_.legalEntity != le)
          	.groupBy(legalEntity, currency).agg(trAmount.sum).toSeq
            .map{
               LInterDebts(journal = this, legalEntity = le,  
                 currency = _.currency, counterparty = _.legalEntity.legalEntity, trAmount = _.trAmount)
            }
          CurrencyTrading.with(ledgers.filter(_.legalEntity == le) + debts)
        }.allCompleted
        lgs.flatMap(_)
      }
    } else Future([]) 
  }
  
  compute debtOriginsAmountList = debtOriginAmounts.filter(o => o.origin?.mainOrigin != this.mainOrigin)
  
  view debtOriginsView = debtOriginsAmountList >> list (frame = false) {
  	x {
      origin >> field(title = fullTitle) 
      label("Account Debt Allowance", visible = !origin) 
      amount.abs >> field(visible = amount != debtAmount)
    }
  }
  func posViewDebit: Layout[DebtOrigin] = ref
  func posViewCredit: Layout[DebtOrigin] = ref
}

