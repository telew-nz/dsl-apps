account UnreconciledCredits(template = "Currency", credit = acc(Error, 0)) 
account UnreconciledDebits(template = "Currency", credit = acc(Error, 1)) 
  

extend enum DashboardIssue {
  case UnreconPayment(role = Accountant, priority = major) = "Unreconciled Payment"
}

trait IReason extends AnyEntity {
  func cashFlows: [CashFlowAmount]?
  func ledgers: Future[[Ledger]?]
  func gstLedger: Ledger? = null
  func refs: [RefEntity]? = []
  var amount: Money?
}

trait PaymentSource extends DebtOrigin {
  field payments: [Payment]? inline by source
  func allManLinks: [DebtOrigin]? = payments + manLinks
}

trait Payment extends CashMovement with DebtOrigin {
  background = "#d68400"
  
  func number: String
  func isIncoming: Bool
  func reasons: [IReason]?
  
  func allManLinks: [DebtOrigin]? = source + manLinks
  func hasSource: Bool = source
  field source: PaymentSource? {visible = source}
  field status: PaymentStatus = Executed
  field refNum: String? {label = "Reference Number"} 
  func entityNumbers = [number, refNum]
  func debtCurrency = currency
  func isInstant: Bool = source?.isInstant
  
  system stored compute sReconState: ReconState {
    label = "Reconciliation State"
  }
  ref compute eReconState: ReconState {
    label = "Reconciliation State"
    value = if (debtAmount) (try? sReconState).alt(reconState) else Recon
    update = {
      status = if (new == Canceled) PaymentStatus.Canceled else PaymentStatus.Executed
    }
    from = [reconState, Canceled]
  }
  event onSave = {
    if (moneyCalced) {
      sReconState = reconState
    } else if (!try? eReconState) {
      sReconState = Unrecon
    }
  }
  
  back func moneyCalced: Bool = {
    ledgers*.as[LMoneyDebt].all(calced)
  }
  
  func canceled = status == Canceled
  func issues = if (status != Canceled) {
    if (moneyCalced && sReconState.isIssue) [UnreconPayment] 
  }
  func fixes(issue: DashboardIssue) = {
    if (issue == UnreconPayment) {
      [Fix(text = "Add a reason for the payment into the Origin field to match $amount")]
    }
  }
  
  func journalDate = date
  func debtDueDate: Date? = null
  func origin = allManLinks.first
  ref field amount: Money[currency] {
    default =? try? origin?.unreconAmount!.abs
    label = "Paid Amount"
    validate = if (back) if (debtAmount && (debtAmount > 0) == isIncoming) "$amount is larger than reconciled amount $(reasons*.amount.sum)"
    update = {
      amountUpdated()
    }
  }
  compute mAmount: Money[currency] {
    label = "Paid Amount"
    value = -amount
    update = {
      amount = new.abs
    }
  }
  func amountUpdated(): Void = {
    if (reasons.count == 1 && !source && !manLinks) reasons.first!.amount = amount 
  }
  field bankData: BankData {
    visible = hasBankAccount
  }
  back func debtLedgers = ledgers*.as[LMoneyDebt]
 
  func paidAmount = amount
  
  object field gen: Int?
  const ref field n: Int? = increment(Payment.gen)
  
  ref field date: Date = now
  func counterparty: LegalEntity?
  ref field account: FinAccount {
    default =? BankAccount.default
    update = {
      if (!try? this.currency) this.currency = new.currency
    }
  }
  compute hasBankAccount: Bool = account.is[BankAccount]
  ref field currency: Currency {
    default =? try? (origin?.debtCurrency).alt(account.currency)
    update = {
      if (currency != account.currency && !accountAmount) {
        if (let a = try? paidAmount) {
          accountAmount = a.as[Money[account.currency]]
        }
      }
    }
  }
  compute recRefs: [RefEntity]? {
    availableFor = List
    value = back{reasons*.refs + debtOrigins}
  }
  field accountAmount: Money[account.currency]? {
    visible = currency != account.currency
  }   
  compute accountAmountEditor: Money[account.currency] {
    value = if (currency == account.currency) amount else accountAmount!
    update = {
      if (currency == account.currency) {
        amount = new
      } else {
        accountAmount = new
      }
    }
  }
  
  register ledgers: [Ledger]? <- back{
    if (!canceled) {
      let insts <- if (isInstant && !manLinks) {
        source!.instantLedgers(this, date)
      }
      var ledgers: [Ledger]? = insts
      if (!ledgers) if (let b = debtAmount) {
        if (let cp = counterparty) {
          ledgers = ledgers + LMoneyDebt(
                journal = this, date = date,
                legalEntity = this.legalEntity, trAmount = b, 
                counterparty = cp, currency = currency)
        } else {
          ledgers = ledgers + LCurrency(account = if (isIncoming) UnreconciledCredits else UnreconciledDebits, 
                    contraAccount = account.ledgerAccount, currency = currency, journal = this, 
                    trAmount = b)
        }
      } 
      let a = if (currency != account.currency) accountAmount else amount
      let cashLedger = LCash(
        journal = this, 
        account = account.ledgerAccount, legalEntity = account.legalEntity, 
        finAccount = account, trAmount = if (isIncoming) a else -a)
      let reasonLedgers <- if (let reasons) {
        let es <- reasons*.ledgers.allCompleted
        es.flatMap(_) + reasons*.gstLedger
      } 
      ledgers = ledgers + reasonLedgers
      procLedgers(cashLedger + ledgers!)
    }
  }
}


