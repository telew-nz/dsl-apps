enum GstRate(rate: UPercent[2]??) {
  case Exempt(rate = null) {background = "#999"}
  case Special(rate = null)
}

enum GstStatus {
  case Registered
  case NotRegistered
}

trait IVendor extends PseudoEntity {
  func legalEntity: LegalEntity
  func getCurrency: Currency
  func pricesIncludingGst: Bool = true
  ref field gstStatus: GstStatus = NotRegistered
  func hasGst: Bool = gstStatus == Registered
  func defaultGstRate: GstRate = Exempt
  view vendorView = grid(labels = Top) {
    gstStatus
  }
  func isTheir: Bool
}

extend trait OurLegalEntity extends IVendor {
  func isTheir: Bool = false
}
entity Vendor extends IVendor {
  func getCurrency: Currency = legalEntity.getCurrency
  ref field legalEntity: LegalEntity inline by vendor {deleteCascade = false}
  func mainEntity = legalEntity
  view form = grid(labels = Top) {
    extension("top")
    vendorView
    extension
  }
  func isTheir = legalEntity.isTheir
  report Report {
    view list = Vendor.all >> nav {list{grid(labels = None) {
      legalEntity.title
    }}}
  }
}
extend trait LegalEntity {
  func iVendor: IVendor? = our?.as[IVendor].or(vendor)
  func makeIVendor: IVendor = {
    iVendor.alt(Vendor(legalEntity = this))
  }
  func makeVendor: Vendor = {
    if (let o = our) throw "Cannot make vendor for our company"
    iVendor?.as[Vendor].alt(Vendor(legalEntity = this))
  }
  field vendor: Vendor? inline by legalEntity 
  compute isVendor: Bool {
    label = "Vendor"
    value = vendor
    update = {
      if (new) vendor = Vendor() else vendor = null
    }
  }
}
extend entity Company {
  extend view form = extend("tabs") {
    grid(labels = Top, label = "Vendor", visible = !our && (vendor || ui.edit)) {
      isVendor
      vendor!.form
    }
  }
}

extend entity Person {
  extend view form = extend("tabs") {
    grid(labels = Top, label = "Vendor", visible = !our && (vendor || ui.edit)) {
      isVendor
      vendor!.form
    }
  }
}

extend entity OurCompany {
  extend view form = vendorView
}

extend entity OurPerson {
  extend view form = vendorView
}