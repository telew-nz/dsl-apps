enum AccTemplate(analytic: Bool, hasCurrency: Bool) {
}

enum AccTemplateType {
  case singleAccount
  case basic
  case accountRouter
}

generator acctemplate {
  field type: AccTemplateType = basic
  
  compute fields = declarations*.as[Field].filter(name != "currency")
  compute keyFields = fields.filter(isKey)
  compute hasCurrency = declarations.exists(name == "currency" && _.is[Field])
  
  func accountField: Dsl = {
    if (type == singleAccount) 'func account: LedgerAccount = LedgerAccount.$name'
    else if (type == accountRouter) 'field account: LedgerAccount'
    else 'key field account: LedgerAccount'
  }
  func editorField(f: Field): String = {
    let upt = 'update = syncAnalit("$(f.name)", new)'
    let dsl = f.dsl.trim
    if (dsl.contains("}")) '$(dsl.dropSuffix("}")) $upt }'
    else '$dsl {$upt}'
  }
    
  func generate: Dsl = '
extend enum AccTemplate {
  case $name(analytic = $(keyFields.nonEmpty), hasCurrency = $hasCurrency)
}
  
register L$name extends Ledger {
  $accountField
  $declarations
  $(keyFields.map("func a$(name.capitalize) = $name").join(" "))
  view aview = grid(labels = Top, width = 0.5){ $(fields*.name.join(" "))}
  
  func mlBalance(journal: MJournal) = ML$name(this, journal = journal, isDebit = balance > 0, amount = balance
                                              $(if(hasCurrency) ", currency = currency" else "") )
} 

inline entity ML$name extends MLedger$(if (hasCurrency) "Currency" else "NoCurrency") {
  singular = "$(name.fromCamelCase)"
  register ledger: L$name? = if (needLedger) L$(name)(this, trAmount = amount) else null
  $(fields.map(f => editorField(f)).join(" "))
  $(declarations.filter(!_.is[Field]))
  view aview = grid(labels = Top, width = 0.5, visible = needLedger){ $(fields*.name.join(" "))}
}

$trialBalance

$incomeStatement
  
'
 func trialBalance = if (!keyFields) '' else
'report TB$name extends AnalitTrialBalance[L$name] {
  plural = "$(name.fromCamelCase) Trial Balance"
  func template: AccTemplate = AccTemplate.$name
    
  view list = query >> nav {
    table(action = openLedger(Ledger.Report(account = subAccount $(if (keyFields) ", " + keyFields.map("a${name.capitalize} = $name") else "")))) {
        $(keyFields*.name) debitBalance creditBalance
    }
  }
  func ledger = L$name
}'
  
 func incomeStatement = if (!keyFields) '' else
'report IS$name extends AnalitIncomeStatement[L$name] {
  plural = "$(name.fromCamelCase) Income Statement"
  func template: AccTemplate = AccTemplate.$name
  view list = query.groupBy($(keyFields*.name)).agg(amount = baseAmount.sum.abs) >> nav {
    table(action = openLedger(Ledger.Report(account = account $(if (keyFields) ", " + keyFields.map("a${name.capitalize} = $name") else "")))) {
       $(keyFields*.name) amount
    }
  }
  func ledger = L$name
}'
  
}

