inline entity EcwidCategoryId {
  field categoryId: Int?
  field service: IEcwidIntegration =? IEcwidIntegration.all.first
  field category: IEcwidCategory by ecwidCategoryIds
}

trait IEcwidCategory {
  system field ecwidCategoryIds: [EcwidCategoryId]? inline {views = []}
  
  func ecwidCategoryName: String = title
  func description: String?
  
  func products: [ISuperProduct]?
  func ecwidProducts = products*.as[IEcwidProduct]
  
  func getEcwidCategoryIdForService(service: IEcwidIntegration) = {
    if (!deleted) {
      var id = ecwidCategoryIds.find(c => c.service == service)
      if (!id) { // add if needed
        id = EcwidCategoryId(service = service)
        ecwidCategoryIds = ecwidCategoryIds + id
      }
      id
    } 
  }
  
  
  func makeRemoveEcwidServiceJob(serviceToRemove: IEcwidIntegration): SystemJob = {
    RemoveCategoryFromEcwid(category = this, service = serviceToRemove)
  }
  

  func makeUpdateEcwidServiceJob(serviceToUpdate: IEcwidIntegration): SystemJob? = {
    if(!deleted &&
       (!serviceToUpdate.categories ||
        serviceToUpdate.categories*.as[IEcwidCategory].contains(this))) {
      UpdateCategoryToEcwid(category = this, service = serviceToUpdate)
    } else {
      makeRemoveEcwidServiceJob(serviceToUpdate)
    }
  }
  
  
  func updateEcwid(): Void = {
    IEcwidIntegration.all.map{ s => 
      makeUpdateEcwidServiceJob(s)?.run()
    }
  }
  
  event onSave = {
  	updateEcwid()
  }
  view ecwidIdsView = ecwidCategoryIds >> list {grid{row{
    categoryId
    service
  }}}
}

extend entity ProductCategory extends IEcwidCategory {
  extend view form = ecwidIdsView
}
