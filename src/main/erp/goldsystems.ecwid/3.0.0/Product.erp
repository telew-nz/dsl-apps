/*
IEcwidProduct

Extend your product type (trait) with this trait to add ecwid integration.
IBasicProduct is extended by default

*/

inline entity EcwidId {
  field ecwidId: Int?
  field service: IEcwidIntegration =? IEcwidIntegration.all.first
  field product: IEcwidProduct by ecwidIds
}

enum EcwidOptionType {
  case Select    = "SELECT"
  case Radio     = "RADIO"
  case CheckBox  = "CHECKBOX"
  case TextField = "TEXTFIELD"
  case TextArea  = "TEXTAREA"
  case DateField = "DATE"
  case Files     = "FILES"
}
class EcwidOptionChoice {
  field text: String
  field priceModifier: Int?
  field priceModifierType: String?
}
class EcwidOption {
  field name: String
  field type: EcwidOptionType?
  field choices: [EcwidOptionChoice]?
  field defaultChoice: Int?
  field required: Bool?
}
  

trait IEcwidProduct extends IEcwidProductBase{
  
  field ecwidIds: [EcwidId]? inline
  
  func description: Text?
  
  func ecwidName: String = title
  
  func ecwidOptions: [EcwidOption]? = []
  
  func ecwidVariations = subProducts*.as[IEcwidProductVariation]
  
  func removeService(serviceToRemove: IEcwidIntegration): Void = {
    ecwidVariations*.removeService(serviceToRemove)
    
    let ecwidIdToRemove = ecwidIds.find(service == serviceToRemove)
    if(ecwidIdToRemove != null) {
      ecwidIds = ecwidIds.filter(service != serviceToRemove)
      this.save()
      RemoveProductFromEcwid(ecwidId = ecwidIdToRemove!.ecwidId, service = serviceToRemove).run()
    }
  }
  

  event onSave() = {
    updateEcwid()
  }
  
  func updateEcwidService(serviceToUpdate: IEcwidIntegration): Void = {
    if (!deleted() &&
        (!superProduct.obsolete || ecwidIds.find(service == serviceToUpdate)) &&
        (!serviceToUpdate.categories ||
         superProduct.categories.exists(c => serviceToUpdate.categories.contains(c)))) {
      if (!ecwidIds.find(service == serviceToUpdate)) { // add if needed
        ecwidIds = ecwidIds + EcwidId(service = serviceToUpdate)
        this.save()
      }
      UpdateProductToEcwid(product = this, service = serviceToUpdate).run()
  	} else {
      removeService(serviceToUpdate)
    }
  }
  
  func updateEcwid(): Void = {
    IEcwidIntegration.all.map{ s => updateEcwidService(s) }
  }
  
  view ecwidIdsView = ecwidIds >> list {grid{row{
    ecwidId
    service
  }}}
}


extend trait IBasicSubProduct extends IEcwidProduct

extend entity Kit {
  extend view form = ecwidIdsView
}
extend entity Good {
  extend view form = ecwidIdsView
}
extend entity Service {
  extend view form = ecwidIdsView
}