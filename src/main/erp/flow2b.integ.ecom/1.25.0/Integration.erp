extend enum DashboardIssue {
  case ECommerceEmptyPaymentMethod(role = Admin, priority = major)
  case ECommerceEmptyDeliveryMethod(role = Admin, priority = major)
}

trait ECommerceIntegration[ID] extends Entity with SystemService with ExtIdSource[ID] with ISalesOrderImportProvider[ID] with SalesOrderUpdater[ID] with ISalesChannel {
  impl ref field legalEntity: OurLegalEntity {
    default =? onlyOne
  }    
  field currency: Currency = Currency.our
      
  system const field initComplete: Bool
  
  field inclGst: Bool {
    label = "Prices including GST"
    default = (try? legalEntity)?.pricesIncludingGst
  }
  impl field stores: [IStore]?
  field categories: [IProductCategory]?
  field brands: [IBrand]?
  field paymentMethods: [ECommercePaymentMethod]? inline by service
  field deliveryMethods: [ECommerceDeliveryMethod]? inline by service
  field pricesAutoUpdate: PricesAutoUpdate? inline by service {
    label = "Auto Update Prices"
  }
  field stockLevel: SyncStockLevel = Available
  field orderTerms: IOrderTerms?
  impl back func orderTerms(order: ESalesOrder[ID]): IOrderTerms? = if (let dm = order.deliveryMethod){
    deliveryMethods.find(method == dm)?.orderTerms
  }
  impl func service = this
  
  impl func salesChannel = this
  impl func paymentMethodAccounts = paymentMethods.filter(account).map(method, account!).toMap
  impl func deliveryMethodProducts = deliveryMethods.filter(product).map(method, product!).toMap
     
  impl func roles: [Role]? = [Admin]
  override func issues: [DashboardIssue]? = {
    let diss: [DashboardIssue]? = super.issues
    diss + ((if (paymentMethods.exists(!account)) [DashboardIssue.ECommerceEmptyPaymentMethod]) + (if (deliveryMethods.exists(!product)) [DashboardIssue.ECommerceEmptyDeliveryMethod])).toSet
  }
  
  impl view form = nav{tabs {
    grid(labels = Top, label = "Main") {
      issuesView
      serviceState >> field(label = "")
      legalEntity
      orderTerms
      mainView >> field(label = "")
    }
    grid(labels = Top, label = "Products") {
      inclGst
      categories
      currency
      stockLevel >> field(compact = true)
      stores
      productViewEx >> field(label = "")
    }
    grid(labels = Top, label = "Payment") {
      paymentMethods
      paymentViewEx >> field(label = "")
    }
    grid(labels = Top, label = "Delivery") {
      deliveryMethods
      deliveryViewEx >> field(label = "")
    }
    grid(labels = Top, label = "Advanced") {
      pricesAutoUpdate
      advancedViewEx >> field(label = "")
    }
  }}
  
  
  view emptyView = group{}
    
  impl event onSave = {
    if (serviceState == Active && !deleted && (created || settingsUpdated)) {
      syncAll()
    }
  }
  back func settingsUpdated: Bool = {
    updated(stockLevel) || updated(serviceState) || 
      updated(stores) || updated(categories) || updated(legalEntity) || updated(currency)
  }
  back func createWebhooks(): Void = {
    createWebhookJobs*.enqueue
  }
  
  back func downloadAllCustomers(): Void = {
    downloadCustomersJobs*.enqueue
  }
  back func downloadPaymentMethods(): Void = {
    downloadPaymentMethodJobs*.enqueue
  }
  back func downloadDeliveryMethods(): Void = {
    downloadDeliveryMethodJobs*.enqueue
  }
  
  back func syncAll(): Future[Void] = {
    if (!initComplete) {
      if (let initialDownload) {
        initialDownload.run
        InitialDownloadCompleteJob(service = this).enqueue
      } else {
        initComplete = true
      }
    }
    downloadPaymentMethods()
    downloadDeliveryMethods()
    downloadAllCustomers()
    downloadNewOrders()
    
    uploadAllAttrs()
    uploadAllAttrTerms()
    uploadAllTaxonomies()
    let _ <- uploadAllProducts()
    createWebhooks()
  }
  
  back func updatePaymentMethod(method: String, update: ECommercePaymentMethod => Void): Void = {
    update(paymentMethods.find(m => m.method == method).alt(ECommercePaymentMethod(service = this, method = method)).save)
  }
  back func updateDeliveryMethod(method: String, update: ECommerceDeliveryMethod => Void): Void = {
    update(deliveryMethods.find(m => m.method == method).alt(ECommerceDeliveryMethod(service = this, method = method)).save)
  }
}

inline entity ECommercePaymentMethod {
  field service: ECommerceIntegration by paymentMethods
  field method: String {label = "Payment Method Code"}
  field name: String? 
  field account: FinAccount?
}

inline entity ECommerceDeliveryMethod {
  field service: ECommerceIntegration by deliveryMethods
  field method: String {label = "Delivery Method Code"}
  field name: String?
  field product: ISubProduct?
  field _orderTerms: IOrderTerms? {views = []}
  compute orderTerms: IOrderTerms? {
    value = _orderTerms.or(service.orderTerms)
    update = {
      _orderTerms = if (new != service.orderTerms) new
    }
  }
}

enum SyncStockLevel(get: Stock? => Qty?) {
  case Available(get = (s: Stock?) => s?.available) = "Available = Fact - Reserve"
  case Outlook(get = (s: Stock?) => s?.outlook) = "Outlook = Fact - Reserve + Expectation"
}
