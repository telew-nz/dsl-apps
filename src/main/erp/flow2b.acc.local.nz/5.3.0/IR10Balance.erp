inline entity IR10Y14Balance {
  system field ir: IncomeTaxReturn by ir10Balance
  
  field f30: Money[NZD]? {label = "30: Accounts receivable (debtors)"}
  field f31: Money[NZD]? {label = "31: Cash and deposits"}
  field f32: Money[NZD]? {label = "32: Other current assets"}
  
  field f33: Money[NZD]? {label = "33: Vehicles"}
  field f34: Money[NZD]? {label = "34: Plant and machinery"}
  field f35: Money[NZD]? {label = "35: Furniture and fittings"}
  field f36: Money[NZD]? {label = "36: Land"}
  field f37: Money[NZD]? {label = "37: Buildings"}
  field f38: Money[NZD]? {label = "38: Other fixed assets"}
  
  field f39: Money[NZD]? {label = "39: Intangibles"}
  field f40: Money[NZD]? {label = "40: Shares/ownership interests"}
  field f41: Money[NZD]? {label = "41: Term deposits"}
  field f42: Money[NZD]? {label = "42: Other non-current assets"}
  
  compute f43: Money[NZD]? {
    value = f30 + f31 + f32 + f33 + f34 + f35 + f36 + f37 + f38 + f39 + f40 + f41 + f42
    label = "43: Total assets"
  }
  
  field f44: Money[NZD]? {label = "44: Provisions"}
  field f45: Money[NZD]? {label = "45: Accounts payable (creditors)"}
  field f46: Money[NZD]? {label = "46: Current loans"}
  field f47: Money[NZD]? {label = "47: Other current liabilities"}
  compute f48: Money[NZD]? {
    value = f44 + f45 + f46 + f47
    label = "48: Total current liabilities"
  }
  field f49: Money[NZD]? {label = "49: Non-current liabilities"}
  compute f50: Money[NZD]? {
    value = f48 + f49
    label = "50: Total liabilities"
  }
  field f51: Money[NZD]? {label = "51: Ownersâ€™ equity"}
  system field unknownAssets: [ISuperProduct]?
  
  back func calc(): Future[Void] = {
    let le = ir.legalEntity
    let p = ir.period
    let other = ir.ir10Other
    
    let balances <- BalanceSheet(legalEntity = le, date = p.end).balances
    balances.foreach {i =>
      let t = i.account.subType
      let a = i.amount
      if (t.type == Asset) {
        if (t == Receivables) f30 += a
        else if (t == Cash) f31 += a
        else if (t == CurrentAssets) f32 += a
        else if (t == FixedAssets) {} 
        else if (t == Intangibles) f39 += a
        else if (t == Shares) f40 += a
        else if (t == TermDeposits) f41 += a
        else if (t == NonCurrentAssets) f42 += a
        else f32 += a
      } else if (t.type == Liability) {
        if (t == Provisions) f44 -= a
        else if (t == Payables) f45 -= a
        else if (t == LoansReceived) f46 -= a
        else if (t == NonCurrentLiability) f49 -= a
        else f47 -= a
      } else if (t.type == Equity) {
        f51 -= a
        if (t == CurrentAccounts) other.f58 -= a
      } else if (t.type == Error) {
      } else {
        throw "Unexpected account type in balance sheet $(t.type)"
      }
    }
    ILProduct.at(p.end)
      .filter(legalEntity == le && subAccount.subType == FixedAssets).result.get
      .foreach{i =>
        let t = i.subProduct!.superProduct.nzAsset
        let a = i.balance
        if (t == Vehicle) f33 += a
        else if (t == Plant) f34 += a
        else if (t == Furniture) f35 += a
        else if (t == Land) f36 += a
        else if (t == Building) f37 += a
        else if (t == Unknown) unknownAssets += i.subProduct!.superProduct
        else f38 += a
      }
  }
}
