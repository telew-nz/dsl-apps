object FinanceNotificator extends SystemService {
  impl func title = "Finance Notificator"

  field sendStm: Bool {
    default = false 
    label = "Generate and send statement of customer accounts"
  }
  field salesReminders: [DueReminder]? {
    label = "Reminders for customer about unpaid sales invoices"
    default = [DueReminder(delta = 5_day, repeat = 5_day)]
  }
  field purchaseReminders: [DueReminder]? {
    label = "Reminders for us about unpaid purchase invoices"
    default = [DueReminder(delta = -2_day, repeat = 5_day)]
  }
  func serviceAccess: [Role]? = [Admin]
  impl view form = nav{grid(labels = Top){
    issuesView
    serviceState
    errorsCount >> field(editMode = AlwaysRead)
    
    salesReminders
    purchaseReminders
    sendStm
    contacts
  }}
  field contacts: [IContact]? {
    validate = if (sendStm && !contacts) {
      "Contacts should be selected in order to send invoices"
    }
  }
  field sendRemindersToCustomers: Bool = true
  //func canSendInvoice = sendInvoiceToCustomers && userEnabled && serviceState != Disabled
  impl func ourContacts = contacts
  func nextReminder(isPurchase: Bool, dueDate: Date): Date? = {
    (if (isPurchase) purchaseReminders else salesReminders).mapOpt(next(dueDate)).min
  }
  impl event onSave = {
    if (updated(salesReminders) || updated(purchaseReminders) || salesReminders.exists(dataUpdated) || purchaseReminders.exists(dataUpdated)) {
      Invoice.all.filter(debtUnpaidAmount)*.touch
    }
  }
}

class DueReminder {
  field unit: DateUnit = day
  field delta: Duration[unit]? {canEditUnit}
  field repeatUnit: DateUnit = day
  field repeat: UDuration[repeatUnit]? {canEditUnit}
  
  impl view form = x{
    delta >> field (showEmpty) label("after due date", fontStyle = Label) 
    label ("repeat every", fontStyle = Label, visible = ui.edit || repeat) repeat
  }
  func next(dueDate: Date): Date? = {
    let dd = dueDate + delta
    if (dd > now) dd 
    else if (let repeat) {
      let n = (dd + repeat*((Date.now - dd).to[Duration[repeatUnit]].as[Duration[repeatUnit, 8]]/repeat).round(ceil, 0))
      if (n > now) n else n + repeat
    }
  }
}


trait AutoSend extends Entity {
  system field autoSend: Bool
  system field failedSend: Bool
  system field ignoreAutoSend: Bool
  
  override func issueTypes = super.issueTypes + ITAutoSendFailed
  func sendOut(): Void
  impl event onSave = {
    if (autoSend && !created && !ignoreAutoSend) {
      failedSend = !activities*.deliveries.exists(status != Failed)
    }
  }
}
object ITAutoSendFailed extends IssueType[AutoSend] {
  impl func title = "Failed to send out to the customer"
  impl func hasIssue(item: AutoSend) = !item.ignoreAutoSend && item.failedSend
  impl func fixes(item: AutoSend) = [Fix(
    text = "Correct the customer's email address and send a message", acts = [Act(
      label = "Send Again", do = item.sendOut()
    )]
  ), Fix (
    text = "Deliver the invoice by a different channel and ignore the issue", acts = [Act(
      label = "Ignore", do = {
        item.ignoreAutoSend = true
        UI.save(item)
      }
    )]
  )]
}


extend trait IOrder  {
  system field dueUpdateJob: OrderDueUpdateJob inline by order {
    default = OrderDueUpdateJob()
  }
}
inline entity OrderDueUpdateJob extends DeferredJob {
  field order: IOrder by dueUpdateJob
  impl func service = FinanceNotificator
  impl func jobDate: Date? = {    
    if (
      !order.closed && !order.canceled
      let ud = {
        if (let dd = order.dueDate where dd > now) {
          if (let ua = order.calcDue.update where ua > now) dd.min(ua) 
          else dd
        } else order.calcDue.update
      }
      where ud > now
    ) {
      ud
    }
  }
  impl func do = {
    order.updateDue(autoIssue = true)
    order.save
  }
}


extend entity Invoice extends DeferredJob {
  impl func service = FinanceNotificator
  impl func jobDate = {
    if (debtUnpaidAmount && !order.isInstant, let dueDate) FinanceNotificator.nextReminder(isPurchase, dueDate)
  }
  impl func do = {
    guard (!order.isInstant let dd = dueDate, let debtUnpaidAmount) else return {}
    
    touch
    save
    Activity.current!.setMainEntity(this)
    let t = if (let dueIn = (dd - Date.now).days.round(floor, 0)) {
      if (isPurchase) {
        if (dueIn > 0) "$name $number is due in $dueIn. $(debtUnpaidAmount.abs) unpaid."
        else "$name $number is $(-dueIn) overdue. $(debtUnpaidAmount.abs) unpaid."
      } else {
        if (dueIn > 0) "$name $number is due in $dueIn. $debtUnpaidAmount unpaid."
        else "$name $number is $(-dueIn) overdue. $debtUnpaidAmount unpaid."
      }
    } else {
      if (isPurchase) {
        "$name $number is due today. $(debtUnpaidAmount.abs) unpaid."
      } else {
      	"$name $number is due today. $debtUnpaidAmount unpaid."
      }
    }
    if (isPurchase || !FinanceNotificator.sendRemindersToCustomers) {
      Activity.current!.comment(t)
    } else {
      //payer?.invoiceContact?.join(this)
      let comment: Html = "<p>$t</p><p>$(mentionPublic)</p>"
      Activity.current!.makePublic(comment)
    }
  }
}