report DebtRecon {
  singular = "Debt Reconciliation"
  field legalEntity: OurLegalEntity =? onlyOne 
  field counterparty: LegalEntity {
    update = {
      currency = new.getCurrency
    }
  }
  field currency: Currency = (try? counterparty.getCurrency).alt(our)
  impl view list = 
    LMoneyDebt.all
      .filter{d => 
           d.counterparty == this.counterparty
        && d.legalEntity == this.legalEntity
        && d.currency == this.currency
      }
      .map{d =>
        (d.date, d.origin, d.sortNum, d.amountDue, d.balanceDue, d.dueDate, 
         d.origin.as[IOrderRevision]?.paymentState, d.origin.reconState,
         d.origin.as[IOrderRevision]?.overdueState,
         debtOriginAmounts = d.origin.debtOriginAmounts.filter(_.origin)
        )
      }
      .sortBy(sortNum.desc) 
    >>   nav{
    table(label = "Transactions") {
      header{
        label("Date", width = 0.27)  
        label("Document", width = 0.4)  
        label("Due Date", width = 0.18)  
        label("Amount", align = Right, width = 0.2) 
        label("Balance", align = Right, width = 0.2)
      }
      body {
        row{
          date >> field(rowSpan = 4)
          origin >> field(title = noCpTitle) 
          dueDate?.date
          amountDue >> field( label = "Amount Due")
          balanceDue >> field(label = "Balance", showEmpty, rowSpan = 4)
        }
        row {
          paymentState >> field(visible = paymentState != Paid)
          overdueState >> field(visible = overdueState == PaymentOverdue)
        }
        row {
          reconState >> field(visible = !origin.is[IOrderRevision] && reconState != Recon)
          space
        }
        row {
          panel(collapsed = true, span = 3, align = Left, label = "Reconcilation", frame = false, visible = debtOriginAmounts) {
            debtOriginAmounts >> table {
              date >> field(width = 0.27) 
              origin >> field(title = noCpTitle, width = 0.31) 
              x (margin = None, width = 0.2, align = Right, label = "Amount") {
                amount
                x (visible = origin && origin!.debtAmount != amount, flow = false) {
                  label("of", fontStyle = Label, stretch = 1) origin!.debtAmount
                }
              } 
            }
          }
        }
      }
    }}
}