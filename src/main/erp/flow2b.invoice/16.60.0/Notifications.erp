notify NewInvoice[Invoice](
  mainEntity = order
  title = "New $name"
  to = invoiceCpc?.legalEntity.toSeq
  contacts = invoiceCpc?.billingContact.toSeq
  preferred = [EMail]
  subject = "$legalEntity: New $name $number"
  embeddedComment = true
  text = {    
'<div style="margin-bottom: 2em;">
<h2 style="font-size: 150%; margin-bottom: 0;">$legalEntity</h2>
<h4 style="margin-top: 0;margin-bottom: 2em;font-weight: 400;"><a href="$billPublicUrl" style="text-decoration: none; color: black;">New $name</a></h4>
$(if (let comment) '<div style="margin-bottom: 2em">$comment</div>')
$htmlViewOnline
$htmlUnpaid
$htmlPayTo
</div>
'
  }
) { 
  trigger Trg(when = created && !isPurchase && (Date.now - date) < 7_day)
}

notify OverdueInvoice[Invoice](
  mainEntity = order
  title = "Payment Overdue"
  to = invoiceCpc?.legalEntity.toSeq
  contacts = invoiceCpc?.billingContact.toSeq
  preferred = [EMail]
  subject = "$legalEntity: Payment Overdue $overdueDays days: $name $number"
  embeddedComment = true
  text = {    
'<div style="margin-bottom: 2em;">
<h2 style="font-size: 150%; margin-bottom: 0;">$legalEntity</h2>
<h4 style="margin-top: 0;margin-bottom: 2em;font-weight: 400;"><a href="$billPublicUrl" style="text-decoration: none; color: black;">Payment Overdue $overdueDays days</a></h4>
$(if (let comment) '<div style="margin-bottom: 2em">$comment</div>')
$htmlViewOnline
$htmlUnpaid
$htmlPayTo
</div>
'
  }
) 
 
extend entity Invoice {
  back func billPublicUrl = publicUrl(invoiceCpc?.billingContact!, readOnly = true)
  back func htmlPayTo: Html? = {
    if(!isQuote && invoiceToPay) if(let paymentAccounts) {
'<ul style="list-style: none; padding: 0;">
   <label>Pay to Bank account:</label>
   $(paymentAccounts.map(acc => '<li style="font-family: monospace">$(acc.number)</li>'.as[Html]))
   <label style="margin-top:0.5em; display: block">Reference:</label>
   <li style="font-family: monospace">$(order.number)</li>
</ul>'
    }
  }
  back func htmlUnpaid: Html? = {
    if (!isQuote) {
      if (let invoiceToPay) {
'<p>
   <span style="font-size: 150%;font-family: monospace;font-weight: bold; ">$invoiceToPay</span>   
   $(if(let overdueDays) '<span style="color: #c00; font-size: 120%; font-weight: bold"> overdue $overdueDays days</span>' else '<span> to pay</span>')
   $(if(let dueDate where !overdueDays) '<span> by </span><span style="font-size: 120%">$(dueDate.format("EEEEE dd MMMMM yyyy"))</span>')
   
</p>'
      }
      else if (invoiceToRefund) '<p><span style="font-size: 150%;font-family: monospace;font-weight: bold;">$invoiceToPay</span><span> will be <b>refunded</b></span></p>'
      else '<p style = "font-size: 150%;font-weight: bold;">PAID</p>'
    } else if (let dueDate) {
      '<span>To accept by </span><span style="font-size: 120%">$(dueDate.format("EEEEE dd MMMMM yyyy"))</span>'
    }
  }
  back func htmlViewOnline: Html = {
'<div style="margin-bottom: 3em;">
  <a href="$billPublicUrl" style="color: white; background: $(if (isQuote) '#ad69a3' else if (overdueDays) '#d04747' else '#46abad'); padding: 0.7em 1em; text-decoration: none;">View$(if (overdueDays) " Overdue") $name $number Online &gt;</a>
</div>'
  }
  
  func overdueDays = if (let dueDate where invoiceToPay) (Date.now - dueDate).days.round(ceil, 0).positive.noUnit
}