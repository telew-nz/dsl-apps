trait IOrderItem extends InlineEntity {
  func order: IOrder 
  func currency: Currency
  func gstAmount: Money[currency]?
  func invoiceQty: Qty? = 0
  func invoiceAmount(noGst: Bool): Money[currency]? = if (noGst) invoiceAmountNoGst else invoiceAmountWithGst
  func invoiceAmountNoGst: Money[currency]?
  func invoiceAmountWithGst: Money[currency]?
  func ledgerAccount: LedgerAccount
  func gstRate: UPercent[2]?? 
  func itemDlv: IOrderDlv?
  func canceled: Bool
  func rowView: Layout[IOrderItem]
  func cashFlow: CashFlow 

  func canBeDeleted = true
  system field rollbacked: Bool
  func rollbackItem(): Void = {
    prev!.active = true
    prev!.save
    rollbacked = true
    delete
  }
  
  impl event onSave = {
    if (active && canceled) {
      setActive(false)
      wasCanceled = true
    } else if (wasCanceled && !active && !canceled) {
      setActive(true)
      wasCanceled = false
    }
  }
  impl event onDelete = {
    if (deleted && !rollbacked && origin(invoices).filter(!deleted)) {
      rollback
      setActive(false)
    }
  }
  
  func consider = active || wasCanceled 
  func invoiceNotIssued = !invoices || (!active && invoices.last == order.invoices.last)
  func rowView(revision: Invoice): Layout[IOrderItem] = {
    if (!revision.isNote) rowView
    else if (!active && !invoices.contains(revision)) rowView.deletedLayout
    else if (invoices.first == revision) {
      if (let p = this.prev) rowView.modifiedLayout(p)
      else rowView.createdLayout
    } else rowView
  }
  func setActive(v: Bool): Void = {
    active = v
  }

  system field prev: IOrderItem? by next {deleteCascade = false}
  system field next: IOrderItem? by prev {deleteCascade = false}
  system field invoices: [Invoice]? by items
  field active: Bool = true
  field wasCanceled: Bool = false
  func prev(revision: Invoice): this? = if (revision == invoices.first) prev else null
  
  func needsRevision: Bool
  func sameAsPrev: Bool
  compute itemRevisions: [this]? = (prev?.itemRevisions + prev)*.as[this]
}