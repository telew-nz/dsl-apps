trait IInvoice extends Debt with DebtOrigin {
  background = "#d68400"
  plural = "Invoices"
  singular = "Invoice"
     
  func number: String
  func paidAmount: Money[currency]?
  func setoffs: [Setoff]?
  func deliveryDate: Date?
  func vendor: LegalEntity
  func customer: LegalEntity
  func debtDueDate = dueDate
  func references: [String]?
  func isPurchase: Bool

  func overpaid = paidAmount > total
  func items: [IInvoiceItem]?
  func cancel(): Void
  //func deliveryDate: Date? = if (items.all(delivered)) items*.delivery*.deliveryDate.max

  compute gstAmount: Money[currency]? {
    value = items*.gstAmount.sum
  }
  compute totalWithoutGst: Money[currency]? {
    value = items*.invoiceAmount.sum
    visible = gstAmount
  }
  ref compute total: Money[currency]? = totalWithoutGst + gstAmount
  
  compute paidAmounts: [IInvoiceItem: Money[currency]?]? = {
    var a = paidAmount - items.filter(invoiceAmount < 0)*.invoiceAmount.sum
    items.filter(invoiceAmount > 0).map{i => 
      let v = a.min(i.invoiceAmount)
      a = (a - v)
      (i, v)
    }.toMap
  }
  func paidAmountForItem(item: IInvoiceItem): Money[currency]? = paidAmounts.apply(item)
  
  register gstLedgers: [LTax]? = {
    items.filter(gstRate).groupBy(gstRate!.value).agg(gstAmount.sum, invoiceAmount.sum).toSeq.map {
      LTax(account = TaxesPayable, tax = Gst, currency = this.currency, journal = this, rate = #0, 
           trAmount = if (isPurchase) gstAmount else -gstAmount, taxedAmount = invoiceAmount)
    }
  }
  
  field paymentTerms: IPaymentTerms?
  stored compute dueDate: Date? = {
    if (paidAmount != total) paymentTerms?.paymentDueDate(issueDate, deliveryDate)
  }
  ref stored compute overdueState: PaymentOverdueState {
    value = {
      if (paidAmount == total) Paid
      else if (let dd = dueDate) {
        if (dd < now) PaymentOverdue else NoPaymentOverdue
      }
      else NoPaymentOverdue
    }
    visible = overdueState == PaymentOverdue
  }
  func canDispatch: Future[Bool] = {
    (if (let terms = paymentTerms, let t = total) terms.canDispatch(customer, t, paidAmount)
    else Future(true))
  } 
  compute prepaymentDate: Date? = {
    if (let prepayment = paymentTerms?.deliveryPrepayment, let a = total*prepayment) {
      setoffs.accum(amount.sum).find(amount >= a).map(#0.date)
    } else date
  } 
  func issueDate: Date = date
  
  report Report {
    field bookkeeper: Person? {
      from = Person.all.filter(is(Bookkeeper))
    }
    field state: [PaymentState]?
    field from: Date?
    field to: Date?
    
    view list = IInvoice.all.filter(
      journalDate >=? from && journalDate <=? to 
      && (!this.state || this.state.contains(reconcilationState.paymentState))
      && isFor(bookkeeper, Bookkeeper)
    ).sortBy(journalDate.desc) >> nav{list(canAdd = false){}}
  }
}
