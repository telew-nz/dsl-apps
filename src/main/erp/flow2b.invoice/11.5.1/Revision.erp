trait IInvoiceRevision extends Journal {
  func currency: Currency
  func date: Date
  func items: [IInvoiceItem]?
  compute gstAmount: Money[currency]? {
    value = items*.gstAmount.sum
  }
  compute totalWithoutGst: Money[currency]? {
    value = items*.invoiceAmount.sum
    visible = gstAmount
  }
  ref compute total: Money[currency]? = totalWithoutGst + gstAmount
  func invoice: IInvoice
  func number: String
  func prev: InvoiceRevision?
  func isNote = false
  func moneyCalced: Bool = {
    (if (total != prev?.total) moneyLedger?.calced else true) && (if (let prev) prev.moneyCalced else true)
  }
  
  register gstLedgers: [LTax]?
  register moneyLedger: LDebt?
  register deliveryLedgers: [LDebt]? 
  func updateLedgers(): Void = {
    gstLedgers = if (!invoice.isInstant) {
      gstLedgers(this, date)
    }
    moneyLedger = if (!invoice.isInstant) {
      let pt = prev?.total
      if (let t = total - pt, let cp = invoice.debtCounterparty) {
        let setoffD = (invoice.balanceChange.abs - pt).clamp(0, total)
        LDebt(account = if (invoice.isPurchase) AccountsPayable else AccountsReceivable, 
              origin = invoice, journal = this, 
              trAmount = if (invoice.isPurchase) -t else t, counterparty = cp, 
              currency = currency, manLink = invoice.debits || invoice.credits)
      }
    }
    deliveryLedgers = if (!invoice.isInstant) {
      if (let cp = invoice.orderCounterparty) {
        deliveryMap.fullJoin(prev?.deliveryMap, left.alt(0) - right.alt(0)).toSeq
          .mapOpt{
            LDebt(account = ledgerAccount, origin = _.delivery?.as[Journal].alt(invoice), journal = this, 
                  trAmount = if (invoice.isPurchase) #2 else -(#2), 
                  counterparty = cp, currency = currency, manLink = true)
          }
      }
    }
  }
  
  func gstMap = if (invoice.legalEntity.hasGst) {
    items
      .groupBy(rate = gstRate)
      .agg(gstAmount = gstAmount.sum, invoiceAmount = invoiceAmount.sum)
  }
  func gstLedgers(journal: Journal, date: Date): [LTax]? = {
    gstMap.fullJoin(prev?.gstMap, (
      gstAmount = left?.gstAmount - right?.gstAmount, 
      invoiceAmount = left?.invoiceAmount - right?.invoiceAmount
    )).toSeq.filter(gstAmount || invoiceAmount).map {
      LTax(account = TaxesPayable, tax = Gst, currency = this.currency, journal = journal, date = date, 
           origin = invoice, rate = #0,
           isAdjustment = isNote,
           trAmount = if (invoice.isPurchase) gstAmount else -gstAmount, 
           taxedAmount = if (invoice.isPurchase) invoiceAmount else -invoiceAmount,
           isInstantPayment = invoice.isInstant
          )
    }
  }
  func deliveryMap = {
    items
    	.groupBy(delivery, ledgerAccount)
        .agg{
           if (this.invoice.legalEntity.hasGst) _*.invoiceAmount.sum else _.map(invoiceAmount + gstAmount).sum
        }
  }
  
  func cashFlows(amount: Money): [CashFlowAmount]? = {
    if (let ams = items.groupBy(cashFlow).agg(invoiceAmount.sum).toSeq) {
      ams.distribute[Money](amount, by = invoiceAmount)
        .filter(value)
        .map(i => CashFlowAmount(category = i.cashFlow, amount = i.value!))
    } else {
      [CashFlowAmount(category = invoice.cashFlow, amount = amount)]
    }
  }
}


enum InvoiceIssueState {
  case InvoiceNotIssued {background = "#ed9"}
  case InvoiceIssued {color = "white" background = "#090"}
  case InvoiceModified {background = "#f99"}
}

entity InvoiceRevision extends IInvoiceRevision {
  background = "#d68400"
  field date: Date 
  func journalDate = date
  field invoice: IInvoice by revisions {
    available(Inline).for(All)
  }
  field currency: Currency
  field prev: InvoiceRevision? {
    available(Inline).for(Inline)
    deleteCascade = false
  }
  compute diffTotal {
    value = total - prev?.total
  }
  compute diffGstAmount = gstAmount - prev?.gstAmount
  compute diffTotalWithoutGst = totalWithoutGst - prev?.totalWithoutGst
  func legalEntity = invoice.legalEntity
  func deletedItems = items.filter(i => !i.active && !i.next && i.revisions.last == this)
  field items: [IInvoiceItem]? inline by revisions {deleteCascade = false}
  ref compute n: Int? = if (let p = prev) p.n + 1 else 0
  ref compute number = {
    if (n) "$(invoice.number).R$n" else invoice.number
  }
  ref compute isNote: Bool = prev
  func isDebit: Bool = diffTotal > 0
  func title = number
  func sign = if (isDebit) "+" else "âˆ’"
  
  event onSave = {
    invoice.touch
    if (registerUpdated) {
      updateLedgers()
    }
  }
  compute logo: Image? = vendor.as[Company]?.logo 
  compute payer {
    value = invoice.payer!.legalEntity
    available(All).for(All)
    availableFor = All
  }
  compute vendor {
    value = invoice.vendor!.legalEntity
    available(All).for(All)
    availableFor = All
  }
  view form {
    printable = true
    layout = nav{grid (labels = None, width = 1.2) {
      row {
        h1(width = 0.75, if (isNote) (if (isDebit) "Debit Note" else "Credit Note") else "Tax Invoice")
        y (width = 0.45, margin = Bottom) {
          logo >> field(height = 4, margin = None)
          vendor.legalName >> field(fontStyle = H1, margin = None, visible = !logo)
        }
      }
      
      row {
        grid (width = 0.35, labels = Top, labelsWidth = 0.15, margin = Bottom, spacing = false) {
          invoice >> field(label = "Invoice Number", title = number)
          number >> field(visible = this.isNote, label = "Revision Number")
          date
          invoice.dueDate >> field(label = "Payment Due")
        }
        payer >> grid (width = 0.4, labels = Top, margin = Bottom, spacing = false) {
          legalName >> field(label = "Customer")
          legalInfo.gstView
          invoice.references >> field(label = "Reference")
        }
        vendor >> grid(width = 0.45, labels = Top, margin = Bottom, spacing = false) {
          legalInfo.gstView
          contacts*.as[PhoneContact]*.phone.join(", ") >> field(label = "Phone")
          contacts*.as[EmailContact]*.email.join(", ") >> field(label = "EMail")
        }
      }
      
      (items + prev?.deletedItems).groupBy(deliveryGroup).toSeq >> subtotal {
        deliveryGroup.rowView
        table {
          header{label("Product", width = 0.55) 
                 label("Quantity", align = Right, width = 0.2) 
                 label("Price", align = Right, width = 0.2) 
                 label("Amount", align = Right, width = 0.25)}
          body{row{rowView(this)}}
          //row{product qty price amount}
          //row(visible = discount){label("Discount") space space discount}
        }
      }
      
      row {
        space(width = 0.55)
        y (width = 0.65, margin = Right) {
          grid (labels = TopRight, width = 0.65, spacing = false, visible = prev) {
            row {
              label("Total incl GST", fontStyle = Label, width = 0.2)
              total >> field(showEmpty = true, label = "New", width = 0.15, fontStyle = Bold) 
              prev!.total >> field(showEmpty = true, label = "Previous", width = 0.15)  
              diffTotal >> field(showEmpty = true, showPlus = true, label = "This Note", width = 0.15)  
            }
            row (labels = false) {
              label("GST", fontStyle = Label, width = 0.2)
              gstAmount >> field(showEmpty = true, width = 0.15) 
              prev!.gstAmount >> field(showEmpty = true, width = 0.15)  
              diffGstAmount >> field(showEmpty = true, showPlus = true, width = 0.15)  

            }
            row (labels = false) {
              label("Total excl GST", fontStyle = Label, width = 0.2)
              totalWithoutGst >> field(showEmpty = true, width = 0.15) 
              prev!.totalWithoutGst >> field(showEmpty = true, width = 0.15)  
              diffTotalWithoutGst >> field(showEmpty = true, showPlus = true, width = 0.15)  
            }
          }
          grid (labels = TopRight, width = 0.65, spacing = false, visible = prev) {
            row {
              label("Payment", fontStyle = Label, width = 0.2, align = Right)
              invoice.paidAmount >> field(showEmpty = true, label = "Balance", width = 0.15) 
              invoice.receivedAmount >> field(showEmpty = true, label = "Paid", width = 0.15)  
              -invoice.refundedAmount >> field(showEmpty = true, showPlus = true, label = "Refund", width = 0.15)  

            }
            row (labels = false) {
              label("Amount to Pay", fontStyle = Label, width = 0.2)
              invoice.unpaidAmount >> field(showEmpty = true, width = 0.15, fontStyle = Bold) 
              space(width = 0.3)
            }
          }
          grid (labels = TopRight, visible = !prev, width = 0.65, spacing = false) {
            row (labels = false) {
              label("Total incl GST", fontStyle = Label, width = 0.4)
              total >> field(showEmpty = true, width = 0.25, fontStyle = Bold) 
            }
            row (labels = false) {
              label("GST", fontStyle = Label, width = 0.4)
              gstAmount >> field(showEmpty = true, width = 0.25) 
            }
            row (labels = false) {
              label("Total excl GST", fontStyle = Label, width = 0.4)
              totalWithoutGst >> field(showEmpty = true, width = 0.25) 
            }
            row (labels = false) {
              label("Payment", fontStyle = Label, width = 0.4)
              invoice.paidAmount >> field(showEmpty = true, label = "Balance", width = 0.25) 
            }
            row (labels = false) {
              label("Amount to Pay", fontStyle = Label, width = 0.4)
              invoice.unpaidAmount >> field(showEmpty = true, width = 0.25, fontStyle = Bold)
            }
          }
        }
        
        
      }
    }
    generalLedgerButton
    }
  }
}