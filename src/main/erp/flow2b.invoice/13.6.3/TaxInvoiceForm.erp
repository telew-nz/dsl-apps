extend entity InvoiceRevision {
  view form {
    printable = true
    layout = nav{grid (labels = None, width = 1.2) {
      y (margin = Around, visible = !isLastRevision){
        y(background = "#fff9e4", border = Around) {
          x(margin = Around){
            label ("Not the last revision" color = "#ff6200")
            button("Open last") {
              invoice.revisions.last!.form.open()
            }
          }
        }
      }
      row {
        h1(width = 0.75, label = name)
        y (width = 0.45, margin = Bottom) {
          logo >> field(height = 4, margin = None)
          vendor.legalName >> field(fontStyle = H1, margin = None, visible = !logo)
        }
      }
      
      row {
        grid (width = 0.35, labels = Top, labelsWidth = 0.15, margin = Bottom, spacing = false) {
          invoice >> field(label = "Invoice", title = number)
          invoice.revisions >> list(label = "Revisions", frame = false) {
            y (margin = None){
              x(visible = _ != this, margin = None) {_ >> field(title = number) date}
              x(visible = _ == this, margin = None) {number >> field(fontStyle = Bold)  date}
            }
          }
        }
        payer >> grid (width = 0.4, labels = Top, margin = Bottom, spacing = false) {
          legalName >> field(label = "Customer")
          legalInfo.gstView >> field(frame = false)
          invoice.references >> field(label = "Reference")
        }
        vendor >> grid(width = 0.45, labels = Top, margin = Bottom, spacing = false) {
          legalName >> field(visible = logo, label = "Vendor")
          legalInfo.gstView  >> field(frame = false)
          contacts*.as[PhoneContact]*.phone.join(", ") >> field(label = "Phone")
          contacts*.as[EmailContact]*.email.join(", ") >> field(label = "EMail")
        }
      }
      
      (items + prev?.deletedItems).groupBy(deliveryGroup).toSeq >> subtotal {
        deliveryGroup.rowView
        table {
          header{label("Product", width = 0.55) 
                 label("Quantity", align = Right, width = 0.2) 
                 label("Price", align = Right, width = 0.2) 
                 label("Amount", align = Right, width = 0.25)}
          body{row{rowView(this)}}
          //row{product qty price amount}
          //row(visible = discount){label("Discount") space space discount}
        }
      }
      
      row {
        y (width = 0.59) {
          grid (labels = None, width = 0.59, spacing = false) {
            row {
              label("Amount Due", fontStyle = Label, width = 0.19, visible = invoice.revUnpaidAmount)
              invoice.revisions.filter(unreconAmount) >> list(width = 0.4) {
                x{
                  unreconAmount >> field(fontStyle = Bold) 
                  dueDate
                }
              }
            }
            row {
              label("Bank Accounts", fontStyle = Label, width = 0.19, visible = paymentAccounts && invoice.revUnpaidAmount)
              paymentAccounts >> list(width = 0.4, visible = invoice.revUnpaidAmount, margin = None){
                number 
              }
            }
            row {
              label("Paid", fontStyle = Label, width = 0.19, visible = invoice.paidAmount)
              invoice.paidAmount >> field(width = 0.4) 
            }
            row {
              label("Received", fontStyle = Label, width = 0.19, visible = invoice.refundedAmount)
              invoice.receivedAmount >> field(width = 0.4, showPlus = true, visible = invoice.refundedAmount) 
            }
            row {
              label("Refund", fontStyle = Label, width = 0.19, visible = invoice.refundedAmount)
              -invoice.refundedAmount >> field(width = 0.4) 
            }
          }
        }
        
        y (width = 0.61, margin = Right) {
          grid (labels = TopRight, visible = !prev, width = 0.61, spacing = false) {
            row(labels = false) {
              label("Total incl GST", fontStyle = Label, width = 0.36)
              total >> field(showEmpty = true, width = 0.25, fontStyle = Bold) 
            }
            row(labels = false) {
              label("GST", fontStyle = Label, width = 0.36)
              gstAmount >> field(showEmpty = true, width = 0.25) 
            }
            row(labels = false) {
              label("Total excl GST", fontStyle = Label, width = 0.36)
              totalWithoutGst >> field(showEmpty = true, width = 0.25) 
            }
          }

          
          grid (labels = TopRight, width = 0.61, spacing = false, visible = prev) {
            row {
              label("Total incl GST", fontStyle = Label, width = 0.16)
              total >> field(showEmpty = true, label = "New", width = 0.15, fontStyle = Bold) 
              prev!.total >> field(showEmpty = true, label = "Previous", width = 0.15)  
              diffTotal >> field(showEmpty = true, showPlus = true, label = "This Note", width = 0.15)  
            }
            row (labels = false) {
              label("GST", fontStyle = Label, width = 0.16)
              gstAmount >> field(showEmpty = true, width = 0.15) 
              prev!.gstAmount >> field(showEmpty = true, width = 0.15)  
              diffGstAmount >> field(showEmpty = true, showPlus = true, width = 0.15)  

            }
            row (labels = false) {
              label("Total excl GST", fontStyle = Label, width = 0.16)
              totalWithoutGst >> field(showEmpty = true, width = 0.15) 
              prev!.totalWithoutGst >> field(showEmpty = true, width = 0.15)  
              diffTotalWithoutGst >> field(showEmpty = true, showPlus = true, width = 0.15)  
            }
          }
        }
      }

    }
    generalLedgerButton
    }
  }
}