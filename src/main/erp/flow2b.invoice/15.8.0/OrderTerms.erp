interface IIOrderTerms {
  func due(order: IOrder): Due
  func orderState(order: IOrder): OrderState
  func canDispatch(state: OrderState) = state != Quote

  func dispatchDate(order: IOrder): Date? = null
  
  func hasAcceptance: Bool = false
  func reserveQuote: Bool = false
  func toDispatchState: OrderState = ToDispatch
  func updatableStates(order: IOrder): [OrderState]
}

trait IOrderTerms extends RefEntity with IIOrderTerms {
  event onSave = {
    IOrder.all.filter(terms == this)*.touch
  }
}

extend entity Customer {
  field _orderTerms: IOrderTerms? {views = []}
  compute orderTerms: IOrderTerms? {
    value = _orderTerms.or(Workspace.current.salesOrderTerms)
    update = {
      _orderTerms = if (new != Workspace.current.salesOrderTerms) new
    }
  }
}
extend entity Vendor {
  field _orderTerms: IOrderTerms? {views = []}
  compute orderTerms: IOrderTerms? {
    value = _orderTerms.or(Workspace.current.purchaseOrderTerms)
    update = {
      _orderTerms = if (new != Workspace.current.purchaseOrderTerms) new
    }
  }
}
extend trait Workspace {
  field salesOrderTerms: IOrderTerms? {label = "Default sales order terms" views = []}
  field purchaseOrderTerms: IOrderTerms? {label = "Default purchase order terms"views = []}
  extend view form = extend("main") {
    salesOrderTerms
    purchaseOrderTerms
  }
}

class Due {
  field amount: Money?
  field date: Date?
}

enum PaymentTrigger(state: OrderState, date: IOrder => Date?) {
  case Dispatched(state = Dispatched, date = _.dispatchDate){background = "#dea", label = "Order Dispatched"}
  case Delivered(state = Delivered, date = _.deliveryDate){background = "#dea", label = "Order Delivered"}
}

object DefaultOrderTerms extends IIOrderTerms {
  func orderState(order: IOrder): OrderState = {
    if (order.deliveryComplete && order.paid) Complete
    else if (let ds = order.orderStateDelivery) ds
    else if (order.paid) Prepaid
    else if (order.accepted) Accepted
    else Quote
  }
  func due(order: IOrder): Due = Due(amount = order.total)
  func hasAcceptance: Bool = true
  func reserveQuote: Bool = true
  
  func updatableStates(order: IOrder): [OrderState] = {
    let s = order.calcState
    if (s == Canceled) [OrderState.Canceled]
    else {
      order.deliveryStates + (if (order.paid) OrderState.Prepaid else OrderState.Quote) + OrderState.Canceled + OrderState.Accepted
    }
  }
}

entity OrderTerms extends IOrderTerms {
  field title: String
  ref field reserveQuote: Bool {
    visible = acceptance || prepayment
  }
  ref field acceptance: OTAcceptance?
  ref field prepayment: OTPrepayment?
  ref field delivery: OTDelivery? {
    label = "Delivery Terms"
  }
  ref field postdelivery: OTPostdelivery? {
    validate = if ((prepayment?.percent.withoutUnit == 1) != postdelivery.isEmpty) {
      if (!postdelivery) "Postdelivery is required unless it's full prepayment - 100%"
      else "Postdelivery should be empty for full prepayment"
    }
  }
  func dispatchDate(order: IOrder): Date? = if (let delivery){
    Date.now + delivery.dueIn
  }
  func canDispatch(state: OrderState) = state != Quote && (!prepayment || state != Accepted)
  
  
  view form = nav{grid{
    title
    reserveQuote
    acceptance
    prepayment
    delivery
    postdelivery
  }}
  
  func due(order: IOrder): Due = {
    if (let ds = order.orderStateDelivery) {
      if (let post = postdelivery) {
        Due(amount = order.total, date = post.dueDate(post.trigger.date(order)!))
      } else {
        Due(amount = order.total, date = order.date + prepayment!.dueIn)
      }
    } else if (acceptance && !order.accepted) {
      acceptance!.due(order)
    } else if (let pre = prepayment) {
      pre.due(order, if (acceptance) order.accepted! else order.date)
    } else {
      Due()
    }
  }
  func orderState(order: IOrder): OrderState = {
    if (order.deliveryComplete && order.paid) Complete
    else if (let ds = order.orderStateDelivery) ds
    else if (let acceptance) {
      if (!order.accepted) acceptance.orderState(order)
      else if (let pre = prepayment) {
        pre.orderState(order, Accepted)
      } else Accepted
    } else if (let pre = prepayment) {
      if (order.isEmpty) Quote
      else {
        pre.orderState(order, Quote)
      }
    } else ToDispatch
  }
  func hasAcceptance: Bool = acceptance
  
  func toDispatchState: OrderState = {
    if (prepayment) Prepaid
    else if (acceptance) Accepted
    else ToDispatch
  }
  
  func updatableStates(order: IOrder): [OrderState] = {
    let s = order.calcState
    if (s == Canceled) [OrderState.Canceled]
    else if (s == Quote) {
      if (toDispatchState == Accepted) {
        order.deliveryStates + OrderState.Quote + OrderState.Canceled + OrderState.Accepted
      } else if (hasAcceptance) {
        [OrderState.Quote, OrderState.Canceled, OrderState.Accepted]
      } else {
        [OrderState.Quote, OrderState.Canceled]
      }
    } else {
      let tds = toDispatchState
      if (s == Accepted && tds != Accepted) {
        [OrderState.Quote, OrderState.Canceled, OrderState.Accepted]
      } else {
        var dd = order.deliveryStates
        if (tds == Accepted) dd += OrderState.Quote
        dd + tds + OrderState.Canceled
      }
    }
  }
}

class OTAcceptance {
  singular = "Acceptance"
  field dueIn: Duration[unit, 0] {canEditUnit}
  field unit: DateUnit = day
  view form = x{label("In") dueIn}
  
  func due(order: IOrder): Due = Due(date = order.date + dueIn)
  func orderState(order: IOrder): OrderState = {
    if (order.accepted) Accepted 
    else if (let dd = order.dueDate) {
      if (Date.now >= dd) Unaccepted
      else Quote
    } else Quote
  }
}
class OTPrepayment {
  singular = "Prepayment"
  field dueIn: Duration[unit, 0] {canEditUnit}
  field unit: DateUnit = day
  field percent: UPercent[2] {
    validate = if (percent > 1) "Cannot be more than 100%"
  }
  view form = x{percent label("in") dueIn}
   
  func due(order: IOrder, date: Date): Due = {
    let dd = date + dueIn
    Due(amount = order.total*percent, date = date + dueIn)
  }
  func orderState(order: IOrder, prevState: OrderState): OrderState = {
    if (order.unpaidAmount <= 0) Prepaid 
    else if (let dd = order.dueDate) {
      if (Date.now >= dd) Unpaid
      else prevState
    } else prevState
  }
}
class OTDelivery {
  singular = "Delivery"
  field dueIn: Duration[unit, 0]? {canEditUnit}
  field unit: DateUnit = day
  view form = x {
    label("Dispatch in") dueIn >> field(canEditUnit = true) label("maximum")
  }
}
interface OTPostdelivery {
  func trigger: PaymentTrigger
  func dueDate(orderDate: Date): Date
}

class OTDueIn extends OTPostdelivery {
  singular = "Full Payment Due"
  field dueIn: Duration[unit, 0] {canEditUnit}
  field unit: DateUnit = day
  field trigger: PaymentTrigger = Dispatched
  
  func dueDate(orderDate: Date): Date = orderDate + dueIn
  view form = x {
    label("In") dueIn >> field(canEditUnit = true) label("after") trigger
  }
}
class OTNextMonth extends OTPostdelivery {
  singular = "Next Month Payment"
  field dayOfMonth: Int = 21
  field trigger: PaymentTrigger = Dispatched
  view form = x {
    label("On") dayOfMonth label("after") trigger
  }
  
  func dueDate(orderDate: Date): Date = {
    let dof = dayOfMonth.as[Duration[day]]!
    if (orderDate.day < dof) orderDate.start(of = month) + dof - 1_day
    else orderDate.start(of = month) + 1_month + dof - 1_day
  }
}

