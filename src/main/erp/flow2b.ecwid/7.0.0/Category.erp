class UpdateCategoryToEcwidJob extends SystemJob {
  field service: IEcwidIntegration
  field category: ProductCategory
  
  class EcwidCategoryRequest {
    field name: String
    field description: Html?
    field productIds: [Int]?
    field enabled = true
  }

  class AddResponse {
    field id: Int
  }
  
  func do = {
  	let categoryRequest = EcwidCategoryRequest (
      name = category.title,
      description = category.description,
      productIds = category.products*.extId(service)
    )
    
    if (let catId = category.extId(service)) {
      service.https("categories/$catId").put(categoryRequest.json)
    } else if (categoryRequest.productIds) {
      let responseAdd <- service.https("categories").post(categoryRequest.json)
      category.updateExtId(service, responseAdd.body!.parseJson[AddResponse].id)
      responseAdd
    }
  }
}


class RemoveCategoryFromEcwidJob extends SystemJob {
  field service: IEcwidIntegration
  field category: ProductCategory
  
  class DeleteResponse {
    field deleteCount: Int
  }
  
  func do = {
    if (let categoryId = category.extId(service)) {
      let response <- service.https("categories/$categoryId").delete
      
      if(let resBody = response.body) {
        if((resBody.parseJson[DeleteResponse].deleteCount > 0) &&
           (!category.deleted)) {
          category.deleteExtId(service)
        }
      }
    }
  }
}


extend entity ProductCategory extends HasExtId {
  extend view form = extView
}
