class AsbCsvData extends BankStatementItem {
  impl field date: Date {format = "yyyy/MM/dd"}
  field transId: Int
  field type: String
  field chequeNumber: String?
  field payee: String?
  field memo: String?
  impl field amount: Dec[2]?
  
  impl func itemType = {
    if (type == "BANK FEE") BankFee 
    else Payment
  }

  impl func data(account: FinAccount): BankData = {
    BankData(
      bankId = transId
      type = type
      memo = memo
      thierAccount = {
        if (type == "TFR OUT") memo.all(Regex("TO ([\d-]+\s[\d-]*)")).first?.group(1).trim.replace(" ", "")
        else if (type == "DEBIT") memo.all(Regex("FC([\d-]+)")).first?.group(1).trim.replace(" ", "")
      }
      thierName = {
        if (let i = payee.lastIndexOf(" FROM ")) payee[i + 6 .. ]
        else if (payee != "DEBIT") payee
      }
    )
  }
}

extend enum BankStatementType {
  case AsbCsv(
    canProcess = file.is("csv") && data.toString.lines(CSV).drop(6).first == "Date,Unique Id,Tran Type,Cheque Number,Payee,Memo,Amount", 
    process = {
      let lines = data.toString.lines(CSV)
      let a = lines.drop(1).first
      ProcessResult(
       accountNumber = "$(a[6 .. 7])-$(a[17 .. 20])-$(a[31 .. ' '))",
       items = lines.drop(8).map(splitTo[AsbCsvData](",", CSV)))
    }, 
    knowOurAccount = true
  )
}