class AsbCsvData extends BankStatementItem {
  impl field date: Date {format = "yyyy/MM/dd"}
  field transId: Int
  field type: String
  field chequeNumber: String?
  field payee: String?
  field memo: String?
  field amount: Dec[2]?
  
  impl func data(account: FinAccount): BankData = {
    let currency: Currency? = if (let curRegEx) Currency.all.find(name == curRegEx.group(1)!)
    BankData(
      bankId = transId, amount = amount
      type = type
      memo = memo
      thierAccount = {
        if (type == "TFR OUT") {
          if (memo.startsWith("TO CARD ")) "%" + memo.dropPrefix("TO CARD ").trim
          else memo.all(Regex("TO ([\d-]+\s[\d-]*)")).first?.group(1).trim.replace(" ", "")
        }
        else if (type == "TFR IN") memo.all(Regex("EX ([\d-]+\s[\d-]*)")).first?.group(1).trim.replace(" ", "")
        else if (type == "DEBIT") memo.all(Regex("FC([\d-]+)")).first?.group(1).trim.replace(" ", "")
        else if (type == "D/D") memo(" ".last? .. ]
      }
      thierName = {
        if (payee == "D/C FROM CO-OPERATIVE BANK") memo
        else if (payee !in ["DEBIT","CREDIT", "MB TRANSFER"] && type !in ["BANK FEE", "TFR OUT", "TFR IN"]) {
          (payee(" FROM ".last? .. ])(" TO ".last? .. ] 
        }
      }
      tax = {
    	if (payee == "IRD:TAX ON INTEREST") WithholdingTax
      }
      itemType = {
        if (type == "BANK FEE") BankFee 
        else if (type == "INT") Interest
        else if (type == "DEBIT" && payee == "IRD:TAX ON INTEREST") Tax
        else if (type == "D/C" && memo.endsWith("ASB ACCEPT")) CardTransfer
        else if (type == "D/C" && payee == "D/C FROM Payment" && memo.startsWith("Payment Disbursement ")) CardTransfer
      }
      currency = currency
      amountInCurrency = if (let currency, let curRegEx) {
        let a = curRegEx.group(2)!.toNum!.as[Money[currency]]
        if (amount < 0) -a else a
      }
    )
  }
  
  func curRegEx = memo.all(Regex("IMT (\S+) (\d+[.]\d+)")).onlyOne
}

extend enum BankStatementType {
  case AsbCsv(
    canProcess = file.is("csv") && data.toString.lines(CSV).first(10).contains("Date,Unique Id,Tran Type,Cheque Number,Payee,Memo,Amount"), 
    process = {
      let lines = data.toString.lines(CSV)                                                                                                             
      let a = lines.drop(1).first
      ProcessResult(
        accountNumber = "$(a[6 .. 7])-$(a[17 .. 20])-$(a[31 .. ' '))",
        items = lines.dropWhile(_).drop(1).mapOpt(splitTo[AsbCsvData](CSV)),
        start = Date(lines(3)("From date " .. ]!, "yyyyMMdd").start(day)
        end = Date(lines(4)("To date " .. ]!, "yyyyMMdd").start(day)
      )
    }, 
    knowOurAccount = true
  )
}