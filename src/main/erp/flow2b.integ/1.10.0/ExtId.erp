trait ExtIdSource[T] extends RefEntity {
  system field sourceIds: [ExtId]? inline by source
  back func extId(item: HasExtId): T? = {
    sourceIds.find(e => e.item == item && e.hasId)?.extId?.as[T]
  }
  back func updateExtId(item: HasExtId, extId: T?): T? = {
    item.updateExtId(this, extId)
  }
  back func deleteExtItem(item: IHasExtId): Void = {
    let i = item.extIdItem
    sourceIds.find(e => e.item == i)?.delete
  }
  back func findExtId[R](extId: T?, map: HasExtId => R?): R? = {
    if (let ei = extId) {
      ExtId.all.filter(i => i.source == this && i.hasId && i.extId == ei)
        .map(map(item))
        .find(_).map(_!)
        .result.get
    } else {
      null
    }
  }
  
  back func extUrl(item: HasExtId): Url? = {
    sourceIds.find(e => e.item == item)?.extUrl
  }
}
  
interface IHasExtId {
  func extIdItem: HasExtId
  func deleted: Bool
  back func extId[T](source: ExtIdSource[T]): T?
  back func updateExtId[T](source: ExtIdSource[T], extId: T?): T?
  back func deleteExtId[T](source: ExtIdSource[T]): T? = updateExtId(source, null)
  back func extUrl[T](source: ExtIdSource[T]): Url?
  back func updateExtUrl[T](source: ExtIdSource[T], extUrl: Url?): Url?
  back func deleteExtUrl[T](source: ExtIdSource[T]): Url? = updateExtUrl(source, null)
}

interface IHasExtIdDecorator extends IHasExtId {
  func extIdItem: HasExtId
  func deleted: Bool = extIdItem.deleted
  back func extId[T](source: ExtIdSource[T]): T? = extIdItem.extId(source)
  back func updateExtId[T](source: ExtIdSource[T], extId: T?): T?  = {
    extIdItem.updateExtId(source, extId)
  }
  back func extUrl[T](source: ExtIdSource[T]): Url? = extIdItem.extUrl(source)
  back func updateExtUrl[T](source: ExtIdSource[T], extUrl: Url?): Url? = {
    extIdItem.updateExtUrl(source, extUrl)
  }
}

trait HasExtId extends AnyEntity with IHasExtId {
  func extIdItem: HasExtId = this
  
  system field extIds: [ExtId]? inline by item
  back func extId[T](source: ExtIdSource[T]): T? = {
    extIds.find(e => e.source == source)?.extId?.as[T]
  }
  back func extUrl[T](source: ExtIdSource[T]): Url? = {
    extIds.find(e => e.source == source)?.extUrl
  }
  back func updateExtId[T](source: ExtIdSource[T], extId: T?): T? = {
    let r = if (let i = extIds.find(e => e.source == source)) {
      if (let e = extId) {
        i.extId = e
        i.hasId = true
        i.save
      } else {
        i.hasId = false
        i.deleteIfEmpty
      } 
    } else if (let e = extId) {
      ExtId(item = this, source = source, extId = e, hasId = true).save
    }
    extId
  }
   back func updateExtUrl[T](source: ExtIdSource[T], extUrl: Url?): Url? = {
    if (let i = extIds.find(e => e.source == source)) {
      i.extUrl = extUrl
      i.deleteIfEmpty
    } else if (let e = extUrl) {
      ExtId(item = this, source = source, extUrl = e, extId = 0, hasId = false).save
    }
    extUrl
  }
  
  view extView = nav {grid{
    extIds >> table {
      source extId extUrl
    }
  }}
}

inline entity ExtId {
  system field item: HasExtId by extIds {deleteCascade = false}
  system field source: ExtIdSource by sourceIds
  field extId: Any
  field hasId: Bool
  field extUrl: Url?
  func isEmpty = !hasId && !extUrl
  func deleteIfEmpty = if (isEmpty) delete else save
  view form = grid(labels = None) {
    row {source source extId extUrl}
  }
}

