trait ExtIdSource[T] {
  system field sourceIds: [ExtId]? inline by source
  func extId(item: HasExtId): T? = {
    sourceIds.find(e => e.item == item)?.extId?.as[T]
  }
  func updateExtId(item: HasExtId, extId: T?): T? = {
    item.updateExtId(this, extId)
  }
}
  
interface IHasExtId {
  func deleted: Bool
  func extId[T](source: ExtIdSource[T]): T?
  func updateExtId[T](source: ExtIdSource[T], extId: T?): T?
}

interface IHasExtIdDecorator extends IHasExtId {
  func extIdItem: IHasExtId
  func deleted: Bool = extIdItem.deleted
  func extId[T](source: ExtIdSource[T]): T? = extIdItem.extId(source)
  func updateExtId[T](source: ExtIdSource[T], extId: T?): T?  = extIdItem.updateExtId(source, extId)
}

inline trait HasExtId extends IHasExtId {
  system field extIds: [ExtId]? inline by item
  func extId[T](source: ExtIdSource[T]): T? = {
    extIds.find(e => e.source == source)?.extId?.as[T]
  }
  func updateExtId[T](source: ExtIdSource[T], extId: T?): T? = {
    if (let i = extIds.find(e => e.source == source)) {
      if (let e = extId) {
        i.extId = e
        i.save
      } else {
        i.delete
      }
    } else if (let e = extId) {
      ExtId(item = this, source = source, extId = e).save
    }
    extId
  }
  
  view extView = nav {grid{
    extIds >> table {
      source extId
    }
  }}
}

inline entity ExtId {
  system field item: HasExtId by extIds {deleteCascade = false}
  field source: ExtIdSource by sourceIds
  field extId: Any
  view form = grid(labels = None) {
    row {entity source extId}
  }
}

