entity Project {
  field title: String
  
  compute capital: Dec[2]? = capitalItems*.amount.sum + equipments*.total.sum
  field capitalItems: [ProjectCapitalItem] inline by project
  field equipments: [ProjectEquipment] inline by project
    
  compute costPerWeek = personnel*.salaryPerWeek.sum + operatingCosts*.costPerWeek.sum
  compute costPerYear = costPerWeek*365/7
  field personnel: [ProjectRole] inline by project
  field operatingCosts: [ProjectOperatingCostItem] inline by project
    
  compute grossProfitPerWeek = revenues*.profitPerWeek.sum
  compute grossProfitPerYear = grossProfitPerWeek*365/7  
  field revenues: [ProjectRevenue] inline by project
    
  compute profitPerWeek = grossProfitPerWeek - costPerWeek
  compute profitPerYear = profitPerWeek*365/7
  compute roi = capital/profitPerYear
    
  view form = nav {
    grid {
	    title >> field(label = "")
        row{profitPerWeek, profitPerYear, roi}
      	
      	row {grossProfitPerWeek, grossProfitPerYear}
      	revenues
        
        row {costPerWeek, costPerYear}
      	personnel
        operatingCosts
         
        capital
        capitalItems
        equipments
    }
  }
}

entity ProjectCapitalItem {
  field project: Project
  field title: String
  field amount: Dec[2]
}

inline entity ProjectEquipment {
  field project: Project
  field equipment: Equipment
  field capacity: Dec[2]
  field unit: Unit
  field total: Dec[2]
}

entity ProjectOperatingCostItem {
  field project: Project
  field title: String
  compute costPerWeek: Dec[2] = costPerYear*7/365 {
    update = { costPerYear = costPerWeek*365/7}
  }
    
  field costPerYear: Dec[2]
  
}

inline entity ProjectRole {
  field project: Project
  field role: Role
  field hours: WeekCount
  compute salaryPerWeek: Dec[2] = hours.perWeek*role.salaryPerHour
  compute salaryPerYear: Dec[2] = salaryPerWeek*365/7
}

entity ProjectRevenue {
  field project: Project
  field title: String
  field avarageCheck: Dec[2]
  field foodCost: Dec[2]
  field orders: WeekCount
  compute revenuePerWeek: Dec[2] = avarageCheck*orders.perWeek
  compute profitPerWeek: Dec[2] = revenuePerWeek*(1 - foodCost)
  compute profitPerYear: Dec[2] = profitPerWeek*365/7
}

class WeekCount {
  field mon: Int {width = 0.07}
  field tue: Int {width = 0.07}
  field wen: Int {width = 0.07} 
  field thu: Int {width = 0.07}  
  field fri: Int {width = 0.07}    
  field sat: Int {width = 0.07}  
  field sun: Int {width = 0.07}  
  compute perWeek: Int = (mon + tue + wen + thu + fri + sat + sun) 
}