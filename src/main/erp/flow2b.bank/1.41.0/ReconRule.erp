trait IReconRule extends Entity {
  singular = "Reconciliation Rule"
  plural = "Reconciliation Rules"
  
  back func backDescr: Text?
  func pr: Dec[4]?
  back func apply(movements: [CashMovement]?, payment: Payment): CashMovement?
  back func isCounterparty(actor: LegalEntity, payment: Payment): Bool = false
  
  field _priority: Dec[4]??
  ref compute priority: Dec[4]? = _priority.alt(pr)
  
  compute frontDescr {
    value = back{backDescr}
    availableFor = Inline
  }
  
  
  ref field state: ReconRuleState = defaultState
  func defaultState: ReconRuleState = Active
  
  impl view ref = x{state title}
  impl view form = nav{grid{
    x{state title >> field(fontStyle = H2, stretch = 1)}
    frontDescr >> field(label = "")
    priority
  }}
  report Report {
    impl view list = IReconRule.all.sortBy(priority.desc) >> nav{list {
      x{state title >> field(stretch = 1) priority >> field(fontStyle = Label)}
    }}
  }
  object back func process(movements: [CashMovement]?, payment: Payment): CashMovement? = {
    IReconRule.all
      .filter(state == Active)
      .sortBy(priority.desc).result.get
      .fold(null[CashMovement]) {
        if (let acc) acc
        else apply(movements, payment)
      }
  }
  object back func isCounterparty(actor: LegalEntity, payment: Payment): Bool = {
    IReconRule.all
      .filter(state == Active)
      .sortBy(priority.desc)
      .exists(_.isCounterparty(actor, payment))
  }
}

enum ReconRuleState {
  case Active {background = "#dea" icon = "robot"}
  case Inactive {background = "#999" icon = "times-circle"}
}

extend role Accountant {
  extend view menu = extend("Finance") {
    IReconRule
  }
}

entity ReconRule extends IReconRule {
  singular = "Reconciliation Rule"
  impl field title: String
  field descr: Text? {label = "Description"}
  impl func backDescr: Text? = descr
  impl func pr: Dec[4]? = 0
  field filters: [IReconFilter]? inline by rule
  field recon: IRecon inline by rule {label = "Reconciliation"}
  back func filter(movements: [CashMovement]?, payment: Payment): Bool = {
    filters.all(_.filter(movements, payment))
  }
  back func recon(movements: [CashMovement]?, payment: Payment): CashMovement? = recon.apply(movements, payment)
  
  impl back func apply(movements: [CashMovement]?, payment: Payment): CashMovement? = {
    if (filter(movements, payment)) {
      recon(movements, payment)
    }
  }
  override view form = nav{grid{
    x{state title >> field(fontStyle = H2, stretch = 1)}
    descr
    
    priority
    filters
    recon
  }}
}

trait IReconFilter extends InlineEntity {
  field rule: ReconRule by filters
  
  back func filter(movements: [CashMovement]?, payment: Payment): Bool
}

trait IRecon extends InlineEntity {
  field rule: ReconRule by recon
  
  back func apply(movements: [CashMovement]?, payment: Payment): CashMovement?
}