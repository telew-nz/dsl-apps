entity POIInvoice extends DashboardItem {
  singular = "Imported Invoice"
  plural = "Imported Invoices"
  field invoiceType: InvoiceType?
  
  func number = "$(poi.number).$n"
  impl func title = "$number $invoiceNumber"
  impl func itemRoles = [BusAdmin]
  func isEmpty = !items && !totalNoGst && !invoiceType
  
  ref field poi: PurchaseOrderImport {label = "Import"}
  ref const field n: UInt = {
    poi.invGen += 1
    poi.save
    poi.invGen!
  }
  func success: Bool = !savedIssues && !poi.savedIssues
  
  field invoiceDate: Date?
  
  ref compute date = invoiceDate.alt(poi.date)
  ref field invoiceNumber: String?
  field poNumber: String? {label = "Purchase Order Number"}
  const field vendorNumber: String?
  field vendor: Vendor? {
    default = poi.vendor
    init = {
      new?.legalEntity?.legalInfo?.gstNumber = vendorNumber
      new?.gstStatus = Registered
    }
  }
  impl event onSave = {
    if (updated(vendor) || updated(invoiceType) let vendor) {
      if (let vendorNumber) {
        vendor.legalEntity.legalInfo?.gstNumber = vendorNumber
        vendor.legalEntity.save
      }
      if (invoiceType in [TaxInvoice, DebitNote, AdjustmentNote, CreditNote]) {
        vendor.gstStatus = Registered
        vendor.save
      }
    }
  }
  func vendorId: String? = if (let vendor) vendor.id.toString else vendorNumber
  field dueDate: Date?
  
  field rowsWithGst: Bool { label = "Rows include GST"}
  field currency: Currency = our
  field totalNoGst: Money[currency]?
  field gst: Money[currency]?
  ref compute totalWithGst: Money[currency]? = totalNoGst + gst
  
  field items: [POIInvoiceItem]? inline by invoice
  
  impl view ref = x {title date.date totalWithGst}
  
  impl view form = nav{grid{
    issuesView
    x { poi >> field(stretch = 1) number >> field(fontStyle = Label)}
    stateView
    row {
      invoiceNumber invoiceDate dueDate
    }
    row {
      y (label = "Vendor") {
        vendorNumber >> field(fontStyle = Label)
        vendor
      } rowsWithGst poNumber
    }
    extension
    row {
      totalNoGst gst totalWithGst 
    }
    items >> table {
      y (label = "Description", width = 0.56, margin = None) {
        code >> field(fontStyle = Label) 
        desc
      }
      qty >> field(width = 0.12) 
      gstRateEditor >> field(width = 0.12, label = "GST") 
      amount >> field(width = 0.2) 
    }    
  }}
  impl view traitItem = x {
    stateView >> field(stretch = 0) number >> field (fontStyle = Label) invoiceNumber invoiceDate?.date vendor poi.fileName
  }
  view stateView = x {invoiceType >> field(label = "")}
  report Report {
    impl view list = POIInvoice.all >> nav{list(canAdd = false){traitItem}}
  }
}
 
enum InvoiceType {
  case TaxInvoice {background = "#ed9"}
  case DebitNote {background = "#ffb57f"}
  case CreditNote {background = "#ffb57f"}
  case AdjustmentNote {background = "#ffb57f"}
  case Order {background = "#cce0ff"}
}

inline entity POIInvoiceItem {
  field invoice: POIInvoice by items
  
  field code: String?
  field desc: PlainText? {
    label = "Description"
  }
  field qty: Qty?
  field amount: Money[invoice.currency]?
  field gstRate: UPercent[2]??  
  compute gstAmount: Money[invoice.currency]? = if (let gstRate) {
    if (invoice.rowsWithGst) {
      (amount.as[Dec[8]]*gstRate/(gstRate + 1)).as[Money[invoice.currency]] 
    } else {
      amount*gstRate
    }
  }
  compute amountNoGst = if (invoice.rowsWithGst) amount - gstAmount else amount
  compute amountWithGst = if (invoice.rowsWithGst) amount else amount + gstAmount
  field gstIssue: Bool
  compute gstRateEditor: UPercent[2]? {
    value = gstRate.alt(0).as[UPercent[2]]
    update = {
      gstRate = new
      gstIssue = false
    }
  }
}