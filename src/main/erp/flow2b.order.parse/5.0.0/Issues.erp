trait IPoiIssueType extends IssueType[OrderImport]
trait IPoiInvoiceIssueType extends IssueType[ImportedOrder]

object ITPOINoInvoices extends IPoiIssueType {
  impl func title = "Unable to find invoices"
  impl func hasIssue(item: OrderImport) = item.state == Complete && !item.orders
  impl func fixes(item: OrderImport) = [
    Fix(text = "Make sure the uploaded field is a valid tax invoice")
  ]
}

extend entity OrderImport {
  override func issueTypes = super.issueTypes + ITPOINoInvoices
}

object ITPOINoInvoiceType extends IPoiInvoiceIssueType {
  impl func title = "The document does not seem to be an Invoice"
  impl func hasIssue(item: ImportedOrder) = !item.type && item.poi.state == Complete
  impl func fixes(item: ImportedOrder) = [
    Fix(text = "Make sure the uploaded field is a valid tax invoice"),
    Fix(text = "Edit and select the invoice type")
  ]
}

object ITPOINoTotal extends IPoiInvoiceIssueType {
  impl func title = "POI: Unable to extract total amount"
  impl func hasIssue(item: ImportedOrder) = !item.totalNoGst && item.items && item.poi.state == Complete
}
object ITPOINoTotalMatch extends IPoiInvoiceIssueType {
  impl func title = "POI: Total does not match to the sum of the items amount"
  impl func hasIssue(item: ImportedOrder) = item.poi.state == Complete && item.totalNoGst && {
    (item.rowsWithGst && item.items*.amount.sum != item.totalWithGst)
      || (!item.rowsWithGst && item.items*.amount.sum != item.totalNoGst)
  }  
}
object ITPOINoItems extends IPoiInvoiceIssueType {
  impl func title = "POI: Unable to extract items"
  impl func hasIssue(item: ImportedOrder) = !item.items && item.poi.state == Complete
}
object ITPOIGstRate extends IPoiInvoiceIssueType {
  impl func title = "POI: GST rate is incorrect"
  impl func hasIssue(item: ImportedOrder) = item.items.exists(gstIssue) && item.poi.state == Complete
}

extend entity ImportedOrder {
  override func issueTypes = super.issueTypes + issueTypes0 + issueTypesAdd
  func issueTypes0 = [ITPOINoInvoiceType, ITPOINoTotal, ITPOINoTotalMatch, ITPOINoItems, ITPOIGstRate]
  func issueTypesAdd: [IssueType[ImportedOrder]]? = []
}
