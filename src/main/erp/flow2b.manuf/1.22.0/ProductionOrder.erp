entity ProductionOrder extends IProductionOrder {
  impl func number = "PR" + n
  
  impl field method: Method {
    available(Inline).for(Inline)
    update = {
      this.qty = this.qty.as[UQty[method.unit]]
    }
  }
  impl field qty: UQty[method.unit] = 1
  impl ref compute title: String = if (let n = refNum) "$number ($n) $method" else "$number $method"
  func items = materials
  impl compute materials = method.materials.map(mm => ProMaterial(pro = this, mm = mm, qty = mm.qty*qtyK))
  compute resources = method.resources.map(mr => ProResource(pro = this, mr = mr, duration = mr.scale(qtyK)))
  impl compute outputs: [ProOutput] = method.outputs.map(mo => ProOutput(pro = this, mo = mo, qty = mo.qty*qtyK))

  register outStocks: [ProdReg]? {
    value = materials*.makeOutStocks
    availableFor = Inline
    available(Inline).for(Inline)
    autoRecalc = true
  }
  register inStocks: [ProdReg]? {
    value = outputs*.makeInStocks
  }
  
  impl view form = nav {
    grid{
      issuesView
      row{number refNum space}
      row{method qty outputStockType >> field(compact)}
      x {status >> field(editMode = AlwaysEdit) availability }
      row {dispatchDate deliveryDate space}
      row {legalEntity supplier >> field(visible = ui.edit || sSupplier || sReceiver) receiver >> field(visible = ui.edit || sSupplier || sReceiver)}
      row {factory materialStore >> field(visible = ui.edit || sMaterialStore || sOutputStore) outputStore >> field(visible = ui.edit || sMaterialStore || sOutputStore)}
      outputs >> table{
        outputProduct >> field(width = 0.5, rowSpan = 3) qty >> field(width = 0.25) costWeight  >> field(width = 0.25)
      }
      materials >> table{
        header{label("Product", width = 0.5)  label("Quantity", align = Right, width = 0.25) label("", width = 0.25) }
        body{
          row {material >> field(rowSpan = 3) qty space}
          row {y(span = 2, visible = status == Planned || !isAv){stockDesc}}
          row {y(span = 2, visible = !isAv){availableView}}
        }
      }
      resources >> table{
        resource >> field(width = 0.5) duration >> field(width = 0.25) space(width = 0.25, label = "")
      }
    }
    generalLedgerButton
    button("To One-Off") {
      convertToOneOff()
    }
  }
  func convertToOneOff() = {
    let outputs = this.outputs
    let materials = this.materials
    let resources = this.resources
    let pro = convertTo(OneoffProductionOrder)
    pro.outputs = outputs*.oneOff(pro)
    pro.materials = materials*.oneOff(pro)
    pro.resources = resources*.oneOff(pro)
    pro
  }
  impl view ref = x{status >> field(editMode = AlwaysEdit) availability title}
}
 
class ProMaterial extends IIProdMoveItemOut {
  impl func moveItemActive = pro.status != Canceled
  impl func prodMoveOut = pro
  impl func moveOutStockType = Inventory
  
  field pro: ProductionOrder
  field mm: MethodMaterial inline
  compute material = mm.material
  impl field qty: UQty[unit]

  impl func product = material.product
  impl func unit = mm.unit
  impl compute outStocks = pro.outStocks.filter(_.docItem == mm)
  override func moveDocItem = mm
  
  //TODO: S/N
  impl func outStockItems: [ProdItemReg]? = []
  impl func productItems: [ProductItem]? = []
  
  func oneOff(order: OneoffProductionOrder) = this.copy(OproMethodMaterial, order = order, qty = qty)
}

class ProResource {
  field pro: ProductionOrder
  field mr: MethodResource inline
  compute resource = mr.resource
  field duration: UDuration[DateUnit.hour]
  
  func oneOff(order: OneoffProductionOrder) = this.copy(OproMethodResource, order = order)
}

class ProOutput extends IIProdMoveItemIn {
  impl func moveItemActive = pro.status != Canceled
  impl func prodMoveIn = pro
  impl func moveInStockType = pro.outputStockType.stock

  
  field pro: ProductionOrder
  field mo: MethodOutput inline
  compute outputProduct {
    label = "Product" 
    value = mo.outputProduct
  }
  compute costWeight = mo.costWeight
  impl field qty: UQty[unit]

  override func moveDocItem = mo
  impl func product = outputProduct.product
  impl func unit = mo.unit
  
  func oneOff(order: OneoffProductionOrder) = this.copy(OproMethodOutput, order = order, qty = qty, costWeight = costWeight)
  
  //TODO: S/N
  impl func productItems: [ProductItem]? = []
}