trait IKit extends ISubProduct {
  impl func trackItems = null
  field items: [KitItem] inline by kit
  impl func products: [IProduct : UQty] = items.flatMap{i =>
    i.product.products.mapValues{ 
      _*i.qty
    }.toSeq
  }.toMap!
  impl compute productCost = {
    let costs = items.map{
      let pc = product.productCost
      (date = pc?.date, cost = pc?.cost*qty)
    }
    if (costs.all(date)) {
      ProductCost(date = costs.map(date!).min, cost = costs*.cost.sum)
    }
  }
  override final func productItems: [ProductItem]? = []
  
  final impl func calcInvoiceDesc = {
    let t: Text? = if (superProduct.showKitItems) "<p>".as[Text] + items.map("$(qty) $product").join("<br>").as[Text] + "</p>".as[Text]
    t + superProduct._invoiceDesc
  }
}

inline entity KitItem {
  field kit: IKit by items
  field product: ISubProduct by includedInKitItems {
    available(Ref).with(unit, altUnits, vendors, productCost)
    update = {
      this.unit = new.unit
      this.qty = this.qty.as[UQty[this.unit]]
    }
  }
  field unit: ProductUnit {
    from = product.units
  }
  field qty: UQty[unit] {
    label = "Quantity"
    default = 1
    canEditUnit = true
  }
}

basicProduct Kit (skuPrefix = "KIT", kit = true) {
  field color: ColorItem?
  field size: Size?
  field condition: ProdCondition?
  
  impl func prodCondition: ProdCondition? = condition
  impl view customView = grid {
    row{color size condition}
  } 
}


trait IBasicKit extends IBasicSubProduct with IKit {
  impl func trackItems = null
  impl view form = nav(onBarcode = if (ui.edit) addBarcode(barcode)){
    tabs {
      grid (labels = None, label = "Main") {
        mainView
      }
      superProductTabView
      subProductTabView
      extension("tabs")
      grid (labels = Top, label = "More") {
        unitsView
        barcodes
        extension
      }
    }
    superProductNavView
    nfcWriteBtn
    extension("nav")
  }
}


trait IVariableKit extends IVariableSuperProduct {
  impl var variations: [IKitVariation]?
}

trait IKitVariation extends ISubVariation with IKit {
  impl func superProduct: IVariableKit
  impl view form = nav(onBarcode = if (ui.edit) addBarcode(barcode)){
    tabs {
      grid (labels = None, label = "Main") {
        mainView
      }
      grid (labels = Top, label = "Related") {
        kits
        extension("rel")
      }
      subProductTabView
      extension("tabs")
    }
    nfcWriteBtn
    extension("nav")
  }
}
