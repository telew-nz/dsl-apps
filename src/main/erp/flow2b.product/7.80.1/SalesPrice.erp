class SalesPrice {
  field currency: Currency = our
  field regular: UMoney[currency]? {
    update = {
       if (!old || current == old!) current = new
    }
  }
  field current: UMoney[currency]?
  func isEmpty: Bool = !regular && !current
  
  func to(currency: Currency, date: Date): Future[SalesPrice] = {
    guard (currency != this.currency) else return this 
 
    let c <- current.to[UMoney[currency]](date)
    let r <- if (regular == current) c else regular.to[UMoney[currency]](date)
    SalesPrice(currency = currency, regular = r, current = c)
  }
  func mul(qty: UQty?): SalesPrice = {
    SalesPrice(currency = currency, regular = regular*qty, current = current*qty)
  }
  func add(o: SalesPrice): SalesPrice = {
    SalesPrice(currency = currency, regular = regular + o.regular, current = current + o.current)
  }
  func add(o: Money?): SalesPrice = {
    SalesPrice(currency = currency, regular = (regular + o).positive, current = (current + o).positive)
  }
  func round(rounding: Rounding): SalesPrice = {
    SalesPrice(currency = currency, regular = rounding.apply(regular), current = rounding.apply(current))
  }
  func round(rounding: Rounding?): SalesPrice = if (let rounding) {
    round(rounding)
  } else this
  func withCurrent(current: UMoney?): SalesPrice = this.copy(current = current)
  object func sum(sps: [SalesPrice]?): SalesPrice = {
    if (let sps) {
      var r = sps.first
      sps.drop(1).foreach{s => r = r + s}
      r
    } else empty
  }
  func convertGst(gstRate: UPercent?, priceInclGst: Bool, expectedInclGst: Bool): SalesPrice = {
    if (priceInclGst == expectedInclGst) this
    else SalesPrice(
      currency = currency, 
      regular = object.convertGst(gstRate, regular, priceInclGst, expectedInclGst),
      current = object.convertGst(gstRate, current, priceInclGst, expectedInclGst) 
    )
  }
  
  object func convertGst(gstRate: UPercent?, price: UMoney?, priceInclGst: Bool, expectedInclGst: Bool): UMoney? = {
    if (priceInclGst == expectedInclGst) price 
    else if (expectedInclGst) price*(gstRate + 1.0000)
    else price/(gstRate + 1.0000)
  }
  object func empty: SalesPrice = SalesPrice(regular = 0, current = 0)
  impl view form = x {
    regular >> field(fontStyle = if (current == regular) Normal else Strikeout)
    add("Clearance", visible = ui.edit && current == regular) {current = regular*0.9}
    button("No Clearance", visible = ui.edit && current != regular) {current = regular}
    current >> field(visible = current != regular)
  }
}

class RentAmount {
  field dayAmount: UMoney?
  field nightAmount: UMoney?
  field bond: UMoney?
  compute amount = dayAmount + nightAmount
  
  func convertGst(gstRate: UPercent?, priceInclGst: Bool, expectedInclGst: Bool): RentAmount = {
    if (priceInclGst == expectedInclGst) this
    else RentAmount(
      dayAmount = SalesPrice.convertGst(gstRate, dayAmount, priceInclGst, expectedInclGst),
      nightAmount = SalesPrice.convertGst(gstRate, nightAmount, priceInclGst, expectedInclGst),
      bond = bond
    )
  }
}

interface Rounding {
  func apply(number: UNumber?): UNumber? 
  view form
}

class Floor extends Rounding {
  field increment: UNumber = 1.00
  field delta: Number? = -0.01
  impl func apply(number: UNumber?): UNumber? = (number.roundInc(floor, increment) + delta).positive
  impl view form = grid{row{increment delta}}
}
class Round extends Rounding {
  field increment: UNumber = 1.00
  field delta: Number? = -0.01
  impl func apply(number: UNumber?): UNumber? = (number.roundInc(round, increment) + delta).positive
  impl view form = grid{row{increment delta}}
}
class Ceiling extends Rounding {
  field increment: UNumber = 1.00
  field delta: Number? = -0.01
  impl func apply(number: UNumber?): UNumber? = (number.roundInc(ceil, increment) + delta).positive
  impl view form = grid{row{increment delta}}
}
