extend trait ISuperProduct {
  field categories: [ProductCategory]? by products {
    available(Inline).for(All)
  }
  func autoCategories: [IProductCategory]? = []
  compute allCategories: [IProductCategory]? {
    label = "Categories"
    value = autoCategories + categories
    update = {
      this.categories = new.filter(!autoCategories.contains(_))*.as[ProductCategory]
    }
  }
  
  field tags: [ProductTag]? by products 
  func autoTags: [IProductTag]? = []
  compute allTags: [IProductTag]? {
    label = "Tags"
    value = autoTags + tags
    update = {
      this.tags = new.filter(!autoTags.contains(_))*.as[ProductTag]
    }
  }
  
  
  field brands: [Brand]? by products {
    available(Inline).for(All)
  }
  
  ref field crossSells: [CrossSell]? by products {
    available(Inline).for(Ref)
  }
  func crossSellProducts: [ISuperProduct]? = (crossSells*.products - this).distinct
  field upsells: [ISuperProduct]? by downsells
  field downsells: [ISuperProduct]? by upsells
  
  view categoriesView = grid(label = "Categories"){
    allCategories
    revenueType
    expenseType
    brands
    allTags
    crossSells
    upsells
    downsells
    kits
    shippingClass
  }
  field _shippingClass: ShippingClass? by products 
  compute shippingClass: ShippingClass? {
    value = _shippingClass.or(allCategories*.shippingClass.first)
    update = {
      _shippingClass = if (new != allCategories*.shippingClass.first) new
    }
  }
  
  field _revenueType: RevenueType? by products
  ref override compute revenueType: RevenueType? {
    value = _revenueType.or(allCategories*.revenueType.first)
    update = {
      _revenueType = if (new != allCategories*.revenueType.first) new
    }
  }
  field _expenseType: ExpenseType? by products
  ref override compute expenseType: ExpenseType? {
    visible = !isTangible
    value = _expenseType.or(allCategories*.expenseType.first)
    update = {
      _expenseType = if (new != allCategories*.expenseType.first) new
    }
  }
}

trait IProductTaxonomy extends RefEntity {
  func photos: [Image]?
  func superCategory: IProductTaxonomy?
  func subCategories: [IProductTaxonomy]?
  func name: String
  func description: Text?
  func products: [ISuperProduct]?
  func tree: [this] = this + superCategory?.tree
  func level: UInt = 1 + superCategory?.level
  func up(delta: UInt?): this? = {
    if (delta) superCategory?.up((delta - 1).positive)
    else this
  }
  func collectPhotos: [Image]? = {
    if (let p = photos) p
    else if (let p = products*.collectPhotos.first(1)) p
    else subCategories*.collectPhotos.first(1)
  }
}

trait IProductCategory extends IProductTaxonomy {
  impl func superCategory: IProductCategory?
  impl func subCategories: [IProductCategory]?
  func shippingClass: ShippingClass? = null
  func revenueType: RevenueType? = null
  func expenseType: ExpenseType? = null
}
entity ProductCategory extends Entity with IProductCategory {
  impl field photos: [Image]?
  impl field superCategory: ProductCategory? by subCategories
  impl field subCategories: [ProductCategory]? read by superCategory
  impl ref field name: String
  impl field description: Text?
  impl field products: [ISuperProduct]? by categories
  impl ref compute title = if (let s = superCategory) "$s / $name" else name
  field _shippingClass: ShippingClass? by categories 
  impl ref stored compute shippingClass: ShippingClass? {
    value = _shippingClass.or(superCategory?.shippingClass)
    update = {
      _shippingClass = if (new != superCategory?.shippingClass) new
    }
  }
  field _revenueType: RevenueType? by categories 
  impl ref stored compute revenueType: RevenueType? {
    value = _revenueType.or(superCategory?.revenueType)
    update = {
      _revenueType = if (new != superCategory?.revenueType) new
    }
  }
  field _expenseType: ExpenseType? by categories 
  impl ref stored compute expenseType: ExpenseType? {
    value = _expenseType.or(superCategory?.expenseType)
    update = {
      _expenseType = if (new != superCategory?.expenseType) new
    }
  }
  impl event onSave = {
    subCategories*.touch
    if (updated(shippingClass) || updated(expenseType) || updated(revenueType)) {
      products*.touch
    }
  }
  
  impl view form = nav{grid{
    photos
    superCategory
    name >> field(label = "" fontStyle = H1)
    subCategories >> list {field(title = _.name)}
    revenueType
    expenseType
    shippingClass
    description
    products
  }}
}

trait IBrand extends IProductTaxonomy  {
  impl func superCategory: IBrand?
  impl func subCategories: [IBrand]?
}
entity Brand extends Entity with IBrand with ProductAttrTerm {
  impl object func attrName = "Brand"
  
  field logo: Image? 
  field actors: [Actor]?
  impl func photos: [Image]? = logo.toSeq
  impl func superCategory = superBrand
  impl func subCategories = subBrands
  
  field superBrand: Brand? by subBrands
  field subBrands: [Brand]? read by superBrand
  impl ref field name: String
  impl field description: Text?
  impl field products: [ISuperProduct]? by brands
  impl ref compute title = if (let s = superBrand) "$s / $name" else name

  impl view form = nav{grid{
    logo
    superBrand
    name >> field(label = "" fontStyle = H1)
    subBrands >> list {field(title = _.name)}
    actors
    description
    products
  }}
  
  impl event onSaveOrDelete = {
  }
}

extend entity Actor {
  field brands: [Brand]? by actors
}

trait IProductTag extends IProductTaxonomy {
  impl func photos: [Image]? = []
  impl func name = title
  impl func superCategory = null
  impl func subCategories = []
}
entity ProductTag extends Entity with IProductTag {
  impl field photos: [Image]?
  impl field title: String
  impl field description: Text?
  impl field products: [ISuperProduct]? by tags
  impl view form = nav{grid{
    photos
    title >> field(label = "" fontStyle = H1)
    description
    products
  }}
}

trait ICrossSell extends IProductTaxonomy {
  impl func photos: [Image]? = []
  impl func name = title
  impl func superCategory = null
  impl func subCategories = []
}
entity CrossSell extends Entity with ICrossSell {
  impl field title: String
  impl field description: Text?
  impl field products: [ISuperProduct]? by crossSells

  impl view form = nav{grid{
    title >> field(label = "" fontStyle = H1)
    description
    products
  }}
}

entity ShippingClass extends IProductTaxonomy {
  impl func photos: [Image]? = []
  impl func superCategory = null
  impl func subCategories = []
  impl func name: String = title
  
  impl field title: String
  impl field description: Text?
  impl field products: [ISuperProduct]? by _shippingClass
  field categories: [ProductCategory]? by _shippingClass

  impl view form = nav{grid{
    title >> field(label = "" fontStyle = H1)
    description
    products
  }}
}

trait IIncomeType extends IProductTaxonomy {
  background = "#d68400"
  impl func photos = []
  impl func superCategory = null
  impl func subCategories = []
  impl func name = title
  
  ref field gstRate: GstRate? = null
  ref field specialGstRate: UPercent[2]? {
    visible = gstRate == Special
  }
  compute getGstRate: UPercent[2]?? {
    value = {
      if (gstRate == Special) specialGstRate
	  else if (gstRate) gstRate!.rate 
    }
    views = []
  }
  ref field nonTaxable: UPercent? {
    validate = if (0 > taxable || taxable > 1.00_PercentUnit) "Taxable percentage should be between 0% and 100%"
  }
  compute taxable: UPercent? {
    label = "Taxable Percentage"
    value = (1.00_PercentUnit - nonTaxable).as[UPercent]
    update = {
      nonTaxable = (1.00_PercentUnit - new).as[UPercent]
    }
  }
  func gstRate(vendor: IVendor?): UPercent[2]?? = if (vendor?.hasGst) {
    getGstRate.or(vendor!.defaultGstRate.rate)
  } 
  func hasGst(vendor: IVendor?): Bool = gstRate(vendor)
} 
 
entity ExpenseType extends IIncomeType {
  impl field title: String
  
  field categories: [ProductCategory]? by _expenseType
  impl field products: [ISuperProduct]? by _expenseType
  
  impl field description: Text?
  impl view form = nav{
    grid{
      title
      extension
      taxable >> field(showEmpty)
      gstRate
      specialGstRate
      description
      
      categories
      products
    }
    extension("nav")
  }
}


entity RevenueType extends IIncomeType {
  impl field title: String
  field categories: [ProductCategory]? by _revenueType
  impl field products: [ISuperProduct]? by _revenueType
  
  impl field description: Text?
  impl view form = nav{
    grid{
      title
      extension
      taxable >> field(showEmpty)
      gstRate
      specialGstRate
      description
      
      categories
      products
    }
    extension("nav")
  }
}

