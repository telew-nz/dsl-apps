trait IPriceModel extends RefEntity {
  object func ours: [IPriceModel]? = OurLegalEntity.ours*.priceModels
  func currency: Currency = vendor.getCurrency
  ref field vendor: IVendor by priceModels =? OurLegalEntity.onlyOne
  func priority: Dec[8]? = 0
  
  func isDynamic = false
  func isSalesModel = true
  func isRentModel = false
  back func contains(product: IBaseProduct): Bool {
    default value = price(product)
  }
  back func price(product: IBaseProduct): IPrice?
}

trait IDynamicPriceModel extends IPriceModel 
trait IStaticPriceModel extends IPriceModel {
  func supports(product: IBaseProduct): Bool = false
  func createPrice(product: IBaseProduct): IStaticPrice = throw "createPrice Unsupported"
}

interface IPrice {
  list compute salesPrice: Future[SalesPrice?] {
    value = back{
      if (model.isSalesModel) salesPrice(currency, vendor.pricesIncludingGst)
    }
  }
  list compute rentPrices: Future[[DurPrice]?] {
    value = back{
      if (model.isRentModel) rentPrices(currency, vendor.pricesIncludingGst)
    }
  }
  func bond: UMoney[currency]? = 0
  
  func model: IPriceModel
  func currency = model.currency
  func vendor: IVendor = model.vendor
  func product: IBaseProduct
  func gstRate: UPercent[2]? = product.gstRate(vendor).alt(0)
  back func salesPrice(currency: Currency, inclGst: Bool): Future[SalesPrice]
  back func salesAmount(currency: Currency, customer: ICustomer?, qty: UQty, inclGst: Bool): Future[SalesPrice] {
    default value = {
	  let p <- salesPrice(currency, inclGst)
      p.mul(product.toBase(qty))
    }
  }
  back func rentPrices(currency: Currency, inclGst: Bool): Future[[DurPrice]?] = {
    throw "Rent prices are not supported"
  }
  back func rentAmount(currency: Currency, customer: ICustomer?, itemsQty: UQty, dur: UQty?, nightsCount: UQty[Night]?, inclGst: Bool): Future[RentAmount] = {
    let rps <- rentPrices(currency, inclGst)
    if (let rp = rps.first) {
      if (let d1 = dur.unit?.as[DurUnit]?.defDur, let d2 = rp.unit.defDur) {
        RentAmount(dayAmount = rp.price * itemsQty * (dur*d1)/(rp.dur*d2))
      } else {
        RentAmount(dayAmount = rp.price * itemsQty * dur/rp.dur)
      }
    } else {
      RentAmount()
    }
  }
  view form
}

trait IStaticPrice extends AnyEntity with IPrice {
  impl func model: IStaticPriceModel
  impl field product: IBaseProduct by staticPrices {
    available(Ref).with(productCost)
  }
  func updateSalesPrice(salesPrice: SalesPrice): Bool = false
}

extend trait IVendor {
  field priceModels: [IPriceModel]? read by vendor  
  back func price(product: IBaseProduct): IPrice? = {
    priceModel(product)?.price(product)
  }
  
  back func priceModel(product: IBaseProduct): IPriceModel? = {
    priceModels.sortBy(priority).find(contains(product))
  }
}

extend entity IVendor {
  extend view vendorView = priceModels
}

extend trait IBaseProduct {
  compute salesPrice: SalesPrice? {
    availableFor = List
    value <- {
      if (let p = ourPrices.find(model.isSalesModel)) p.salesPrice
    }
    update = {
      updateSalesPrice(OurLegalEntity.ours.first!, new)
    }
  }
  compute rentPrices: [DurPrice]? {
    availableFor = List
    value <- {
      if (rentStatus == ForRent) {
        if (let p = ourPrices.find(model.isRentModel)) p.rentPrices
      }
    }
  }
  compute currentPrice: UMoney? {
    label = "Discounted Price"
    value = salesPrice?.current
    visible = !dynamicPrices
    update = {
      let sp = salesPrice.alt(SalesPrice())
      sp.current = new
      updateSalesPrice(OurLegalEntity.ours.first!, sp)
    }
  }
  compute regularPrice: UMoney? {
    label = "Sales Price"
    value = salesPrice?.regular
    visible = !dynamicPrices
    update = {
      let sp = salesPrice.alt(SalesPrice())
      sp.regular = new
      updateSalesPrice(OurLegalEntity.ours.first!, sp)
    }
  }
  func updateSalesPrice(le: OurLegalEntity, price: SalesPrice?): Void = {
    if (let sp = staticPrices.find(model.vendor == le && model.isSalesModel)) {
      if (let p = price) sp.updateSalesPrice(p)
      else sp.delete
    } else if (let price) {
      let pl = IPriceModel.ours*.as[IStaticPriceModel]
        .find(vendor == le && supports(this))
        .alt(PriceList(vendor = le))
      pl.createPrice(this).updateSalesPrice(price)
    }
  }
  
  compute ourPrices: [IPrice]? {
    value = prices.filter(vendor.is[OurLegalEntity])
  } 
  compute purchasePrices: [IPrice]? {
    value = prices.filter(!vendor.is[OurLegalEntity])
  }
  
  compute prices = staticPrices + dynamicPrices
  
  field staticPrices: [IStaticPrice]? inline by product
  
  compute dynamicPrices: [IPrice]? {
    availableFor = Inline
    value = back {
      IDynamicPriceModel.all
        .groupBy(vendor).agg(price = sortBy(b => b.priority).mapOpt(b => b.price(this)).first)
        .flatMap(price.toSeq)
    }
  }
  
  view baseMainView = grid(labels = Top) {
    //edit {
    //  row {regularPrice currentPrice space }
    //}
    salesPrice >> field(frame = false, visible = !ui.edit || dynamicPrices)
    edit{row{regularPrice currentPrice space}}
    rentPrices >> field(frame = false)
    ourPrices >> list(label = "Prices", visible = !OurLegalEntity.ours.onlyOne){ grid(labels = None) {
      row {vendor model}
      form >> field(frame = false)
	}}
  }
  view supplyView = grid(labels = Top, label = "Supply", visible = purchasePrices) {
    purchasePrices >> list(label = "Purchase Prices"){ grid(labels = None) {
      row {vendor model}
      form >> field(frame = false)
	}}
  }
}
