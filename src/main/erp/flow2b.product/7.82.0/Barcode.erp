extend trait ISubProduct {
  field barcodes: [Barcode]? inline by item
  
  func addBarcode(number: String): Barcode = {
    (try? barcodes!).find(b => b.number == number)
      .alt(Barcode(item = this, number = number, unit = (try? this.unit).alt(Item)))
  }  
  view labelView = labelView(salesPrice)
  view labelView(salesPrice: SalesPrice?) = y (newEngine, spacing = false, style = LabelPrint) {
    title  >> field(align = Center)
    x {label(" ", stretch = 1) salesPrice >> field(frame = false, align = Center) label(" ", stretch = 1)} 
    barcode >> field(barcode = barcodeFormat, width = 0.8, height = 0.25, fontSize = 0, align = Center)
  }

  compute barcode = barcodes.find(b => b.unit == this.unit)?.number.alt(number)
  compute barcodeFormat = if (let bc = barcodes.find(b => b.unit == this.unit)) bc.format else "CODE128"
}

trait IBarcode extends AnyEntity {
  func number: String
  func barcodeItem: RefEntity
  func unit: ProductUnit {
    default value = Item
  }
  func qty: UQty[unit] = 1.as[UQty[unit]]
  
  back object func find(number: String): IBarcode? = {
    if (let b = IBarcode.all.find(b => b.number == number).result.get) b
    else if (number.length == 12) {
      let n = "0$number"
      IBarcode.all.find(b => b.number == n).result.get
    }
  }
}

extend trait HasNumber extends IBarcode {
  impl func barcodeItem = this
}


inline entity Barcode extends IBarcode {
  impl func barcodeItem: RefEntity = item
  field item: ISubProduct by barcodes
  compute unitOne: UQty[unit] {
    label = "Unit"
    value = 1.as[UQty[unit]]
    canEditUnit
  } 
  impl field unit: ProductUnit {
    default = item.unit
    from = item.units
  }
  impl field number: String
  impl view form = x{unitOne number >> field(stretch = 1)}
  func format = if (number.length == 13 && number.toNum) "EAN13" else if (number.length == 12 && number.toNum) "UPC" else "CODE128"
}
