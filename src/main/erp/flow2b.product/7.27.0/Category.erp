extend trait ISuperProduct {
  field tags: [ProductTag]? by products 
  field categories: [ProductCategory]? by products {
    available(Inline).for(All)
  }
  field brands: [Brand]? by products {
    available(Inline).for(All)
  }
  
  ref field crossSells: [CrossSell]? by products {
    available(Inline).for(Ref)
  }
  func crossSellProducts: [ISuperProduct]? = (crossSells*.products - this).distinct
  field upsells: [ISuperProduct]? by downsells
  field downsells: [ISuperProduct]? by upsells
  
  view categoriesView = grid(label = "Categories"){
    categories
    brands
    tags
    crossSells
    upsells
    downsells
    kits
  }
}

entity ProductCategory {
  field photos: [Image]?
  field superCategory: ProductCategory? by subCategories
  field subCategories: [ProductCategory]? read by superCategory
  ref field name: String
  field description: Text?
  field products: [ISuperProduct]? by categories
  ref compute title = if (let s = superCategory) "$s / $name" else name
  func collectPhotos: [Image]? = {
    if (let p = photos) p
    else if (let p = products*.collectPhotos.first(1)) p
    else subCategories*.collectPhotos.first(1)
  }
  view form = nav{grid{
    photos
    superCategory
    name >> field(label = "" fontStyle = H1)
    subCategories >> list {field(title = _.name)}
    description
    products
  }}
}

entity Brand {
  field logo: Image? 
  field actors: [Actor]?
  field superBrand: Brand? by subBrands
  field subBrands: [Brand]? read by superBrand
  ref field name: String
  field description: Text?
  field products: [ISuperProduct]? by brands
  ref compute title = if (let s = superBrand) "$s / $name" else name

  view form = nav{grid{
    logo
    superBrand
    name >> field(label = "" fontStyle = H1)
    subBrands >> list {field(title = _.name)}
    actors
    description
    products
  }}
}

extend entity Actor {
  field brands: [Brand]? by actors
}

entity ProductTag {
  field photos: [Image]?
  field title: String
  field description: Text?
  field products: [ISuperProduct]? by tags
  view form = nav{grid{
    photos
    title >> field(label = "" fontStyle = H1)
    description
    products
  }}
}

entity CrossSell {
  ref field title: String
  field description: Text?
  field products: [ISuperProduct]? by crossSells

  view form = nav{grid{
    title >> field(label = "" fontStyle = H1)
    description
    products
  }}
}