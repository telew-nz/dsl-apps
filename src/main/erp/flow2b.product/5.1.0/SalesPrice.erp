class SalesPrice {
  field regular: Money?
  field current: Money?
  field isEmpty: Bool
  
  func mul(qty: Qty?): SalesPrice = {
    SalesPrice(regular = regular*qty, current = current*qty)
  }
  func add(o: SalesPrice): SalesPrice = {
    SalesPrice(regular = regular + o.regular, current = current + o.current)
  }
  object func sum(sps: [SalesPrice]?): SalesPrice = {
    if (let sps) {
      var r = sps.first
      sps.drop(1).foreach{s => r = r + s}
      r
    } else empty
  }
  func convertGst(gstRate: Percent?, priceInclGst: Bool, expectedInclGst: Bool): SalesPrice = {
    if (priceInclGst == expectedInclGst) this
    else SalesPrice(
      regular = convertGst(gstRate, regular, priceInclGst, expectedInclGst),
      current = convertGst(gstRate, current, priceInclGst, expectedInclGst) 
    )
  }
  
  object func convertGst(gstRate: Percent?, price: Money?, priceInclGst: Bool, expectedInclGst: Bool): Money? = {
    if (priceInclGst == expectedInclGst) price 
    else if (expectedInclGst) price*(gstRate.withoutUnit + 1.0000)
    else price/(gstRate.withoutUnit + 1.0000)
  }
  object func empty: SalesPrice = SalesPrice(regular = 0, current = 0, isEmpty = true)
  view form = x {
    regular >> field(fontStyle = if (current == regular) Normal else Strikeout)
    current >> field(visible = current != regular)
  }
}