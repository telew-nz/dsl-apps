trait IKit extends ISubProduct {
  func trackItems = null
  field items: [KitItem] inline by kit
  func products: [IProduct : Qty] = items.flatMap{i =>
    i.product.products.mapValues{ 
      _*i.qty
    }.toSeq
  }.toMap!
  ref compute productCost = {
    let costs = items.map{
      let pc = product.productCost
      (date = pc?.date, cost = pc?.cost*qty)
    }
    if (costs.all(date)) {
      ProductCost(date = costs.map(date!).min, cost = costs*.cost.sum)
    }
  }
  final func productItems: [ProductItem]? = []
}

entity Kit extends IBasicSubProduct with IKit with TWeightSize {
  func isTangible = true
  func trackItems = null
  field title: String
  field description: Text?
  field shortDescription: Text?
  field status: ProductStatus = default
  func skuPrefix = "KIT"
  
  view form = nav(onBarcode = if (ui.edit) addBarcode(barcode)){
    tabs {
      grid (labels = Top, label = "Main") {
        photos >> field(height = 10, label = "")
        statusView
        title >> field(label = "", fontStyle = H1)
        row {sku refNum space}
        items >> table {
          product >> field(width = 0.7)
          qty >> field(width = 0.3)
        }
        
        row {weight width height depth}
        barcodes
        productCost >> field(frame = false)
        baseMainView
        shortDescription
        extension("main")
        description
      }
      categoriesView
      extension("tabs")
      grid (labels = Top, label = "Other") {
        customerSuperView
        customerProductView
        extension
      }
    }
    extension("nav")
  }
}

inline entity KitItem {
  field kit: IKit by items
  field product: ISubProduct by includedInKitItems {
    available(Ref).with(unit, altUnits)
    update = {
      this.unit = new.unit
      this.qty = this.qty.as[Qty[this.unit]]!
    }
  }
  field unit: ProductUnit {
    from = product.units
  }
  field qty: Qty[unit] {
    default = 1
    canEditUnit = true
  }
}