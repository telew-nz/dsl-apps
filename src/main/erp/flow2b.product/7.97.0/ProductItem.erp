trait IProductItem extends RefEntity {
  var products: [IProduct]?
  func itemView: Layout
  func number: String
  
  view labelView = grid(labels = None, margin = Around, pageBreakAfter){
    title
    number >> field(barcode = "CODE128")
  }
}

entity ProductItem extends IProductItem with HasN {
  impl compute products: [IProduct]? {
    value = [product]
    update = {
      this.product = new*.as[IProductInv].first!
    }
  }
  
  ref field product: IProductInv by productItems
  object func map[R](product: IBaseProduct, qty: UQty, items: [IProductItem]?, f: (item: IProductItem?, trQty: UQty) => R): [R] = {
    if (product.trackItems == Always) {
      if (items.count != qty) throw "There should be $qty items but there $items"
   	  items!.map(f(_, 1.as[UQty[ProductUnit.Item]]))
    } else {
      [f(null, qty)]
    }
  }
  
  object func flatMap[R](product: IBaseProduct, qty: UQty, items: [IProductItem]?, f: (item: IProductItem?, trQty: UQty) => [R]?): [R]? = {
    if (product.trackItems == Always) {
      if (items.count != qty) throw "There should be $qty items but there $items"
   	  items!.flatMap(f(_, 1.as[UQty[ProductUnit.Item]]))
    } else {
      f(null, qty)
    }
  }
  
  object func distribute[R](product: IBaseProduct, qty: UQty, amount: UMoney?, items: [IProductItem]?, f: (item: IProductItem?, trQty: UQty, trAmount: UMoney?) => R): [R] = {
    if (product.trackItems == Always) {
      if (items.count != qty) throw "There should be $qty items but there $items"
      items!.distribute(amount, 1).map(f(item, 1.as[UQty[ProductUnit.Item]], value))
    } else {
      [f(null, qty, amount)]
    }
  }
  
  impl func numPrefix: String = "SN"
  object field gen: Int?
  impl func nextN = increment(ProductItem.gen)
  impl func title = "$product $(entityNumbers.mapOpt(i => i).join(' '))"
  ref field serialNum: String? {
    label = "Serial Number"
  }
  override func entityNumbers = [number, refNum, serialNum]
  
  impl view form = nav {
    grid(labels = Top) {
      product
      row{number refNum serialNum}
      extension
    }
    button("Print Label") {
      labelView.print
    }
    nfcWriteBtn
    extension("nav")
  } 
  
  impl view itemView = grid{
    _ >> field(label = "", title = number, visible = !ui.navEdit)
    row(visible = ui.navEdit){number refNum serialNum}
  }
  
  
}