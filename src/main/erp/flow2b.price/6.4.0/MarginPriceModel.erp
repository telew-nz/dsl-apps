interface IMargin {
  field regularMargin: Percent[2]?{
    default = 0.5
    update = {
      if (old == currentMargin) currentMargin = new
    }
  }
  field currentMargin: Percent[2]? {
    default = 0.5
  }
  
}

entity MarginPriceModel extends IDynamicPriceModel with IMargin {
  func priority: Dec[8]? = 1.0
  back func contains(product: IBaseProduct): Bool = product.superProduct.productCost
  back func price(product: IBaseProduct): IPrice? = {
    if (let pc = product.superProduct.productCost) {
      let sp = product.superProduct
      let m = productMargins.find(m => m.product == product || m.product == sp)?.as[IMargin]
        .or(categoryMargins.find(m => sp.categories.contains(m.category)))
        .alt(this)
      MarginPrice(model = this, product = product, productCost = pc, rounding = rounding, 
                  regularMargin = m.regularMargin, currentMargin = m.currentMargin)
    }
  }
  
  field rounding: Rounding = Round()
  field categoryMargins: [CategoryMargin]? inline by model
  field productMargins: [ProductMargin]? inline by model
  view form = nav{grid{
    vendor
    row{regularMargin currentMargin}
    rounding
    categoryMargins >> table {
      category regularMargin currentMargin
    }
    productMargins >> table {
      product regularMargin currentMargin
    }
  }}
  
  func title = "Margin Price Model" 
}
            
class MarginPrice extends IPrice with IMargin {
  field product: IBaseProduct
  field model: MarginPriceModel
  field productCost: ProductCost
  field rounding: Rounding

  func salesPrice(currency: Currency, inclGst: Bool): Future[SalesPrice] = {
    let regular = if (let c = productCost.cost, let p = 1.0000 - regularMargin) c/p 
    let current = if (let c = productCost.cost, let p = 1.0000 - currentMargin) c/p 
    let rp <- regular.to[Money[currency]](now)
    let cp <- {
      if (current != regular) {
        current.to[Money[currency]](now)
      } else Future(rp)
    }
    SalesPrice(regular = rp, current = cp)
      .convertGst(gstRate, !vendor.hasGst, inclGst)
      .round(rounding)
  }
  
  view form = grid {row{
    salesPrice >> field(frame = false)
    x (label = "Margin", width = 0.25, flow = false, visible = vendor.legalEntity.our) {
      regularMargin >> field(fontStyle = if (currentMargin == regularMargin) Normal else Strikeout, visible = currentMargin != regularMargin || ui.edit) 
      currentMargin
    }
  }}
}

inline entity CategoryMargin extends IMargin {
  field model: MarginPriceModel by categoryMargins
  field category: ProductCategory
}
inline entity ProductMargin extends IMargin {
  field model: MarginPriceModel by productMargins
  field product: IBaseProduct
}
