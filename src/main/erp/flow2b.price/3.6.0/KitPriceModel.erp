entity KitPriceModel extends PriceModel {
  func title = "Kit Price Model"
  func priority: Number? = 10000
  field discount: Percent[2]?
  func date: Date[day] = now
  func superProductPrice(superProduct: ISuperProduct): IPrice? = if (let kit = superProduct.as[Kit]){
    let prices = kit.products.toSeq.map {i =>
      if (let p = vendor.productPrice(i.#0)) KitItemPrice(price = p, qty = i.#1)
    }
    if (prices.all(_)) {
      KitPrice(vendor = vendor, kit = kit, prices = prices.map(_!), discount = discount.withoutUnit)
    }
  }
}

class KitPrice extends IPrice {
  field vendor: Actor
  field kit: Kit
  field prices: [KitItemPrice]
  field discount: Dec[4]?
  compute salesPrice: Future[Money?] = salesPrice(vendor.getCurrency, vendor.pricesIncludingGst)
  func superProduct: ISuperProduct = kit
  func salesPrice(currency: Currency, inclGst: Bool): Future[Money?] = {
    let ps <- prices.map{p => p.price.salesAmount(currency, customer = null, p.qty, inclGst)}.allCompleted
    ps.sum * (1.0000 - discount)
  }
  func salesAmount(currency: Currency, customer: LegalEntity?, qty: Qty, inclGst: Bool): Future[Money?] = {
    let ps <- prices.map{p => p.price.salesAmount(currency, customer, p.qty*qty, inclGst)}.allCompleted
    ps.sum * (1.0000 - discount)
  }
  func originalPrice(currency: Currency, inclGst: Bool): Future[Money?] = {
    let ps <- prices.map{p => p.price.originalAmount(currency, customer = null, p.qty, inclGst)}.allCompleted
    ps.sum * (1.0000 - discount)
  }
  func originalAmount(currency: Currency, customer: LegalEntity?, qty: Qty, inclGst: Bool): Future[Money?] = {
    let ps <- prices.map{p => p.price.originalAmount(currency, customer, p.qty*qty, inclGst)}.allCompleted
    ps.sum * (1.0000 - discount)
  }
  view form = salesPrice
}

class KitItemPrice {
  field price: IPrice
  field qty: Qty
}