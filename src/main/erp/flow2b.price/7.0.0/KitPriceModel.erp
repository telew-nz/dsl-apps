entity KitPriceModel extends IDynamicPriceModel {
  impl func title = "Kit Price Model"
  impl func priority: Dec[8]? = 0.5
  field discount: Percent[2]? {
    validate = if (discount > 1.0000) "Discount cannot be more than 100%"
  }
  func date: Date[day] = now
  impl func priceSales(product: IBaseProduct): IPriceSales? = if (let kit = product.as[Kit]){
    let prices = kit.products.toSeq.map {i =>
      if (let p = vendor.priceSales(i.#0)) KitItemPrice(price = p, qty = i.#1)
    }
    if (prices.all(_)) {
      KitPrice(model = this, kit = kit, prices = prices.map(_!), discount = discount.noUnit)
    }
  }
}

class KitPrice extends IPriceSales {
  impl field model: KitPriceModel
  field kit: Kit
  field prices: [KitItemPrice]
  field discount: Dec[4]?
  
  impl func product = kit
  impl func salesPrice(currency: Currency, inclGst: Bool): Future[SalesPrice] = {
    let ps <- prices.map{p => p.price.salesAmount(currency, customer = null, p.qty, inclGst)}.allCompleted
    SalesPrice.sum(ps).mul((1.0000 - discount).positive)
  }
  impl func salesAmount(currency: Currency, customer: ICustomer?, qty: UQty, inclGst: Bool): Future[SalesPrice] = {
    let ps <- prices.map{p => p.price.salesAmount(currency, customer, p.qty*qty, inclGst)}.allCompleted
    SalesPrice.sum(ps).mul((1.0000 - discount).positive)
  }
  impl view form = salesPrice >> field(frame = false)
}

class KitItemPrice {
  field price: IPriceSales
  field qty: UQty
}