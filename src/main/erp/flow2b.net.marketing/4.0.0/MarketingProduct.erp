entity MarketingProduct {
  field title: String
  field website: Website by products
  field status: PublicationStatus
  field readiness: Readiness
  field devProducts: [DevProduct]?
  field description: Text? 
  field advantages: [ProductAdvantage]? read by product
  field industries: [ProductIndustry]? read by product
  //field features: [MarketingFeature]? read by product
  func anchor = title.replace(" ", "_")
    
  func indexIndustry: ProductIndustry = ProductIndustry(
    product = this, 
    status = Published,
    advantages = advantages.map(ProductIndustryAdvantage(advantage = _))
    //features = features.map(ProductIndustryFeature(feature = _))
  )
    
  view form = nav{grid {
    website
    title
    status
    readiness
    devProducts
    description
    advantages >> list(editMode = AlwaysRefRead) {_}
    industries >> list(editMode = AlwaysRefRead) {_}
    //features >> list(editMode = AlwaysRefRead) {_}
  }}
}

entity ProductIndustry {
  func title = "$product: $industry"
  ref field product: MarketingProduct by industries
  ref field industry: Industry by marketingProducts
  field status: PublicationStatus

  field description: Text?
  func getDescription: Text? = description.or(product.description)
  field advantages: [ProductIndustryAdvantage]? inline by productIndustry
  //field features: [ProductIndustryFeature]? inline by productIndustry
  func headerHtml: Html = 
'<h2 class="ready-$(product.readiness)">$(product.title)</h1>
<div class="desc">$getDescription</div>'
    
  func html(header: Bool): Html = 
'<section class="product" id="$(product.anchor)">
  <div class="cnt">
    $(if (header) headerHtml else "")
    <ul class="advantages">$(advantages.filter(advantage.status == Published)*.html)</ul>
  </div>
</section>'
    
  view form = nav{grid {
    product
    industry
    status
    description
    advantages >> list{grid(labels = None){
      advantage
      screenshot
      description
    }}
    //features >> list{grid(labels = None){
    //  feature
    //  description
    //}}
  }}
}

extend entity Industry {
  field marketingProducts: [ProductIndustry]? read by industry
}

enum PublicationStatus {
  case Draft {background = "#999"}
  case Published {
    color = "white"
    background = "#090"
  }
}

enum Readiness {
  case Planned {background = "#ed9"}
  case InDevelopment {background = "#8bf"}
  case CommingSoon {background = "#dea"}
  case Production {
    color = "white"
    background = "#090"
  }
}
