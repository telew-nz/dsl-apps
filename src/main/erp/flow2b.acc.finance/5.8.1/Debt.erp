analytic Counterparty(dataType = "LegalEntity")

acctemplate Counterparty {
  key field counterparty: LegalEntity?
}
acctemplate InterDebts(type = singleAccount) {
  key field currency: Currency
  key field counterparty: LegalEntity?
}

class DebtItem(origin: Journal, amount: Money, dueDate: Date?) {
  compute overdueState: OverdueState = if (debtOverdue > 0) Overdue else NoOverdue
  compute debtOverdue: Duration[day]? {
    value = if (let d = dueDate) (Date.now - d).days.round(floor, 0)
  }
}

trait LDebt extends Ledger {
  func origin: Journal = journal  
  func counterparty: LegalEntity
  func debtItems: [DebtItem]?
}

report DebtRecon {
  singular = "Debt Reconciliation"
  field counterparty: LegalEntity
  view list = LMoneyDebt.all
  .filter(d => d.counterparty == this.counterparty && (d.amount < 0 || d.origin.unrecon || d.origin.debtAllowance))
  .sortBy(date.desc)
  .flatMap {d =>
    if (d.amount < 0) {
      if (let ua = d.origin.unreconAmount) [
        DebtReconRow(legalEntity = d.legalEntity, credit = d.origin, currency = d.currency, amount = -ua, debitMsg = "Unreconciled")
      ] +
      d.origin.debtOriginAmounts.map{ o =>
        DebtReconRow(legalEntity = d.legalEntity, credit = d.origin, currency = d.currency amount = o.amount, debit = o.origin, debitMsg = if (!o.origin) "Account Debt Allowance")
      }
    } else {
      if (let ua = d.origin.unreconAmount) [
        DebtReconRow(legalEntity = d.legalEntity, creditMsg = "Unreconciled", currency = d.currency, amount = ua, debit = d.origin)
      ] + if (let ua = d.origin.debtAllowance) [
        DebtReconRow(legalEntity = d.legalEntity, creditMsg = "Account Debt Allowance", currency = d.currency, amount = ua, debit = d.origin)
      ]
    }
  } >> nav{table {
    header{
      label("Legal Entity", width = 0.2, visible = !OurLegalEntity.onlyOne) 
      label("Debit", width = 0.4)
      label("Amount", width = 0.2, align = Right)
      label("Credit", width = 0.4)
    }
    body {
      legalEntity 
      y(label = "Debit", margin = None){x{debit >> field(title = title) debitAmount} debitMsg} 
      amount 
      y(label = "Credit", margin = None){x{credit >> field(title = title) creditAmount} creditMsg}
    }
  }}
}

class DebtReconRow {
  field legalEntity: OurLegalEntity
  field debit: DebtOrigin?
  field debitAmount: Money? = debit?.debtAmount
  field debitMsg: String?
  field credit: DebtOrigin?
  field creditAmount: Money? = -credit?.debtAmount
  field creditMsg: String?
  field currency: Currency
  field amount: Money[currency]?
}