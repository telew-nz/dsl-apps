trait FinanceAccount extends Entity {
  background = "#d68400" 
  func ledgerAccount: LedgerAccount
    
  object func onlyOne: FinanceAccount? = OurLegalEntity.ours*.accounts.onlyOne
  ref field legalEntity: OurLegalEntity by accounts 
  ref field currency: Currency
  func cashFlow: CashFlowCategory = Exchange
     
  ref compute balance: Money[currency]? = cashLedger?.balance
  system compute lastTransactionDate = cashLedger?.date
  system compute cashLedger: LCash? = LCash.current.find(financeAccount == this)
    
  object view refList = FinanceAccount.all.sortBy(lastTransactionDate.alt(Date.min).desc) >> list{grid(labels = None, width = 0.4){
    row{title >> field(width = 0.25) balance >> field(width = 0.15, align = Right)}
  }}
  object view list = FinanceAccount.all.sortBy(title) >> nav{list{grid(labels = None){
    row{title >> field(width = 0.7) balance >> field(width = 0.3, align = Right)}
  }}} 
   
  object view widget = FinanceAccount.all
    .filter(balance)
    .map{
      let baseBalance <- balance.to[Money[legalEntity.getCurrency]](now)
      (account = _, balance, baseBalance)
     }
     .sortBy(account.title)
     .total(baseBalance.sum) 
  >> nav(background = "#d68400", more = FinanceAccount.list.open){
    total {
      baseBalance >> field(align = Right)
      list(action = account.form.open){grid(labels = None){
         row{account.title >> field(width = 0.7) balance >> field(width = 0.3, align = Right)}
      }}}}
}

extend entity OurLegalEntity {
  field accounts: [FinanceAccount]? {label = "Finance Accounts"}
}
extend entity OurCompany {
  extend view form = accounts
}
extend entity OurPerson {
  extend view form = accounts
}
 
entity CashAccount extends FinanceAccount {
  background = "#d68400"
  func title = "$legalEntity: $name"
  ref field name: String
  func ledgerAccount: LedgerAccount = Cash
  field location: IStore? by cashAccounts
  view form = nav{grid{
    legalEntity
    name
    currency
    location
    balance
  }}
}

extend trait IStore {
  field cashAccounts: [CashAccount]? read by location
}
extend entity Store {
  extend view form = cashAccounts
}
  
entity BankAccount extends FinanceAccount {
  background = "#d68400"
  func title = "$legalEntity: $name"
  ref field name: String
  func ledgerAccount: LedgerAccount = Bank
  field number: String?
  view form = nav{grid{
    legalEntity
    name
    number
    currency
    balance
  }}
}

entity CurrentAccount extends FinanceAccount {
  background = "#d68400"
  func ledgerAccount: LedgerAccount = CurrentAccounts
  func cashFlow: CashFlowCategory = CurrentAccounts
  func title = "$legalEntity: $(name.alt(counterparty))"
  ref field counterparty: Actor
  ref field name: String?
  view form = nav{grid{
    legalEntity
    counterparty
    name
    currency
    balance
  }}
}

 