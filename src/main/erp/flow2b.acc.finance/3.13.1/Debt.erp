analytic Counterparty(dataType = "LegalEntity")
  
acctemplate Debt {
  key field currency: Currency
  key field counterparty: LegalEntity
  field origin: Journal?
}

acctemplate Counterparty {
  key field counterparty: LegalEntity?
}

class OriginAmount {
  field origin: Journal?
  field dueDate: Date?
  field amount: Money
  compute overdueState: OverdueState = if (debtOverdue > 0) Overdue else NoOverdue
  compute debtOverdue: Duration[day]? {
    value = if (let d = dueDate) (Date.now - d).days.floor(0)
  }
}

enum OverdueState {
  case NoOverdue {background = "#dea"}
  case Overdue {background = "#e99"}
}

trait DebtOrigin extends Journal {
  func debtDueDate: Date?
}

extend register LDebt {
  stored compute origins: [OriginAmount]? = {
    if (let p = prev?.origins.find(origin == this.origin)?.amount.as[Money[currency]]) {
      if (let rest = p + amount) prev?.origins.filter(origin != this.origin) + OriginAmount(origin = origin, amount = rest)
      else prev?.origins.filter(origin != this.origin)
    } else if (let a = amount) prev?.origins + OriginAmount(origin = origin, amount = a)
    else prev?.origins
  }
  func summary(date: Date, currency: Currency) = {
    let baseAmount <- balance.to[Money[currency]](date)
    let currency = currency
    let origins = origins.map(OriginAmount(origin = origin, dueDate = origin?.as[DebtOrigin]?.debtDueDate, amount = amount.as[Money[currency]]))
    (counterparty = counterparty, account = account, amount = balance, baseAmount = baseAmount, 
     origins = if (origins.all(!origin)) [] else origins
    )
  }
}


account AccountsReceivable(template = "Debt", number = 110, type = Asset)
account PrepaidExpenses(template = "Debt", number = 130, type = Asset)
account AllowanceForDoubtfulDebts(template = "Debt", number = 190, type = Asset, label = "Allowance for Doubtful Debts")
  
account AccountsPayable(template = "Debt", number = 210, type = Liability) 
account UnearnedRevenues(template = "Debt", number = 230, type = Liability)
  
account IntercompanyDebts(template = "Debt", number = 300, type = AssetOrLiability) 
account Loans(template = "Debt", number = 310, type = AssetOrLiability) 
account CurrentAccounts(template = "Cash", number = 320, type = AssetOrLiability) 
account BadDebts(template = "Counterparty", number = 670, type = Expense)

entity CurrentAccount extends FinanceAccount {
  background = "#d68400"
  func ledgerAccount: LedgerAccount = CurrentAccounts
  func title = "$legalEntity: $(name.alt(counterparty))"
  ref field counterparty: Actor
  ref field name: String?
  view form = nav{grid{
    legalEntity
    counterparty
    name
    currency
    balance
  }}
}

report Debt {
  background = "#d68400"
  field moneyOnly: Bool = true
  field debtType: LedgerAccountType? {from = [LedgerAccountType.Asset, LedgerAccountType.Liability]}
  field legalEntity: LegalEntity? {
    default = LegalEntity.ours.onlyOne
    from = LegalEntity.ours
  }
  field counterparty: LegalEntity?
  field currency: Currency?
  func baseCurrency = currency.or(legalEntity?.getCurrency).alt(Currency.our)
  field date: Date?
    
  view list = LDebt.at(date)
    .filter(
         balance
      && (!moneyOnly || (account != UnearnedRevenues && account != PrepaidExpenses))
      && legalEntity ==? this.legalEntity 
      && currency ==? this.currency 
      && account?.type ==? this.debtType
      && counterparty ==? this.counterparty
    )
    .map(summary(this.date.alt(Date.now), this.baseCurrency))
    .groupBy(counterparty).subtotal(amount = baseAmount.sum) 
    .total(amount = amount.sum)
   >> nav {
      total {
        row {label("Total"), space, amount}
        subtotal {
          row{counterparty, space, amount}
          subtotal {
            row (fontSize = 1) {account, space,  amount}
            table (action = origin?.form?.open) {
              origin?.title >> field(width = 0.5, label = "") 
              y(width = 0.35, label = "Due Date") {
                dueDate
                x {
                  overdueState >> field(visible = debtOverdue > 0)
                  debtOverdue >> field(visible = debtOverdue > 0)
                }
              }
              amount >> field(width = 0.3)
            }
          }
        }
      }
   }
}
