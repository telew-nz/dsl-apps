extend trait LegalEntity {
  view debtStatement {
    printable = true
    label = "Debt Statement"
    layout = nav{grid(labels = None){
      h1("Debt Statement")
      title
      debtReport(money = true)
    }}
  }
  back func unpaidAmount(origin: DebtOrigin): Money? = {
     LMoneyDebt.current
      .filter(balance && counterparty == this)
      .flatMap(debtItems)
      .filter(o => o.origin == origin)
     *.amount.agg(sum)
      .result.get
  }
  view debtReport(money: Bool) = LDebt.current
    .filter(balance  
       && money == account.money
       && counterparty == this)
    .flatMap(debtItems)
    .map{
      let baseAmount <- amount.to[Money[this.getCurrency]](Date.now)
      (origin, dueDate = dueDate, debtOverdue, amount, baseAmount)
    }
    .total(amount = baseAmount.sum)
  
    >> total {
      row(fontSize = 1) {label("Total"), space, amount}
      table (action = origin?.form?.open) {
        origin?.title >> field(width = 0.35, label = "") 
        x(width = 0.4, label = "Due Date") {
          dueDate?.date 
          debtOverdue >> field(label = "Overdue", visible = debtOverdue > 0)
          label ("overdue", fontStyle = Label, visible = debtOverdue > 0)
        }
        amount >> field(width = 0.25)
      }
    }
  func calcDebtBalance: Future[DebtBalance] = {
    let debt <- LMoneyDebt.current
      .filter(counterparty == this)
      .map(balance.to[Money[this.getCurrency]](now))
      .agg(sum)
    DebtBalance(currency = getCurrency, debt = debt, 
                debitAllowance = customer?.allowance, 
                creditAllowance = -vendor?.allowance)
  }
  compute debtBalance {
    value <- calcDebtBalance
    views = []
  }
}



enum ChargeType {
  case Cash {background = "#b6e2c6"}
  case Account {background = "#dec4ee"}
}

extend entity Customer {
  field allowance: Money[getCurrency]? {
    canEditUnit
    update = {
      if (new && !accountTerms) {
        let d <- CustomerAccountTerms.default
        accountTerms = d
      }
    }
    views = []
  }
  field accountTerms: CustomerAccountTerms? {
    views = []
    available(Inline)
    visible = allowance
    validate = if (allowance && !accountTerms) "Account terms should be selected"
  }
  compute debtBalance {
    value <- legalEntity.calcDebtBalance
    views = []
  }
  extend view form = extend {
    x(label = "Customer Account Debt Allowance"){allowance accountTerms}
    legalEntity.debtReport(money = true) >> field(label = "Money Debt")
    button("Reconcilation") {
      DebtRecon(counterparty = legalEntity).list.open
    }
    legalEntity.debtReport(money = false) >> field(label = "Delivery Debt")
  }
  func calcChargeType: ChargeType = if (allowance) Account else Cash
  ref compute chargeType: ChargeType {
    value = calcChargeType
    views = [] 
  }
  impl event onSave = {
    if (updated(allowance) || updated(accountTerms)) {
      DebtOrigin.all.filter(_.debtCounterparty == this.legalEntity).update(debtAllowance = 0)
      LMoneyDebt.current.filter(_.counterparty == this.legalEntity)*.touch
    }
  }
}


extend entity Vendor {
  field allowance: Money[getCurrency]? {
    canEditUnit
    views = []
    update = {
      if (new && !accountTerms) {
        let d <- CustomerAccountTerms.default
        accountTerms = d
      }
    }
  }
  field accountTerms: CustomerAccountTerms? {
    available(Inline)
    visible = allowance
    views = []
    validate = if (allowance && !accountTerms) "Account terms should be selected"
  }
  
  compute debtBalance {
    value <- legalEntity.calcDebtBalance
    views = []
  }
  
  extend view form = extend {
    x(label = "Account Debt Allowance"){allowance accountTerms}
    legalEntity.debtReport(money = true) >> field(label = "Money Debt")
    button("Reconcilation") {
      DebtRecon(counterparty = legalEntity).list.open
    }
    legalEntity.debtReport(money = false) >> field(label = "Delivery Debt")
  }
  func calcChargeType: ChargeType = if (allowance) Account else Cash
  ref compute chargeType: ChargeType {
    value = calcChargeType
    views = [] 
  }
  impl event onSave = {
    if (updated(allowance) || updated(accountTerms)) {
      DebtOrigin.all.filter(_.debtCounterparty == this.legalEntity).update(debtAllowance = 0)
      LMoneyDebt.current.filter(_.counterparty == this.legalEntity)*.touch
    }
  }
}
 
entity CustomerAccountTerms {
  impl field title: String
  field start: Date {
    default = Date.now.start(of = month) - 1_minute 
    label = "First statement date"
  }
  field stmUnit: DateUnit = month
  field stmEvery: Duration[stmUnit, 0] {
    canEditUnit
    default = 1_month
    label = "Statement every"
  }
  field dueUnit: DateUnit = day
  
  field dueIn: Duration[dueUnit, 0] {
    canEditUnit
    default = 20_day
    label = "Due in after the statement"
  }
  func dueDate(date: Date): Date = {
    period(date).end + dueIn
  }
  func period(date: Date): Period = {
    date.period(start, stmEvery)
  }
  
  func prevPeriod(date: Date): Period = {
    (date - stmEvery).period(start, stmEvery)
  }
  
  impl view form = nav{grid {
    title
    start
    stmEvery
    dueIn
  }}
  object func default: Future[CustomerAccountTerms?] = CustomerAccountTerms.all.first.result
}
