class OriginAmount extends HasDate {
  field date: Date
  field origin: DebtOrigin
  field amount: Money
}

enum OverdueState {
  case NoOverdue {background = "#dea"}
  case Overdue {background = "#e99"}
}

trait DebtOrigin extends Entity with Journal {
  func debtCurrency: Currency
  func debtAmount: Money[debtCurrency]?
  func debtCounterparty: LegalEntity?
  compute unreconAmount: Money[debtCurrency]? = debtAmount + reconAmount
  ref system stored compute reconAmount: Money[debtCurrency]? {
    value = back {
      debtLedgers*.backLinkAmounts.sum - fwdLinks*.backLinkAmount(this).sum
    }
  }
  func isPurchase: Bool
  func unrecon: Bool = unreconAmount
  func recon: Bool = !unreconAmount
  
  field manLinks: [DebtOrigin]? by manLinks {
    label = "Links"
    singular = "Link"
    plural = "Links"
    from = DebtOrigin.all.filter(o => o.debtCurrency == debtCurrency && o.debtCounterparty == debtCounterparty)
  }
  func mainOrigin: DebtOrigin = this
  func allOrigins: [DebtOrigin]? = [this]
  func allManLinks: [DebtOrigin]? = manLinks
  
  back func debtLedgers: [LMoneyDebt]? = []
  back func debtCashFlows: [CashFlowAmount]?
  const system field fwdLinks: [LMoneyDebt]? by backLinks {deleteCascade = false}
  back func debtOrigins: [DebtOrigin]? = fwdLinks*.origin + debtLedgers*.backLinks

  event onSave = {
    if (registerUpdated) {
      (allManLinks + origin(manLinks))*.allOrigins.filter(m => m.journalDate <= this.journalDate)*.debtLedgers*.touch
    }
  }
  compute debtOriginAmounts: [OriginAmount]? {
    availableFor = Inline
    value = back {
      fwdLinks.map{d => 
        OriginAmount(date = d.date, origin = d.origin, amount = -d.backLinkAmount(this).as[Money[d.currency]]!)
      } + debtLedgers.flatMap{l =>
        l.backLinks.zip(
          l.backLinkAmounts, 
          map = OriginAmount(date = l.date, origin = left, amount = right.as[Money[l.currency]]!)
        )
      }
    }
  }
  back func paymentCashFlows: [CashFlowAmount]? = {
    debtOriginAmounts.flatMap{d => 
      if (let cfs = d.origin.debtCashFlows) {
        cfs
          .distribute(d.amount, by = _.amount)
          .map(CashFlowAmount(cashFlow = item.cashFlow, amount = value!))          
      } else {
        [CashFlowAmount(cashFlow = Errors, amount)]
      }
    }//.groupBy(cashFlow).agg(CashFlowAmount(cashFlow = key, amount.sum!)).values
  }
 
}