class OriginAmount extends HasDate {
  impl field date: Date
  field origin: DebtOrigin?
  field amount: Money
  
  impl func toString = "OriginAmount($amount $date $origin)"
}

enum OverdueState {
  case NoOverdue {background = "#dea"}
  case Overdue {background = "#e99"}
}

trait DebtOrigin extends Journal {
  func debtCurrency: Currency
  func debtAmount: Money[debtCurrency]?
  func debtCounterparty: LegalEntity?
  func isLowPriorityDebt = false
  func isPurchase: Bool
  func isInstant: Bool //No debt account has been used to pay taxed amount

  impl func canRead = debtCounterparty?.isTheir
  
  
  ref system stored compute debtAllowance: Money[debtCurrency]?
  func noCpTitle {
    default value = title
  }
  
  compute calcDebtAllowance: Money? = {
    if (front) {
      if (id) debtAllowance 
      else if (let db = debtCounterparty?.debtBalance(legalEntity, debtCurrency)) {
        db.debtAllowance(debtAmount)
      }
    } else {
      debtAllowance
    }
  }
  
  compute debtUnpaidAmount: Money[debtCurrency]? = debtAmount + reconAmount - debtAllowance

  compute unreconAmount: Money[debtCurrency]? = debtAmount + calcReconAmount
  compute calcReconAmount: Money? = {
    if (front) {
      if (id) reconAmount 
      else try? calcDebtAllowance!
    } else reconAmount
  }
  ref system stored compute reconAmount: Money[debtCurrency]? {
    value = back {
      debtLedgers*.backLinkAmounts.sum - fwdLinks*.backLinkAmount(this).sum + debtAllowance
    }
  }
  func unrecon: Bool = unreconAmount
  func recon: Bool = !unreconAmount
  
  field manLinks: [DebtOrigin]? by manLinks {
    label = "Links"
    singular = "Link"
    plural = "Links"
    from = DebtOrigin.all.filter(o => o.debtCurrency == debtCurrency && o.debtCounterparty == debtCounterparty)
  }
  func mainOrigin: DebtOrigin = this
  func allOrigins: [DebtOrigin]? = [this]
  
  //By default no man links, a separate app is required
  object func considerManLinks = false
  func allManLinks: [DebtOrigin]? = [] 
  
  back func debtLedgers: [LMoneyDebt]? = []
  back func debtCashFlows: [CashFlowAmount]?
  const system field fwdLinks: [LMoneyDebt]? by backLinks {deleteCascade = false}
  back func debtOrigins: [DebtOrigin]? = fwdLinks*.origin + debtLedgers*.backLinks

  //impl event onSave = {
  //  if (registerUpdated) {
  //    (allManLinks + origin(manLinks))*.allOrigins.filter(m => m.journalDate <= this.journalDate)*.debtLedgers*.touch
  //  }
  //}
  compute debtOriginAmounts: [OriginAmount]? {
    availableFor = Inline
    value = back {
      fwdLinks.map{d => 
        OriginAmount(date = d.date, origin = d.origin, amount = -d.backLinkAmount(this).as[Money[d.currency]]!)
      } + debtLedgers.flatMap{l =>
        l.backLinks.zip(l.backLinkAmounts)
          .map(OriginAmount(date = l.date.max(left.journalDate), origin = left, amount = right.as[Money[l.currency]]))
      } + if (let debtAllowance = calcDebtAllowance) [OriginAmount(date = journalDate, amount = debtAllowance)]
    }
  }
  back func paymentCashFlows: [CashFlowAmount]? = {
    debtOriginAmounts.flatMap{d => 
      if (let o = d.origin) {
        if (let cfs = o.debtCashFlows) {
          cfs
            .distribute(d.amount, by = _.amount)
            .map(CashFlowAmount(cashFlow = item.cashFlow, amount = value!))          
        } else {
          if (let debtAmount) [CashFlowAmount(cashFlow = Errors, amount = debtAmount)]
        }
      } else {
        if (let debtAmount) [CashFlowAmount(cashFlow = Debts, amount = debtAmount)]
      }
    }//.groupBy(cashFlow).agg(CashFlowAmount(cashFlow = key, amount.sum!)).values
  }
 
}

class DebtBalance(currency: Currency, balance: Money[currency]?, balanceDue: Money[currency]?, creditAllowance: Money[currency]?, debitAllowance: Money[currency]?) {
  func allowedDebit: Money[currency]? = (debitAllowance - balanceDue).max(0)
  func allowedCredit: Money[currency]? = (creditAllowance - balanceDue).min(0)
  func debtAllowance(amount: Money?): Money[currency]? = {
    if (amount.unit == currency) {
      if (amount < 0) -amount.max(allowedCredit)
      else -amount.min(allowedDebit)
    }
  }
}