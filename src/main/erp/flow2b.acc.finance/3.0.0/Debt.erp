analytic Counterparty(dataType = "LegalEntity")
analytic Origin(dataType = "DebtOrigin")
  
trait DebtOrigin extends Journal
acctemplate Debt {
  key field currency: Currency
  key field counterparty: LegalEntity
  field origin: DebtOrigin?
}

acctemplate Counterparty {
  key field counterparty: LegalEntity?
}

class OriginAmount {
  field origin: DebtOrigin?
  field amount: Money
}

extend register LDebt {
  stored compute origins: [OriginAmount]? = {
    if (let p = prev?.origins.find(origin == this.origin)?.amount.as[Money[currency]]) {
      if (let rest = p + amount) prev?.origins.filter(origin != this.origin) + OriginAmount(origin = origin, amount = rest)
      else prev?.origins.filter(origin != this.origin)
    } else if (let a = amount) prev?.origins + OriginAmount(origin = origin, amount = a)
    else prev?.origins
  }
}

account AccountsReceivable(template = "Debt", number = 110, type = Asset)
account PrepaidExpenses(template = "Debt", number = 130, type = Asset)
account NotesReceivable(template = "Debt", number = 150, type = Asset)
account AllowanceForDoubtfulDebts(template = "Debt", number = 190, type = Asset, label = "Allowance for Doubtful Debts")
  
account AccountsPayable(template = "Debt", number = 210, type = Liability) 
account UnearnedRevenues(template = "Debt", number = 230, type = Liability)
account NotesPayable(template = "Debt", number = 250, type = Liability) 
  
account BadDebts(template = "Counterparty", number = 570, type = Expense)
  
report Debt {
  field moneyOnly: Bool = true
  field debtType: LedgerAccountType? {from = [LedgerAccountType.Asset, LedgerAccountType.Liability]}
  field legalEntity: LegalEntity? = (LegalEntity.ours.onlyOne) {from = LegalEntity.ours}
  field counterparty: LegalEntity?
  field currency: Currency?
  func baseCurrency = currency.or(legalEntity?.getCurrency).alt(Currency.our)
  field date: Date?
    
  view list = LDebt.at(date)
    .filter(
         balance
      && (!moneyOnly || (account != UnearnedRevenues && account != PrepaidExpenses))
      && legalEntity ==? this.legalEntity 
      && currency ==? this.currency 
      && account?.type ==? this.debtType
      && counterparty ==? this.counterparty
    )
    .map{i =>
      let baseAmount <- i.balance.to[Money[baseCurrency]](this.date.alt(Date.now))
      let currency = i.currency
      let origins = i.origins.map(OriginAmount(origin = origin, amount = amount.as[Money[currency]]))
      (i.counterparty, i.account, amount = i.balance, baseAmount, origins = if (origins.all(!origin)) [] else origins)
    }
    .groupBy(counterparty).subtotal(amount = baseAmount.sum) 
    .total(amount = amount.sum)
   >> nav {
      total {
        row {label("Total"), amount}
        subtotal {
          row{counterparty, amount}
          subtotal {
            row (fontSize = 1) {account,  amount}
            table (action = origin?.form?.open) {
              origin?.title >> field(width = 0.7) amount >> field(width = 0.3)
            }
          }
        }
      }
   }
}
