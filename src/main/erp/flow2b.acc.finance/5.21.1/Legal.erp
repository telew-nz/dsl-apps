trait LegalStructure extends InlineEntity {
  field company: OurCompany by legalStructure
  func currency = company.getCurrency
  func specificAccounts: [LedgerAccount]? = []
  func canCloseIncomeSummary: Bool
  func incomeSummaryClosing(journal: Journal, balance: Money[currency]): [Ledger]?
  func title = company.title
  func gstBasis: GstBasis?
}

enum GstBasis {
  case Payment
  case Invoice
}
 
extend trait LegalEntity {
  func legalName: String = legalInfo.name.alt(title)
  func legalInfo: LegalInfo
  field altNames: [String]? {
    label = "Alternative Names"
    singular = "Name"
  }
  func isName(name: String?): Bool = if (let n = name.trim.lower) {
    title.lower == n || legalInfo.name.lower == n || altNames*.lower.contains(n)
  } else {
    false
  }
  func hasNameIn(in: String?): Bool = if (let in) {
    (title.length >= 3 && in.containsIgnoreCase(title)) || 
    (if (let n = legalInfo.name) n.length >= 3 && in.containsIgnoreCase(n)) || 
    altNames.exists(n => n.length >= 3 && in.containsIgnoreCase(n))
  }
  func addName(name: String?): Void = if (let name) {
    if (!isName(name)) {
      altNames = altNames + [name]
      save
    }
  }
  back object func filterByName(name: String?): [LegalEntity]? = if (let name) {
    LegalEntity.all.filter(isName(name)).result.get
  }
  back object func findNames(in: String?): [LegalEntity]? = if (let in) {
    LegalEntity.all.filter(hasNameIn(in)).result.get
  }
}
  
class LegalInfo {
  field name: String? {label = "Legal Name"}
  field address: Address? 
  view gstView = group {
  }
  view gstCustomerView = group {
  }
  impl view form = grid{
    name 
    address
  }
}

