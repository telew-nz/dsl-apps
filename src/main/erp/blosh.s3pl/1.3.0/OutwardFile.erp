extend entity Malcolm3plIntegration {
  func outwardFile(sd: SalesDelivery) = doOutwardFile(sd)
  func transferOutwardFile(tr: Transfer) = doOutwardFile(tr)
  func doOutwardFile(sd: IOutgoingDelivery): Future[File] = {
    let a = sd.pathDst!.locationAddress!
    let so = sd.as[SalesDelivery]?.order?.as[SalesOrder]
    let r = sd.deliveryReceiver!
    let txt = "OrderNo,OrderDate,DeliverTo,DeliverStreet,DeliverSuburb,DeliverCity,CustomerContact,CustomerEmail,CustomerOrderNo,ProductCode,ProductDescription,Qty,UnitCode,DeliveryInstructions\n" +
      sd.items*.as[IDeliveryPItem].filter(product.isTangible).flatMap{i =>
        i.product.productQtys(i.qty).map {j =>
          let city = "$(a?.city) $(a?.postcode)".trim
          let comment = sd.activities.first?.comment
          let to = (a.person + " " + a.company).trim.alt(r.title)
          let s: Csv = "$(sd.number),$(sd.dispatchDate.timezone('NZ').format('dd/MM/yyyy')),$to,$((a.number + ' ' + a.street).trim),$(a.suburb),$city,$r,$(r.contacts*.as[EmailContact].first?.email),$(so?.referenceNumber.alt(sd.number)),$(j.product.refNum),$(j.product.title),$(j.qty.withoutUnit),EA,$comment"
          s
        }
      }.join("\n").as[Csv]
    FileGeneric(name = "Order $(sd.number).csv", content = txt.toData)
  }
}