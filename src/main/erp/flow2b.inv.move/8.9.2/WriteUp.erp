entity InventoryWriteUp extends IProdQtyItem with HasProductItemsInline with StockCorrection with IBaseIncomingDelivery with IDeliveryPItem {
  singular = "Write Up"
  impl func deliveryStatus: DeliveryStatus = Delivered
  impl ref field product: IProduct {
    from = IProduct.all.filter(_.isTangible)
    available(Ref).with(unit, altUnits)
    update = {
      productUpdated()
    }
  }
  impl func needProductItems: Bool = true
  field stockType: StockType = Inventory
  override func stockTypeDst = stockType
  override func stockTypeSrc = null
  compute price: UMoney[currency]? {
    value = amount/qty
    update = {
      amount = qty*new
    }
  }
  impl func dispatchDate = date
  impl func deliveryDate = date
  field amount: UMoney[currency]?
  override func currency = legalEntity.getCurrency
  impl func deliverySupplier = legalEntity.legalEntity
  impl func deliveryReceiver = legalEntity.legalEntity
  
  impl compute number = "WU$n"
  impl func title: String = "$number $product"
  override func journalDate: Date = date
  impl func deliverySrc = null
  impl func deliveryDst = store
  impl func items: [IDeliveryItem]? = [this]
  impl func itemDelivery: IDelivery? = this
  func outcomeStockReg: ProdReg? = stock
  
  compute baseQty: UQty[product.unit] = product.toBase(qty)
  register stock: ProdReg = ProdReg(
    legalEntity = legalEntity, store = store, product = product,
    stockType = stockType, date = date, doc = this,
    qty = baseQty
  )
  register invLedgers = {
    ProductItem.distribute(product, baseQty, amount, productItems) {
      stockType.obj.new(
         journal = this, product = product, productItem = item, 
         qty = trQty, trAmount = trAmount,
         contraAccount = InventoryAdjustments
      )
    }
  }
  
  impl view form = nav{
    grid {
      date
      number
      refNum
      legalEntity
      store
      product
      stockType
      qty >> field(canEditUnit = true)
      price
      amount
      snView
    }
    generalLedgerButton
  }
  impl object view list = InventoryWriteUp.all >> nav{list{grid(labels = None){
    row{fullNum date legalEntity store}
    row{product qty price amount}
  }}}
}
  
