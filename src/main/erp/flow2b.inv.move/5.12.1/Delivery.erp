trait IDelivery {
  background = "#2e8e67"
  func isForCurrent(role: Role): Bool = isFor(User.current, role)
    
  func number: String
  func legalEntity: LegalEntity
  func deliverySupplier: LegalEntity
  func deliveryReceiver: LegalEntity
  func deliveryStatus: DeliveryStatus = status
  func deliveryStore: Store? = null
  func deliverySource: Store?
  func items: [IDeliveryItem]?
  func hasInTransit = true 
  field owner: Actor?
  ref field dispatchDate: Date {
    default = now
    dynamicLabel = dispatchDateLabel
    update = {
      if (new > deliveryDate || !hasInTransit) deliveryDate = new
    }
  }
  func dispatchDateLabel = if (this.hasInTransit) "Dispatch Date" else "Date"
  ref field deliveryDate: Date {
    default = now
    isValid = deliveryDate >= dispatchDate
    dynamicLabel = deliveryDateLabel
    visible = hasInTransit
    update = {
      if (new < dispatchDate && status == Planned) dispatchDate = new
    }
  }
  func deliveryDateLabel = "Delivery Date"
  ref field status: DeliveryStatus {
    default = Planned
    update = {
      if (new == InTransit) {
        dispatchDate = now
        if (dispatchDate > deliveryDate) deliveryDate = dispatchDate
      } else if (new == Delivered) {
        if (!hasInTransit || old == Planned) dispatchDate = now
        deliveryDate = now
      }
    } 
    from = {
      let v = if (hasInTransit) DeliveryStatus.all! else [DeliveryStatus.Planned, DeliveryStatus.Delivered, DeliveryStatus.Canceled]
      if (canCancel) v.toSet else v.filter(_ != DeliveryStatus.Canceled)!.toSet
    }
  }
  func canCancel: Bool = true
  func dueDate: Date? = if (status == Planned) dispatchDate else if (status == InTransit) deliveryDate
  ref compute overdueState: DeliveryOverdueState {
    value = {
      if (status == Delivered) Delivered
      else if (dueDate && Date.now > dueDate!) DeliveryOverdue
      else NoDeliveryOverdue
    } 
    visible = overdueState == DeliveryOverdue
  }
  func hasInvIssues = overdueState == DeliveryOverdue && !isBeforeInit
  func isBeforeInit = deliveryStore && deliveryStore!.initialDate(legalEntity) ?> deliveryDate
  func active = status == Planned || status == InTransit
}

enum DeliveryStatus {
  case Planned {
    background = "#ed9"
  }
  case InTransit {
    background = "#dea"
  }
  case Delivered {
    color = "white"
    background = "#090"
  }
  case Canceled {
    background = "#ddd"
  }
}

inline trait IDeliveryItem {
  func itemDelivery: IDelivery?
  
  func isAvailableNow: Bool = true
  func isUnavailable = !isAvailableNow
  func awaitingForDate: Date? = null
  func availablePeriods: [Period]? = if (let d = awaitingForDate) [Period(start = d, end = Date.max)]
}


