trait IFProductItems extends InlineEntity {
  func product: ISubProduct
  func needProductItems: Bool
  func unit: ProductUnit
  func itemsQty: UQty[unit]?
  func productItems: [IProductItem]?
 
  front func printLabels(): Void = {
    labelsView.print()
  }
  func labelsQty: UQty[unit]? = itemsQty
  view labelsView = allProductItems >> list(frame = false){
    form >> field(pageBreakAfter)
  }
  compute allProductItems: [ProdProdItem]? = allProductItems(productItems.drop(itemsQty - labelsQty), labelsQty)
  func allProductItems(productItems: [IProductItem]?, qty: Qty?): [ProdProdItem]? = if (qty > 0) {
    ProdProdItem(product = product, unit = unit, item = if (let p = productItems.first) p) + allProductItems(productItems.drop(1), qty - 1.as[Qty[qty.unit!]])
  }
}
  
trait FProductItems extends IFProductItems {  
  impl field productItems: [IProductItem]? {
    from = product.productItems
    visible = productItems || product.trackItems
    validate = if (product.trackItems == Always
                   && productItems.count != itemsQty
                   && needProductItems) {
      "Product Items are required for all movements of $product"
    }
    init = {
     new.products = [product.as[IProduct]!]
    }
  }
  view snView = productItems
  
}

trait FProductItemsInline extends IFProductItems {
  impl field productItems: [IProductItem]? inline {
    init = {
      new.products = [product.as[IProduct]!]
    }
    visible = productItems || product.trackItems
    validate = if (product.trackItems == Always
                   && productItems.count != itemsQty
                   && needProductItems) {
      "Product Items are required for all movements of $product"
    }
  }
  view snView = productItems >> list{
    itemView
  }
}

class ProdProdItem(product: ISubProduct, unit: ProductUnit, item: IProductItem?) {
  impl func form = if (let i = item) i.labelView else product.labelView
}

