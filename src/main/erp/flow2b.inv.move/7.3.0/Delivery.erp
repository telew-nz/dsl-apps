trait IDelivery extends Journal {
  background = "#2e8e67"
  func isForCurrent(role: Role): Bool = isFor(User.current, role)
       
  func number: String
  func legalEntity: OurLegalEntity
  func deliverySupplier: LegalEntity?
  func deliveryReceiver: LegalEntity?
  func deliverySrc: IStore?
  func deliveryDst: IStore?
  func canBeDeliveredInstantly(store: IStore): Bool = true
  func items: [IDeliveryItem]?
  func hasInTransit = true 
  func dispatchDate: Date
  func deliveryDate: Date
  func deliveryStatus: DeliveryStatus
  func pathState: DeliveryStatus = deliveryStatus
  
  func canCancel: Bool = true
  func dueDate: Date? = if (deliveryStatus == Planned) dispatchDate else if (deliveryStatus == InTransit) deliveryDate
  ref compute overdueState: DeliveryOverdueState {
    value = {
      if (deliveryStatus == Delivered) Delivered
      else if (dueDate && Date.now > dueDate!) DeliveryOverdue
      else NoDeliveryOverdue
    } 
    visible = overdueState == DeliveryOverdue
  }
  func hasInvIssues = overdueState == DeliveryOverdue && !isBeforeInit
  func isBeforeInit = (deliverySrc?.initialDate(legalEntity) ?> deliveryDate) && (deliveryDst?.initialDate(legalEntity) ?> deliveryDate)
  func active = deliveryStatus.active
}

trait IDeliveryPath extends IDelivery with RoutePath with HasTracking {
  ref field dispatchDate: Date {
    default = defaultDate
    dynamicLabel = dispatchDateLabel
    update = {
      if (new > deliveryDate || !hasInTransit) deliveryDate = new
      updatedDispatchDate()
    }
  } 
  func updatedDispatchDate(): Future[Void] = {
  }
  func dispatchDateLabel = if (this.hasInTransit) "Dispatch Date" else deliveryDateLabel
  ref field deliveryDate: Date {
    default = defaultDate
    validate = if (deliveryDate < dispatchDate) "Delivery Date should be more or equal Dispatch Date"
    dynamicLabel = deliveryDateLabel
    visible = hasInTransit
    update = {
      if (new < dispatchDate && status == Planned) dispatchDate = new
      updatedDeliveryDate()
    }
  }
  
  func updatedDeliveryDate(): Future[Void] = {
  }
  func pathCanBeExecuted = !freightCompany
  func defaultDate: Date = now
  func deliveryDateLabel = "Delivery Date"
  func deliveryStatus: DeliveryStatus = status
  ref field status: DeliveryStatus {
    default = Planned
    update = {
      if (new == InTransit) {
        dispatchDate = now
        if (dispatchDate > deliveryDate) deliveryDate = dispatchDate
      } else if (new == Delivered) {
        if (!hasInTransit || old == Planned) dispatchDate = now
        deliveryDate = now
      }
    } 
    from = {
      let v = if (hasInTransit) DeliveryStatus.all! else [DeliveryStatus.Planned, DeliveryStatus.Delivered, DeliveryStatus.Canceled]
      if (canCancel) v.toSet else v.filter(_ != DeliveryStatus.Canceled)!.toSet
    }
  }
}

enum DeliveryStatus(active: Bool) {
  case Planned(active = true) {background = "#ed9"}
  case InTransit(active = true) {background = "#dea"}
  case Delivered(active = false) {
    color = "white"
    background = "#090"
  }
  case Canceled(active = false) {background = "#ddd"}
}

interface IDeliveryItem {
  func isAvailableNow: Bool = true
  func isUnavailable = !isAvailableNow
  func awaitingForDate: Date? = null
  func availablePeriods: [Period]? = if (let d = awaitingForDate) [Period(start = d, end = Date.max)]
}

interface IDeliveryPItem extends IDeliveryItem {
  func itemDelivery: IDelivery?
  func product: ISubProduct
  func isTangible: Bool = product.isTangible
  func unit: ProductUnit
  func qty: Qty[unit]
  func active: Bool
  func stockItem = this.as[AnyEntity]
  
  compute baseQty: Qty[product.unit] = product.toBase(qty)  
}


