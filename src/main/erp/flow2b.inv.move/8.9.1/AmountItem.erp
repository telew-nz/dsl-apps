trait IAmountItem extends InlineEntity {
  func currency: Currency
  func pricesIncludingGst: Bool
  func considerGst: Bool
  func defaultGstRate: UPercent[2]?? = null
   
  field sAmount: UMoney[currency]?
  compute gstRateK  = 1.0000 + gstRate.alt(0).withoutUnit
  field storedWithGst: Bool {
    default = pricesIncludingGst
    update = {
      if (new) {
        sAmount = sAmount*gstRateK
        sDiscount = sDiscount*gstRateK
      } else {
        sAmount = sAmount/gstRateK
        sDiscount = sDiscount/gstRateK
      }
    }
  }
  compute amountNoGst: UMoney[currency]? {
    value = if (storedWithGst) sAmount/gstRateK else sAmount
    update = {
      storedWithGst = false
      sAmount = new
    }
  }
  compute amountWithGst: UMoney[currency]? {
    value = if (storedWithGst) sAmount else sAmount*gstRateK
    update = {
      storedWithGst = true
      sAmount = new
    }
  }
  compute amount = if (considerGst) amountNoGst else amountWithGst
  
  compute amountEditor: UMoney[currency]? {
    label = "Amount"
    value = if (pricesIncludingGst) amountWithGst else amountNoGst
    update = {
      if (pricesIncludingGst) this.amountWithGst = new 
      else this.amountNoGst = new
    }
  }
  compute gstAmount: Money[currency]? = {
    amountWithGst - amountNoGst - discountWithGst + discountNoGst
  }
  
  func allowNegativeTotal = false
  field sDiscount: UMoney[currency]? {
    validate = if (!allowNegativeTotal && sDiscount > sAmount) "Discount cannot be more than amount"
  }
  compute discountNoGst: UMoney[currency]? {
    value = if (storedWithGst) sDiscount/gstRateK else sDiscount
    update = {
      storedWithGst = false
      sDiscount = new
    }
  }
  compute discountWithGst: UMoney[currency]? {
    value = if (storedWithGst) sDiscount else sDiscount*gstRateK
    update = {
      storedWithGst = true
      sDiscount = new
    }
  }
  compute discountEditor: UMoney[currency]? {
    label = "Discount"
    value = if (pricesIncludingGst) discountWithGst else discount
    update = {
      if (pricesIncludingGst) this.discountWithGst = new 
      else this.discountNoGst = new
    }
  }
  compute discount: UMoney[currency]? = if (considerGst) discountNoGst else discountWithGst
  
  compute totalWithGst: Money[currency]? {
    value = amountWithGst - discountWithGst
  }
  compute totalNoGst: Money[currency]? {
    value = amountNoGst - discountNoGst
  }
  compute total: Money[currency]? {
    value = if (pricesIncludingGst) totalWithGst else totalNoGst
  }
  func total(withGst: Bool): Money[currency]? = {
    if (withGst) totalWithGst else totalNoGst
  }
  
  
  field gstRate: UPercent[2]?? = try? defaultGstRate!
  compute gstValue: UPercent[2]? {
    label = "GST Rate"
    value = gstRate.alt(0).as[UPercent[2]]
    update = {
      updateGst(new)
    }
    visible = hasGst
  }
  compute hasGst: Bool {
    value = gstRate
    update = {
      updateGst(if (new) 0 else null)
    }
  }
  func updateGst(new: UPercent[2]??): Void = {
    if (storedWithGst || pricesIncludingGst) {
      sAmount = amountWithGst
      sDiscount = discountWithGst
      storedWithGst = true
    }
    gstRate = new
  }
  
  view gstEditor = x(label = if (pricesIncludingGst) "Including GST" else "Excluding GST", flow = false) {
    hasGst >> field(label = "", stretch = 1) gstValue >> field(label = "Rate", align = Right)
  }
}

trait IAmountQtyItem extends IAmountItem {
  func qty: UQty
  compute priceNoGst: UMoney[currency]? {
    value = amountNoGst/qty
    update = {amountNoGst = new*qty}
  }
  compute priceWithGst: UMoney[currency]? {
    value = amountWithGst/qty
    update = {amountWithGst = new*qty}
  }
  compute priceEditor: UMoney[currency]? {
    label = "Price"
    value = if (pricesIncludingGst) priceWithGst else priceNoGst
    update = {
      if (pricesIncludingGst) this.priceWithGst = new 
      else this.priceNoGst = new
    }
  }
}

trait IProdQtyItem extends InlineEntity {
  func product: IBaseProduct
  field unit: ProductUnit {
    default =? (try? product)?.unit
    from = product.units    
  }
  func pricesIncludingGst = true
  func considerGst = true
  func currency: Currency = default
  field qty: UQty[unit] {
    canEditUnit
    default = 1
    label = "Quantity"  
  }
  func productUpdated(): Void = {
    this.silentUpdate(unit, product.unit)
    this.qty = this.qty.as[UQty[this.unit]]!
  }
}

trait IProdAmountQtyItem extends IProdQtyItem with IAmountQtyItem {
  func productUpdated(): Void = {
    super.productUpdated()
    updateGstRate()
  }
  func updateGstRate(): Void 
}