trait IProdMovePath extends RefEntity with HasTracking {
  func pathStart: Date
  func pathSrc: ILocation?
  func pathDst: ILocation?
  func pathEnd: Date
  var pathState: PathState
  func pathCanDo: Bool = true
  func pathItems: [IProdMoveItem]?
  
  func isPathOur = !freightCompany && pathSrc && pathDst && pathSrc != pathDst
  func isPathToDo = pathCanDo && isPathOur
  
  field route: IRoute? by items
}



trait IRoute extends Entity {
  background = "#2e8e67"
  object field gen: Int?
  field items: [IProdMovePath]? by route {
    available(List)
    from = IProdMovePath.all.filter(pathState.active && isPathOur).sortBy(!pathCanDo, pathStart)
  }
}

trait FPathDates extends IProdMovePath {
  func defaultDate: Date = now
  
  impl func pathStart = dispatchDate
  impl func pathEnd = deliveryDate

  ref field dispatchDate: Date {
    default = defaultDate
    update = {
      if (new > deliveryDate) deliveryDate = new
      if (new > now && pathState == Complete) pathState = Planned
    }
  } 
  ref field deliveryDate: Date {
    default = defaultDate
    validate = if (deliveryDate < dispatchDate) "Delivery Date should be more or equal Dispatch Date"
    update = {
      if (new < dispatchDate && pathState == Planned) dispatchDate = new
    }
  }
  
  func pathStateUpdated(old: PathState): Void = if (pathState != old) {
    if (pathState == InTransit) {
      if (old != Complete) dispatchDate = now
    } else if (pathState == Complete) {
      if (old != InTransit) dispatchDate = now
      deliveryDate = now
    }
  }
}