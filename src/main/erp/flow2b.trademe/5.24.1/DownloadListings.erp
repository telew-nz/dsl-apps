class DownloadListingsJob extends SystemJob {
  field service: ITrademeIntegration
  func do = {
    let _ <- nextPageSellingItems(number = 1, filter = "All")
    service.initComplete = true
    service.save
  }
  
  back func nextPageSellingItems(number: UInt, filter: String): Future[Any] = {
    let result <- service.https("MyTradeMe/SellingItems/$filter.json").param("page", number).get
    let page = result.body!.parseJson[SellingPage]
    let stocktake = if (let store = service.initialDownload?.stocktakeStore) {
      Stocktake(store = store).save
    }
    page.list.foreach{i =>
      DownloadListingJob(service = service, stocktake = stocktake, listingId = i.listingId).enqueueFirst()
    }
      
    
    if (page.hasNext) {
      nextPageSellingItems(number + 1, filter)
    } else {
      Future(1)
    }
  }
}

class DownloadListingJob extends SystemJob {
  field service: ITrademeIntegration
  field stocktake: Stocktake?
  field listingId: Int
  
  func do = {
    if (!TrademeListing.all.find(l => l.listingId == this.listingId).result.get) {
      let r <- service.https("Listings/${listingId}.json").get
      let ld = r.body!.parseJson[ListingDetails]
      let product = (if (let stocktake) {
        ld.make(service, stocktake).product
      } else {
        ld.make(service)
      }).as[ISubProduct]!

      product.trademeCategories = [TrademeCategory.all.find(cat => cat.number == ld.category).result.get!]
      product.save
      if (!product.trademeListings) {
        TrademeListing(status = Active, product = product, listingId = listingId,
                       template = service.initialDownload!.template, qtySold = ld.quantitySold).save
      }
    }
  }
}

back class SellingPage extends IPage[SellingItem]  {
  extFieldNaming = UpperCamelCase
  field list: [SellingItem]?
}

back class SellingItem {
  extFieldNaming = UpperCamelCase
  field listingId: Int
}

back class ListingDetails extends ESuperProduct[Int] {
  extFieldNaming = UpperCamelCase
  field listingId: Int
  field title: String
  field startPrice: Money?
  field buyNowPrice: Money?
  field startDate: Date {jsonMap = TrademeDate}
  field endDate: Date {jsonMap = TrademeDate}
  field category: String
  field quantity: Money?
  field photos: [TrademePhoto]?
  field quantitySold: Int?
  field body: PlainText
  
  field bidderAndWatchers: Int?
  field bidCount: Int?
  field viewCount: Int?
  
  func isTangible = true
  func extId = listingId
  func description = body
  func salesPrice = {
    let p = buyNowPrice.or(startPrice)
    SalesPrice(regular = p, current = p)
  }
  func stock = quantity.alt(1)
}

back class TrademePhoto extends EFile[Int] {
  extFieldNaming = UpperCamelCase
  field key: Int
  field value: TrademePhotoSizes
  
  func extId = key
  func extUrl = value.fullSize
}

back class TrademePhotoSizes {
  extFieldNaming = UpperCamelCase
  field fullSize: Url
}