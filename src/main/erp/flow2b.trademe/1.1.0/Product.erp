trait TrademeProduct {
  func trademeTitle: String = title
  func trademeDescription: Text? = description
  func description: Text?
  func superProduct: ISuperProduct
  func subProducts: [IProduct]?
  func unit: ProductUnit
    
  field trademeListings: [TrademeListing]? read by product
  field trademeTemplates: [TrademeTemplate]?
  field trademeStatus: TrademeProductStatus = (Inactive) {
    isValid = trademeStatus != Listed || (trademeDescription && findTrademeTemplates*.integration*.legalEntity*.priceModels.exists(contains(superProduct)))
  }
  func findTrademeTemplates: [TrademeTemplate]? = {
    if (trademeTemplates) trademeTemplates
    else if (let c = superProduct.categories.filter(trademeTemplates).first) c.trademeTemplates
    else []
  }
  func trademeSalesPrice(template: TrademeTemplate): Future[Money?] = {
    let price: Money? <- template.integration.legalEntity.superProductSalesPrice(superProduct)
    price + (price*template.extraCharge.withoutUnit).min(template.maximumExtraCharge)
  }
    
  func onSave = {
    updateTrademe()
  }
  func updateTrademe(): Void = {
    let templates = if (trademeDescription && trademeStatus == Listed) this.findTrademeTemplates else []
    trademeListings.filter(status.valid && !templates.contains(template)).foreach{l =>
      UpdateListingJob(listing = l).run()
    }
    templates.foreach{t =>
      let listing = trademeListings.find(status.valid && template == t).alt(TrademeListing(product = this, template = t).save())
      UpdateListingJob(listing = listing).run()
    }
    
  }
}

enum TrademeProductStatus {
  case Listed {background = "#dea"}
//  case Auto {background = "#ed9"}
  case Inactive {background = "#ddd"}
}


extend trait IBasicProduct extends TrademeProduct 

extend entity ProductCategory {
  field trademeTemplates: [TrademeTemplate]? by productCategories
  field trademeCategory: TrademeCategory? {
    visible = trademeTemplates
    isValid = !trademeTemplates || trademeCategory
    from = TrademeCategory.all.filter(leaf)
  }
}
