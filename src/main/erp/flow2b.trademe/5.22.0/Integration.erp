trait ITrademeIntegration extends SystemService with OAuth1Hmac with ExtIdSource[Int] with ISalesChannel {
  field title: String = "Trade Me"
  func roles: [Role]? = [Admin]
  func authParams = ["scope" -> "MyTradeMeRead,MyTradeMeWrite"]
  
  const field initComplete: Bool

  field initialDownload: TrademeInitialDownload? inline by service
  func extUrl: Url
  func webUrl: Url
  func https(path: String): HttpRequest = HttpRequest(url = "$extUrl$path", ssl = true).oauth(this)
  
  field manageListings: Bool = true
  field templates: [TrademeTemplate]? read by integration {deleteCascade = true}
  system field retrieveSoldItemsJob: RetrieveSoldItemsJob inline by service
  system field retrieveQuestionsJob: RetrieveQuestionsJob inline by service
  ref field legalEntity: OurLegalEntity {
    available(Ref).with(priceModels)
    default =? onlyOne
  }
  field approvedDealsSeller: Bool
  
  field finAccount: TrademeFinAccount inline by integration {
    label = "Member Account"
  }
  field pingAccount: PingAccount inline by integration
  field payNowAccount: PayNowAccount inline by integration
  field trademe: Vendor {
    label = "Trademe Company"
    default <- {
      let comp <- Company.all.find(title.contains("Trade Me") || title.contains("Trademe")).result
      let v = comp.alt(
        Company(
          title = "Trade Me", currency = NZD, legalInfo = LegalInfo(name = "Trade Me Limited"),
          altNames = ["Trademe"]
        )
      ).makeVendor
      v.gstStatus = Registered
      v
    }
  }
  
  view form = nav{
    tabs {
      grid(label = "Main") {
        issuesView
        title
        legalEntity
        serviceState
        manageListings
        authorizeView
        approvedDealsSeller
        trademe
        templates
        initialDownload
      }
      grid(label = "Member Account") {
        finAccount.form.unnav
      }
      grid(label = "Ping") {
        pingAccount.form.unnav
      }
    }
    button("↻ Sales") {
      retrieveSoldItemsJob.run()
      UI.info("Job is posted")
    }
    button("↻ Questions") {
      retrieveQuestionsJob.run()
      UI.info("Job is posted")
    }
    button("↻ Member Account") {
      finAccount.run()
      UI.info("Job is posted")
    }
  }
  view test = nav{
    grid{
      button("Update Categories") {
        UpdateCategoriesJob(service = this).run()
        UI.info("Job is posted")
      }
      button("Retrieve Latest Sales") {
        retrieveSoldItemsJob.run()
        UI.info("Job is posted")
      }  
      button("Retrieve Questions") {
        retrieveQuestionsJob.run()
        UI.info("Job is posted")
      }
      button("Download Listings") {
        DownloadListingsJob(service = this).run
        UI.info("Job is posted")
      }
      button("Update All Listings") {
        templates*.run()
        UI.info("Job is posted")
      }
    }
  }
  
  event onSave = {
    if (!TrademeCategory.all.first(1).result.get) {
      UpdateCategoriesJob(service = this).run()
    }
    if (serviceState == Active && initialDownload && !initComplete) {
      DownloadListingsJob(service = this).run
    }
    if (manageListings && serviceState == Active && updated(serviceState)) {
      retrieveSoldItemsJob.run()
      retrieveQuestionsJob.run()
      TrademeQuestion.all.filter(status != Answered && answer).foreach {q =>
        PostAnswerJob(question = q).run()
      }
      finAccount.run()
      templates*.run()
    }
  }
}

inline entity TrademeInitialDownload {
  field service: ITrademeIntegration by initialDownload
  field products: Bool = true
  field photos: Bool = true
  field template: TrademeTemplate
  field stocktakeStore: IStore =? IStore.onlyOne
  view form = grid{
    row {products photos}
    template
    stocktakeStore
  }  
}

entity TrademeIntegration extends ITrademeIntegration {
  func authTmpTokenUrl: Url = "https://secure.trademe.co.nz/Oauth/RequestToken"
  func authAuthorizeUrl: Url = "https://secure.trademe.co.nz/Oauth/Authorize"
  func authTokenExchangeUrl: Url = "https://secure.trademe.co.nz/Oauth/AccessToken"
    
  func authConsumerKey: String = "A4FA0ACAFE93D0DD7AF5BA7D04D5395F"
  func authConsumerSecret: String = "F8E7FC4A2164F73BBAEB4435A26B5522"
    
  func extUrl = "api.trademe.co.nz/v1/"
  func webUrl: Url = "http://www.trademe.co.nz/"
  object view list = TrademeIntegration.all >> list{grid(labels = None){
    x {title serviceState}
  }}
}
