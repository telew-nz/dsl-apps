extend enum DashboardIssue {
  case InsufficientBalance(role = BusAdmin, priority = default)
}
extend entity SalesItem {
  system field trademeListing: TrademeListing? by salesItems
}
entity TrademeListing extends DeferredJob with DashboardItem with NewEngine {
  const ref field product: ISubProduct by trademeListings
  const field template: TrademeTemplate
  ref const field status: ListingStatus = Entered
  
  ref field promotion: TrademePromotion? = product.trademePromotion
  func active = status == Active
  system const field qtySold: Int?
  system const field listingId: Int?
  system const field hasBids: Bool
  ref system const field views: Int?
  ref system const field watchers: Int?
  system const field bids: Int?
  ref system const field startDate: Date?
  system const field endDate: Date?
  const field questions: [TrademeQuestion]? by listing
  const system field salesItems: [SalesItem]? by trademeListing
  compute salesOrders = salesItems*.order
  impl func entityNumbers = [listingId.toString]
  compute url: Url? = if (let l = listingId) "$(template.integration.webUrl)Browse/Listing.aspx?id=$l" else ""
  func duration: Duration? = if (let startDate, let endDate) endDate - startDate
  impl func title = "$product: $(startDate?.date)"
  impl func service: ITrademeIntegration = template.integration
  impl func jobDate = if (active) endDate?.add(10_minute) else null
  impl func issues = if (status == InsufficientBalance) [InsufficientBalance]
  impl func fixes(issue: DashboardIssue): Future[[Fix]?] = if (issue == InsufficientBalance) [
    Fix(text = (if (listingId) "Cannot be update" else "Cannot be listed") +  "due to insufficient balance", acts = [
      Act(label = "Credit Account", do = UI.openUrl("https://www.trademe.co.nz/Payments/ChooseMethod.aspx"))
    ]) 
  ]
  impl func do = {
    if (status == Active) {
      status = Expired
      save()
      RetrieveListingStats(listing = this).enqueue

      if (product.trademeStatus == Listed &&
          service.manageListings &&
          !product.trademeListings.exists(l => l.status == Active && l.template == this.template) &&
          product.trademeTemplates.contains(this.template)
      ) {
        UpdateListingJob(prevListing = this, listing = TrademeListing(
            product = product, template = template, status = Entered
        )).enqueueFirst
      }
    }
  }
  impl view form = nav{
    grid{
      product
      template
      x{status promotion}
      qtySold
      row {views watchers bids}
      listingId
      row {startDate endDate}
      url
      questions
      salesOrders

      //button("Relist") {
      //  enqueue()
      //  UI.info("Job is posted")
      //}
    }
    button("Update") {
      let _ <- back {
        UpdateListingJob(listing = this).enqueue()
      }
      UI.info("Job is posted")
    }
    button("â†» Stats") {
      RetrieveListingStats(listing = this).enqueue()
      UI.info("Job is posted")
    }
  }
  impl view ref = x {status promotion title}
  report Report {
    field product: ISubProduct? 
    field template: TrademeTemplate?
    field status: [ListingStatus]? = [ListingStatus.Active]
    impl view list = TrademeListing.all.filter(product ==? this.product && template ==? this.template && (!this.status || this.status.contains(status))) >> nav{table{
      status product qtySold endDate
    }}
  }
}

class RelistRequest {
  extFieldNaming = UpperCamelCase
    
  field listingId: Int
  field returnListingDetails: Bool = true
}

enum ListingStatus(valid: Bool) {
  case Entered(valid = true){background = "#ed9"}
  case InsufficientBalance(valid = false) {background = "#f99"}
  case Active(valid = true){background = "#dea"}
  case Parked(valid = true) {background = "#ddd"}
  case Withdrawn(valid = false) {background = "#ddd"}
  case Exhausted(valid = false) {background = "#ddd"}
  case Expired(valid = false) {background = "#ddd"}
  case Invalid(valid = false) {background = "#f99"}
}
