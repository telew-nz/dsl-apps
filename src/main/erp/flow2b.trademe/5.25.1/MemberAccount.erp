entity TrademeFinAccount extends FinAccount with ScheduledJob with HasLowBalance {
  ref const field integration: ITrademeIntegration by finAccount
  func title = "$integration: Member Account"
  func service = integration
  func schedule = "0 30 * * * ?" //Every hour
  system field lastUpdate: Date?
  func currency: Currency = NZD
  func legalEntity = integration.legalEntity
  
  field promoExpense: ExpenseType? {
    label = "Promotion Expense"
  }
  field feeExpense: ExpenseType?
  
  func itemRoles = [BusAdmin, Accountant]
  func fixes(issue: DashboardIssue): Future[[Fix]?] = if (issue == TooLowBalance) [
    Fix(text = "Credit trademe account", acts = [Act(label = "Credit", do = UI.openUrl("https://www.trademe.co.nz/Payments/ChooseMethod.aspx"))]),
    Fix(text = "Set up auto billing", acts = [Act(label = "Set up", do = UI.openUrl("https://www.trademe.co.nz/MyTradeMe/AutoBilling.aspx"))]),
    Fix(text = "If you have already set up auto billing, you can ignore the issue and clean the low balance level", acts = [Act(label = "Ignore", do = {
      lowBalance = null
      UI.save(this)
    })])
  ]
  
  func hasBankData = true
  func ledgerAccount: LedgerAccount = Cash
  func defaultLowBalance: LowBalance? = LowBalance()
  event onSave = {
    if (!created && balance > origin(balance)) {
      TrademeListing.all.filter(status == InsufficientBalance).foreach(l => UpdateListingJob(listing = l).enqueue())
    }
  }
  func do = {
    let d = {
      if (let lu = lastUpdate) Date.now.add(10_minute).sub(lu).to[Duration[hour]] 
      else 5.as[Duration[day]]!
    }
    let dd = d.to[Duration[day]] 
    let trs0 <- nextPage(number = 1, filter = {
      if (d < 24_hour) "Last24Hours"
      else if (dd < 7_day) "Last7Days"
      else if (dd < 14_day) "Last14Days"
      else if (dd < 28_day) "Last28Days"
      else if (dd < 45_day) "Last45Days"
      else "Last60Days"
    })
    
    let trs = if (let lastUpdate) {
      trs0.filter(date > lastUpdate)
    } else {
      let sd = Date.now
      let trs1 = trs0.filter(date >= sd)
      if (let tr = trs1.sortBy(date).first) {
        initBalance = InitBalance(date = tr.date, balance = tr.balance - tr.amount).save
      }
      trs1
    }
    
    lastUpdate = Date.now
    save
    let ms <- BankImporter.import(this, trs)
    ms*.as[OutgoingPayment].foreach{op =>
      let tp = op.bankData?.type
      op.reasons = if (let e = if (tp.contains("Fee")) feeExpense else if (tp.contains("Promo")) promoExpense) [
        PaymentExpense(expense = e, amount = op.amount, gstRate = 0.15)
      ]
    }
  }
  
  back func nextPage(number: UInt, filter: String): Future[[FinAccountTr]?] = {
    let result <- service.https("MyTradeMe/MemberLedger/$filter.json").param("page", number).get
    let page = result.body!.parseJson[FinAccountPage]
    if (number > 20) {
      throw "Too many pages"
    }
    let nps <- if (page.hasNext) {
      nextPage(number + 1, filter)
    } 
    page.list + nps
  }
  
  func counterparty(item: BankStatementItem, data: BankData): LegalEntity? = {
    if (item.amount < 0) integration.trademe.legalEntity
  }
  
  func isContraAccount(item: BankStatementItem, data: BankData): Bool = {
    let l = data.fullMemo.lower + data.thierName.lower
    item.amount < 0 && (l.contains("trade me") || l.contains("trademe"))  
  }
  
  view form = nav{
    grid{
      issuesView
      integration

      feeExpense
      promoExpense

      lowBalance
      initBalance >> field(editMode = AlwaysRead)
      balance
    }
    ledgerBtn
  }
}



back class FinAccountPage extends IPage[FinAccountTr] {
  extFieldNaming = UpperCamelCase
  field list: [FinAccountTr]?
}

extend trait CashMovement extends HasExtId

back class FinAccountTr extends BankStatementItem {
  extFieldNaming = UpperCamelCase
  field date: Date {jsonMap = TrademeDate}
  field type: FinTrType {
    jsonMap[Int?](apply = _.id, unapply = i => {FinTrType.all.find(id == i)!})
  }
  field description: String?
  field credit: UMoney?
  field debit: UMoney?
  field balance: Money?
  field referenceId: Int?
  field purchaseId: Int?
  field sku: String?
  field ledgerId: Int
  func amount = if (let cr = credit) cr else -debit
  func data(account: FinAccount) = {
    BankData(type = type.name, bankId = ledgerId, memo = description, balance = balance)
  }
  
  /*func make(service: ITrademeIntegration): CashMovement = {
    let fi = service.finAccount
    fi.findExtId(ledgerId, _.as[CashMovement]).alt {
      let p: CashMovement = if (let credit) {
        if (let oc = fi.incomeAccount)  {
          CashTransfer(
            date = date
            from = oc
            to = fi
            outcome = credit
            income = credit
          )
        } else {
          IncomingPayment(
            date = date,
            account = service.finAccount
            amount = credit            
          )
        }
      } else {
        OutgoingPayment(
          date = date,
          payee = service.trademe.legalEntity,
          account = service.finAccount
          amount = debit!,
          reasons = {
            if (let e = if (type.name.contains("Fee")) fi.feeExpense else if (type.name.contains("Promo")) fi.promoExpense) [
              PaymentExpense(expense = e, amount = debit!, gstRate = 0.15)
            ]
          }
        )
      }
      p.updateExtId(fi, ledgerId)
      p.saveMain
    }
  }*/
}

enum FinTrType(id: Int?) {
  case Other(id = 0)
  case ListingFee(id = 1)
  case HighVolumeFee(id = 2)
  case SuccessFeeBid(id = 3)
  case SuccessFeeOffer(id = 4)
  case SuccessFeeBuyNow(id = 5)
  case GalleryPromotion(id = 100)
  case GalleryPlusPromotion(id = 101)
  case BoldPromotion(id = 102)
  case SubtitlePromotion(id = 103)
  case HighlightPromotion(id = 104)
  case FeaturePromotion(id = 105)
  case FeatureComboPromotion(id = 106)
  case HomepageFeaturePromotion(id = 107)
  case SuperFeaturePromotion(id = 108)
  case SuperFeatureComboPromotion(id = 109)
  case TenDayAuctionFee(id = 110)
  case MultiPhotoFee(id = 111)
  case ScheduledEndDateFee(id = 112)
  case ReserveFee(id = 113)
  case SecondaryCategoryPromotion(id = 114)
  case JobPack5(id = 200)
  case JobPack10(id = 201)
  case JobPack20(id = 202)
  case JobPack30(id = 204)
  case JobPack50(id = 203)
  case JobPack100(id = 205)
  case WithdrawalFee(id = 300)
  case QuotableValueReportFee(id = 301)
  case MonthlyStoreFee(id = 302)
  case CreditViaCreditCard(id = 400)
  case CreditViaBankTransfer(id = 401)
  case CreditViaCheque(id = 402)
  case RebateForListingFee(id = 500)
  case RebateForHighVolumeFee(id = 501)
  case RebateForSuccessFeeBid(id = 502)
  case RebateForSuccessFeeOffer(id = 503)
  case RebateForSuccessFeeBuyNow(id = 504)
  case RebateForGalleryPromotion(id = 505)
  case RebateForGalleryPlusPromotion(id = 506)
  case RebateForBoldPromotion(id = 507)
  case RebateForSubtitlePromotion(id = 508)
  case RebateForHighlightPromotion(id = 509)
  case RebateForFeaturePromotion(id = 510)
  case RebateForFeatureComboPromotion(id = 511)
  case RebateForHomepageFeaturePromotion(id = 512)
  case RebateForSuperFeaturePromotion(id = 513)
  case RebateForSuperFeatureComboPromotion(id = 514)
  case RebateForTenDayAuctionFee(id = 515)
  case RebateForMultiPhotoFee(id = 516)
  case RebateForScheduledEndDateFee(id = 517)
  case RebateForReserveFee(id = 518)
  case RebateForSecondaryCategoryPromotion(id = 519)
  case RefundOfListingFee(id = 600)
  case RefundOfSuccessFee(id = 601)
  case RefundOfGalleryPromotion(id = 602)
  case RefundOfGalleryPlusPromotion(id = 603)
  case RefundOfBoldPromotion(id = 604)
  case RefundOfSubtitlePromotion(id = 605)
  case RefundOfHighlightPromotion(id = 606)
  case RefundOfFeaturePromotion(id = 607)
  case RefundOfFeatureComboPromotion(id = 608)
  case RefundOfHomepageFeaturePromotion(id = 609)
  case RefundOfSuperFeaturePromotion(id = 610)
  case RefundOfSuperFeatureComboPromotion(id = 611)
  case RefundOfTenDayAuctionFee(id = 612)
  case RefundOfMultiPhotoFee(id = 613)
  case RefundOfScheduledEndDateFee(id = 614)
  case RefundOfReserveFee(id = 615)
  case RefundOfSecondaryCategoryPromotion(id = 616)
  case DiscountOfListingFee(id = 617)
  case DiscountOfSuperfeature(id = 618)
  case DiscountOfHighlight(id = 619)
  case DiscountOfFeature(id = 620)
  case DiscountOfBoldTitle(id = 621)
  case DiscountOfFeatureCombo(id = 622)
  case DiscountOfSubtitle(id = 623)
  case RebateForGoldTradeSuccessFee(id = 624)
}
