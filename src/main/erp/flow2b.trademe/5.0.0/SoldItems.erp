inline entity RetrieveSoldItemsJob extends ScheduledJob {
  field service: ITrademeIntegration by retrieveSoldItemsJob
  func schedule = "0 30 * * * ?" //Every hour
  field lastUpdate: Date?
  func do: Future[Any] = {
    let d = if (let lu = lastUpdate) Date.now.add(10_minute).sub(lu).to[Duration[hour]] else 24000.as[Duration[hour]]
    let dd = d.to[Duration[day]] 
    this.lastUpdate = Date.now
    save()
    nextPage(number = 1, filter = {
      if (d < 1_hour) "LastHour"
      else if (d < 24_hour) "Last24Hours"
      else if (dd < 3_day) "Last3Days"
      else if (dd < 7_day) "Last7Days"
      else if (dd < 30_day) "Last30Days"
      else "Last45Days"
    })
  }
  
  back func nextPage(number: UInt, filter: String): Future[Any] = {
    service.https("MyTradeMe/SoldItems/$filter.json").param("page", number).get.flatMap{result =>
      let page = result.body!.parseJson[SoldItemsPage]
      let _ <- page.list.map {i =>
        i.listing = TrademeListing.all.find(l => l.listingId == i.listingId).result.get
         .alt(throw "Cannot find listing $(i.listingId)")
        i.make(i.listing.template)
      }.allCompleted
        
      if (page.totalCount > page.page*page.pageSize) {
        nextPage(number + 1, filter)
      } else {
        Future(1)
	  }
    }
  }
}

back class SoldItemsPage {
  extFieldNaming = UpperCamelCase
  field totalCount: Int?
  field page: Int
  field pageSize: Int?
  field list: [SoldItem]?
}

back class SoldItem extends ESalesOrder[Int] with ESalesOrderItem[Int] with EIncomingPayment[Int] {
  extFieldNaming = UpperCamelCase
  field listingId: Int
  field salePrice: Money
  field selectedShipping: String
  field buyerDeliveryAddress: String?
  field messageFromBuyer: String?
  field successFees: Money?
  field shippingPrice: Money?
  field offerId: Int?
  field invoiceId: Int?
  field buyer: TrademeAccount
  field deliveryAddress: TrademeLocation?
  field quantitySold: Int
  field purchaseId: Int
  field price: Money
  field orderId: Int?
  field referenceNumber: String?
  field soldDate: Date {jsonMap = TrademeDate}
  field isPayNowPurchase: Bool
  field isPaymentPending: Bool
  field shippingType: ShippingOptionType {jsonMap[Int?](apply = id, unapply = i => ShippingOptionType.all.find(id == i)!)}
  
  field listing: TrademeListing
  
  func extId = purchaseId
  func number = referenceNumber
  func orderDate = soldDate
  func currencyCode = "NZD"
  func billingAddress = deliveryAddress
  func customerExtId = buyer.memberId
  func customer = buyer
  func payments = if (isPayNowPurchase) [this]
  func qty = quantitySold
  func amount: Money = price*quantitySold
  func paymentDate = soldDate
  func paymentMethod: String = "PAYNOW"
  func items = [this.as[ESalesOrderItem[Int]]!] + if (shippingPrice) [TMDeliveryItem(item = this)]
  func product(pr: ISalesOrderImportProvider[Int]) = {
    listing.product
  }
  
  func make(pr: ISalesOrderImportProvider[Int]): Future[SalesOrder] = {
    let so <- super.make(pr)
    if (so.created) {
      listing.qtySold = listing.qtySold + quantitySold
      listing.save
    }
    so
  }
}

back class TMDeliveryItem(item: SoldItem) extends ESalesOrderDeliveryItem[Int] {
  func extId = -item.purchaseId
  func amount = item.shippingPrice
  func deliveryMethod = item.shippingType.name
}


