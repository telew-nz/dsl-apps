extend enum DashboardIssue {
  case UnansweredTrademeQuestion(role = Salesperson, priority = major)
}

entity TrademeQuestion extends HasN with DashboardItem with NewEngine {
  const ref field listing: TrademeListing by questions
  impl func nextN: Int? = increment(Ticket.gen)
  impl func numPrefix: String = "TMQ"
  
  impl func title = "$number $customer $listing"
  
  const ref field status: QuestionStatus = Asked
  const field question: PlainText
  const system field listingQuestionId: Int
  const ref field customer: Customer
  
  field answer: PlainText? {
    validate = if (answerLength > maxLength) "The answer should have 500 characters maximum"
  }
  compute answerLength = answer.trim.length
  compute maxLength = 500
  impl func itemPriority: Priority = major
  impl func itemRoles = [Salesperson]
  impl func activeRoles = if (status.active) [Salesperson]
  impl func issues: [DashboardIssue]? = if (status.active && !answer) [UnansweredTrademeQuestion]
  impl func fixes(issue: DashboardIssue) = if (issue == UnansweredTrademeQuestion) [
    Fix(text = "Answer the question"),
    Fix(text = "Ignore the question", acts = [
      Act(label = "Ignore", do = {
        Query(this).foreach {i =>
          i.status = Ignored
          i.save
        }.result
      })
    ])
  ]
  
  impl view form = nav {
    grid(labels = Top) {
      issuesView
      x(flow = false){
        status >> field(compact)
        listing >> field(stretch = 1, fontStyle = H2, title = title)
        number >> field(fontStyle = H3)
      }
      customer
      question
      x (visible = status != Answered && ui.edit) { answerLength label("of") maxLength}
      answer >> field(visible = status != Answered)
      answer >> field(editMode = AlwaysRead, visible = status == Answered)
    }
    extension("nav")
  }
  impl event onSave = {
    if ((status == Asked || status == Ignored) && answer) {
      PostAnswerJob(question = this).enqueue
    }
  }
  impl view ref = x(flow = false){status title}
}
enum QuestionStatus(active: Bool) {
  case Asked(active = true) {
    background = "#ed9"
    icon = "play"
  }
  case Answered(active = false) {
    color = "white"
    background = "#090"
    icon = "check-circle"
  }
  case Ignored(active = false) {
    background = "#999"
    icon = "times-circle"
  }
}

inline entity RetrieveQuestionsJob extends ScheduledJob {
  impl func dependsOnRegisters = false
  impl field service: ITrademeIntegration by retrieveQuestionsJob
  impl func schedule = "30 */5 * * * ?" //Every 5 minutes
  field lastUpdate: Date?
  impl func do: Future[Any] = {
    nextPage(number = 1)
  }
  back func nextPage(number: UInt): Future[Any] = {
    let result <- service.https("Listings/questions/unansweredquestions.json").param("page", number).get
    let page = result.body!.parseJson[QuestionsPage]
    page.list*.make(service)

    if (page.hasNext) {
      nextPage(number + 1)
    } else {
      Future(1)
    }
  }
}

class PostAnswerJob extends SystemJob {
  impl func dependsOnRegisters = false
  field question: TrademeQuestion
  impl func service = question.listing.template.service
  impl func do: Future[Any] = {
    let _ <- service.https("Listings/$(question.listing.listingId)/questions/$(question.listingQuestionId)/answerquestion.json")
      .post(Answer(answer = question.answer!).json)  
    question.status = Answered
    question.save
  }
}

back class QuestionsPage extends IPage[Question] {
  extFieldNaming = UpperCamelCase
  impl field list: [Question]?
}

back class Question {
  extFieldNaming = UpperCamelCase
  field listingId: Int
  field listingQuestionId: Int
  field comment: String
  field isSellerComment: Bool
  field askingMember: TrademeAccount
  
  func make(service: ITrademeIntegration): TrademeQuestion? = if (!isSellerComment) {
    if (!TrademeQuestion.all.find(q => q.listing.listingId == this.listingId && q.listingQuestionId == this.listingQuestionId).result.get) {
      if (let l = TrademeListing.all.find(l => l.listingId == this.listingId).result.get) {
        TrademeQuestion(
          listing = l
          listingQuestionId = listingQuestionId
          customer = askingMember.make(service).save
          question = comment
        ).saveMain
      }
    }
  }
}

back class Answer {
  field answer: PlainText
}
