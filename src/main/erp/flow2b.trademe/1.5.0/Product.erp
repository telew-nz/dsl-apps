trait TrademeProduct {
  func trademeTitle: String = title
  func trademeDescription: Text? = description
  func description: Text?
  func superProduct: ISuperProduct
  func subProducts: [IProduct]?
  func unit: ProductUnit
    
  const field trademeListings: [TrademeListing]? by product
  field trademeTemplates: [TrademeTemplate]?
  field trademeStatus: TrademeProductStatus? {
    isValid = trademeStatus != Listed || 
      (trademeDescription && findTrademeTemplates*.integration*.legalEntity*.priceModels.exists(contains(superProduct)))
  }
  func findTrademeTemplates: [TrademeTemplate]? = {
    if (trademeTemplates) trademeTemplates
    else if (let c = superProduct.categories.filter(trademeTemplates).first) c.trademeTemplates
    else []
  }

  func onSave = {
    updateTrademe()
  }
  func updateTrademe(): Void = {
    let templates = if (trademeDescription && trademeStatus == Listed) this.findTrademeTemplates else []
    trademeListings.filter(status.valid && !templates.contains(template)).foreach{l =>
      UpdateListingJob(listing = l).run()
    }
    templates.foreach{t =>
      trademeListings.find(status.valid && template == t)
        .or{
          let qty: Number? = if (superProduct.isTangible) StockRegister.current
            .filter(this.subProducts.contains(good) && (!t.stores || t.stores.contains(store)))
            .agg(available.sum)
          if (superProduct.isTangible && qty > 0) TrademeListing(product = this, template = t).save()
        }.map{listing =>
          UpdateListingJob(listing = listing).run()
          1
        }
    }    
  }
}

extend interface IPrice {
  func trademeSalesPrice(template: TrademeTemplate): Future[Money?] = {
    let p <- salesPrice(NZD)
    trademePrice(template, p)
  }
  func trademeOriginalPrice(template: TrademeTemplate): Future[Money?] = {
    let p <- originalPrice(NZD)
    trademePrice(template, p)
  }
  func trademePrice(template: TrademeTemplate, price: Money?): Money? = {
    price + (price*template.extraCharge.withoutUnit).min(template.maximumExtraCharge)
  }
}

enum TrademeProductStatus {
  case Listed {background = "#dea"}
//  case Auto {background = "#ed9"}
  case Inactive {background = "#ddd"}
}


extend trait IBasicProduct extends TrademeProduct 

extend entity ProductCategory {
  field trademeTemplates: [TrademeTemplate]? by productCategories
  field trademeCategory: TrademeCategory? {
    visible = trademeTemplates
    isValid = !trademeTemplates || trademeCategory
    from = TrademeCategory.all.filter(leaf)
  }
}
