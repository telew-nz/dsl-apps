class SubAccountAmount(account: LedgerSubAccount, currency: Currency, amount: Money[currency]?) {
  compute debit: Money[currency]? = (if (amount > 0) amount else 0)
  compute credit: Money[currency]? = (if (amount < 0) -amount else 0)  
}


inline trait Ledger extends Register {
  func aview: Layout[Ledger]
  func currency: Currency = legalEntity.getCurrency
    
  key field legalEntity: OurLegalEntity = journal.legalEntity
  func account: LedgerAccount
    
  order field date: Date = journal.journalDate
  order field cor: Int?
  func isCorrection = cor == 1
  func isClosing = cor == 2
  field journal: Journal
  field contraAccount: LedgerAccount?
  field trAmount: Money[currency]?
  
  stored compute amount: Money[currency]? = {
    if (isCorrection) trAmount - prev?.balance 
    else if (isClosing) -prev?.balance 
    else trAmount
  }
  stored compute baseAmount: Money[legalEntity.getCurrency]? <- amount.to[Money[legalEntity.getCurrency]](date)
  stored compute balance: Money[currency]? = prev?.balance + amount
  
  func subBalances: [SubAccountAmount]? = if (let balance) {
    [SubAccountAmount(account = subAccount, currency = currency, amount = balance)]
  }
  compute subAccount: LedgerSubAccount {
    availableFor = List
    value = {
    	if (prev?.balance > 0) {
          account.debit.alt(account.credit!)
        } else if (prev?.balance < 0) {
          account.credit.alt(account.debit!)
        } else  if (balance > 0) {
          account.debit.alt(account.credit!)
        } else {
          account.credit.alt(account.debit!)
        }
    }
  }
  
  register incomeSummary: [Ledger]? = {
    if (cor && createIncomeSummary && amount && baseAmount) {
      if (currency == legalEntity.getCurrency) {
        [LIncomeSummary(journal = journal, trAmount = -amount, contraAccount = account)]
      } else {
        [LIncomeSummary(journal = journal, trAmount = -baseAmount, contraAccount = account),
         CurrencyTrading.spotTrade(journal, -amount!, -baseAmount!)
        ]
      }
    } 
  }
  func createIncomeSummary = true
  
  compute debit: Money[currency]? = (if (amount > 0) amount else 0)
  compute credit: Money[currency]? = (if (amount < 0) -amount else 0)  
  compute debitBalance: Money[currency]? {
    value = if (balance > 0) balance else 0
    label = "Debit"
  }
  compute creditBalance: Money[currency]? {
    value = if (balance < 0) -balance else 0
    label = "Credit"
  }
  
  func mlBalance(journal: MJournal): MLedger
  
  report Report {
    generateFilters = false
    field legalEntity: OurLegalEntity? 
    field account: LedgerSubAccount? {search = true}
    field from: Date?
    field to: Date?  
    field currency: Currency?
    field journal: Journal?

    view list = Ledger.all.filter(l =>
          l.legalEntity ==? this.legalEntity
       && (!this.account || l.account.contains(this.account!))
       && from ?<= l.date && l.date <=? to
       && l.journal ==? this.journal
       && l.currency ==? this.currency
    ).sortBy(l => (l.date.desc, l.account.number)) >> nav{table(action = journal.form.open){
       journal.title >> field(label = "Journal", width = 0.2), date,
       subAccount >> field(label = "Account", width = 0.4) legalEntity 
       aview >> field(label = "Analytics", width = 0.4) 
       debit credit balance
    }}
  }
}

