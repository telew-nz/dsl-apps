extend entity SalesOrder {
  field assetReturns: [SalesAssetReturn]? inline by salesOrders {
    visible = preEntered && items*.as[SalesRentItem]
    views = []
    update = {
      setDefaultSalesReturn()
    }
  }
  
  func setDefaultSalesReturn(): Void = {
    if (let d = assetReturns.last) {
      this.items*.orderDeliveryItem*.as[RentDeliveryItem]
        .filter(!assetReturn)
        .foreach(assetReturn = d)
    }
  }
  compute ar {
    value = assetReturns.onlyOne
    update = {
    }
  }
  extend view form = extend("deliveries") {
    assetReturns >> list{nav{inline{returnHeader}}}
  }
  extend view posView = extend("delivery") {
    row {
      ar!.destination >> field(visible = ar, label = "Return Store") ar!.dispatchDate >> field(visible = ar) ar!.deliveryDate >> field(visible = ar && ar!.pickUpLocation)
    }
  }
  
  system ref compute hasRents = items.exists(_.is[SalesRentItem])
  ref stored compute assetReturnState: OrderAssetReturnState {
    value = {
      let items = this.items*.as[SalesRentItem]
      if (status != Active || !items) NotReturned
      else if (items.all(assetReturned)) Returned
      else if (items.exists(assetReturned)) PartiallyReturned
      else if (items.exists(assetReturnInTransit)) ReturnedInTransit
      else ReturnPlanned
    } 
    views = []
    visible = hasRents && state == Processing
    update = {
      if (new != assetReturnState) {
        assetReturns.foreach{d => 
          d.status = if (new == OrderAssetReturnState.NotReturned) DeliveryStatus.Planned else DeliveryStatus.Delivered
        }
      }
    }
    from = [OrderAssetReturnState.NotReturned, OrderAssetReturnState.Returned].toSet + assetReturnState
  }
  extend view statusesView = assetReturnState
 
  func updateBond(): Void = {
    let bb <- items*.orderDeliveryItem*.as[RentDeliveryItem].mapOpt{i =>
      if (let pm = vendor.priceModel(i.product.superProduct)?.as[BondPriceModel]) {
        (pm, item = i)
      } else null
    }.groupBy(pm).agg(bond = key.bondAmount(_*.item))
     .values.allCompleted
   
    if (let b = bb.sum) {
      if (let bondItem = items*.as[SalesBondItem].first) {
        bondItem.amount = b
      } else {
        SalesBondItem(order = this, amount = b)
      }
    }
  }
}



enum OrderAssetReturnState {
  case NotReturned {background = "#ddd"}
  case ReturnPlanned {background = "#ed9"}
  case PartiallyReturned {background = "#ed9"}
  case ReturnedInTransit {background = "#dea"}
  case Returned {color = "white" background = "#090"}
}