inline entity RentDeliveryItem extends IDeliveryProductItem {
  func storeRequired = true
  field assetReturn: AssetReturn? {
    default = {
      order.as[SalesOrder]!.assetReturns.first
        .or(if (order.real) SalesAssetReturn(primary = true, salesOrders = [order.as[SalesOrder]!]) else null)
    } 
    from = {
      sales!.order.assetReturns
    }
    validate = if (!assetReturn && order.real) "Asset return should be selected for every rent item"
  }
  func needsRevision: Bool = updated(product) || updated(duration) || updated(unit) || updated(itemsQty)
  field product: AssetRentService {
    available(Ref).with(unit, altUnits, vendors, assets)
    update = {
      this.silentUpdate(unit, new.unit)
      this.silentUpdate(duration, this.duration.as[Qty[this.unit]])
      doUpdateDuration()
    }
  }
  field unit: ProductUnit {
    from = product.units
    update = {
      duration = duration.as[Qty[unit]]
    }
  }
  field duration: Qty[unit] {
    default = assetReturn?.items.find(_ != this)?.duration.alt(1)
    update = {
      if (old) {
        if (let s = sales) s.sAmount = s.sAmount*(new.as[Dec[12]]/old!)
        if (let p = purchase) p.sAmount = p.sAmount*(new.as[Dec[12]]/old!)
      }
      doUpdateDuration()
    }
  }
  
  func doUpdateDuration(): Void = {
    updateAmount()
    if (assetReturn?.items.count > 1) {
      this.assetReturn = SalesAssetReturn(salesOrders = [order.as[SalesOrder]!]) 
    } 
    assetReturn?.updateDeliveryDate()
    let assetReturns = order.as[SalesOrder]!.assetReturns
    if (assetReturns.count > 1) {
      assetReturns.filter(status == Planned)
      .groupBy(dispatchDate, deliveryDate)
      .values
      .foreach{returns =>
        if(returns.count > 1) {
          returns.drop(1).foreach{i =>
            i.items.foreach{j => j.assetReturn = returns.first}
            i.delete
          }
        }
      }
    }
    sales?.order?.updateBond()
  }
  
  func toDuration: Duration = {
    let d = product.toBase(duration)
    let u = product.unit
    if (u == Minute) d.as[Duration[minute]]
    else if (u == Hour) d.as[Duration[hour]]
    else d.as[Duration[day]]
  }
  func durationSeconds: Duration[second] = toDuration.seconds
    
  func updateAmount(): Void = {
    if (let s = sales) {
      let currency = s.order.currency
      let vendor = s.order.vendor
      let customer = try? s.order.customer!
      let amount <- Query(vendor).map{
        if (let pr = productPrice(product)) pr.rentSalesAmount(currency, customer, itemsQty, toDuration, s.order.pricesIncludingGst)
        else Future(0)
      }.result
      if (amount) s.amountEditor = amount
    }

    if (let p = purchase) {
      let currency = p.order.currency
      let vendor = p.order.vendor
      let customer = try? p.order.customer
      let amount <- Query(vendor).map{
        if (let pr = productPrice(product)) pr.rentSalesAmount(currency, customer, itemsQty, toDuration, p.order.pricesIncludingGst)
        else Future(0)
      }.result
      if (amount) p.amountEditor = amount
    }
    updateGst()
  }
  
  func qty = duration*itemsQty
  field itemsQty: Qty[ProductUnit.Item] {
    default = 1
    label = "Quantity"
    update = {
      if (old) {
        if (let s = sales) s.sAmount = s.sAmount*(new.as[Dec[12]]/old!)
        if (let p = purchase) p.sAmount = p.sAmount*(new.as[Dec[12]]/old!)
        updateAmount()
      }
      sales?.order?.updateBond()
    }
  }
  field assets: [FixedAsset]? {
    validate = if (assets && assets.count != itemsQty.withoutUnit) "Chosen number of assets should be the same as quantity"
    from = FixedAsset.all.filter(i => i.product == product.product)
  }
  
  field sales: SalesRentItem? inline by deliveryItem {
    default = if (let d = delivery?.as[ISalesDelivery]) createSales(d)
  }
  func purchase: IPurchaseItem? = null
  func createSales(sd: ISalesDelivery): SalesRentItem = SalesRentItem(deliveryItem = this, order = sd.salesOrders.first)
  func createPurchase(pd: IPurchaseDelivery): IPurchaseItem = throw "Unsupported"
  
  func clearSales: Void = sales = null
  func clearPurchase: Void = {}
    
  view form = grid(labels = None, width = 1.2, margin = TopBottom) {
    row{product >> field(width = 0.6), itemsQty >> field(width = 0.3), duration >> field(width = 0.3)}
    availableView >> field(visible = delivery?.is[SalesDelivery])
    assets >> field(editMode = Original)
  }
  view availableView = grid {
    label("Unavailable", visible = !isAvailableNow, printable = false, color = "#f21") 
    /*availablePeriods >> list(visible = !isAvailableNow || !delivery, printable = false, frame = false) {
      x {
        start
        label("—")
        if (end == Date.max) "∞" else end
        button("Set") {
          this.delivery!.dispatchDate = start
          this.delivery!.save()
          this.assetReturn!.updateDeliveryDate()
          this.assetReturn!.save()
        }
      }
    }*/
  }
  
  
  compute unsafePeriods: [Period]? = _availablePeriods?.unsafe
  compute availablePeriods: [Period]? = _availablePeriods?.safe 
  compute _availablePeriods: AvailablePeriods? = {
    let excludeItems = (try? ins!)*.id + (try? outs!)*.id
    let vendor = assetReturn!.order.orderVendor!
    let returnSchedule = assetReturn!.deliveryDst!.schedule
    FAProdReg.at((true, Date.min))
      .find(_.product == this.product.product && _.legalEntity == sales?.order?.vendor && _.store ==? delivery?.as[SalesDelivery]?.source)
      .map{reg => 
        let pm = vendor.priceModel(this.product.superProduct).alt(EmptyPriceModel)
        AvailablePeriods(  
          unsafe = reg.availablePeriods(itemsQty, this.period.start.min(Date.now), priorityDate = order.date, excludeItems),
          safe = RentAdjuster.adjustPeriods(toDuration, pm, reg.store.schedule, returnSchedule, 
             reg.availablePeriods(itemsQty, Date.now, priorityDate = null, excludeItems))
        )
      }
  }

  func updateDuration(): Void = {
    if (assetReturn!.deliveryDate > delivery!.deliveryDate) {
      let new = (order.legalEntity.priceModel(product)
          ?.rentDuration(delivery!.toRentDeliveryDesc, assetReturn!.toRentDeliveryDesc)
           .alt(assetReturn!.deliveryDate - delivery!.deliveryDate)).seconds
      let old = durationSeconds
      if (old != new && new > 0) {
        silentUpdate(duration,
           if (unit == Minute) new.minutes
           else if (unit == Hour) new.hours
           else new.days)
         if (let s = sales) s.sAmount = s.sAmount*(new.as[Dec[12]]/old)
         if (let p = purchase) p.sAmount = p.sAmount*(new.as[Dec[12]]/old)
         updateAmount()
      }
    }
  }
  
  compute isAvailableNow = {
    if (delivery?.status == Planned) unsafePeriods.exists(includes(period)) 
    else outs && outs.all(available >= 0)
  }
  func awaitingForDate: Date? = if (isUnavailable) availablePeriods.first?.start
    
  compute period = Period(
    start = delivery?.dispatchDate.alt(Date.now), 
    end = assetReturn?.deliveryDate.alt(delivery?.dispatchDate.alt(Date.now) + toDuration))
 

  register outs: [FAProdReg]? {
    value = {
      if (let d = delivery?.as[SalesDelivery]) {
        if (d.status != Canceled && active) {
          product.product.productQtys(itemsQty).map {i =>
            FAProdReg(
              product = i.product, legalEntity = d.legalEntity, store = d.source!, 
              date = d.dispatchDate, document = d, priorityDate = order.date, isFuture = d.status == Planned, 
              qty = -i.qty)
          }
        } else null
      }
    }
    availableFor = Inline
  }
  register assetsOut: [FAReg]? = if (let d = delivery?.as[SalesDelivery]) {
    if (d.status != Canceled && active) {
      assets.map{asset =>
        FAReg(
          asset = asset, toAvailable = -1, toInUse = 1, autoProd = false,
          date = d.dispatchDate, document = d, isFuture = d.status == Planned)
      }
    }  else []
  }
   
  register ins: [FAProdReg]? {
    value = if (let d = assetReturn?.as[SalesAssetReturn]) {
      if (d.status != Canceled && delivery?.status != Canceled && active) {
        product.product.productQtys(itemsQty).map {i =>
          FAProdReg(
            product = i.product, legalEntity = d.legalEntity, store = d.destination, 
            date = d.deliveryDate, document = d, priorityDate = order.date, isFuture = d.status != Delivered, 
            qty = i.qty)
        }
      } else null
    }
    availableFor = Inline
  }
  
  register assetsIn: [FAReg]? = if (let d = assetReturn?.as[SalesAssetReturn]) {
    if (d.status != Canceled && delivery?.status != Canceled && active) {
      assets.map{asset =>
        FAReg(
          asset = asset, toStore = d.destination, toAvailable = 1, toInUse = -1, autoProd = false,
          date = d.deliveryDate, document = d, isFuture = d.status != Delivered)
      }
    }  else []
  }
}

class AvailablePeriods {
  field safe: [Period]?
  field unsafe: [Period]?
}