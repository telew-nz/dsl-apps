interface IContinuousPriceModel extends IPriceModel {
  func openedPeriod: TimePeriod 
  compute closedPeriod: TimePeriod = TimePeriod(start = openedPeriod.end, end = openedPeriod.start)
  func overnightCharge: Qty[RentDurationUnit]?
  func hasNight = openedPeriod.start != openedPeriod.end
  func hasNightCharge = hasNight && overnightCharge
  func currency: Currency
  func pricesIncludingGst: Bool
  func durations: [RentDuration]
  ref compute rentDurationUnits: [RentDurationUnit]? = durations*.rentUnit.toSet
  private func calcDuration(deliveryDate: Date, returnDate: Date): Duration? = {
    (returnDate - deliveryDate) - _durNextDate(deliveryDate, returnDate)
  }
  private func _durNextDate(date: Date, end: Date): Duration? = {
    if (date < end) {
      let sd = date.start(day)
      Period(start = date, end = end).intersection(closedPeriod.addDate(sd)).duration + _durNextDate(end.time.addDate(sd + 1_day), end)
    }
  }
  private func addNights(date: UtcDate[day], start: Date, ret: Date): Date = {
    if (date <= ret.date) {
      let n = closedPeriod.addDate(date)
      if (ret > n.start) {
        addNights(date + 1_day, start, ret + Period(start = start.as[UtcDate]!, end = n.end).intersection(n).duration)
      } else ret
    } else ret
  }
  
  func rentDuration(delivery: RentDeliveryDesc, assetReturn: RentDeliveryDesc, unit: RentDurationUnit?): Qty[RentDurationUnit]? = {
    if (
      let dd = calcDuration(delivery.dispatchDate, assetReturn.deliveryDate)    
    ) {
      toRentDuration(dd, unit)
    } else {
      0.as[Qty[unit.alt(durations.first.rentUnit)]]
    }
  }
  func toRentDuration(dd: Duration?, unit: RentDurationUnit?):Qty[RentDurationUnit]?  = {
    let u = durations.find(d => d.rentUnit == unit) 
      .or(durations.sortBy(duration.desc).find(_.duration <= dd && _.maxDuration ?>= dd))
      .or(durations.sortBy(duration).find(_.duration >= dd))
      .alt(durations.first)
    let r = (dd.to[Duration[u.duration.unit!]].as[Qty[8]]/u.duration).ceilInc(u.increment).as[Qty[u.rentUnit]]
    let rdur = u.duration*r
    if (rdur != dd) {
      toRentDuration(rdur, unit)
    } else {
      r
    }
  }
  
  func rentReturnDate(delivery: RentDeliveryDesc, returnSchedule: Schedule?, qty: Qty[RentDurationUnit]?): Date = {
    let date = delivery.dispatchDate + toDuration(qty)
    adjustToSchedule(delivery, returnSchedule, qty, if (hasNight) {
      addNights(delivery.dispatchDate.date, delivery.dispatchDate, date)
    } else date).max(delivery.dispatchDate)
  }
  
  func nightsCount(deliveryDate: Date, returnDate: Date): Qty[Night]? = {
    if (hasNight) {
      (returnDate.date - deliveryDate.date).days.as[Qty[Night]]
    }
  }
  
  private func adjustNightSlot(date: Date): Date = {
    if (let nightSlot = closedPeriod.period(date)) nightSlot.end.as[Date]! else date
  }
  
  private func adjustToSchedule(delivery: RentDeliveryDesc, returnSchedule: Schedule?, qty: Qty[RentDurationUnit]?, date: Date) = {
    let d = date.as[Date]!
    if (let shedule = returnSchedule) {
      if (let n = closedPeriod.period(d)) {
        if (qty && chargeOvernight(delivery)) shedule.workingOrPrevPeriod(n.start.as[Date]!).end
        else if (shedule.isWorkingDate(n.end.as[Date]!)) n.end
        else shedule.prevPeriod(n.end.as[Date]!).end
      } else if (shedule.isWorkingDate(d)) {
        d
      } else {
        shedule.prevPeriod(d).end
      }
    } else d
  }
  
  func toDuration(qty: Qty[RentDurationUnit]?): Duration? = {
    if (qty) duration(qty.unit!.as[RentDurationUnit]!)*qty
  }
  func duration(unit: RentDurationUnit): Duration? = {
    durations.find(u => u.rentUnit == unit)?.duration
  }
}

class RentDuration {
  field rentUnit: RentDurationUnit = default
  field unit: DateUnit = day
  field duration: Duration[unit] {
    canEditUnit
    default = 1
  }
  field increment: Number = 1
  field maxDuration: Duration[unit]? 
  view form = grid(labels = Top) {
    row {rentUnit >> field(compact = true, label = "") duration >> field(align = Right) increment >> field(align = Right) maxDuration >> field(align = Right)}
  }
}

interface IContinuousProductPrice extends IPrice {
  func model: IContinuousPriceModel
  func currency = model.currency
  func overnightCharge: Money[currency]? = if (let oc = model.overnightCharge) durationPrice(oc)
  func prices: [DurationPrice]?
  func gstRate: UPercent[2]?
  
  protected func durationPrice(duration: Qty[RentDurationUnit]?): Money[currency]? = {
    if (let dur = model.toDuration(duration)) {
      let ps = prices.map(price, duration = model.toDuration(_.duration)).sortBy(duration.desc)
      if (let pp = ps.find(dur >= _.duration).or(ps.last), let duration = pp.duration) {
        pp.price*dur/duration
      }
    }
  }
  func rentSalesAmount(currency: Currency, customer: ICustomer?, itemsQty: Qty, duration: Qty[RentDurationUnit]?, nightsCount: Qty[Night]?, inclGst: Bool): Future[RentAmount] = {
    let am = durationPrice(duration)
    let a <- (am*itemsQty).to[Money[currency]](now)
    let nm = overnightCharge*nightsCount*itemsQty
    let n <- nm.to[Money[currency]](now)
    RentAmount(dayAmount = a, nightAmount = n)
  }
  func salesPrice(currency: Currency, inclGst: Bool): Future[SalesPrice] = {
    let p <- prices.find(unit.isFirst)?.price.to[Money[currency]](now)
    SalesPrice(regular = p, current = p)
      .convertGst(gstRate, model.pricesIncludingGst, inclGst)
  }
}
