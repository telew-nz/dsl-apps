inline entity RentDeliveryItem extends IDlvProgressItem with FProductItems {
  override func storeRequired = true 
  impl func moveInStockType = Asset
  impl func moveOutStockType = Asset
  
  impl func needsRevision: Bool = updated(product) || updated(dur) || updated(durUnit) || updated(itemsQty) 
  impl func sameAsPrev(prev: IDlvProductItem): Bool = {
    let p = prev.as[RentDeliveryItem]!
    p.product == product && p.itemsQty == itemsQty && p.dur == dur && p.durUnit == durUnit 
  }
  impl field product: ISubProduct {
    available(Ref).with(vendors, productItems, invoiceDesc)
    from = ISubProduct.all.filter(_.isTangible && _.rentStatus.forRent)
    init = {
      new.superProduct.status = NotForSale
      new.superProduct.rentStatus = ForRent
    }
    update = {
      let pm <- priceModel
      this.silentUpdate(durUnit, if (pm == PriceModelNone) default else pm.rentDurUnits.first.alt(default))
      this.silentUpdate(dur, this.dur.as[UQty[this.durUnit]])
      durUpdated()
    }
  }
  impl func unit: ProductUnit = Item
  impl field dur: UQty[durUnit] {
    label = "Duration"
    canEditUnit = true
    default = delivery.items*.as[IDlvProgressItem].filter(_ != this)*.dur.first.alt(1.as[UQty[DurUnit.default]])
    update = {
      if (let oo = old, let s = sales) {
        s.sAmount = s.sAmount*(new.as[UDec[12]]/oo)
      }
      durUpdated()
    }
  }
  impl field durUnit: DurUnit {
    default = dur.unit!.as[DurUnit]!
    update = {
      this.dur = this.dur.as[UQty[durUnit]]
    }
    from = priceModel.get.rentDurUnits
  }
    
  func durUpdated(): Future[Void] = {
    let _ <- {
      if (delivery.items*.as[IDlvProgressItem].exists(i => i.dur != this.dur)) {
        let dd <- delivery.calcRetDeliveryDate
        let sdd = delivery.as[SalesDelivery]!
        if (sdd.retDeliveryDate != dd) {
          let sd = SalesDelivery(store = sdd.store, salesOrders = [order.as[SalesOrder]!], dispatchDate = sdd.dispatchDate, retDeliveryDate = dd, allItems = [this]) 
          sd.updateRetDeliveryDate()
        } else {
          dd
        }
      } else {
        delivery.updateRetDeliveryDate()
      }
    }
    let dlvs = order.as[SalesOrder]!.deliveries*.as[SalesDelivery]
    if (dlvs.count > 1) {
      dlvs.filter(!state.started)
        .groupBy(dispatchDate, retDeliveryDate)
        .values
        .foreach{returns =>
          if(returns.count > 1) {
            returns.drop(1).foreach{i =>
              i.items.foreach{j => j.delivery = returns.first}
              i.delete
            }
          }
        }
    }
    
    let _ <- updateAmount()
    if (let o = sales?.order) o.updateBond()
  }
  
  override func updateAmount(): Future[Void] = {
    let pm <- priceModel
    let _ <- if (let s = sales) {
      let currency = s.order.currency
      let vendor = s.order.vendor
      let customer = try? s.order.customer!
      let pam <- productPrice
      let ra <- if (let pr = pam) {
        Query(pr).map{p =>
          p.rentAmount(currency, s.order.pricesIncludingGst, customer, itemsQty, rentPeriod)
        }.result
      }

      if (let r = ra) {
        s.amountEditor = r.current
      }
    }

    updateGst()
  }
  
  impl func qty = dur*itemsQty
  override field itemsQty: UQty[ProductUnit.Item] {
    default = 1
    label = "Quantity"
    update = {
      let _ <- if (let old) {
        if (let s = sales) s.sAmount = s.sAmount*(new.as[UDec[12]]/old)
        if (let p = purchase) p.sAmount = p.sAmount*(new.as[UDec[12]]/old)
        updateAmount()
      }
      if (let o = sales?.order) o.updateBond()
    }
  }
  
  impl field sales: SalesRentItem? inline by deliveryItem {
    default = if (let d = delivery?.as[ISalesDelivery]) createSales(d)
  }
  impl func purchase: IPurchaseItem? = null
  func createSales(sd: ISalesDelivery): SalesRentItem = SalesRentItem(deliveryItem = this, order = sd.salesOrders.first)
  func createPurchase(pd: IPurchaseDelivery): IPurchaseItem = throw "Unsupported"
  
  func clearSales: Void = sales = null
  func clearPurchase: Void = {}
    

  impl view form = grid(labels = None, width = 1.2, margin = TopBottom) {
    row{product  >> field(width = 0.6), itemsQty >> field(width = 0.3), dur >> field(width = 0.3)}
    stockDesc
    availableView
    productItems >> field(editMode = Original)
  }
  
  func toDuration = delivery.dlvCompleteDate - delivery.dlvInTransitDate

  compute priceAndModel: Future[(IIPriceModel, IPriceRent?)] = {
    let vendor = sales!.order.vendor
    let prod = this.product.superProduct
    back {
      if (let pm = vendor.priceModelRent(prod)) (pm.as[IIPriceModel], pm.priceRent(this.product)) 
      else (PriceModelNone, null)
    }
  }
  compute priceModel: Future[IIPriceModel] = priceAndModel.map(#0)
  compute productPrice: Future[IPriceRent?] = priceAndModel.map(#1)
  compute unsafePeriods: [Period]? = _availablePeriods?.unsafe
  compute availablePeriods: [Period]? = _availablePeriods?.safe 
  compute _availablePeriods: AvailablePeriods? = {
    let excludeItems = (try? inStocks!)*.id + (try? outStocks!)*.id
    let vendor = delivery.order.orderVendor!
    let returnSchedule = delivery.completeShDate.schedule
    ProdReg.at((1, Date.min))
      .find(_.product == this.product && _.stockType == Asset && _.legalEntity == sales?.order?.vendor && _.store ==? delivery.store)
      .map{reg => 
        AvailablePeriods(  
          unsafe = reg.availablePeriods(itemsQty, this.period.start.min(Date.now), priorityDate = order.date, excludeItems),
          safe = RentAdjuster.adjustPeriods(
            dur, if (let pm = vendor.priceModelRent(this.product.superProduct)) pm.as[IIPriceModel] else PriceModelNone, 
            reg.store.getSchedule, returnSchedule, reg.availablePeriods(itemsQty, Date.now, priorityDate = null, excludeItems)
          )
        )
      }

  }

  override compute isAvNow = {
    if (!delivery.dlvState.started) unsafePeriods.exists(includes(period)) 
    else outStocks && outStocks.all(available >= 0)
  }
  override func awaitingForDate: Date? = if (isUnav) availablePeriods.first?.start
    
  compute period = Period(
    start = delivery.dlvInTransitDate, 
    end = delivery.dlvCompleteDate)
}

class AvailablePeriods {
  field safe: [Period]?
  field unsafe: [Period]?
}