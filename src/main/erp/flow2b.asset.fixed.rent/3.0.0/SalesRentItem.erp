class ODGDeliveryReturn extends OrderDeliveryGroup {
  field delivery: Delivery
  field assetReturn: AssetReturn
  
  view rowView = row(fontSize = 1) {x(span = 4){
    label("Delivery") delivery.number delivery.deliveryDate >> field(visible = true, stretch = 1)
    label("Return") assetReturn.number assetReturn.deliveryDate >> field(visible = true)
  }}
}

inline entity SalesRentItem extends ISalesItem with OrderProductItem {
  singular = "Rent"
  func complete = delivered && paid && assetReturned
  func orderDeliveryItem: IOrderDeliveryItem? = deliveryItem
  field deliveryItem: RentDeliveryItem inline by sales
  func qty = deliveryItem.qty
  func deliveryGroup: OrderDeliveryGroup = {
    if (let d = delivery) {
      if (let r = deliveryItem.assetReturn) ODGDeliveryReturn(delivery = d, assetReturn = r)
      else ODGDelivery(delivery = d) 
    } else ODGEmpty
  }
  compute price: Money[currency]? {
    value = amount/qty
    update = {amount = new*qty}
  }
  view form = grid(width = 1.2, margin = None) {
    row {deliveryItem.product >> field(label = "", width = 0.35) 
         deliveryItem.duration >> field(canEditUnit = true, width = 0.2)
         deliveryItem.itemsQty >> field(canEditUnit = true, width = 0.2) 
         priceEditor >> field(width = 0.2) 
         amountEditor >> field(width = 0.25)
    }
    read {
      space(width = 0.95) discountEditor >> field(width = 0.25)
    }

    deliveryItem.delivery >> field(visible = this.order.deliveries.count > 1)
    deliveryItem.assetReturn >> field(visible = this.order.assetReturns.count > 1)
    invoice
    deliveryItem.availableView >> field(label = "", visible = delivery?.is[SalesDelivery] || !delivery)
    deliveryItem.assets
    edit {
      panel(collapsed = !discount, label = "More", frame = false, margin = None) {
        grid(width = 1.2, margin = Around) {
          row{gstEditor >> field(width = 0.2) discountEditor >> field(width = 0.25) space(width = 0.75)}
        }
      }
    }
  }
  
  func revenueLedger(baseAmount: Money): Ledger = {
    LSales(account = Sales, journal = delivery!, product = deliveryItem.product, qty = deliveryItem.baseQty, 
           counterparty = order.customer, trAmount = -baseAmount)
  }
  func assetReturned = deliveryItem.assetReturn?.status == Delivered
  func assetReturnInTransit = deliveryItem.assetReturn?.status == InTransit
    
  func quoteSwitch(quote: Bool): Void = {
    if (!order.assetReturns) {
      let ar = SalesAssetReturn(primary = true, salesOrders = [order])
      order.setDefaultSalesReturn()
      ar.updateDeliveryDate()
    }
  }
  
  view rowView = group {
    row{deliveryItem.product >> field(rowSpan = 2)
        y(margin = None){
          deliveryItem.duration >> field(canEditUnit = true, align = Right)
          deliveryItem.itemsQty >> field(canEditUnit = true, align = Right) 
        }
        priceEditor >> field (align = Right)
        amountEditor >> field (align = Right)
    }
    row(visible = discount){label("Discount", span = 2, align = Right) discountEditor}
  }
}
