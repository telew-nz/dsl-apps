object RentAdjuster {
  func adjustPeriods(duration: UQty[ProductUnit], pm: IRentPriceModel, 
                       deliverySchedule: ISchedule, returnSchedule: ISchedule, periods: [Period]?
                     ): [Period]? = 
  {
    periods
      .mapOpt{p =>
        if (let nd = deliverySchedule.nextWorkingDate(p.start)) {
          adjustPeriod(duration, pm, deliverySchedule, returnSchedule, 
                       Period(start = nd, end = p.end), 0)
        }
      }
  }
  
  func adjustPeriod(duration: UQty[ProductUnit], pm: IRentPriceModel, 
                    deliverySchedule: ISchedule, returnSchedule: ISchedule, 
                    period: Period, attempts: Int?): Period? = 
  {
    let s = period.start
    let sDesc = RentDeliveryDesc(deliveryDate = s, dispatchDate = s, schedule = deliverySchedule)
    let e = pm.rentReturnDate(sDesc, returnSchedule, duration)
    let dur1 = pm.rentDuration(
      sDesc, 
      RentDeliveryDesc(deliveryDate = e, dispatchDate = e, schedule = returnSchedule), 
      duration.unit!.as[DurUnit]!
    )
    if (e > period.end) null
    else if (e == s || dur1 < duration) {
      if (attempts < 10) {
        if (let np = deliverySchedule.nextPeriod(s)) {
          let s2 = np.start
          if (s2 < period.end) {
            adjustPeriod(duration, pm, deliverySchedule, returnSchedule,
              Period(start = s2, end = period.end), 
              attempts + 1)
          }
        }
      }
    } else period
  }
}