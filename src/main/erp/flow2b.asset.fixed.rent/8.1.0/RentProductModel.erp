trait TRentPriceModel extends IStaticPriceModel {
  func currency: Currency
  func products: [TRentProductPrice]?
  func createProduct(product: IBaseProduct, prices: [DurPrice]?, bond: UMoney?): TRentProductPrice
}


trait TRentProductPrice extends IStaticPrice {
  field prices: [DurPrice]? {
    singular = "Price"
    update = {
      new.foreach(_.price.currency = this.currency)
    }
    init = {
      new.unit =? model.rentDurUnits.first
    }
    default = if (let p = model.rentDurUnits.first) [DurPrice(unit = p)]
  }
  
  field bond: UMoney[currency]? 
  override compute gstRate: UPercent[2]? = product.gstRate(model.vendor).alt(0)
  func superProduct = product.superProduct
    
  impl event onSave = product.touch
  
  back func addDuration(unit: DurUnit, price: SalesPrice): Void = {
    addDuration(1.as[UQty[unit]], price)
  }
  back func addDuration(duration: UQty[DurUnit], price: SalesPrice): Void = {
    prices = prices + DurPrice(unit = duration.unit!.as[DurUnit]!, dur = duration, price = price)
  }
  
  impl view form = grid(labels = None, spacing = false) {
    prices >> list(frame = false, margin = None){grid(labels = None, margin = None){row{
      unit.name.fromCamelCase >> field(fontStyle = H3) 
      price >> field(align = Right) 
    }}}
    row{h3("Bond", visible = bond) bond >> field(align = Right) }
  }
  view formEx = grid(labels = None){
    grid(labels = None, visible = !ui.isSelected) {
      row {
        product >> field(editMode = AlwaysRead, title = product) 
        prices.first >> x{
          dur >> field(stretch = 1, canEditUnit = false) 
          price >> field(align = Right) 
        }
      }
    }
    read {
      product >> field(visible = ui.isSelected, title = product)
      form >> field(visible = ui.isSelected)
    }
    edit {
      product >> field(visible = ui.isSelected, title = product)
      prices >> list(frame = false, visible = ui.isSelected){x{
        dur  price  >> field(stretch = 1)
      }}
      row{h3("Bond", visible = ui.isSelected) bond >> field(visible = ui.isSelected)}
    }
  }
}