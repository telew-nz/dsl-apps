interface IRentPriceModel extends IPriceModel {
  func night: NightDesc?
  func slots: [RentSlot]?
  func currency: Currency
  func pricesIncludingGst: Bool
  func chargeOvernight(delivery: RentDeliveryDesc): Bool = {
    delivery.items.exists(chargeOvernight)
  }
  func calcDuration(deliveryDate: Date, returnDate: Date): Duration? = {
    let p = Period(start = deliveryDate, end = returnDate)
    p.duration - _durNextDate(deliveryDate.date, returnDate, p)
  }
  func _durNextDate(date: UtcDate[day], end: UtcDate[day], period: Period): Duration? = {
    if (date < end) period.intersection(night!.closed.addDate(date)).duration + _durNextDate(date + 1_day, end, period)
  }
  func addNights(date: UtcDate[day], start: Date, ret: Date): Date = {
    if (date <= ret.date) {
      let n = night!.closed.addDate(date)
      if (ret > n.start) {
        addNights(date + 1_day, start, ret + Period(start = start.as[UtcDate]!, end = n.end).intersection(n).duration)
      } else ret
    } else ret
  }
  func adjustReturnDate(delivery: RentDeliveryDesc, returnSchedule: Schedule?, qty: Qty[RentDurationUnit]?, date: Date): Date = {
    adjustToSchedule(delivery, returnSchedule, qty, if (night) {
      addNights(delivery.dispatchDate.date, delivery.dispatchDate, date)
    } else date)
  } 
  
  func adjustToSchedule(delivery: RentDeliveryDesc, returnSchedule: Schedule?, qty: Qty[RentDurationUnit]?, date: Date) = {
    let d = date.as[Date]!
    if (let shedule = returnSchedule) {
      if (let n = night?.slot?.period(d)) {
        if (qty && chargeOvernight(delivery)) shedule.workingOrPrevPeriod(n.start.as[Date]!).end
        else if (shedule.isWorkingDate(n.end.as[Date]!)) n.end
        else shedule.prevPeriod(n.end.as[Date]!).end
      } else if (shedule.isWorkingDate(d)) {
        d
      } else {
        shedule.prevPeriod(d).end
      }
    } else d
  }
  
  func nightsCount(deliveryDate: Date, returnDate: Date): Qty[Night]? = {
    if (night) {
      (returnDate.date - deliveryDate.date).days.as[Qty[Night]] 
    }
  }
   
  func rentDuration(delivery: RentDeliveryDesc, assetReturn: RentDeliveryDesc, unit: RentDurationUnit?): Qty[RentDurationUnit]? = {
    if (slots) {
      let dd = adjustNightSlot(delivery.dispatchDate)
      if (let sc = slotsCount(dd.date, assetReturn.deliveryDate.date, Period(start = dd, end = assetReturn.deliveryDate))) {
        let u: RentDurationUnit = unit.filter(slotsCount).or(RentDurationUnit.all.sortBy(slotsCount.desc).find(_.slotsCount <= sc && !mainUnit)).alt(default)
        if (let usc = u.slotsCount) {
          sc.as[Qty[u]]/usc
        } else {
          0.as[Qty[u]]
        }
      } else {
        0.as[Qty[unit.alt(RentDurationUnit.default)]]
      }
    } else {
      super.rentDuration(delivery, assetReturn, unit)
    }
  }
  
  func rentReturnDate(delivery: RentDeliveryDesc, returnSchedule: Schedule?, qty: Qty[RentDurationUnit]?): Date = {
    if (slots) {
      adjustToSchedule(
        delivery, returnSchedule, qty, 
        moveDate(
          date = adjustNightSlot(delivery.dispatchDate), 
          slotsCount = qty.unit!.as[RentDurationUnit]!.slotsCount*qty)
      ).max(delivery.dispatchDate)
    } else {
      super.rentReturnDate(delivery, returnSchedule, qty)
    }
  }
  func adjustNightSlot(date: Date): Date = {
    if (let nightSlot = night?.slot?.period(date)) nightSlot.end.as[Date]! else date
  }
  func moveDate(date: Date, slotsCount: Dec[2]?): Date = {
    if (slotsCount <= 0 || !slots) date
    else {
      let t = date.time
      if (let slot = slots.sortBy(timePeriod.end).find(t < timePeriod.end)) {
        if (slotsCount >= 1) {
          moveDate(date.date.addDate(slot.timePeriod.end).as[Date]!, slotsCount - 1)
        } else {
          (date.date.addDate(slot.timePeriod.start) + slot.timePeriod.duration*slotsCount).as[Date]!
        }
      } else {
		moveDate(date.start(of = day) + 1_day, slotsCount)
      }
    }
  }
  func slotsCount(date: Date[day], end: Date[day], period: Period): Dec[2]? = {
    if (date <= end) {
      slots.map{s => s.timePeriod.addDate(date)}.filter(_.intersects(period)).count + slotsCount(date + 1_day, end, period)
    }
  }
}

class RentSlot {
  field timePeriod: TimePeriod {label = "Period"}
  view form = grid(labels = None) {
    timePeriod >> field(frame = false)
  }
}

class NightDesc {
  field closed: TimePeriod {
    label = "Night period when the operation is closed"
  }
  field slot: TimePeriod {
    label = "Night slot when the customer can be charged for the night period only"
  }
  view form = grid(labels = Top) {
    closed >> field(frame = false)
    slot >> field(frame = false)
  }
}

interface IRentProductPrice extends IPrice {
  func model: IRentPriceModel
  func currency = model.currency
  func overnightCharge: Money[currency]?
  func prices: [DurationPrice]
  func gstRate: Percent[2]?
  func rentSalesAmount(currency: Currency, customer: LegalEntity?, item: RentDeliveryItem, inclGst: Bool): Future[Money?] = {
    var am = if (model.slots) {
      let ps = prices.sortBy(unit.slotsCount.desc)
      if (let sc = item.duration.unit!.as[RentDurationUnit]!.slotsCount*item.duration) {
        let pp = ps.find(sc >= unit.slotsCount).alt(ps.last)
        pp.price*sc/pp.unit.slotsCount
      }
    } else {
      let ps = prices.sortBy(unit.duration.desc)
      if (let dur = model.toDuration(item.duration)) {
        let pp = ps.find(dur >= unit.duration).alt(ps.last)
        pp.price*dur.to[Duration[pp.unit.duration.unit!]]/pp.unit.duration
      }
    }
    if (item.chargeOvernight) am = am + overnightCharge*item.nightsCount
    (am*item.itemsQty).to[Money[currency]](now)
  }
  func salesPrice(currency: Currency, inclGst: Bool): Future[Money?] = {
    convertGst(gstRate, prices.find(unit.isFirst)?.price, model.pricesIncludingGst, inclGst).to[Money[currency]](now)
  }
}
