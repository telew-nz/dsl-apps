trait Order extends Entity with DashboardItem {
  background = "#a73e6c"
  ref field referenceNumber: String?
  func orderCustomer: ICustomer?
  func orderVendor: IVendor?
  object field gen: Int?
  const ref field n: Int? = increment(Order.gen)
  ref field currency: Currency {
    default = Currency.our
  }
  func counterparty: LegalEntity?
  func pricesIncludingGst = if (let v = orderVendor) v.pricesIncludingGst else legalEntity.pricesIncludingGst
  func legalEntity: OurLegalEntity
  ref compute deliveryOverdueState: DeliveryOverdueState {
    value = {
      if (status == Canceled) NoDeliveryOverdue
      else if (deliveries.exists(overdueState == DeliveryOverdue)) DeliveryOverdue
      else NoDeliveryOverdue
    } 
    visible = deliveryOverdueState == DeliveryOverdue
  }
  ref compute paymentOverdueState: PaymentOverdueState {
    value = {
      if (status == Canceled) NoPaymentOverdue
      else if (invoices.exists(overdueState == PaymentOverdue)) PaymentOverdue
      else NoPaymentOverdue
    } 
    visible = paymentOverdueState == PaymentOverdue
  }
  func preEntered: Bool
  ref compute invoiceIssueState: InvoiceIssueState {
    value = {
      if (invoices.all(issueState == InvoiceNotIssued)) InvoiceNotIssued
      else if (invoices.exists(issueState != InvoiceIssued)) InvoiceModified
      else InvoiceIssued
    }
    visible = invoiceIssueState != InvoiceIssued
  }
  ref field status: OrderStatus = Active
  field date: Date = now
  func canceled = status == Canceled
  func deliveries: [Delivery]?
  func invoices: [Invoice]?
  func real: Bool = status == Active
  func nakedN: String = n.format(3, 0)!
  func allItems: [OrderItem]?
  func items: [OrderItem]?
  compute isInstant: Bool = !counterparty || if (let i = invoices.onlyOne, let d = deliveries.onlyOne, let r = i.revisions.onlyOne) {
    d.deliveryStatus == Delivered 
    	&& (d.deliveryDate - this.date).abs <= 3600_second 
    	&& (r.date - this.date).abs <= 3600_second 
        && i.isInstantPaid
  }

  event onSave = {
    deliveries*.touch
    invoices*.touch
  }
  ref compute state: OrderState {
    value = {
      if (status == Canceled) Canceled
      else if (status == Quote) Quote
      else if (complete) Complete
      else Processing
    }
    update = {
      this.status = new.status
      if (new == Quote) {
        if (!items) {
          deliveries*.delete()
          invoices*.delete()
        }
        items*.quoteSwitch(false)
      } else if (old == Quote && new == Processing) {
        if (!deliveries) default(deliveries)
        if (!invoices) default(invoices)
        setDefaultDelivery()
        setDefaultInvoice()
        items*.quoteSwitch(true)
      } 
    }
    from = [OrderState.Quote, if (complete) OrderState.Complete else OrderState.Processing, OrderState.Canceled]
  }
  func complete = allItems && items.all(complete) && !invoices.exists(overpaid)
  func active: Bool = state != Complete && state != Canceled
  compute gstAmount: Money[currency]? = items*.gstAmount.sum
  compute totalNoGst: Money[currency]? = items*.orderAmountNoGst.sum
  ref stored compute total: Money[currency]? = items*.orderAmountWithGst.sum
  
  ref stored compute deliveryState: OrderDeliveryState {
    value = {
      if (status != Active || !deliveries) NotDelivered
      else if (deliveries.all(status == Delivered)) Delivered
      else if (deliveries.exists(status == Delivered)) PartiallyDelivered
      else if (deliveries.exists(status == InTransit)) InTransit
      else Planned
    }
    visible = state == Processing
    update = {
      if (new != deliveryState) {
        deliveries.foreach{d => d.status = if (new == OrderDeliveryState.Planned) DeliveryStatus.Planned else DeliveryStatus.Delivered}
      }
    }
    from = [OrderDeliveryState.Planned, OrderDeliveryState.Delivered].toSet + deliveryState
  }
  
  ref stored compute paymentState: PaymentState {
    value = {
      if (invoices.exists(overpaid)) Overpaid
      else if (!total || invoices.all(paid)) Paid
      else if (invoices.exists(paidAmount)) PartiallyPaid
      else Unpaid
    }
    visible = state == Processing
  } 
  
  ref compute dropShipState: DropShipState {
    value = {
      if (deliveries.exists(_.is[DropShipDelivery])) {
        if (deliveries.all(_.is[DropShipDelivery])) DropShip
        else PartialDropShip
      } else NotDropShip
    }
  	visible = dropShipState != NotDropShip
  }
    
  func orderDate = date
  func journalDate = date
  func orderCurrency = currency
  func orderTotal = total
    
  func setDefaultDelivery(): Void = {
    if (let d = deliveries.last) this.items*.orderDeliveryItem.filter(!delivery).foreach(delivery = d)
  }
  func setDefaultInvoice(): Void = {
    if (let d = invoices.last) this.items.filter(!invoice).foreach(invoice = d)
  }
}

