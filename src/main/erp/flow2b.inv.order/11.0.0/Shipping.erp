trait IShipping extends PseudoEntity with IProdMovePath {
  func dlv: OrderDlv
  var dlvDate: Date
  func startAttached: Bool
  
  impl func mainEntity = dlv
  impl func pathStart = start
  impl func pathEnd = end
  
  field detached: Bool
  
  field start: Date {
    label = "Dispatch Date"
    default = if (startAttached) dlvDate else dlvDate - 1_day
    visible = !startAttached || detached 
    update = {
      if (let o = old) {
        if (startAttached || detached) {
          silentUpdate(end, end + (new - o))
        } else if (new > end) {
          silentUpdate(end, new)
        }
      }
      updDlvDate
    }
  }
  
  field end: Date {
    label = "Receiving Date"
    default = if (!startAttached) dlvDate else dlvDate + 1_day
    visible = startAttached || detached 
    update = {
      if (let o = old) {
        if (!startAttached) {
          silentUpdate(start, start + (new - o))
        } else if (new < start) {
          silentUpdate(start, new)
        }
      }
      updDlvDate
    }
  }
  
  func dlvDateUpdated(date: Date): Void = {
    if (!detached) {
      if (startAttached) {
        start = date
      } else {
        end = date
      }
    }
  }
  func updDlvDate: Void = {
    if (startAttached) {
      if (detached) detached = dlvDate != start
      else dlvDate = start
    } else {
      if (detached) detached = dlvDate != end
      else dlvDate = end
    }
  }
}

trait Shipping extends IShipping {
  field loc: ILocation? {
    label = "Location"
    from = Query(dlv.counterparty).flatMap(deliveryLocations)
    init = {
      new.as[IActorLocation]?.actor = dlv.counterparty
    }
  }
  
  impl view form = grid(width = 1.2) {
    row {
      loc 
      y(margin = None, label = "Dispatch Date", visible = ui.edit || detached || !startAttached){
        start
        add("Date", visible = !detached && startAttached) {detached = true}
      }
      y(margin = None, label = "Receiving Date", visible = ui.edit || detached || startAttached){
        end
        add("Date", visible = !detached && !startAttached) {detached = true}
      }
    }
    freightView
  }
}