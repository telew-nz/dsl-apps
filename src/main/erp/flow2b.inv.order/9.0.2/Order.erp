trait Order extends Entity with DashboardItem with Journal with HasDebtCounterparty with IOrder {
  background = "#a73e6c"
  ref field referenceNumber: String?
  func orderCustomer: ICustomer?
  func orderVendor: IVendor?
  object field gen: Int?
  const ref field n: Int? = increment(Order.gen)
  ref field currency: Currency {
    default = Currency.our
  }
  func references = if (let r = referenceNumber) [r]
  
  func isQuote = state == Quote
  func pricesIncludingGst = if (let v = orderVendor) v.pricesIncludingGst else legalEntity.pricesIncludingGst
  func legalEntity: OurLegalEntity
  ref compute deliveryOverdueState: DeliveryOverdueState {
    value = {
      if (status == Canceled) NoDeliveryOverdue
      else if (deliveries.exists(overdueState == DeliveryOverdue)) DeliveryOverdue
      else NoDeliveryOverdue
    } 
    visible = deliveryOverdueState == DeliveryOverdue
  }
  func preEntered: Bool
  ref field status: OrderStatus = Active
  ref field date: Date = now
  func canceled = status == Canceled
  func deliveries: [Delivery]?
  
  func real: Bool = status == Active
  func nakedN: String = n.format(3, 0)!
  func allItems: [OrderItem]?
  func items: [OrderItem]?

  back func calcInstant: Bool = !debtCounterparty || if (let d = deliveries.onlyOne) {
    d.deliveryStatus == Delivered 
    	&& (d.deliveryDate - this.date).abs <= 3600_second 
        && isInstantPaid
        && if (let r = invoices.onlyOne) (r.date - this.date).abs <= 3600_second else !invoices
  }

  event onSave = {
    deliveries*.touch    
  }
  ref compute state: OrderState {
    value = {
      if (status == Canceled) Canceled
      else if (status == Quote) Quote
      else if (complete) Complete
      else Processing
    }
    update = {
      this.status = new.status
      if (new == Quote) {
        deliveries*.delete()
        items*.quoteSwitch(false)
      } else if (old == Quote && new == Processing) {
        if (!deliveries) default(deliveries)
        setDefaultDelivery()
        items*.quoteSwitch(true)
      } 
    }
    from = [OrderState.Quote, if (complete) OrderState.Complete else OrderState.Processing, OrderState.Canceled]
  }
  func complete = allItems && items.all(complete)// && (customer.debtAllowance || !invoices.exists(overpaid))
  func active: Bool = state != Complete && state != Canceled
  
  ref stored compute deliveryState: OrderDeliveryState {
    value = {
      if (status != Active || !deliveries) NotDelivered
      else if (deliveries.all(status == Delivered)) Delivered
      else if (deliveries.exists(status == Delivered)) PartiallyDelivered
      else if (deliveries.exists(status == InTransit)) InTransit
      else Planned
    }
    visible = state == Processing
    update = {
      if (new != deliveryState) {
        deliveries.foreach{d => d.status = if (new == OrderDeliveryState.Planned) DeliveryStatus.Planned else DeliveryStatus.Delivered}
      }
    }
    from = [OrderDeliveryState.Planned, OrderDeliveryState.Delivered].toSet + deliveryState
  }
  
  ref compute dropShipState: DropShipState {
    value = {
      if (deliveries.exists(_.is[DropShipDelivery])) {
        if (deliveries.all(_.is[DropShipDelivery])) DropShip
        else PartialDropShip
      } else NotDropShip
    }
  	visible = dropShipState != NotDropShip
  }
  
  front func procBarcode(s: String): Future[Void] = {
    let r <- back{IBarcode.find(s)}
    if (let r) {
      if (let p = r.barcodeItem.as[ISubProduct]) {
        addQty(p, r.qty, [])
      } else if (let p = r.barcodeItem.as[ProductItem]) {
        addQty(p.product, r.qty, [p])
      } else {
        UI.info("Barcode item is not product $(r.barcodeItem)")
      }
    } else {
      UI.info("No barcode $s found")
    }
  }
  
  func addQty(product: ISubProduct, qty: Qty, productItems: [ProductItem]?): OrderItem = {
    if (let ii = items*.as[OrderProductItem]
        .find(i => i.product == product && i.unit == qty.unit)
    ) {
      if (let i = ii?.deliveryItem?.as[DeliveryItem]) {
        i.qty = (i.qty + qty)!
        i.productItems = i.productItems + productItems
        ii
      } else {
        addItem(product, qty, productItems)
      }
    } else {
      addItem(product, qty, productItems)
    }
  }
  func addItem(product: ISubProduct, qty: Qty, productItems: [ProductItem]?): OrderItem
    
  func orderDate = date
  func journalDate = date
  func orderCurrency = currency
  func orderTotal = total
    
  func setDefaultDelivery(): Void = {
    if (let d = deliveries.last) this.items*.orderDeliveryItem.filter(!delivery).foreach(delivery = d)
  }
  
  compute refItems {
    availableFor = List
    value = RefProductItems.create(items*.as[OrderProductItem].map(RefProductQty(product = product, qty = qty)))
  }
  
  back func deliveryInstantLedgers(journal: Journal, date: Date): Future[[Ledger]?] = {
    deliveries!.first.ledgers(journal, date)
  }
}

