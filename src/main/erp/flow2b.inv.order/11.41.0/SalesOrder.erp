trait ISalesChannel extends Entity
entity SalesChannel extends ISalesChannel {
  impl field title: String
  field description: Text?
}

entity SalesOrder extends Order with ISalesOrder {
  singular = "Sale"
  plural = "Sales"
  
  icon = "shopping-cart"
  background = "#a73e6c"
  impl func orderCustomer = customer
  impl func orderVendor = vendor
  func counterparty = customer?.legalEntity
  func salesPersonAccess: Bool = !User.current!.onlyTheirOrders || isForCurrent(Salesperson)
  impl func deliveryCounterparty = customer?.legalEntity
  field sPayer: Customer? 
  impl ref compute payer: Customer? {
    available(Ref).with(cptAccs).field(legalEntity, with(legalInfo))
    value = sPayer.or(customer)
  }
  impl func debtCounterparty: LegalEntity? = payer?.legalEntity
  compute _receiver: LegalEntity? {
    value = deliveries*._receiver.onlyOne
    update = {
      deliveries*._receiver = new
    }
  }
  compute receiver: LegalEntity? {
    value = deliveries*.receiver.onlyOne
    update = {
      deliveries*.receiver = new
    }
  }
  impl func multiupdate = {
    let os = deliveries*.as[DropShipDelivery]*.purchaseOrders
    deliveries + os + os*.vendor
  }
  impl compute priceLevel: IPriceLevel = payer?.priceLevel.alt(PriceLevelDefault)
  compute shipTo: ILocation? = deliveries*.shipTo.onlyOne
  compute shipToText {
    availableFor = List
    value = shipTo?.locationAddress?.title
  }
  override func issueTypes = super[ISalesOrder].issueTypes + ITSOReturnOverdue
  
  impl field deliveries: [ISalesDelivery] inline by salesOrders {
    default = [SalesDelivery(primary = true, salesOrders = [this])]
    visible = preEntered
    from = ISalesDelivery.all.filter(i => i.legalEntity == legalEntity)
  }
  field salesChannel: ISalesChannel? {
    default = deliveries.onlyOne?.as[SalesDelivery]?.store?.as[IStore with ISalesChannel]
  }
  impl func itemRoles = [Salesperson]
  impl func addItem(prod: ISubProduct, qty: UQty, productItems: [ProductItem]?): Future[OrderItem] = {
    let si = SalesItem(order = this, deliveryItem = DeliveryItem(product = prod, unit = qty.unit?.as[ProductUnit]!, qty = qty, productItems = productItems))
    let _ <- si.deliveryItem.updateAmount()
    si
  }
  
  override ref field vendor: OurLegalEntity {
    default =? onlyOne
    available(All).for(All).with(priceModels)
    update = {
      items*.orderDlvItem*.updateGst()
    }
  }
  impl ref field customer: Customer? by salesOrders {
    available(Ref).with(debtBalance, allowance, accountTerms, orderTerms)
    update = {
      this.currency =? try? customer?.legalEntity?.currency.alt(vendor.getCurrency)
      (try? deliveries).foreach(_.receiver = new?.defaultReceiver?.legalEntity)
      payer = new?.defaultPayer
      if (let tt = new?.orderTerms) terms = tt
      contactPerson = null
    }
    validate = if (!customer && !calcInstant) "Customer cannot be empty for incomplete orders"
  }
  impl field contactPerson: CompanyPerson? {
    label = "Contact Person"
    from = Query(1).flatMap{_ => 
      if (let customer) customer.legalEntity.people
      else CompanyPerson.all.filter(_.company.as[LegalEntity]?.customer)
    }
    update = {
      if (let customer = new?.company?.as[LegalEntity]?.customer) {
        this.customer = customer
      }
    }
    init = {
      new.company =? customer?.legalEntity
    }
  }
 
  func isTheir = customer?.isTheir
  impl field allItems: [ISalesItem]? inline by order {
    //validate = if (!allItems) "No items in the order"
  }
  impl compute items: [ISalesItem]? {
    value = allItems.filter(active || wasCanceled)
    update = {
      allItems = new + allItems.filter(!_.active && !_.wasCanceled)
    }
    init = {
      new.order = _
    }
    visible = preEntered
  }
  ref compute availability: GoodAvailability {
    value = {
      if (canceled) Available
      else if (deliveries.exists(!dlvState.started && _.availability == Unavailable)) Unavailable
      else if (deliveries.exists(!dlvState.started && _.availability == UnavailableProdItems)) UnavailableProdItems
      else if (deliveries.exists(!dlvState.started && _.availability == Unavailable)) AvailableLater
      else Available
    }
	visible = availability.isIssue
  }
  
  impl compute preEntered = (try? vendor)
  impl func numPrefix = "S"
  override func entityNumbers: [String?]? = super.entityNumbers + ["SO" + n.format(3, 0)]
  impl func title = "$fullNum $customer"
  impl func legalEntity = vendor
  
  //Views
  impl view form = nav(
    onBarcode = if (ui.edit) this.procBarcode(barcode)
    onNfc = if (ui.edit) this.procNfc(message)
  ){
    tabs {
      posView
      grid(labels = Top, width = 1.2, label = "Deliveries"){  
        deliveries >> list(frame = false){nav{
          deliveryHeader
          button("Packing Slip") {
            packingSlip.print()
          }
        }}
        extension("deliveries")
      }
      grid(labels = Top, width = 1.2, label = "More"){ 
        salesChannel
	    extension
      }
      extension("tabs")
    }
    invoiceButtons
    //button("Update Prices") {updatePrices()}
  }
  
  view stateView = x {
    procState >> field(editMode = AlwaysEdit, noTab)
    state >> field(noTab)
    paymentState 
    overdueState
    deliveryState
    deliveryOverdueState
    availability
    dropShipState
  }
  
  compute sd {
    value = deliveries.onlyOne?.as[SalesDelivery]
    update = {}
  }
  compute prevOrder {
    value = sd?.prev?.order
    label = "Previous"
  }
  compute nextOrder { 
    value = sd?.next?.order
    label = "Next"
  }
  override func activeItemType: String = if (isQuote) "Sales Quote" else "Sales Order"
  
  view posView = grid (labels = Top, label = "Main", width = 1.2) {
    issuesView
    
    row {number >> field(noTab) refNums >> field(noTab) date >> field(noTab, label = "Order Date") }
    x{stateView >> field(stretch = 0) acceptView salesChannel  >> field(stretch = 1, editMode = AlwaysRead) }
    followUpDate
    row {totalNoGst >> field(width = 0.2) totalWithGst >> field(width = 0.2) vendor >> field(noTab, width = 0.4) terms >> field(noTab, width = 0.4)}
    row {customer sd!.store >> field(visible = sd) sd!.start >> field(noTab, visible = sd)}
    row {contactPerson sd!.retStore >> field(visible = sd && sd!.hasProgress)  sd!.end >> field(visible = sd && sd!.hasProgress) } 

    x(visible = ui.edit) {
      add("Shipping", visible = sd && customer && !sd!.shipping) {SDShipping(dlv = sd!)}
      add("Return Shipping", visible = sd && customer && !sd!.retShipping && sd!.hasProgress) {SDRetShipping(dlv = sd!)}
      add("Repeat", visible = sd && customer && !sd!.repeat) {DlvRepeat(dlv = sd!)}
      add("Payer", visible = sd && customer && !sPayer && !_receiver) {sPayer = customer}
      add("Receiver", visible = sd && customer && !_receiver && !sPayer) {_receiver = customer?.legalEntity}
    }
    
    row {payer >> field(visible = _receiver || sPayer) receiver >> field(visible = _receiver || sPayer) space}
    
    sd!.shipping >> field(visible = sd && sd!.shipping)
    sd!.retShipping >> field(visible = sd && sd!.retShipping && sd!.hasProgress)
    
    prevOrder >> field(title = title, editMode = AlwaysRead)
    sd!.repeat >> field(visible = sd && sd!.repeat)
    nextOrder >> field(title = title, editMode = AlwaysRead)
    
    extension("delivery")
    extension
    items >> list(mode = EditSelect) {
      form 
    }
    
    invoicesView
    
    unpaidAmount >> field(fontStyle = H2, label = "Amount to Pay")
    paymentsView
    
  } 
 
  report Report {
    field superProduct: ISuperProduct?
    field product: IProduct?
    field customer: LegalEntity? {auto = true}
    field salesPerson: User? {
      from = User.all.filter(is(Salesperson))
    }
    
    impl view list = SalesOrder.all.filter(
      isFor(salesPerson, Salesperson)
      && ((!this.product && !this.superProduct) || items*.as[ISalesItem with OrderProductItem].exists(product ==? this.product && product.superProduct ==? this.superProduct))
    ) 
      >> nav{list{grid(labels = None){row{
        itemImage >> field(miniature = List, width = 0.15)
        dash >> field(width = 0.85)
      }}}}
  }
  
  impl view ref = y {
    x{title total date >> field(fontStyle = Label) }
    stateView
  }
  impl view dash = grid(labels = None) {
    row {
      y (width = 0.6, spacing = false) {
        date >> field(fontStyle = Label)
        x{number >> field(fontStyle = Label) customer >> field(title = title) total}
        stateView
      }
      shipToText >> field(width = 0.4)
    }
    refItems
  }
 
}

object ITSOReturnOverdue extends IssueType[SalesOrder] {
  impl func title = "Rental products are not returned on time"
  impl func hasIssue(item: SalesOrder) = item.deliveries*.as[SalesDelivery].exists(isReturnOverdue)
  impl func fixes(item: SalesOrder) = [
    Fix(text = "Mark as complete if already returned", acts = [Act(label = "Complete", icon = "check", color = "#5ba738", do = {
      item.state = Complete
      UI.save(item)
    })])
    Fix(text = "Mark as post processing if returned but require some work", acts = [Act(label = "Post Processing", icon = "broom", color = "#358c72", do = {
      item.state = PostProcessing
      UI.save(item)
    })])
    Fix(text = "Contact the customer")
  ]
}
