trait Delivery extends IDeliveryPath with DebtOrigin {
  background = "#2e8e67"
  field manualNumber: String?
  func number: String
  func defaultDate: Date = if (order.created) order.date else Date.now
  func order: Order = orders.first
  func orders: [Order]
  func vendor: IVendor?
  func customer: ICustomer?
  field allItems: [IOrderDeliveryItem]? inline by delivery {deleteCascade = false}
  compute items: [IOrderDeliveryItem]? {
    value = allItems.filter(active)
    update = { 
      allItems = new + allItems.filter(!active)
    }
  }
  func journalDate = deliveryDate
  func store: Store? = null
    
  func dueDate(orders: [Order], policy: IDeliveryPolicy?): Date? = {
    if (status == Delivered || status == Canceled || !policy) null
    else if (let invoices = items*.sales*.invoice.distinct) {
      let canDispatch = invoices.map(canDispatch)
      if (canDispatch.contains(false)) null
      else if (let p = policy, let orderItems = orders*.items) {
        if (let d = p.dueDate(orderItems, this, invoices.map(prepaymentDate!).max)) d.max(dispatchDate)
        else dispatchDate
      } 
      else if (status == Planned) dispatchDate 
      else deliveryDate
    }
    else null
  }
  func canCancel = false
  register ledgers: [Ledger]? <- if (!isInstantDelivery) {
    let ls <- ledgers(this, deliveryDate)
    CurrencyTrading.with(ls)
  }
  func isInstantDelivery = false
  func ledgers(journal: Journal, date: Date): Future[[Ledger]?]
  
  
  func salesLedgers(journal: Journal, date: Date): Future[[Ledger]?] = {
    let baseCurrency = legalEntity.getCurrency
      
    let lss <- items*.sales.groupBy(currency, invoice?.payer, order.customer).toSeq.map{c =>
      let items = c.#3
      let amount = items*.amount.sum
      let discount = items*.discount.sum
      let baseDiscount <- discount.to[Money[baseCurrency]](date)
      
      let debt = if (let p = c.payer) {
        LDebt(account = UnearnedRevenues, origin = this, journal = journal, date = date, 
              counterparty = p.legalEntity, trAmount = amount - discount, currency = c.currency)
      }
      let disc = if (let dis = baseDiscount) {
        LCounterparty(account = DiscountsProvided, journal = journal, date = date, 
                      counterparty = c.customer?.legalEntity, trAmount = dis)
      } 
      let ls <- items*.revenueLedgers(journal).allCompleted
      debt.toSeq + disc + ls.flatMap(_)
    }.allCompleted
    lss.flatMap(_)
  }
  
  func purchaseLedgers(journal: Journal, date: Date): Future[[Ledger]?] = {
    let baseCurrency = legalEntity.getCurrency
      
    let lss <- items*.purchase.groupBy(currency, invoice?.payee, order.vendor).toSeq.map{c =>
      let items = c.#3
      let amount = items*.amount.sum
      let discount = items*.discount.sum
      let baseDiscount <- discount.to[Money[baseCurrency]](date)
      
      let debt = if (let p = c.payee) {
        LDebt(account = PrepaidExpenses, origin = this, journal = journal, date = date, 
              counterparty = p.legalEntity, trAmount = -amount + discount, currency = c.currency)
      }
      let disc = if (let dis = baseDiscount) {
        LCounterparty(account = DiscountsReceived, journal = journal, date = date, 
                      counterparty = c.vendor?.legalEntity, trAmount = -dis)
      } 
      let ls <- items*.expenseLedgers(journal).allCompleted
      debt.toSeq + disc + ls.flatMap(_)
    }.allCompleted
    lss.flatMap(_)
  }
}

