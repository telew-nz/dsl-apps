trait Invoice extends IInvoice {
  background = "#d68400"
  field date: Date = now
  func currency: Currency = orders.first.currency
  field manualNumber: String?
    
  func overpaid = paidAmount > total
  func order: Order = orders.first
  func orders: [Order]
  func items: [OrderItem]?
  func cancel(): Void
  func deliveryDate: Date? = if (items.all(delivered)) items*.delivery*.deliveryDate.max

  compute gstAmount: Money[currency]? {
    value = items*.gstAmount.sum
  }
  compute totalWithoutGst: Money[currency]? {
    value = items*.invoiceTotal.sum
    visible = gstAmount
  }
  ref compute total: Money[currency]? = totalWithoutGst + gstAmount
  
  func journalDate: Date = date
  compute paidAmounts: [OrderItem: Money[currency]?]? = {
    var a = paidAmount - items.filter(amount < 0)*.amount.sum
    items.filter(amount > 0).map{i => 
      let v = a.min(i.amount)
      a = (a - v)
      (i, v)
    }.toMap
  }
  func paidAmountForItem(item: OrderItem): Money[currency]? = paidAmounts.apply(item)
  
  register gstLedgers: [LTax]? = {
    items.filter(gstRate).groupBy(gstRate!.value).agg(gstAmount.sum, amount.sum).toSeq.map {
      LTax(account = TaxesPayable, tax = Gst, currency = this.currency, journal = this, rate = #0, 
           trAmount = if (isPurchase) gstAmount else -gstAmount, taxedAmount = amount)
    }
  }
  func isPurchase: Bool
}