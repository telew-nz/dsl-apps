entity PurchaseOrder extends Order with IPurchaseOrder {
  icon = "shopping-cart"
  background = "#a73e6c"
  impl func orderCustomer = customer
  impl func orderVendor = vendor
  func counterparty = vendor?.legalEntity
  impl func deliveryCounterparty = vendor?.legalEntity
  impl func activeRoles = {
    if (active) [BusAdmin]
  }
  impl field deliveries: [IPurchaseDelivery] inline by purchaseOrders {
    default = [PurchaseDelivery(primary = true, purchaseOrders = [this])]
    visible = preEntered
    from = IPurchaseDelivery.all.filter(i => i.legalEntity == legalEntity) 
  }
  compute pickUp: ILocation? = deliveries*.dlvPrePath*.pathSrc.onlyOne
  compute pickUpText {
    availableFor = List
    value = pickUp?.locationAddress?.title
  }
  compute _supplier: LegalEntity? {
    value = deliveries*._supplier.onlyOne
    update = {
      deliveries*._supplier = new
    }
  }
  compute supplier: LegalEntity? {
    value = deliveries*.supplier.onlyOne
    update = {
      deliveries*.supplier = new
    }
  }
  
  impl ref field vendor: Vendor? by purchaseOrders {
    available(Ref).with(cptAccs)
    update = {
      this.currency =? try? vendor?.legalEntity?.currency.alt(customer.getCurrency)
      deliveries.foreach(_.supplier = new?.defaultSupplier?.legalEntity)
      if (let tt = new?.orderTerms) terms = tt
      payee = new?.defaultPayee
      items*.orderDlvItem*.updateGst()
    }
    validate = if (back) if (!vendor && !calcInstant) "Vendor cannot be empty for incomplete orders"
  }
  override ref field customer: OurLegalEntity {
    default =? onlyOne
    update = {
      items*.orderDlvItem*.updateGst()
    }
  }
  field sPayee: Vendor? 
  impl ref compute payee: Vendor? {
    available(Ref).with(debtBalance, allowance, accountTerms).field(legalEntity, with(legalInfo))
    value = sPayee.or(vendor)
  }
  impl func debtCounterparty: LegalEntity? = payee?.legalEntity
  
  impl field allItems: [IPurchaseItem]? inline by order {
    validate = if (!allItems) "No items in the order"
  }
  impl compute items: [IPurchaseItem]? {
    value = allItems.filter(active || wasCanceled)
    update = {
      allItems = new + allItems.filter(!_.active && !_.wasCanceled)
    }
    init = {
      new.order = _
    }
    visible = preEntered
  }
  impl compute preEntered = try? customer
  impl ref compute number: String = "PO" + n.format(3, 0)
  impl func entityNumbers: [String]? = [number] + referenceNumber.toSeq
  impl func title = if (let r = referenceNumber) "$number ($r) $vendor" else "$number $vendor"
  impl func legalEntity: OurLegalEntity = customer
  func hasGoods: Bool = items*.as[IPurchaseItem with OrderProductItem].exists(product.isTangible)
  impl func addItem(prod: ISubProduct, qty: UQty, productItems: [ProductItem]?) = {
    let pi = PurchaseItem(order = this, deliveryItem = DeliveryItem(product = prod, unit = qty.unit?.as[ProductUnit]!, qty = qty, productItems = productItems))
    let _ <- pi.deliveryItem.updateAmount()
    pi
  }
 
  compute prevOrder {
    value = pd?.prev?.order
    label = "Previous"
  }
  compute nextOrder { 
    value = pd?.next?.order
    label = "Next"
  }
  
  //Views
  impl view form = nav(
    onBarcode = if (ui.edit) this.procBarcode(barcode)
    onNfc = if (ui.edit) this.procNfc(message)
  ){tabs {
      posView
      grid(labels = Top, width = 1.2, label = "Deliveries"){
        deliveries >> list(frame = false){nav{
          deliveryHeader
          generalLedgerButton
        }}
        extension("deliveries")
      }
      grid(labels = Top, width = 1.2, label = "More"){ 
	    extension
      }
      extension("tabs")
    }
    invoiceButtons
    //button("Update Prices") {updatePrices()}
  }
  
  view stateView = x {
    state //>> field(editMode = AlwaysEdit)
    paymentState 
    overdueState
    deliveryState
    deliveryOverdueState
    dropShipState
  }
  
  compute pd {
    value = deliveries.onlyOne?.as[PurchaseDelivery]
    update = {}
  }
  
  view posView = grid (labels = Top, label = "Main", width = 1.2) {
    issuesView
    row {number >> field(noTab) referenceNumber >> field(noTab) date >> field(noTab, label = "Order Date")}
    x{stateView >> field(stretch = 0) acceptView }
    
    row {totalNoGst >> field(width = 0.2) totalWithGst >> field(width = 0.2) customer >> field(noTab, width = 0.4) terms >> field(noTab, width = 0.4)}
    row {vendor pd!.store >> field(visible = pd) pd!.start >> field(noTab, visible = pd)}
    row {space pd!.retStore >> field(visible = pd && pd!.hasProgress)  pd!.end >> field(visible = pd && pd!.hasProgress) } 
    
    x(visible = ui.edit){
      add("Shipping", visible = pd && vendor && !pd!.shipping) {PDShipping(dlv = pd!)}
      add("Return Shipping", visible = pd && vendor && !pd!.retShipping && pd!.hasProgress) {PDRetShipping(dlv = pd!)}
      add("Repeat", visible = pd && vendor && !pd!.repeat) {DlvRepeat(dlv = pd!)}
      add("Payee", visible = pd && vendor && !sPayee && !_supplier) {sPayee = vendor}
      add("Receiver", visible = pd && vendor && !_supplier && !sPayee) {_supplier = vendor?.legalEntity}
    }
    
    row {payee >> field(visible = _supplier || sPayee) supplier >> field(visible = _supplier || sPayee) space}
    
    pd!.shipping >> field(visible = pd && pd!.shipping)
    pd!.retShipping >> field(visible = pd && pd!.retShipping && pd!.hasProgress)
    extension("delivery")
    prevOrder >> field(title = title, editMode = AlwaysRead)
    pd!.repeat >> field(visible = pd && pd!.repeat)
    nextOrder >> field(title = title, editMode = AlwaysRead)
    
    extension
    items >> list(mode = EditSelect) {
      form 
    }
    
    invoicesView
    
    unpaidAmount >> field(fontStyle = H2, label = "Amount to Pay")  
    paymentsView
  } 
  
  
  report Report {
    field superProduct: ISuperProduct?
    field product: IProduct?
    field vendor: LegalEntity? {auto = true}
    field inventoryOnly: Bool
    
    impl view list = PurchaseOrder.all.filter(
      (!inventoryOnly || hasGoods)
      && ((!this.product && !this.superProduct) || items*.as[IPurchaseItem with OrderProductItem].exists(product ==? this.product && product.superProduct ==? this.superProduct))
    ) >> nav{list{grid(labels = None){row{
      itemImage >> field(miniature = List, width = 0.15)
      dash >> field(width = 0.85)
    }}}}
  }
  
  impl view ref = y {
    x{title total date}
    stateView 
  }
  impl view dash = grid(labels = None) {
    row {
      y {
        date
        x{title total}
        stateView
      }
      pickUpText
    }
    refItems
  }
}