trait ISalesDelivery extends OrderDlv {
  background = "#2e8e67"
  //impl func deliverySupplier = supplier?.legalEntity
  //impl func deliveryReceiver = getReceiver?.legalEntity
  
  //func supplier: IVendor?
  
  field _receiver: ICustomer? {
    update = updateReceiver()
  }
  ref compute receiver: ICustomer? {
    value = _receiver.or(order.orderCustomer)
    update = {
      this._receiver = if (new == order.orderCustomer) null else new
    }
  }
  //func isTheir = customer?.isTheir
  field salesOrders: [SalesOrder] by deliveries {
    available(All)
    default =? defaultSalesOrders
  }
  func shipTo: ILocation? = null
  func deliveryHeader: Layout
    
  func defaultSalesOrders: [SalesOrder]? = []
  func updateReceiver(): Void = {}
  func availability: GoodAvailability = Available
  impl func ledgers(journal: Journal, date: Date): Future[[Ledger]?] = salesLedgers(journal, date)
} 
 
entity SalesDelivery extends ISalesDelivery with IProdMoveOut with DlvFPathState {
  icon = "truck"
  background = "#2e8e67"
  
  impl func journalDate = date
  impl func dlvPrePath = null
  impl func dlvStart = date
  impl func dlvEnd = date
  impl func dlvPostPath = shipping
  impl func dlvHasProgress = false
  impl func dlvState = DlvState.path0(state)
  impl func dlvOrderStates = if (shipping) [Dispatched, Delivered] else [Delivered]
  impl func dlvDispatchDate = date
  impl func dlvInProgessDate = date
  impl func dlvReturningDate = date
  impl func dlvCompleteDate = shipping?.deliveryDate.alt(date)
  
  impl func moveOutDate = date
  impl func moveOutState = state.move0
  impl func hasInTransit = shipping
  impl func moveOutLE = legalEntity
  impl func moveOutStore = source!
  impl func moveOutStockType = stockType
  impl func moveOutItems = items*.as[IOrderDlvItem with IProdMoveItemOut]
  
  impl ref compute number: String {
    value = if (primary) "SD" + orders.first.nakedN else "SD" + n.format(3, 0)
  }
  override func entityNumbers = [number, refNum, shipping?.trackingNumber.replace(" ", "")]
  impl func orders: [Order] = salesOrders
  //impl list compute supplier = order.orderVendor
  //impl compute customer = order.orderCustomer
    
  impl func legalEntity = order.legalEntity
  impl func title = "$number $(try? receiver?.title)"
  
  //impl func vendor = legalEntity
  override func canDispatch = state == Planned && availability == Available && orders.all(canDispatch)

  impl func pathStateUpdated(old: PathState): Void = {
    if (state == InTransit) {
      if (old != Complete) date = now
    } else if (state == Complete) {
      if (old != InTransit) date = now
       shipping?.deliveryDate = now
    }
  }
  
  field primary: Bool
  field stockType: StockType = Inventory
  const ref field n: Int? = if (primary) order.n else increment(Order.gen)
  ref field source: IStore? {
    default = onlyOne
    validate = if (!source && items.exists(storeRequired)) "Source field in delivery $(this) is required"
  }
  field shipping: SDShipping? inline by delivery {
    visible = receiver
  }

  compute shipToText {
    availableFor = List
    value = shipTo?.locationAddress?.title
  }
  //func debtDueDate = deliveryDate
  
  ref field date: Date {
    label = "Dispatch Date"
    default = now
    update = {
      if (let shipping where shipping.deliveryDate < new) {
        shipping.deliveryDate = new
      }
    }
  }
  
  impl view deliveryHeader = grid(width = 1.2){
    issuesView
    stateView
    date
    row {source, receiver space}
    shipping
    possibleDispatchPeriods >> list(frame = false){ 
      x {
        start
        label("—")
        if (end == Date.max) "∞" else end
        button("Set") {
          this.date = start
          this.save()
        }
      }
    }
    items >> list(editMode = AlwaysRead, mode = EditSelect){form.unnav}
  }
  
  impl view form = nav{grid(width = 1.2, labels = Top) {
      salesOrders
      number    
      deliveryHeader
    }
    generalLedgerButton
  } 
  
  impl view ref = y {
    x{title date}
    stateView
  }
  impl view dash = grid(labels = None) {
    row {
      y {
        date
        title
        stateView
      }
      shipToText
    }
    refItems
  }
  impl event onSave = {
    if (primary) n = order.n 
    else if (!n) n = increment(Order.gen)
    salesOrders*.touch
  }
  
  view stateView = x{
    state >> field(editMode = AlwaysEdit) 
    availability >> field(visible = availability.isIssue) 
    dispatchState
    stockType >> field(visible = ui.edit || stockType != Inventory)
  }
  
  impl object view list = SalesDelivery.all >> nav{list(canAdd = false){grid(labels = None){row{
    itemImage >> field(miniature = List, width = 0.15)
    dash >> field(width = 0.85)
  }}}}
}

entity SDShipping extends PseudoEntity with IProdMovePath {
  impl func mainEntity = delivery
  impl func pathStart = delivery.date
  impl func pathSrc = delivery.source
  impl func pathDst = dst
  impl func pathEnd = deliveryDate
  impl func pathState = delivery.state
  
  field delivery: SalesDelivery
  field deliveryDate: Date {
    default = now
    update = {
      if (new < delivery.date) {
        delivery.date = new
      }
    }
  }
  
  field dst: ILocation? {
    label = "Destination"
    from = Query(delivery.receiver?.legalEntity).flatMap(deliveryLocations)
  }
  
  impl view form = grid(width = 1.2) {
    row {deliveryDate dst space}
    freightView
  }
}
