entity SalesInvoice extends Invoice with DebtDebit {
  background = "#d68400"
  field primary: Bool
  const ref field n: Int? = if (primary) order.n else increment(Order.gen)
  field orders: [SalesOrder] by invoices
  field items: [ISalesItem]? inline by invoice {deleteCascade = false}
  func hasSalesIssues = debitPaymentState == Overpaid || overdueState == PaymentOverdue
  ref compute number: String {
    value = manualNumber.alt(if (primary) "SI" + orders.first.nakedN else "SI" + n)
    update = {manualNumber = new}
  }
  func isPurchase: Bool = false
  func cashFlowCategory: CashFlowCategory = Sales
  func startingBalance: Money[currency]? = -total
  compute customer = counterparty
  ref compute counterparty: LegalEntity = order.customer
  func paidAmount = debitRepaid
  func cancelable = true    
  compute vendor = legalEntity
  ref compute legalEntity = order.vendor
  func title = "$number $(try? counterparty?.title)"
    
  func setoffs: [Setoff]? = credits
  func cancel(): Void = cancelationDate = Date.now
  func debitOriginLedger(setoff: Setoff): Future[LDebt] = {
    LDebt(account = AccountsReceivable, origin = this, journal = setoff.journal, date = setoff.date, trAmount = -setoff.amount, counterparty = customer, currency = currency)
  }
  register receivableLedger = LDebt(account = AccountsReceivable, origin = this, journal = this, trAmount = total, counterparty = customer, currency = currency)
  register unearnedLedgers <- items.groupBy(delivery).toSeq.map{
    let am <- _.#1*.orderTotal.sum.to[Money[currency]](date)
    LDebt(account = UnearnedRevenues, origin = _.delivery?.as[Journal].alt(this), journal = this, 
          trAmount = -am, counterparty = customer, currency = currency)
  }.allCompleted
  
  register receivableCancelLedger: LDebt? = if (let d = cancelationDate) receivableLedger.copy(date = d, trAmount = -receivableLedger.trAmount) else null
  register unearnedCancelLedgers: [LDebt]? = if (let d = cancelationDate) unearnedLedgers.map(_.copy(date = d, trAmount = -_.trAmount)) else null
  func isTheir = customer.isTheir
  
  func onSave = {
    if (primary) n = order.n 
    else if (!n) n = increment(Order.gen)
    orders*.touch
    credits*.credit*.touch
  }
   
  view invoiceHeader = grid(width = 1.2){
    statusesView
    row {total  date paymentTerms}
    row {gstAmount dueDate  space}
    row {totalWithoutGst cancelationDate  space}
    
    credits >> field(label = "Payments", singular = "Payment")
  }
  view form = nav{grid(width = 1.2, labels = Top) {
    row {number customer vendor}
    invoiceHeader
    orders
    items >> list(editMode = AlwaysRead){form}
    
  } generalLedgerButton} 
  object view list = SalesInvoice.all >> nav{list{grid(labels = None) {
    row{number, date.as[Date[day]]!, customer, total}
    statusesView
  }}}
  view statusesView = x {
    debitPaymentState
    debitUnpaid + " unpaid" >> field(visible = debitState == PartiallyReconciled)
    overdueState
  }
  view taxInvoice {
    printable = true
    label = "Tax Invoice"
    layout = nav{grid (labels = None, width = 1.2){
      x {
        vendor.legalName >> field(fontSize = 1.6, stretch = 1)
        h1("Tax Invoice", margin = Bottom)
      }
      row {
        vendor >> grid(width = 0.4, labels = Left, labelsWidth = 0.15, margin = Bottom, spacing = false) {
          legalInfo.gstView
          contacts.phones.join(", ") >> field(label = "Phone")
          contacts.emails.join(", ") >> field(label = "EMail")
        }
        customer >> grid (width = 0.4, labels = Right, labelsWidth = 0.15, margin = Bottom, spacing = false) {
          legalName >> field(label = "Customer")
          legalInfo.gstView 
        }
        grid (width = 0.4, labels = Right, labelsWidth = 0.15, margin = Bottom, spacing = false) {
          number >> field(align = Right)
          date >> field(align = Right, label = "Invoice Date")
          dueDate >> field(align = Right, label = "Payment Due")
        }
      }
        
      
      [(totalWithoutGst, gstAmount, total, items.groupBy(deliveryGroup).toSeq)] >> total {
        group {
          row(fontSize = 1) {label("Total Excluding GST", span = 3, align = Right) totalWithoutGst} 
          row(fontSize = 1) {label("GST", span = 3, align = Right) gstAmount}
          row(fontSize = 1) {label("Total", span = 3, align = Right) total}
        }
        subtotal {
          deliveryGroup.rowView
          table {
            header{label("Product", width = 0.55) 
                   label("Quantity", align = Right, width = 0.2) 
                   label("Price", align = Right, width = 0.2) 
                   label("Amount", align = Right, width = 0.25)}
            body{row{rowView}}
            //row{product qty price amount}
            //row(visible = discount){label("Discount") space space discount}
          }
        }
      }
    }}
  }
  view ref = x{title statusesView}
}
