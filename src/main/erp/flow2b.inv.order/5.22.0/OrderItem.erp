interface OrderDeliveryGroup {
  func rowView: Layout[OrderDeliveryGroup]
}
class ODGDelivery extends OrderDeliveryGroup {
  field delivery: Delivery
  view rowView = row(fontSize = 1) {x(span = 4){label("Delivery") delivery.number delivery.deliveryDate >> field(visible = true)} }
}
object ODGEmpty extends OrderDeliveryGroup {
  view rowView = row(fontSize = 1) {label("No Delivery", span = 4)}
}

inline trait OrderItem {
  func order: Order
  var invoice: Invoice?
  func delivered: Bool = delivery?.status == Delivered || !orderDeliveryItem
  func inTransit: Bool = delivery?.status == InTransit
  func paidAmount: Money[currency]? = if (amount > 0) invoice?.paidAmountForItem(this) else amount
  func orderPaidAmount = paidAmount
  func paid: Bool = amount <= 0 || paidAmount == amount
  func complete = delivered && paid
  func currency = order.orderCurrency 
  func orderTotal = amount - discount
  func invoiceTotal = amount - discount
  func orderDeliveryItem: IOrderDeliveryItem?
  func delivery: Delivery? = orderDeliveryItem?.delivery
  func deliveryGroup: OrderDeliveryGroup = if (let d = delivery) ODGDelivery(delivery = d) else ODGEmpty
  
  field storedWithGst: Bool {
    update = {
      if (new) {
        sAmount = sAmount*gstRateK
        sDiscount = sDiscount*gstRateK
      } else {
        sAmount = sAmount/gstRateK
        sDiscount = sDiscount/gstRateK
      }
    }
  }
  
  field sAmount: Money[currency]?
  compute gstRateK = 1 + gstRate?.value.withoutUnit
  
  compute amount: Money[currency]? {
    value = if (storedWithGst) sAmount/gstRateK else sAmount
    update = {
      storedWithGst = false
      sAmount = new
    }
  }
  compute amountWithGst: Money[currency]? {
    value = if (storedWithGst) sAmount else sAmount*gstRateK
    update = {
      storedWithGst = true
      sAmount = new
    }
  }
  compute amountEditor: Money[currency]? {
    label = "Amount"
    dynamicLabel = {
      if (let r = gstRate) if (order.pricesIncludingGst) "Amount incl $(r.value.as[Percent]) GST" else "Amount excl $(r.value.as[Percent]) GST"
      else "Amount"
    }
    value = if (order.pricesIncludingGst) amountWithGst else amount
    update = {
      if (order.pricesIncludingGst) this.amountWithGst = new 
      else this.amount = new
    }
  }
  compute gstAmount: Money[currency]? = {
    amountWithGst - amount - discountWithGst + discount
  }
  
  field sDiscount: Money[currency]?
  compute discount: Money[currency]? {
    value = if (storedWithGst) sDiscount/gstRateK else sDiscount
    update = {
      storedWithGst = false
      sDiscount = new
    }
  }
  compute discountWithGst: Money[currency]? {
    value = if (storedWithGst) sDiscount else sDiscount*gstRateK
    update = {
      storedWithGst = true
      sDiscount = new
    }
  }
  compute discountEditor: Money[currency]? {
    label = "Discount"
    dynamicLabel = {
      if (let r = gstRate) if (order.pricesIncludingGst) "Discount incl $(r.value.as[Percent]) GST" else "Discount excl $(r.value.as[Percent]) GST"
      else "Discount"
    }
    value = if (order.pricesIncludingGst) discountWithGst else discount
    update = {
      if (order.pricesIncludingGst) this.discountWithGst = new 
      else this.discount = new
    }
  }
  
  field gstRate: GstRateValue? 
  compute gstEditor: Percent[2]? {
    label = "GST"
    value = gstRate?.value
    update = {
      gstRate = GstRateValue(value = new)
    }
  }
  func quoteSwitch(quote: Bool): Void = {}
  func rowView: Layout[OrderItem]

}

inline trait OrderProductItem extends OrderItem {
  func orderDeliveryItem: IOrderDeliveryItem? = deliveryItem
  func deliveryItem: IDeliveryProductItem
  func product = deliveryItem.product
  func baseQty = deliveryItem.baseQty
  func qty = deliveryItem.qty
  func unit = deliveryItem.unit
  compute price: Money[currency]? {
    value = amount/qty
    update = {amount = new*qty}
  }
  compute priceWithGst: Money[currency]? {
    value = amountWithGst/qty
    update = {amountWithGst = new*qty}
  }
  compute priceEditor: Money[currency]? {
    label = "Price"
    value = if (order.pricesIncludingGst) priceWithGst else price
    update = {
      if (order.pricesIncludingGst) this.priceWithGst = new 
      else this.price = new
    }
  }
  
  
  compute suggestedAccessories: [Accessory]? = {
    product.superProduct.accessories.filter(!order.items*.as[OrderProductItem]*.product*.superProduct.contains(accessory))
  }    
}