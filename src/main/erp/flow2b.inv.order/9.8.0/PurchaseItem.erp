inline trait IPurchaseItem extends OrderItem {
  impl field order: PurchaseOrder by allItems
  impl func ledgerAccount: LedgerAccount = PrepaidExpenses
  impl func cashFlow: CashFlow = Purchases
  impl func canBeDeleted = if (let p = orderDeliveryItem?.sales) !p.active else true
  override func setActive(v: Bool): Void = {
    if (this.active != v) {
      this.active = v
      if (let p = orderDeliveryItem?.sales) p.active = v
      orderDeliveryItem?.forceTouch
    }
  }
  func expenseLedgers(journal: Journal, date: Date, baseAmount: UMoney?): [Ledger]?
}

inline trait IPurchaseItemWithDelivery extends IPurchaseItem {
  func journalDelivery: IDelivery? = delivery

  func deliveryItem: IOrderDeliveryItem
  func delivery: Delivery = deliveryItem.delivery
  impl func itemDelivery = deliveryItem.delivery
}
 
inline trait IPurchaseProductItem extends IPurchaseItemWithDelivery with OrderProductItem {
  impl func deliveryItem: IDeliveryProductItem
  //Register
  impl func expenseLedgers(journal: Journal, date: Date, baseAmount: UMoney?) = {
    let d = delivery
    if (!d.is[PurchaseDelivery]) {
      [LProduct(account = CostOfGoodsSold, journal = journal, date = date, product = product, qty = qty, trAmount = baseAmount)]
    } else if (product.isTangible) {
      ProductItem.distribute(product, qty, baseAmount, deliveryItem.productItems) {
        d.as[PurchaseDelivery]!.stockType.obj.new(
           journal = journal, date = date, product = product.as[IProduct]!, 
           productItem = item, 
           qty = trQty, trAmount = trAmount)
      }
    } else {
      let et = product.expenseType
      [LExpenses(journal = journal, date = date, expenseType = et, product = product, qty = qty, trAmount = baseAmount)]
    }
  }
}

inline entity PurchaseItem extends IPurchaseProductItem with HasProductItemsInline {
  icon = "box"
  singular = "Item"
  impl field deliveryItem: DeliveryItem inline by purchase
  func stockProd = product
  func itemsQty = qty
  impl func needProductItems: Bool = delivery?.status == Delivered
  impl view form = grid(margin = None, width = 1.2, labels = Top) {
    row (labels = false){
      deliveryItem.product >> field(width = 0.6, placeholder = "Product, Service or Kit") 
      deliveryItem.qty >> field(canEditUnit = true, width = 0.2, align = Right) 
      priceEditor >> field(width = 0.2, align = Right) 
      amountEditor >> field(width = 0.2, align = Right)
    }
    row {
      itemDesc >> field(width = 0.8, visible = ui.isSelected || desc)
      gstEditor  >> field(visible = ui.isSelected, width = 0.2) 
      discountEditor >> field(visible = ui.isSelected || discount, width = 0.2, align = Right)
    }
    deliveryItem.delivery >> field(visible = ui.isSelected && this.order.deliveries.count > 1)
    snView >> field(visible = ui.isSelected && product.trackItems) 
  }
  
  impl view rowView = group {
    row{deliveryItem.product >> field(rowSpan = 2)
        deliveryItem.qty >> field(canEditUnit = true) 
        priceEditor 
        amountEditor}
    row(visible = discount){label("Discount", span = 2, align = Right) discountEditor}
    row(visible = invoiceDesc) {
      invoiceDesc >> field(span = 4)
    }
  }
  
  register quoteInv: LInventory? <- if (deliveryItem.delivery.status != Delivered && amountNoGst && product.isTangible) {
    if (let p = deliveryItem.product.as[IProduct]) {
      let am0 = if (order.legalEntity.hasGst) amountNoGst else amountWithGst
      let am <- am0.to[Money[order.legalEntity.getCurrency]](order.date)
      LInventory(journal = deliveryItem.delivery.as[Journal].alt(order), qty = qty, product = p, isQuote = true,
                 trAmount = am)
    } else {
      Future(null)
    }
  } else {
    Future(null)
  }
}

