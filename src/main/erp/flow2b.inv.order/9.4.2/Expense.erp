inline entity ExpenseItem extends IOrderDeliveryItem with IPurchaseItemWithDelivery with ExpenseRecodable {
  singular = "Expense"
  func orderDeliveryItem: IOrderDeliveryItem? = this
  func deliveryItem = this
  func purchase: IPurchaseItem? = this
  func sales: ISalesItem? = null
  func itemDelivery = delivery
  field expense: IExpenseType {
    update = {
      if (order.legalEntity.hasGst) {
        this.nonClaimable = expense.nonClaimable
        this.gstRate = if (let p = order.orderVendor) expense.gstRate(p)
      }
    }
  }
  func name: String = expense.title
  func cashFlow: CashFlow = Expenses
  
  field nonClaimable: UPercent? {
    default = (try? expense)?.nonClaimable
    validate = if (0 > claimable || claimable > 1.00_PercentUnit) "Claimable percentage should be between 0% and 100%"
  }
  func recode(): Void = {
    this.gstRate = if (let p = order.orderVendor) expense.gstRate(p)
    this.nonClaimable = expense.nonClaimable
    this.save
  }
  compute claimable: UPercent? {
    label = "Claimable Percentage"
    value = (1.00_PercentUnit - nonClaimable).as[UPercent]
    update = {
      nonClaimable = (1.00_PercentUnit - new).as[UPercent]
    }
  }
  
  view form = grid(margin = None, width = 1.2, labels = Top) {
    row (labels = false){
      expense >> field(width = 0.6) 
      space(width = 0.2)
      claimable >> field(showEmpty width = 0.2)
      amountEditor >> field(width = 0.2, align = Right)
    }
    row {
      desc >> field(width = 0.8, visible = ui.isSelected || desc)
      gstEditor  >> field(visible = ui.isSelected && order.as[PurchaseOrder]!.vendor?.hasGst, width = 0.2) 
      discountEditor >> field(visible = ui.isSelected || discount, width = 0.2, align = Right)
    }
    deliveryItem.delivery >> field(visible = ui.isSelected && this.order.deliveries.count > 1)
  }
  
  view rowView = group {
    row{expense >> field(rowSpan = 2)
        space
        space
        amountEditor}
    row(visible = discount){label("Discount", span = 2, align = Right) discountEditor}
    row(visible = invoiceDesc) {invoiceDesc >> field(span = 4)}
  }
  func expenseLedgers(journal: Journal, date: Date, baseAmount: Money?) = {
    [LExpenses(journal = journal, date = date, trAmount = baseAmount, expenseType = expense, claimable = claimable)]
  }
}