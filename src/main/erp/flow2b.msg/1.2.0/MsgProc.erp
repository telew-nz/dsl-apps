back interface MsgProc {
  func priority: Dec[2]?
  
  //Return the same message if unprocessed or modified message to continue processing
  // null - stop processing
  func proc(msg: ExtMsg): ExtMsg? 
}

back interface MsgProcActivity extends MsgProc{
  func canProc(msg: ExtMsg): Bool
  impl func proc(msg: ExtMsg): ExtMsg? = {
    if (canProc(msg)) {
      if (!msg.fillActivity(this)) msg
    } else {
      msg
    }
  } 
  func parseSubject = false
  func refs(msg: ExtMsg): [Entity]? {
    default value = {
      if (let aa = msg.refActivities.first) [aa.mainEntity]
      else if (parseSubject && !msg.flow2b) {
        Entity.findReferences(msg.subject)
      }
    }
  }
  
  func item(msg: ExtMsg, from: Contact?, recipients: [Contact], refs: [Entity]?): Entity? = {
    if (let r = refs.onlyOne) return r 

    Ticket(
      channel = msg.channel
      status = if (msg.isSpam) Canceled else Entered
      subject = (if (msg.isSpam) "SPAM: ") + msg.subject.alt("No Subject")
      roles = if (let r = recipients*.as[Dashboard]*.role.toSet) r else recipients*.as[RoleContactGroup]*.role.toSet
    ).save
  }
}
back object MsgProcDefault extends MsgProcActivity {
  impl func priority = 0.0
  impl func parseSubject = true
  impl func canProc(msg: ExtMsg): Bool = true
}
      


