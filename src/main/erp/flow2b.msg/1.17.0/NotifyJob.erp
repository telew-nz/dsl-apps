extend trait NotifyTemplate[T] {
  func send(data: T): Void = {
    if (enabled) {
      NotifyJob[T](template = this, data = data).enqueue
    }
  }
}

class NotifyJob[T extends Entity] extends SystemJob {
  field template: NotifyTemplate[T] inline
  field data: T
  impl func service = NotificationService
  impl func toString = "Notify Job $template"
  impl func do = {
    guard (template.enabled) else return {}
    
    let a = Activity.current!
    
    guard (let to = template.to(data)
      .mapOpt{
        let contacts = collectContacts.filter(canSend || NotificationService.testMode)
        template.preferred
          .filter(template.from(data, _))
          .mapOpt(channel => contacts.find(c => c.channel == channel))
          .first
          .or{
            contacts.find{contact => 
              template.supported.toSeq.exists{channel => 
                template.from(data, channel) && contact.channel == channel
              }
            }
          }
      }
    ) else return {
      if (template.required) {
        data.touch
        data.saveMain
        a.template = template
        a.comment("Cannot send notification $template: No contacts found for $(template.to(data).join(', '))")
      }
    }
    
    
    data.touch
    data.saveMain
    
    a.isPublic = true
    if (NotificationService.testMode) {
      a.comment("<p>$(to.join(" ").as[Html])</p>")
    } else {
      a.comment("<p>$(to*.atMention.join(" ").as[Html])</p>")
    }
    a.comment(template.comment(data))
    a.template = template
    
    if (NotificationService.testMode) {
      to*.channel.distinct.foreach {channel =>
        a.comment("<p>===================</p>")
        a.comment(template.subject(data, channel))
        a.comment(template.text(data, channel))
      }
    }
  }
}

extend class ActivityPatch {
  extend view mainForm = extend("buttons") {
    button(icon = "retweet", "Resend", visible = activity.template) {
      NotifyJob[Any](template = activity.template!, data = activity.mainEntity).enqueue
    }
  }
}

object NotificationService extends SystemService {
  impl func title = "Notification Service"
  func serviceAccess: [Role]? = [Admin]
  field testMode: Bool
  field contacts: [Contact]? {
    label = "Outgoing contacts"
  }
  impl func ourContacts: [IContact]? = contacts
  impl view form = nav{grid(labels = Top){
     issuesView
     serviceState
     errorsCount >> field(editMode = AlwaysRead)
     contacts
  }}
  view test = nav{grid(labels = Top){
     issuesView
     serviceState
     errorsCount >> field(editMode = AlwaysRead)
     contacts
     testMode
  }}
  impl func login = "notify"
}