extend trait NotifyTemplate[T] {
  func send(data: T): Void = {
    if (enabled) {
      NotifyJob[T](template = this, data = data).enqueue
    }
  }
  extend override func from(data: T, channel: CommChannel): Contact? = {
    NotifyService.contacts.find(c => c.channel == channel)
  }
}

class NotifyJob[T extends Entity] extends SystemJob {
  field template: NotifyTemplate[T] inline
  field data: T
  impl func service = NotifyService
  impl func toString = "Notify Job $template"
  impl func do = {
    guard (template.enabled) else return {}
    
    let a = Activity.current!
    
    guard (let to = template.to(data)
      .mapOpt{
        let contacts = collectContacts.filter(canSend || NotifyService.testMode)
        template.preferred
          .filter(template.from(data, _))
          .mapOpt(channel => contacts.find(c => c.channel == channel))
          .first
          .or{
            contacts.find{contact => 
              template.supported.toSeq.exists{channel => 
                template.from(data, channel) && contact.channel == channel
              }
            }
          }
      }
    ) else return {
      if (template.required) {
        data.touch
        data.saveMain
        a.template = template
        a.comment("Cannot send notification $template: No contacts found for $(template.to(data).join(', '))")
      }
    }
    
    
    data.touch
    data.saveMain
    
    a.isPublic = true
    if (NotifyService.testMode) {
      a.comment("<p>$(to.join(" ").as[Html])</p>")
    } else {
      a.comment("<p>$(to*.atMention.join(" ").as[Html])</p>")
    }
    a.comment(template.comment(data))
    a.template = template
    
    if (NotifyService.testMode) {
      to*.channel.distinct.foreach {channel =>
        a.comment("<p>===================</p>")
        a.comment(template.subject(data, channel))
        a.comment(template.text(data, channel))
      }
    }
  }
}

extend class ActivityPatch {
  extend view mainForm = extend("buttons") {
    button(icon = "retweet", "Resend", visible = activity.template) {
      NotifyJob[Any](template = activity.template!, data = activity.mainEntity).enqueue
    }
  }
}
