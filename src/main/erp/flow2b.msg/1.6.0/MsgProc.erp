interface MsgProc {
  back func priority: Dec[2]?
  
  //Return the same message if unprocessed or modified message to continue processing
  // null - stop processing
  back func proc(msg: ExtMsg): ExtMsg? 
}

interface MsgProcActivity extends MsgProc{
  back func canProc(msg: ExtMsg): Bool
  impl func proc(msg: ExtMsg): ExtMsg? = {
    if (canProc(msg)) {
      if (!fillActivity(msg)) msg
    } else {
      msg
    }
  } 
  back func fillActivity(msg: ExtMsg) = msg.fillActivity(this)
  back func saveAttachs = true
  back func parseSubject = false
  back func refs(msg: ExtMsg): [Entity]? {
    default value = {
      if (let aa = msg.refActivities.first) [aa.mainEntity]
      else if (parseSubject && !msg.flow2b) {
        Entity.findReferences(msg.subject)
      }
    }
  }
  
  back func item(msg: ExtMsg, from: Contact?, recipients: [Contact], refs: [Entity]?): Entity? = {
    if (let r = refs.onlyOne) return r 

    Ticket(
      channel = msg.channel
      status = if (msg.isSpam) Canceled else Entered
      subject = (if (msg.isSpam) "SPAM: ") + msg.subject.alt("No Subject")
      roles = if (let r = recipients*.as[Dashboard]*.role.toSet) r else recipients*.as[RoleContactGroup]*.role.toSet
    ).save
  }
}
back object MsgProcDefault extends MsgProcActivity {
  impl func priority = 0.0
  impl func parseSubject = true
  impl func canProc(msg: ExtMsg): Bool = true
}
      


