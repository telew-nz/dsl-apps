entity Payroll extends HasN with NewEngine with DashboardItem {
  object field gen: Int? 
  impl func nextN = increment(gen)
  impl func numPrefix = "PRL"
  
  impl func itemRoles = [HR]

  field complete: Bool
  ref compute state: TimetableState {
    value = if (complete) Complete else if (end > Date.now) InProgress else Submitted
    from = if (complete || end < Date.now) [Submitted, Complete] else [InProgress]
    update = {
      complete = new == Complete
    }
  }
  compute prev: Payroll? {
    label = "Previous"
    availableFor = Inline
    value = Payroll.all.sortBy(_.start.desc).find(i => i.start < this.start)
  }
  
  impl func activeRoles = if (state != Complete) [HR]

  ref field _start: Date = Date.now.start(week)
  compute start: Date {
    value = _start
    update = {
      _start = new.start(week)
    }
  }
  compute end: Date {
    value = _start + 7_day
    update = {
      _start = new.start(week)
    }
  }
  impl func title = "${start.date} - ${end.date}"
  
  field timetables: [Timetable]? read by payroll {
    cleanCascade
    available(Inline).for(Inline)
    validate = if (!timetables.isUnique(worker, date.date)) "Not unique timetables in payroll $(this)"
  }
  compute totalAll = timetables*.total.sum.to[UDuration[hour, 2]]
  compute totalCustomer = timetables*.rows.filter(customer)*.duration.sum.to[UDuration[hour, 2]]
  
  compute jobTypes = timetables*.rows
    .groupBy(jobType, customer)
    .agg(duration = duration.sum.to[UDuration[hour, 2]])
    .toSeq
    .filter(duration)
    .sortBy(duration.desc)
    .groupBy(jobType).subtotal(duration = duration.sum.to[UDuration[hour, 2]]!)
    .map{i =>
      (i.jobType, i.duration, percent = i.duration.as[UPercent[2]]/totalAll!, 
         items = i.items.map(customer, percent = duration.as[UPercent[2]]/i.duration, duration))
    }
    .sortBy(duration.desc)
  
  impl view form = nav{grid(width = 1.2) {
    x {state start label("—") end number >> field(fontStyle = Label, align = Right, stretch = 1) }
    prev
    [(totalAll, workers)].toSeq >> total {
      table {
        worker >> field(width = 0.25)
        mon >> field(width = 0.1, title = total.noUnit, align = Right)
        tue >> field(width = 0.1, title = total.noUnit, align = Right)
        wed >> field(width = 0.1, title = total.noUnit, align = Right)
        thu >> field(width = 0.1, title = total.noUnit, align = Right)
        fri >> field(width = 0.1, title = total.noUnit, align = Right)
        sat >> field(width = 0.1, title = total.noUnit, align = Right)
        sun >> field(width = 0.1, title = total.noUnit, align = Right)
        total >> field(width = 0.25)
      }
      row{space(span = 8) totalAll}
    }
    
    
    jobTypes >> subtotal(label = "Job Types"){
      row{jobType >> field(title = title) percent duration}
      table {
        y(width = 0.5) {
          customer >> field(title = title) 
          label("Unassigned", visible = !customer, fontStyle = Normal)
        } percent >> field(width = 0.2) duration >> field(width = 0.3)
      }
    }    
  }}
  
  compute workers: [PayrollWorker]? = timetables.groupBy(worker)
    .agg{(items, key) =>
      let tts = items.toMap(date.weekdayShortName.lower)
      PayrollWorker(
        worker = key
        mon = tts("mon")
        tue = tts("tue")
        wed = tts("wed")
        thu = tts("thu")
        fri = tts("fri")
        sat = tts("sat")
        sun = tts("sun")
      )
    }.values.sortBy(total.desc)
  
  report Report {
    impl view list = Payroll.all.sortBy(start.desc) >> nav{list{
      x{state start.date label("—") end.date number >> field(fontStyle = Label, stretch = 1)}
    }}
  }
  impl view ref = x {state title number >> field(fontStyle = Label)}
}

class PayrollWorker {
  field worker: Worker
  field mon: Timetable?
  field tue: Timetable?
  field wed: Timetable?
  field thu: Timetable?
  field fri: Timetable?
  field sat: Timetable?
  field sun: Timetable?
  compute total: UDuration[hour]? = (mon?.total + tue?.total + wed?.total + thu?.total + fri?.total + sat?.total + sun?.total).as[UDuration[hour]]
}
