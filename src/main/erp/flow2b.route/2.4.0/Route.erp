entity Route extends IRoute {
  const ref field n: Int? = increment(Route.gen) 
  list stored compute period: Period? = if (let items = this.items) {
    Period(start = items*.pathStart.min, end = items*.pathEnd.min)
  }
  ref stored compute state: PathState {
    value = {
      if (items.exists(pathState == Draft)) Draft
      else if (items.all(pathState == Complete)) Complete
      else if (items.exists(pathState == Complete || pathState == InTransit)) InTransit
      else Planned
    } 
  }
  func number = "RT" + n
  impl func title = number
  list field executors: [Person]?
  
  impl view form = nav {grid{
    number
    state
    period >> field(frame = false)
    executors
    items >> list(frame = true) {
      listItem
    }
  }}
  
  impl event onSave = {
    executors*.user*.join(this)
  }
  report Report {
    field activeOnly: Bool = true
    field executor: Person?
    
    impl view list = Route.all.filter((!executor || executor.in(executors)) && (!activeOnly || state.active)).sortBy(period?.start) >> nav{list {grid(labels = None){
      row {number period >> field(frame = false)}
      executors
    }}}
  }
}
