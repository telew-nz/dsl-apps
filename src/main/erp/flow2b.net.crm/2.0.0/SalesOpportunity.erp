extend trait IOpportunity{
  field lastContact: Date?
  field nextPlannedContact: Date?
}

entity SalesOpportunity extends IOpportunity[SalesOpportunityStage] {
  func prefix = "S"
  field owner: Person? = Person.current
  ref field stage: SalesOpportunityStage
  field requirements: Text? 
  field userQuantity: Dec[2]?
  field estimatedProjectPrice: Dec[2]?
  func estimatedAmount: Dec[2]? = userQuantity*50*12 + estimatedProjectPrice
  field probability: Dec[2]?
  field products: [OpportunityProduct]? inline by opportunity
  field tools: [SalesOpportunityTool]? inline by opportunity
  
  
  view form = nav{grid(labels = Top, width = 1) {
    row{number,  status}
    stage
    row{lastContact, nextPlannedContact}
    row{owner, source}
    row{userQuantity estimatedProjectPrice, estimatedAmount, probability}
    row{orderedAmount, paidAmount}
    actorView
    roles
    tools
    requirements
    products
    projects
    salesOrders >> list {richRef}
  }}

  object view list = SalesOpportunity.all >> nav {list {
    grid (style = rowStyle) {
      x{("#" + number) status stage owner}
      actor >> field(label = "")
      row{estimatedAmount, probability, orderedAmount, paidAmount}
      row{lastContact, nextPlannedContact, space, space}
    }
  }}
  
  view richRef = row{_, stage, estimatedAmount}
}


enum SalesOpportunityStage {
  case StatusQuo {background = "#ccd"}
  case ExploringSolutions {background = "#9ae"}
  case SelectingSolutions {background = "#eab"}
  case Validation {background = "#ea5"}
  case FinalApproval {background = "#ed9"}
  case Deployment {background = "#9e7"}
}
