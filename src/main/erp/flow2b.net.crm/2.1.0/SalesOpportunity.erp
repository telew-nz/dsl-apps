extend trait IOpportunity{
  field lastContact: Date?
  field nextPlannedContact: Date?
  field country: Country?
  field city: City? {
    from = country?.cities
    visible = country
    init = {new.country =? country}
  }
  field suburb: Suburb? {
    from = city?.suburbs
    visible = city
    init = {new.city =? city}
  }
}

entity Country {
  field title: String
  field cities: [City]? read by country
}
entity City {
  field country: Country by cities
  field title: String
  field suburbs: [Suburb]? read by city
}
entity Suburb {
  field city: City by suburbs
  field title: String
}

entity SalesOpportunity extends IOpportunity[SalesOpportunityStage] {
  func prefix = "S"
  field owner: Person? = Person.current
  ref field stage: SalesOpportunityStage
  field requirements: Text? 
  field userQuantity: Dec[2]?
  field estimatedProjectPrice: Dec[2]?
  func estimatedAmount: Dec[2]? = userQuantity*50*12 + estimatedProjectPrice
  field probability: Percent?
  field products: [OpportunityProduct]? inline by opportunity
  field tools: [SalesOpportunityTool]? inline by opportunity
  
  
  view form = nav{grid(labels = Top, width = 1) {
    row{number,  status}
    stage
    row{country city suburb space}
    row{lastContact, nextPlannedContact}
    row{owner, source}
    row{userQuantity estimatedProjectPrice, estimatedAmount, probability}
    row{orderedAmount, paidAmount}
    tasks
    actorView
    roles
    tools
    requirements
    products
    projects
    salesOrders >> list {richRef}
  }}

  object view list = SalesOpportunity.all >> nav {list {
    grid (style = rowStyle) {
      x{("#" + number) status stage owner}
      actor >> field(label = "")
      row{estimatedAmount, probability, orderedAmount, paidAmount}
      row{lastContact, nextPlannedContact, space, space}
    }
  }}
  
  view richRef = row{_, stage, estimatedAmount}
}


enum SalesOpportunityStage {
  case StatusQuo {background = "#ccd"}
  case ExploringSolutions {background = "#9ae"}
  case SelectingSolutions {background = "#eab"}
  case Validation {background = "#ea5"}
  case FinalApproval {background = "#ed9"}
  case Deployment {background = "#9e7"}
}
