extend trait IOpportunity extends DeferredJob {
  field lastContact: Date?
  field nextPlannedContact: Date?
  field country: Country?
  field city: City? {
    from = country?.cities
    visible = country
    init = {new.country =? country}
  }
  field suburb: Suburb? {
    from = city?.suburbs
    visible = city
    init = {new.city =? city}
  }
  ref field readyToStart: ReadyToStart? 
  field startingDate: Date[day]? {
    visible = readyToStart == At
    validate = if (readyToStart == At && !startingDate) "Starting date should be set for Ready to Start At"
  }
  func service = AlertService
  func multiupdate = actor.toSeq
  func jobDate: Date? = if (let d = nextPlannedContact) {
    if (d > Date.now) d
  }
  func do = {
    //observers*.notify
    nextPlannedContact = null
    save
  }
}  
 
entity Country {
  field title: String
  field cities: [City]? read by country
}
entity City {
  field country: Country by cities
  field title: String
  field suburbs: [Suburb]? read by city
}
entity Suburb {
  field city: City by suburbs
  field title: String
}

entity SalesOpportunity extends IOpportunity[SalesOpportunityStage] {
  func prefix = "S"
  field owner: Person? = Person.current
  ref field stage: SalesOpportunityStage
  field userQuantity: Dec[2]?
  field estimatedProjectPrice: Dec[2]?
  func estimatedAmount: Dec[2]? = userQuantity*50*12 + estimatedProjectPrice
  field probability: Percent?
  field tools: [SalesOpportunityTool]? inline by opportunity
  field ecommerceLevel : EcommerceLevel?
  field locationsType : LocationsType?
  stored ref compute blockStatus = projects.first?.blockStatus
  
  view form = nav{grid(labels = Top, width = 1) {
    row{number,  x{blockStatus status}}
    row{stage >> field(compact = true) readyToStart}
    row{space startingDate}
    row{country city suburb space}
    row{lastContact, nextPlannedContact}
    row{owner, source}
    row{userQuantity estimatedProjectPrice, estimatedAmount, probability}
    row{ecommerceLevel, locationsType}
    row{orderedAmount, paidAmount}
    tasks
    actorView
    roles
    tools
    projects
    salesOrders 
  }}

  object view list = SalesOpportunity.all >> nav {list {
    grid (fontStyle = rowStyle) {
      x{("#" + number) blockStatus status stage readyToStart startingDate owner}
      actor >> field(label = "")
      row{estimatedAmount, probability, orderedAmount, paidAmount}
      row{lastContact, nextPlannedContact, space, space}
    }
  }}
  view ref = x {blockStatus status stage readyToStart title }
  
  view richRef = x{_, blockStatus, status, stage, readyToStart estimatedAmount}
  event onSave = {
    actor?.touch
  }
}


enum SalesOpportunityStage {
  case StatusQuo {background = "#999"}
  case ExploringSolutions {background = "#ed9"}
  case SelectingSolutions {background = "#ccf"}
  case Validation {background = "#8bf"}
  case FinalApproval {background = "#ed9"}
  case Deployment {
    color = "white"
    background = "#090"
  }
}

enum ReadyToStart {
  case Asap {background = "#dea", label = "ASAP"}
  case At {background = "#ed9"}
  case Mature {background = "#f99", label = "When Mature"}
  case Unsure {background = "#f99"}
}
