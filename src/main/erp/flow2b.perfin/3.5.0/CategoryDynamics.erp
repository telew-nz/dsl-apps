report Dynamics {
  plural = "Dynamics"
  field type: [StdCategory]?
  field category: ICategory?
  field to: Date = (Date.now[Date[day]].start(month) - 1_minute).end(month)
  
  view list = AccReg.all
    .filter(a => 
       a.stdCategory != Init && 
       (!type || type.contains(a.stdCategory)) &&
       a.category ==? category
    )
    .flatMap {i =>
      let dp = DynPeriod(start = i.start.start(of = month), amount = i.baseAmount)
      if (i.start == i.end) {
        [dp]
      } else {
        dp.periods(i)
      }
    }
    .groupBy(start = start)
    .agg(amount.sum)
    .sortBy(start)
    .map(start = start, end = start + 1_month - 1_minute, amount)
    .total(amount.sum, median = amount.median)
    .map(i =>
      (amount = i.#0, median = i.#1, 
       items = i.items.map(start = start, end = start + 1_month - 1_minute, amount, median = amount - i.#1))
    )
    >> 
  nav{
    total {
      row{space space amount >> field(showPlus = true) median >> field(showPlus = true)}
      table(action = Transaction.Report(type = type, category = category).list.open) {
        start >> field(width = 0.3) 
        end >> field(width = 0.3) 
        amount >> field(width = 0.25 showPlus = true)
        median >> field(width = 0.25 showPlus = true, label = "Median Î”")
      }
    }
  }
}
class DynPeriod {
  field start: Date
  compute end: Date = start + 1_month - 1_minute
  compute period = Period(start = start, end = end)
  field amount: Money?
  
  func periods(i: AccReg): [DynPeriod] = {
    amount = i.baseAmount(period)
    let s1 = start + 1_month
    this + if (s1 < i.end) {
      DynPeriod(start = s1).periods(i)
    }
  }
}