entity Transfer extends Transaction {
  func title: String = number + " " + date.as[Date[day]]!.toString
  
  func transCategory: Category? = null
  func transCategoryName = "Transfer"
  func transFrom: Account? = from
  func transTo: Account? = to
  func transOutcome: Money[from.currency]? = outcome
  func transIncome: Money[to.currency]? = income
    
  compute number = "T" + n
     
  field from: Account {
    from = Account.all.sortBy(lastTransactionDate.alt(DateTime.min).desc)
  }
  field outcome: Money[from.currency]
  field to: Account {
    from = Account.all.sortBy(lastTransactionDate.alt(DateTime.min).desc)
  }
  field income: Money[to.currency]
    
  view form = nav{grid{
    number
    date
    row{from to} 
    row{outcome income}
  }}
  register finances = [
    FinanceRegister(transaction = this, account = from, date = date, amount = -outcome),
    FinanceRegister(transaction = this, account = to, date = date, amount = income)
  ]
}