entity GoogleMerchantIntegration extends SystemService with ExtIdSource[String] with OAuth2 {
  func title = "Google Merchant Integration $label"
  ref field label: String?
  field merchantId: Int
  field targetCountries: [String] {
    default = ["NZ"]
    label = "Target Country Codes"
  }
  field dryRun: Bool
  field integs: [ECommerceIntegration] {label = "E-Commerce Integrations"}
  func roles: [Role]? = [Admin]
  func authParams = ["access_type" -> "offline", 
                     "include_granted_scopes" -> "true", 
                     "prompt" -> "consent"]
  func authAuthorizeUrl: Url = "https://accounts.google.com/o/oauth2/v2/auth"
  func authTokenExchangeUrl: Url = "https://www.googleapis.com/oauth2/v4/token"
  func authClientId = "905664746404-703m7338nok8remj4flan3q77c4vrpe3.apps.googleusercontent.com"
  func authClientSecret = "jOngIi3U1dFQSwPguJPsnYwh"
  func authScope = "https://www.googleapis.com/auth/content"
  func endpoint = "www.googleapis.com/content/v2/"
  
  func https(path: String?): HttpRequest = {
    HttpRequest(url = "$endpoint$path", ssl = true)
      .auth(this)
  } 
     
  view form = nav{
    grid(labels = Top){
      issuesView
      serviceState
      errorsCount >> field(editMode = AlwaysRead)
      label
      merchantId
      targetCountries
      integs
      dryRun
    }
    authorizeView
  }
  event onSave = {
    if (created || updated(dryRun) || updated(integs) || updated(serviceState) || updated(targetCountries) || updated(merchantId)) {
      if (serviceState == Active && !deleted) {
        updateAllProducts()
      }
    }
  }
  
  back func updateProducts(integ: ECommerceIntegration, change: [ProductUpdate[ICustomerProduct]]): Void = {
    let ch = BatchChange.create(this, change.map{c => 
      if (!c.enabled && !c.deleted) ProductUpdate(c, deleted = true) else c
    })
    if (!ch.isEmpty) {
      targetCountries.foreach{c =>
        UpdateProducts(service = this, integ = integ, country = c, change = ch).run
      }
    }
  }
  
  back func updateAllProducts(): Future[Void] = {
    integs.map {integ =>
      let updates <- ExtId.all.filter(source == integ && item.is[ICustomerProduct]).map{
        integ.productUpdate(item.as[ICustomerProduct]!, shouldDelete = true, photosUpdated = true)
      }.result
      if (let u = updates) updateProducts(integ, u)
    }.allCompleted
  }
}
