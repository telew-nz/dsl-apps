extend register LInventory {
  field isDebit: Bool
    
  stored compute costQueue: QtyAmountQueue[Qty[product.unit], Money[currency]]?
  stored compute qtyBalance: Qty[product.unit]? = prev?.qtyBalance + if (isDebit) qty else -qty
  register error: LBasic?
  func onSave = {
    if (qty) {
      if (isDebit) {
        let q = qtyBalance.min(qty)
        if (q != qty) {
          amount = amount*q/qty
          baseAmount = amount
        }
        if (q > 0) {  
          costQueue = prev?.costQueue.enqueue(FIFO, q, amount)          
        } else {
          costQueue = prev?.costQueue
        }
        if (let d = trAmount - amount) {
          if (contraAccount?.template == Product) error = null
          else error = LBasic(account = InventoryErrors, journal = journal, trAmount = d)
        } else {
          error = null
        }
      } else {
        let r = prev?.costQueue.dequeue(qty)
        costQueue = r.queue
        amount = -r.value
        baseAmount = -r.value
        error = null
      }
    } else if (let q = prev?.costQueue) {
      costQueue = null
      error = null
      costQueue = costQueue.enqueue(FIFO, q.qty, prev?.balance + trAmount)
    } else {
      costQueue = null
      error = null
      amount = -prev?.balance
      baseAmount = amount
    }
    balance = prev?.balance + amount
    createContraLedger()
    calcLedger
  }
  
  register contraLedger: Ledger?
  
  func createContraLedger(): Void = {
    if (contraAccount?.template == Product) {
      contraLedger = LProduct(this, product = product, account = contraAccount!, contraAccount = Inventory, trAmount = -amount, qty = qty!)
    } else {
      contraLedger = null
    }
  }
}

extend entity MLInventory {
  func calculatedAmount = !isDebit && qty
}