report BalanceSheet {
  plural = "Balance Sheet"
  field legalEntity: OurLegalEntity? 
  field date: Date?
  func baseCurrency = legalEntity?.getCurrency.alt(Currency.our)
    
  view list = Ledger.at(date)
    .filter(
      legalEntity ==? this.legalEntity
    )
    .flatMap(subBalances) 
    .groupBy(account = if ((account.type.isIncome && account.type.isError) || account.closeWithPeriod  || account == CurrencyTrading) LedgerSubAccount.IncomeSummary else account, currency)
    .agg(balance = amount.sum)
    .sortBy(account.number)
    .groupBy(account)
    .map{i => 
      let balances  <- (i.#1*.balance*.to[Money[baseCurrency]](this.date.alt(now)).allCompleted)
      (i.account, balance = balances.sum)
     }
     .groupBy(account.type).subtotal(balance.sum)
    >> 
  nav {
    subtotal {
      row {type, balance}
      table(action = {
        if (account.template.analytic) {
          Reflection.object[AnalitTrialBalance]("TB$(account.template)")
            .new(legalEntity = legalEntity, date = this.date, account = account)
            .list.open
        } else {
           Ledger.Report(account = account, legalEntity = legalEntity, to = date)
            .list.open
        }
      }) {
         account >> field(width = 0.5, background = ""), balance  >> field(width = 0.3)
      }
    }
  }
}
